# Copyright 2021 The Cobalt Authors. All Rights Reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//starboard/raspi/shared/toolchain/raspi_shared_toolchain.gni")

config("common_flags") {
  ldflags = [
    # This is a quirk of Raspbian, these are required to link any GL-related
    # libraries.
    "-L$raspi_home/busterroot/opt/vc/lib",
    "-Wl,-rpath=$raspi_home/busterroot/opt/vc/lib",
    "-L$raspi_home/busterroot/usr/lib/arm-linux-gnueabihf",
    "-Wl,-rpath=$raspi_home/busterroot/usr/lib/arm-linux-gnueabihf",
    "-L$raspi_home/busterroot/lib/arm-linux-gnueabihf",
    "-Wl,-rpath=$raspi_home/busterroot/lib/arm-linux-gnueabihf",

    # Cleanup unused sections
    "-Wl,-gc-sections",
    "-Wl,--unresolved-symbols=ignore-all",
  ]

  libs = [
    "asound",
    "rt",
    "openmaxil",
    "bcm_host",
    "vcos",
    "vchiq_arm",
    "brcmGLESv2",
    "brcmEGL",

    # Static libs must be last, to avoid __dlopen linker errors
    "EGL_static",
    "GLESv2_static",
  ]

  cflags = [
    # The compiler can't identify exhaustive switch statements and therefore
    # sometimes can't guarantee that a non-void function returns.
    "-Wno-return-type",

    # The compiler warns about missing braces for an if statement even if there
    # is no else statement.
    "-Wno-dangling-else",

    # The compiler ignores attributes on template arguments, at least sometimes.
    "-Wno-ignored-attributes",

    # Some attributes, like |maybe_unused|, are ignored by the compiler.
    "-Wno-attributes",
  ]

  cflags_cc = [
    # The compiler is claiming that |partition_alloc::internal::PartitionPage|
    # is a non-standard layout type, probably because |SlotSpanMetadata|, one of
    # its members, has both public and private members.
    # TODO: b/446904317 - do more research to ensure we're not enabling
    # undefined behavior by ignoring this warning. We should see if there's an
    # extension available to correctly implement offsetof in this case and, if
    # so, whether we're already using it. Otherwise, we may want to make a
    # customization to remove the problematic use of offsetof.
    "-Wno-invalid-offsetof",
  ]

  if (cobalt_pending_clean_up) {
    cflags += [ "-Wno-multichar" ]
  }
}

config("compiler_flags") {
  cflags = []
  cflags_c = []
  cflags_cc = []
  defines = []
  ldflags = []

  defines += [
    # Cobalt on Linux flag
    "__STDC_FORMAT_MACROS",  # so that we get PRI*
    "_GNU_SOURCE=1",
  ]

  if (is_debug) {
    cflags += [ "-O0" ]
  } else if (is_devel) {
    cflags += [ "-O2" ]
  } else {
    cflags += [ "-Wno-unused-but-set-variable" ]
  }

  cflags += [
    # Generated by code in the raspi/shared/open_max.
    "-Wno-sign-compare",

    # Generated by many starboard implementation files.
    "-Wno-unused-parameter",
    "-Wno-unused-variable",
  ]

  cflags += [
    # Force char to be signed.
    "-fsigned-char",

    # Disable strict aliasing.
    "-fno-strict-aliasing",

    # Allow Skia"s SkVx.h to convert between vectors of different element
    # types or number of subparts.
    "-flax-vector-conversions",

    # Need for stack unwinding
    "-funwind-tables",

    # To support large files
    "-D_FILE_OFFSET_BITS=64",

    # Suppress some warnings that will be hard to fix.
    "-Wno-unused-local-typedefs",
    "-Wno-unused-result",
    "-Wno-unused-function",
    "-Wno-deprecated-declarations",
    "-Wno-missing-field-initializers",
    "-Wno-extra",
    "-Wno-comment",  # Talk to my lawyer.
    "-Wno-narrowing",
    "-Wno-unknown-pragmas",
    "-Wno-type-limits",  # TODO: We should actually look into these.

    # It"s OK not to use some input parameters. Note that the order
    # matters: Wall implies Wunused-parameter and Wno-unused-parameter
    # has no effect if specified before Wall.
    "-Wno-unused-parameter",

    # This is a quirk of Raspbian, these are required to include any
    # GL-related headers.
    "-I$raspi_home/busterroot/opt/vc/include",
    "-I$raspi_home/busterroot/opt/vc/include/interface/vcos/pthreads",
    "-I$raspi_home/busterroot/opt/vc/include/interface/vmcs_host/linux",
    "-I$raspi_home/busterroot/usr/include/arm-linux-gnueabihf",
  ]

  if (is_debug || is_devel) {
    cflags += [ "-g" ]
  }

  cflags_c += [ "-std=c11" ]
  cflags_cc += [
    "-Wno-literal-suffix",

    # Generated by Audio Renderer and Audio Sink implementations.
    "-Wno-reorder",
  ]
}

config("platform_configuration") {
  configs = [ ":common_flags" ]
  if (current_toolchain == default_toolchain && sb_is_modular) {
    configs += [ "//starboard/build/config/modular/arm/hardfp" ]
  } else {
    configs +=
        [ "//starboard/raspi/shared/platform_configuration:compiler_flags" ]

    if (is_debug || is_devel) {
      configs += [ "//build/config/compiler:rtti" ]
    }
    if (current_toolchain == starboard_toolchain) {
      configs += [ "//starboard/build/config:starboard" ]
    }
  }
}

config("no_pedantic_warnings") {
  cflags = [
    # Do not warn for implicit type conversions that may change a value.
    "-Wno-conversion",
  ]
}
