# Copyright 2023 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")
import("//chrome/android/features/android_library_factory_tmpl.gni")
import("//third_party/jni_zero/jni_zero.gni")

android_library("java") {
  srcjar_deps = [ ":jni_headers" ]
  sources = [
    "java/src/org/chromium/chrome/browser/ui/hats/SurveyClient.java",
    "java/src/org/chromium/chrome/browser/ui/hats/SurveyConfig.java",
    "java/src/org/chromium/chrome/browser/ui/hats/SurveyUiDelegate.java",
  ]
  deps = [
    "//base:base_java",
    "//chrome/browser/android/lifecycle:java",
    "//chrome/browser/profiles/android:java",
    "//third_party/androidx:androidx_annotation_annotation_java",
    "//third_party/jni_zero:jni_zero_java",
  ]
}

android_library_factory("factory_java") {
  deps = [
    ":java",
    "//base:base_java",
    "//chrome/browser/privacy:privacy_preference_manager_java",
    "//chrome/browser/profiles/android:java",
    "//chrome/browser/tabmodel:java",
  ]

  # This internal file will be replaced by a generated file so the resulting
  # android_library target does not actually depend on this internal file.
  sources = [ "internal/java/src/org/chromium/chrome/browser/ui/hats/SurveyClientFactory.java" ]
}

android_library("message_ui_delegate_java") {
  sources = [
    "java/src/org/chromium/chrome/browser/ui/hats/MessageSurveyUiDelegate.java",
  ]
  public_deps = [ ":java" ]
  deps = [
    ":java",
    "//base:base_java",
    "//chrome/browser/tab:java",
    "//chrome/browser/tabmodel:java",
    "//chrome/browser/ui/android/strings:ui_strings_grd",
    "//components/browser_ui/styles/android:java_resources",
    "//components/messages/android:java",
    "//third_party/androidx:androidx_annotation_annotation_java",
    "//ui/android:ui_no_recycler_view_java",
    "//url:gurl_java",
  ]
  resources_package = "org.chromium.chrome.browser.ui.hats"
}

robolectric_library("junit") {
  testonly = true
  sources = [ "java/src/org/chromium/chrome/browser/ui/hats/MessageSurveyUiDelegateUnitTest.java" ]
  deps = [
    ":message_ui_delegate_java",
    "//base:base_java",
    "//base:base_java_test_support",
    "//base:base_junit_test_support",
    "//chrome/browser/tab:java",
    "//chrome/browser/tabmodel:java",
    "//chrome/browser/ui/android/hats:java",
    "//components/messages/android:java",
    "//content/public/android:content_java",
    "//content/public/test/android:content_java_test_support",
    "//third_party/junit",
    "//third_party/mockito:mockito_java",
    "//ui/android:ui_java_test_support",
    "//ui/android:ui_no_recycler_view_java",
    "//url:gurl_java",
    "//url:gurl_junit_test_support",
  ]
}

android_library("unit_device_javatests") {
  testonly = true
  sources =
      [ "java/src/org/chromium/chrome/browser/ui/hats/SurveyConfigTest.java" ]
  deps = [
    ":java",
    "//base:base_java",
    "//base:base_java_test_support",
    "//chrome/browser/profiles/android:java",
    "//chrome/test/android:chrome_java_integration_test_support",
    "//content/public/test/android:content_java_test_support",
    "//third_party/androidx:androidx_test_runner_java",
    "//third_party/junit",
    "//third_party/mockito:mockito_java",
  ]
}

generate_jni("jni_headers") {
  sources = [ "java/src/org/chromium/chrome/browser/ui/hats/SurveyConfig.java" ]
}

source_set("hats") {
  sources = [
    "hats_service_android.h",
    "survey_client_android.h",
    "survey_config_android.h",
    "survey_ui_delegate_android.h",
  ]

  public_deps = [
    "//base",
    "//chrome/browser/ui/hats",
    "//components/messages/android",
    "//components/messages/android:message_enums_java",
    "//content/public/browser",
  ]
}

source_set("impl") {
  sources = [
    "hats_service_android.cc",
    "survey_client_android.cc",
    "survey_config_android.cc",
    "survey_ui_delegate_android.cc",
  ]
  deps = [
    ":hats",
    "//base",
    "//chrome/app:generated_resources",
    "//chrome/browser/prefs",
    "//chrome/browser/profiles",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/ui/android/hats:jni_headers",
    "//chrome/browser/ui/android/hats/internal:jni_headers",
    "//components/messages/android",
    "//components/resources:android_resources",
    "//content/public/browser",
    "//ui/android",
    "//ui/base",
  ]
  public_deps = [ "//chrome/browser:browser_public_dependencies" ]
}

source_set("unit_tests") {
  testonly = true
  sources = [ "survey_client_android_unittest.cc" ]
  deps = [
    ":hats",
    "//base",
    "//base/test:test_support",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/ui/android/hats/test:test_support",
    "//chrome/test:test_support",
    "//content/test:test_support",
    "//ui/android",
  ]

  # TODO(crbug.com/40031409): Fix code that adds exit-time destructors and
  # enable the diagnostic by removing this line.
  configs += [ "//build/config/compiler:no_exit_time_destructors" ]
}

source_set("browser_tests") {
  testonly = true
  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
  sources = [
    "hats_service_android_browsertest.cc",
    "survey_client_android_browsertest.cc",
  ]
  deps = [
    ":hats",
    "//base",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/ui/android/hats/test:test_support",
    "//chrome/browser/ui/hats",
    "//chrome/test:test_support",
    "//chrome/test:test_support_ui_android",
    "//components/messages/android",
    "//components/messages/android/test:test_support_cpp",
    "//content/test:test_support",
    "//net:test_support",
    "//net/android:net_java_test_support_enums_srcjar",
  ]

  # TODO(crbug.com/40031409): Fix code that adds exit-time destructors and
  # enable the diagnostic by removing this line.
  configs += [ "//build/config/compiler:no_exit_time_destructors" ]
}
