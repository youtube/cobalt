# Copyright 2023 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/config.gni")
  import("//build/config/android/rules.gni")
  import("//third_party/jni_zero/jni_zero.gni")
}

static_library("internal") {
  visibility = [
    ":*",
    "//chrome/browser",
    "//chrome/browser/data_sharing:*",
    "//chrome/test:*",
    "//components/data_sharing:test_support",
    "//ios/chrome/browser/data_sharing/model",
  ]

  sources = [
    "avatar_fetcher.cc",
    "avatar_fetcher.h",
    "collaboration_group_sync_bridge.cc",
    "collaboration_group_sync_bridge.h",
    "data_sharing_network_loader_impl.cc",
    "data_sharing_network_loader_impl.h",
    "data_sharing_service_impl.cc",
    "data_sharing_service_impl.h",
    "empty_data_sharing_service.cc",
    "empty_data_sharing_service.h",
    "group_data_model.cc",
    "group_data_model.h",
    "group_data_proto_utils.cc",
    "group_data_proto_utils.h",
    "group_data_store.cc",
    "group_data_store.h",
    "logger_impl.cc",
    "logger_impl.h",
    "preview_server_proxy.cc",
    "preview_server_proxy.h",
  ]

  deps = [
    "//components/data_sharing/internal/protocol",
    "//components/data_sharing/public:logger",
    "//components/endpoint_fetcher",
    "//components/image_fetcher/core",
    "//components/signin/public/identity_manager",
    "//components/sqlite_proto",
    "//components/sync/base",
    "//components/sync/model",
    "//components/sync/protocol",
    "//google_apis/common",
    "//services/data_decoder/public/cpp",
    "//services/network/public/cpp",
    "//sql",
  ]

  public_deps = [
    "//base",
    "//components/data_sharing/public",
    "//components/data_sharing/public/protocol",
    "//components/keyed_service/core",
  ]

  if (is_android) {
    sources += [
      "android/data_sharing_conversion_bridge.cc",
      "android/data_sharing_conversion_bridge.h",
      "android/data_sharing_network_loader_android.cc",
      "android/data_sharing_network_loader_android.h",
      "android/data_sharing_sdk_delegate_android.cc",
      "android/data_sharing_sdk_delegate_android.h",
      "android/data_sharing_service_android.cc",
      "android/data_sharing_service_android.h",
      "android/fake_preview_server_proxy.cc",
      "android/fake_preview_server_proxy.h",
    ]

    deps += [
      ":jni_headers",
      "//components/data_sharing/public:android_utils",
    ]
  }
}

static_library("personal_collaboration_data_internal") {
  sources = [
    "personal_collaboration_data/personal_collaboration_data_service_impl.cc",
    "personal_collaboration_data/personal_collaboration_data_service_impl.h",
    "personal_collaboration_data/personal_collaboration_data_sync_bridge.cc",
    "personal_collaboration_data/personal_collaboration_data_sync_bridge.h",
  ]

  deps = [
    "//base",
    "//components/data_sharing/public:personal_collaboration_data",
    "//components/keyed_service/core",
  ]
}

source_set("unit_tests") {
  testonly = true

  visibility = [ "//components/data_sharing:unit_tests" ]

  sources = [
    "avatar_fetcher_unittest.cc",
    "collaboration_group_sync_bridge_unittest.cc",
    "data_sharing_network_loader_impl_unittest.cc",
    "data_sharing_service_impl_unittest.cc",
    "empty_data_sharing_service_unittest.cc",
    "group_data_model_unittest.cc",
    "group_data_proto_utils_unittest.cc",
    "group_data_store_unittest.cc",
    "logger_impl_unittest.cc",
    "personal_collaboration_data/personal_collaboration_data_service_impl_unittest.cc",
    "personal_collaboration_data/personal_collaboration_data_sync_bridge_unittest.cc",
    "preview_server_proxy_unittest.cc",
  ]

  if (is_android) {
    sources += [
      "android/data_sharing_network_loader_android_unittest.cc",
      "android/data_sharing_sdk_delegate_android_unittest.cc",
      "android/data_sharing_service_android_unittest.cc",
    ]
  }

  deps = [
    ":internal",
    ":personal_collaboration_data_internal",
    "//base",
    "//base/test:test_support",
    "//components/data_sharing:personal_collaboration_data_test_support_proto",
    "//components/data_sharing:test_support",
    "//components/data_sharing/public:logger",
    "//components/endpoint_fetcher",
    "//components/endpoint_fetcher:test_support",
    "//components/image_fetcher/core",
    "//components/image_fetcher/core:test_support",
    "//components/signin/public/identity_manager:test_support",
    "//components/sync:test_support",
    "//net/traffic_annotation:test_support",
    "//services/data_decoder/public/cpp:test_support",
    "//services/network:test_support",
    "//services/network/public/cpp",
    "//testing/gtest",
    "//ui/base",
    "//ui/gfx:test_support",
  ]

  if (is_android) {
    deps += [
      ":native_test_helper_java",
      ":test_jni_headers",
      "//components/data_sharing:test_support",
      "//url",
    ]
  }

  # TODO(crbug.com/40031409): Fix code that adds exit-time destructors and
  # enable the diagnostic by removing this line.
  configs += [ "//build/config/compiler:no_exit_time_destructors" ]
}

if (is_android) {
  android_library("internal_java") {
    visibility = [
      ":*",
      "//chrome/android:chrome_all_java",
      "//chrome/android/javatests:chrome_test_java_org.chromium.chrome.browser.collaboration",
      "//components:*",
      "//components/data_sharing/internal:native_test_helper_java",
    ]
    srcjar_deps = [ ":jni_headers" ]
    sources = [
      "android/java/src/org/chromium/components/data_sharing/DataSharingConversionBridge.java",
      "android/java/src/org/chromium/components/data_sharing/DataSharingNetworkLoaderImpl.java",
      "android/java/src/org/chromium/components/data_sharing/DataSharingSDKDelegateBridge.java",
      "android/java/src/org/chromium/components/data_sharing/DataSharingServiceImpl.java",
      "android/java/src/org/chromium/components/data_sharing/ObserverBridge.java",
    ]

    deps = [
      "//base:base_java",
      "//build/android:build_java",
      "//components/data_sharing/public:logger_interfaces_java",
      "//components/data_sharing/public:logger_java",
      "//components/data_sharing/public:public_java",
      "//components/data_sharing/public/protocol:proto_java",
      "//net/android:net_java",
      "//third_party/android_deps:protobuf_lite_runtime_java",
      "//third_party/androidx:androidx_annotation_annotation_java",
      "//third_party/jni_zero:jni_zero_java",
      "//url:gurl_java",
    ]
  }

  generate_jni("jni_headers") {
    visibility = [
      ":*",
      "//chrome/browser",
    ]

    sources = [
      "android/java/src/org/chromium/components/data_sharing/DataSharingConversionBridge.java",
      "android/java/src/org/chromium/components/data_sharing/DataSharingNetworkLoaderImpl.java",
      "android/java/src/org/chromium/components/data_sharing/DataSharingSDKDelegateBridge.java",
      "android/java/src/org/chromium/components/data_sharing/DataSharingServiceImpl.java",
      "android/java/src/org/chromium/components/data_sharing/ObserverBridge.java",
    ]
  }

  android_library("native_test_helper_java") {
    srcjar_deps = [ ":test_jni_headers" ]
    testonly = true
    sources = [
      "android/java/src/org/chromium/components/data_sharing/DataSharingSDKDelegateAndroidTestSupport.java",
      "android/java/src/org/chromium/components/data_sharing/DataSharingSDKDelegateTestImpl.java",
      "android/java/src/org/chromium/components/data_sharing/TestServiceObserver.java",
    ]
    deps = [
      ":internal_java",
      "//base:base_java",
      "//base:base_java_test_support",
      "//components/data_sharing/public:public_java",
      "//components/data_sharing/public/protocol:proto_java",
      "//google_apis/gaia/android:java",
      "//third_party/android_deps:protobuf_lite_runtime_java",
      "//third_party/jni_zero:jni_zero_java",
      "//third_party/junit",
      "//url:url_java",
    ]
  }

  generate_jni("test_jni_headers") {
    testonly = true
    sources = [
      "android/java/src/org/chromium/components/data_sharing/DataSharingSDKDelegateAndroidTestSupport.java",
      "android/java/src/org/chromium/components/data_sharing/TestServiceObserver.java",
    ]
  }

  robolectric_library("data_sharing_junit_tests") {
    sources = [ "android/java/src/org/chromium/components/data_sharing/DataSharingNetworkLoaderImplTest.java" ]

    deps = [
      ":internal_java",
      "//base:base_java",
      "//base:base_java_test_support",
      "//base:base_junit_test_support",
      "//components/data_sharing/public:public_java",
      "//net/android:net_java",
      "//third_party/jni_zero:jni_zero_java",
      "//third_party/junit",
      "//third_party/mockito:mockito_java",
      "//url:gurl_java",
    ]
  }
}
