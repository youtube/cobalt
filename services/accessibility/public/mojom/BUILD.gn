# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")
import("//services/accessibility/buildflags.gni")

mojom("automation") {
  sources = [ "automation.mojom" ]
  public_deps = [
    "//mojo/public/mojom/base",
    "//ui/accessibility/mojom",
  ]
}

mojom("assistive_technology_type") {
  sources = [ "assistive_technology_type.mojom" ]
  public_deps = [ "//mojo/public/mojom/base" ]
}

mojom("mojom") {
  if (supports_os_accessibility_service) {
    enabled_features = [ "supports_os_accessibility_service" ]
  }
  sources = [ "accessibility_service.mojom" ]
  public_deps = [
    ":automation",
    "//mojo/public/mojom/base",
    "//sandbox/policy/mojom",
  ]
  if (supports_os_accessibility_service) {
    public_deps += [
      ":assistive_technology_type",
      ":os_service_mojom",
      "//third_party/blink/public/mojom:mojom_platform",
      "//ui/accessibility/mojom",
    ]
  }
}

if (supports_os_accessibility_service) {
  mojom("os_service_mojom") {
    sources = [
      "autoclick.mojom",
      "file_loader.mojom",
      "speech_recognition.mojom",
      "tts.mojom",
      "user_interface.mojom",
    ]
    public_deps = [
      ":assistive_technology_type",
      "//mojo/public/mojom/base",
      "//skia/public/mojom:mojom",
      "//ui/gfx/geometry/mojom",
    ]
  }

  group("os_accessibility_service_js") {
    testonly = true
    data = [
      "$root_gen_dir/skia/public/mojom/skcolor.mojom-lite.js",
      "$root_gen_dir/ui/gfx/geometry/mojom/geometry.mojom-lite.js",
      "$target_gen_dir/assistive_technology_type.mojom-lite.js",
      "$target_gen_dir/autoclick.mojom-lite.js",
      "$target_gen_dir/speech_recognition.mojom-lite.js",
      "$target_gen_dir/tts.mojom-lite.js",
      "$target_gen_dir/user_interface.mojom-lite.js",
    ]
    deps = [
      ":assistive_technology_type_js__generator",
      ":os_service_mojom_js__generator",
    ]
  }
}
