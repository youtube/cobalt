# Copyright 2020 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")

generate_jni("content_settings_jni_headers") {
  sources = [
    "java/src/org/chromium/components/content_settings/ContentSettingsObserver.java",
    "java/src/org/chromium/components/content_settings/CookieControlsBridge.java",
  ]
}

android_library("java") {
  sources = [
    "java/src/org/chromium/components/content_settings/ContentSettingsObserver.java",
    "java/src/org/chromium/components/content_settings/CookieControlsBridge.java",
    "java/src/org/chromium/components/content_settings/CookieControlsObserver.java",
  ]
  deps = [
    ":content_settings_enums_java",
    "//base:jni_java",
    "//build/android:build_java",
    "//content/public/android:content_java",
    "//third_party/androidx:androidx_annotation_annotation_java",
  ]
  annotation_processor_deps = [ "//base/android/jni_generator:jni_processor" ]
}

source_set("android") {
  sources = [
    "content_settings_observer_android.cc",
    "content_settings_observer_android.h",
    "cookie_controls_bridge.cc",
    "cookie_controls_bridge.h",
  ]
  deps = [
    ":content_settings_jni_headers",
    "//base",
    "//components/content_settings/browser/ui",
    "//components/content_settings/core/browser",
    "//components/content_settings/core/common",
    "//components/permissions",
    "//content/public/browser",
  ]
}

android_library("content_settings_enums_java") {
  sources = [ "java/src/org/chromium/components/content_settings/ContentSettingsTypeSet.java" ]
  srcjar_deps = [ ":content_settings_enums_javagen" ]
  deps = [ "//third_party/androidx:androidx_annotation_annotation_java" ]
}

java_cpp_enum("content_settings_enums_javagen") {
  sources = [
    "../core/browser/cookie_settings.h",
    "../core/common/content_settings.h",
    "../core/common/content_settings_types.h",
    "../core/common/cookie_controls_enforcement.h",
    "../core/common/cookie_controls_status.h",
  ]
  visibility = [ ":*" ]  # Depend on through :content_settings_enums_java
}

java_cpp_strings("java_pref_names_srcjar") {
  sources = [ "//components/content_settings/core/common/pref_names.cc" ]

  template = "java_templates/PrefNames.java.tmpl"
}
