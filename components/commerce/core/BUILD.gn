# Copyright 2021 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/protobuf/proto_library.gni")
import("//ui/webui/resources/tools/build_webui.gni")

if (is_android) {
  import("//build/config/android/rules.gni")
}

source_set("public") {
  sources = [
    "discount_consent_handler.h",
    "shopping_prompt.h",
  ]
}

source_set("feature_list") {
  sources = [
    "commerce_feature_list.cc",
    "commerce_feature_list.h",
    "flag_descriptions.cc",
    "flag_descriptions.h",
  ]

  deps = [
    ":commerce_heuristics_data",
    ":pref_names",
    "//base",
    "//components/country_codes",
    "//components/flags_ui",
    "//components/prefs",
    "//components/search",
    "//components/variations/service",
    "//third_party/re2:re2",
    "//url:url",
  ]
}

source_set("feature_list_unittests") {
  testonly = true
  sources = [ "commerce_feature_list_unittest.cc" ]
  deps = [
    ":commerce_heuristics_data",
    ":feature_list",
    ":pref_names",
    ":shopping_service_test_support",
    "//base",
    "//base/test:test_support",
    "//components/prefs:test_support",
    "//testing/gtest",
    "//third_party/re2:re2",
  ]
}

static_library("metrics") {
  sources = [
    "metrics/metrics_utils.cc",
    "metrics/metrics_utils.h",
  ]

  deps = [
    ":proto",
    "//base",
    "//components/optimization_guide/core",
    "//components/optimization_guide/proto:optimization_guide_proto",
    "//components/prefs",
  ]
}

proto_library("proto") {
  proto_in_dir = "//"
  sources = [
    "proto/merchant_trust.proto",
    "proto/price_tracking.proto",
  ]
}

if (is_android) {
  proto_java_library("proto_java") {
    proto_path = "."
    sources = [ "proto/price_tracking.proto" ]
  }

  proto_library("merchant_signal_db_proto") {
    proto_in_dir = "//"
    sources = [ "proto/merchant_signal_db_content.proto" ]
  }
} else {
  proto_library("cart_db_content_proto") {
    proto_in_dir = "//"
    sources = [ "proto/cart_db_content.proto" ]
  }

  proto_library("coupon_db_content_proto") {
    proto_in_dir = "//"
    sources = [ "proto/coupon_db_content.proto" ]
  }
}

proto_library("persisted_state_db_content_proto") {
  proto_in_dir = "//"
  sources = [ "proto/persisted_state_db_content.proto" ]
}

proto_library("commerce_subscription_db_content_proto") {
  proto_in_dir = "//"
  sources = [ "proto/commerce_subscription_db_content.proto" ]
}

source_set("commerce_heuristics_data") {
  sources = [
    "commerce_heuristics_data.cc",
    "commerce_heuristics_data.h",
    "commerce_heuristics_data_metrics_helper.cc",
    "commerce_heuristics_data_metrics_helper.h",
  ]

  deps = [
    "//base",
    "//third_party/re2:re2",
  ]
}

source_set("commerce_heuristics_data_unittests") {
  testonly = true
  sources = [ "commerce_heuristics_data_unittest.cc" ]
  deps = [
    ":commerce_heuristics_data",
    "//base",
    "//base/test:test_support",
    "//testing/gtest",
    "//third_party/re2:re2",
  ]
}

source_set("heuristics_provider") {
  sources = [
    "heuristics/commerce_heuristics_provider.cc",
    "heuristics/commerce_heuristics_provider.h",
  ]

  deps = [
    ":commerce_heuristics_data",
    ":feature_list",
    "//base",
    "//components/resources:components_resources_grit",
    "//net",
    "//third_party/re2:re2",
    "//ui/base",
    "//url:url",
  ]
}

source_set("heuristics_provider_unittests") {
  testonly = true
  sources = [ "heuristics/commerce_heuristics_provider_unittest.cc" ]
  deps = [
    ":feature_list",
    ":heuristics_provider",
    "//base",
    "//base/test:test_support",
    "//components/prefs:test_support",
    "//testing/gtest",
    "//third_party/re2:re2",
    "//url:url",
  ]
}

static_library("shopping_service") {
  sources = [
    "bookmark_update_manager.cc",
    "bookmark_update_manager.h",
    "metrics/scheduled_metrics_manager.cc",
    "metrics/scheduled_metrics_manager.h",
    "price_tracking_utils.cc",
    "price_tracking_utils.h",
    "shopping_bookmark_model_observer.cc",
    "shopping_bookmark_model_observer.h",
    "shopping_power_bookmark_data_provider.cc",
    "shopping_power_bookmark_data_provider.h",
    "shopping_service.cc",
    "shopping_service.h",
    "web_wrapper.h",
  ]

  deps = [
    ":account_checker",
    ":commerce_subscription_db_content_proto",
    ":feature_list",
    ":metrics",
    ":pref_names",
    ":proto",
    "//base",
    "//components/bookmarks/browser",
    "//components/keyed_service/core",
    "//components/leveldb_proto",
    "//components/optimization_guide/core",
    "//components/optimization_guide/proto:optimization_guide_proto",
    "//components/power_bookmarks/core",
    "//components/power_bookmarks/core:proto",
    "//components/prefs:prefs",
    "//components/resources:components_resources_grit",
    "//components/search:search",
    "//components/session_proto_db:core",
    "//components/signin/public/identity_manager:identity_manager",
    "//components/sync/driver",
    "//services/network/public/cpp:cpp",
    "//ui/base",
    "//url:url",
  ]

  public_deps = [
    "//components/commerce/core/subscriptions:subscriptions",
    "//services/data_decoder/public/cpp",
  ]

  if (is_android) {
    sources += [
      "android/shopping_service_android.cc",
      "android/shopping_service_android.h",
    ]

    deps += [
      ":shopping_service_jni_headers",
      "//url:gurl_android",
    ]
  }
}

if (is_android) {
  generate_jni("shopping_service_jni_headers") {
    sources = [ "android/java/src/org/chromium/components/commerce/core/ShoppingService.java" ]
  }
}

source_set("shopping_service_test_support") {
  testonly = true

  sources = [
    "mock_shopping_service.cc",
    "mock_shopping_service.h",
    "shopping_service_test_base.cc",
    "shopping_service_test_base.h",
    "test_utils.cc",
    "test_utils.h",
  ]

  # Expose all deps for test support.
  public_deps = [
    ":feature_list",
    ":pref_names",
    ":shopping_service",
    "//base/test:test_support",
    "//components/bookmarks/browser",
    "//components/bookmarks/test",
    "//components/commerce/core:proto",
    "//components/commerce/core/subscriptions",
    "//components/optimization_guide/core",
    "//components/optimization_guide/proto:optimization_guide_proto",
    "//components/power_bookmarks/core",
    "//components/prefs:test_support",
    "//components/signin/public/identity_manager:test_support",
    "//components/sync:test_support",
    "//services/data_decoder/public/cpp:test_support",
    "//services/network:network_service",
    "//services/network:test_support",
    "//services/network/public/cpp",
    "//testing/gmock",
    "//testing/gtest",
    "//url:url",
  ]
}

source_set("shopping_service_unit_tests") {
  testonly = true

  sources = [
    "bookmark_update_manager_unittest.cc",
    "metrics/scheduled_metrics_manager_unittest.cc",
    "pdp_metrics_unittest.cc",
    "price_tracking_utils_unittest.cc",
    "shopping_bookmark_model_observer_unittest.cc",
    "shopping_power_bookmark_data_provider_unittest.cc",
    "shopping_service_metrics_unittest.cc",
    "shopping_service_unittest.cc",
  ]

  deps = [
    ":account_checker_test_support",
    ":metrics",
    ":pref_names",
    ":shopping_service_test_support",
    "subscriptions:test_support",
    "webui:unit_tests",
    "//components/search",
  ]
}

source_set("pref_names") {
  sources = [
    "pref_names.cc",
    "pref_names.h",
  ]

  deps = [
    "//base",
    "//components/pref_registry",
    "//components/prefs",
  ]
}

source_set("account_checker") {
  sources = [
    "account_checker.cc",
    "account_checker.h",
  ]

  deps = [
    ":feature_list",
    ":pref_names",
    "//base",
    "//components/endpoint_fetcher:endpoint_fetcher",
    "//components/prefs:prefs",
    "//components/signin/public/identity_manager:identity_manager",
    "//components/sync/driver",
    "//components/unified_consent",
    "//net/traffic_annotation:traffic_annotation",
    "//services/data_decoder/public/cpp",
    "//services/network/public/cpp:cpp",
    "//ui/base",
    "//url:url",
  ]
}

source_set("account_checker_test_support") {
  testonly = true

  sources = [
    "mock_account_checker.cc",
    "mock_account_checker.h",
  ]

  deps = [
    ":account_checker",
    "//base",
    "//services/network/public/cpp:cpp",
    "//testing/gmock",
  ]
}

source_set("account_checker_unittests") {
  testonly = true
  sources = [ "account_checker_unittest.cc" ]
  deps = [
    ":account_checker",
    ":feature_list",
    ":pref_names",
    "//base",
    "//base/test:test_support",
    "//components/endpoint_fetcher:test_support",
    "//components/prefs:test_support",
    "//components/signin/public/identity_manager:test_support",
    "//components/sync:test_support",
    "//net/traffic_annotation:test_support",
    "//services/data_decoder/public/cpp:test_support",
    "//services/network:test_support",
    "//services/network/public/cpp:cpp",
    "//testing/gmock",
    "//testing/gtest",
    "//url:url",
  ]
}

source_set("commerce_constants") {
  sources = [
    "commerce_constants.cc",
    "commerce_constants.h",
  ]
}
