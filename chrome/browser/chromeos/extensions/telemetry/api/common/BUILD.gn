# Copyright 2023 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//extensions/buildflags/buildflags.gni")
assert(enable_extensions,
       "Cannot depend on extensions because enable_extensions=false.")

source_set("common") {
  sources = [
    "api_guard_delegate.cc",
    "api_guard_delegate.h",
    "base_telemetry_extension_api_guard_function.cc",
    "base_telemetry_extension_api_guard_function.h",
  ]

  deps = [
    ":hardware_info_delegate",
    "//base",
    "//build:chromeos_buildflags",
    "//chrome/browser/extensions",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/ui",
    "//chrome/common/chromeos/extensions",
    "//components/security_state/content",
    "//components/security_state/core",
    "//content/public/browser",
    "//extensions/browser",
    "//extensions/common",
    "//third_party/abseil-cpp:absl",
  ]

  if (is_chromeos_ash) {
    deps += [ "//components/user_manager" ]
  }

  if (is_chromeos_lacros) {
    deps += [
      "//chromeos/crosapi/mojom",
      "//chromeos/startup",
      "//components/policy/core/common",
    ]
  }
}

source_set("remote_probe_strategy") {
  sources = [
    "remote_probe_service_strategy.cc",
    "remote_probe_service_strategy.h",
  ]

  deps = [
    "//build:chromeos_buildflags",
    "//chromeos/crosapi/mojom",
    "//mojo/public/cpp/bindings",
  ]

  if (is_chromeos_ash) {
    deps += [ "//chrome/browser/ash/telemetry_extension" ]
  }

  if (is_chromeos_lacros) {
    deps += [ "//chromeos/lacros" ]
  }
}

source_set("hardware_info_delegate") {
  sources = [
    "hardware_info_delegate.cc",
    "hardware_info_delegate.h",
  ]

  deps = [
    ":remote_probe_strategy",
    "//base",
    "//chromeos/crosapi/mojom",
  ]
}

source_set("fake_hardware_info_delegate") {
  testonly = true
  sources = [
    "fake_hardware_info_delegate.cc",
    "fake_hardware_info_delegate.h",
  ]

  deps = [
    ":hardware_info_delegate",
    "//base",
  ]
}

source_set("test_support") {
  testonly = true
  sources = [
    "base_telemetry_extension_browser_test.cc",
    "base_telemetry_extension_browser_test.h",
    "fake_api_guard_delegate.cc",
    "fake_api_guard_delegate.h",
  ]

  deps = [
    ":common",
    "//base",
    "//chrome/test:test_support",
    "//extensions:test_support",
    "//testing/gtest",
    "//third_party/abseil-cpp:absl",
  ]
}

source_set("browser_tests") {
  testonly = true
  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
  sources = [
    "base_telemetry_extension_api_guard_function_browsertest.cc",
    "telemetry_extension_capabilities_browser_test.cc",
  ]

  deps = [
    ":common",
    ":fake_hardware_info_delegate",
    ":test_support",
    "//base",
    "//base/test:test_support",
    "//build:chromeos_buildflags",
    "//chrome/common/chromeos/extensions",
    "//chrome/test:test_support_ui",
    "//content/public/browser",
    "//content/test:test_support",
    "//extensions:test_support",
    "//extensions/common",
    "//mojo/public/cpp/bindings",
    "//net",
    "//net:test_support",
    "//testing/gtest",
    "//url",
  ]

  data = [ "//chrome/test/data" ]

  if (is_chromeos_ash) {
    deps += [
      "//chrome/browser/ash:test_support",
      "//chrome/browser/ash/telemetry_extension",
      "//components/user_manager",
    ]
  }

  if (is_chromeos_lacros) {
    deps += [
      "//chromeos/lacros",
      "//chromeos/startup",
      "//components/policy/core/common",
    ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [ "api_guard_delegate_unittest.cc" ]
  deps = [
    ":common",
    ":fake_hardware_info_delegate",
    "//base",
    "//base/test:test_support",
    "//build:chromeos_buildflags",
    "//chrome/browser/extensions:test_support",
    "//chrome/test:test_support",
    "//components/sync_preferences:test_support",
    "//content/public/browser",
    "//extensions/common",
    "//net",
    "//net:test_support",
    "//testing/gtest",
  ]

  if (is_chromeos_ash) {
    deps += [
      "//chrome/browser/ash:test_support",
      "//components/user_manager",
    ]
  }

  if (is_chromeos_lacros) {
    deps += [
      "//chromeos/crosapi/mojom",
      "//chromeos/startup",
      "//components/policy/core/common",
    ]
  }
}
