# Copyright 2021 The Cobalt Authors. All Rights Reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/android/rules.gni")
import("//starboard/android/shared/toolchain/toolchain.gni")
import("//starboard/shared/starboard/media/media_tests.gni")
import("//starboard/shared/starboard/player/buildfiles.gni")
import("//starboard/shared/starboard/player/player_tests.gni")
import("//testing/test.gni")

##########################################################
# Configuration for overall Android Starboard Platform.
##########################################################

config("starboard_platform_libs") {
  libs = [
    "EGL",
    "OpenSLES",
    "mediandk",
  ]
}

# TODO(cobalt, b/375669373): re-enable API without ApplicationAndroid
static_library("starboard_platform") {
  sources = [
    "//starboard/shared/egl/system_egl.cc",
    "//starboard/shared/gles/gl_call.h",
    "//starboard/shared/gles/system_gles2.cc",
    "//starboard/shared/internal_only.h",
    "//starboard/shared/linux/system_get_random_data.cc",
    "//starboard/shared/linux/system_get_total_cpu_memory.cc",
    "//starboard/shared/linux/system_get_used_cpu_memory.cc",
    "//starboard/shared/linux/system_is_debugger_attached.cc",
    "//starboard/shared/linux/system_symbolize.cc",
    "//starboard/shared/opus/opus_audio_decoder.cc",
    "//starboard/shared/opus/opus_audio_decoder.h",
    "//starboard/shared/posix/environment.cc",
    "//starboard/shared/posix/file_atomic_replace.cc",
    "//starboard/shared/posix/page_internal.cc",
    "//starboard/shared/posix/set_non_blocking_internal.cc",
    "//starboard/shared/posix/storage_write_record.cc",
    "//starboard/shared/posix/system_break_into_debugger.cc",
    "//starboard/shared/posix/system_clear_last_error.cc",
    "//starboard/shared/posix/system_get_error_string.cc",
    "//starboard/shared/posix/system_get_last_error.cc",
    "//starboard/shared/posix/system_get_number_of_processors.cc",
    "//starboard/shared/pthread/thread_context_get_pointer.cc",
    "//starboard/shared/pthread/thread_context_internal.cc",
    "//starboard/shared/pthread/thread_context_internal.h",
    "//starboard/shared/pthread/thread_sampler_create.cc",
    "//starboard/shared/pthread/thread_sampler_destroy.cc",
    "//starboard/shared/pthread/thread_sampler_freeze.cc",
    "//starboard/shared/pthread/thread_sampler_internal.cc",
    "//starboard/shared/pthread/thread_sampler_internal.h",
    "//starboard/shared/pthread/thread_sampler_is_supported.cc",
    "//starboard/shared/pthread/thread_sampler_thaw.cc",
    "//starboard/shared/signal/crash_signals.h",
    "//starboard/shared/signal/crash_signals_sigaction.cc",
    "//starboard/shared/signal/suspend_signals.cc",
    "//starboard/shared/signal/suspend_signals.h",
    "//starboard/shared/starboard/application.cc",
    "//starboard/shared/starboard/application.h",
    "//starboard/shared/starboard/audio_sink/audio_sink_create.cc",
    "//starboard/shared/starboard/audio_sink/audio_sink_destroy.cc",
    "//starboard/shared/starboard/audio_sink/audio_sink_internal.cc",
    "//starboard/shared/starboard/audio_sink/audio_sink_internal.h",
    "//starboard/shared/starboard/audio_sink/audio_sink_is_valid.cc",
    "//starboard/shared/starboard/audio_sink/stub_audio_sink_type.cc",
    "//starboard/shared/starboard/audio_sink/stub_audio_sink_type.h",
    "//starboard/shared/starboard/decode_target/decode_target_get_info.cc",
    "//starboard/shared/starboard/decode_target/decode_target_internal.h",
    "//starboard/shared/starboard/decode_target/decode_target_release.cc",
    "//starboard/shared/starboard/drm/drm_close_session.cc",
    "//starboard/shared/starboard/drm/drm_destroy_system.cc",
    "//starboard/shared/starboard/drm/drm_generate_session_update_request.cc",
    "//starboard/shared/starboard/drm/drm_get_metrics.cc",
    "//starboard/shared/starboard/drm/drm_is_server_certificate_updatable.cc",
    "//starboard/shared/starboard/drm/drm_system_internal.h",
    "//starboard/shared/starboard/drm/drm_update_server_certificate.cc",
    "//starboard/shared/starboard/drm/drm_update_session.cc",
    "//starboard/shared/starboard/event_cancel.cc",
    "//starboard/shared/starboard/event_schedule.cc",
    "//starboard/shared/starboard/feature_list.cc",
    "//starboard/shared/starboard/feature_list.h",
    "//starboard/shared/starboard/features.cc",
    "//starboard/shared/starboard/features.h",
    "//starboard/shared/starboard/file_atomic_replace_write_file.cc",
    "//starboard/shared/starboard/file_atomic_replace_write_file.h",
    "//starboard/shared/starboard/file_storage/storage_close_record.cc",
    "//starboard/shared/starboard/file_storage/storage_delete_record.cc",
    "//starboard/shared/starboard/file_storage/storage_get_record_size.cc",
    "//starboard/shared/starboard/file_storage/storage_open_record.cc",
    "//starboard/shared/starboard/file_storage/storage_read_record.cc",
    "//starboard/shared/starboard/log_mutex.cc",
    "//starboard/shared/starboard/log_mutex.h",
    "//starboard/shared/starboard/log_raw_dump_stack.cc",
    "//starboard/shared/starboard/log_raw_format.cc",
    "//starboard/shared/starboard/media/media_can_play_mime_and_key_system.cc",
    "//starboard/shared/starboard/media/media_get_audio_buffer_budget.cc",
    "//starboard/shared/starboard/media/media_get_buffer_alignment.cc",
    "//starboard/shared/starboard/media/media_get_buffer_allocation_unit.cc",
    "//starboard/shared/starboard/media/media_get_buffer_garbage_collection_duration_threshold.cc",
    "//starboard/shared/starboard/media/media_get_buffer_padding.cc",
    "//starboard/shared/starboard/media/media_get_progressive_buffer_budget.cc",
    "//starboard/shared/starboard/media/media_is_buffer_using_memory_pool.cc",
    "//starboard/shared/starboard/microphone/microphone_close.cc",
    "//starboard/shared/starboard/microphone/microphone_create.cc",
    "//starboard/shared/starboard/microphone/microphone_destroy.cc",
    "//starboard/shared/starboard/microphone/microphone_get_available.cc",
    "//starboard/shared/starboard/microphone/microphone_internal.h",
    "//starboard/shared/starboard/microphone/microphone_is_sample_rate_supported.cc",
    "//starboard/shared/starboard/microphone/microphone_open.cc",
    "//starboard/shared/starboard/microphone/microphone_read.cc",
    "//starboard/shared/starboard/new.cc",
    "//starboard/shared/starboard/queue_application.cc",
    "//starboard/shared/starboard/queue_application.h",
    "//starboard/shared/starboard/system_get_random_uint64.cc",
    "//starboard/shared/starboard/system_supports_resume.cc",
    "//starboard/shared/starboard/thread_checker.h",
    "//starboard/shared/starboard/window_set_default_options.cc",
    "//starboard/shared/stub/system_get_total_gpu_memory.cc",
    "//starboard/shared/stub/system_get_used_gpu_memory.cc",
    "//starboard/shared/stub/system_hide_splash_screen.cc",
    "//starboard/shared/stub/system_request_blur.cc",
    "//starboard/shared/stub/system_request_focus.cc",
    "//starboard/shared/stub/system_request_reveal.cc",
    "//starboard/shared/stub/system_sign_with_certification_secret_key.cc",
    "accessibility_extension.cc",
    "accessibility_extension.h",
    "accessibility_get_caption_settings.cc",
    "accessibility_get_display_settings.cc",
    "accessibility_set_captions_enabled.cc",
    "android_media_session_client.cc",
    "application_android.cc",
    "application_android.h",

    # TODO: b/374300500 - posix_emu things should not be used on Android anymore
    # "asset_manager.cc",
    # "asset_manager.h",
    "audio_decoder.cc",
    "audio_decoder.h",
    "audio_decoder_passthrough.h",
    "audio_output_manager.cc",
    "audio_output_manager.h",
    "audio_permission_requester.cc",
    "audio_permission_requester.h",
    "audio_renderer_passthrough.cc",
    "audio_renderer_passthrough.h",
    "audio_sink_get_max_channels.cc",
    "audio_sink_get_min_buffer_size_in_frames.cc",
    "audio_sink_get_nearest_supported_sample_frequency.cc",
    "audio_sink_is_audio_frame_storage_type_supported.cc",
    "audio_sink_is_audio_sample_type_supported.cc",
    "audio_sink_min_required_frames_tester.cc",
    "audio_sink_min_required_frames_tester.h",
    "audio_track_audio_sink_type.cc",
    "audio_track_audio_sink_type.h",
    "audio_track_bridge.cc",
    "audio_track_bridge.h",
    "configuration.cc",
    "configuration.h",
    "configuration_constants.cc",
    "configuration_public.h",
    "continuous_audio_track_sink.cc",
    "continuous_audio_track_sink.h",
    "crash_handler.cc",
    "crash_handler.h",
    "decode_target.cc",
    "decode_target.h",
    "display_util.cc",
    "display_util.h",
    "drm_create_system.cc",
    "drm_session_id_mapper.cc",
    "drm_session_id_mapper.h",
    "drm_system.cc",
    "drm_system.h",
    "egl_swap_buffers.cc",
    "exoplayer/exoplayer_bridge.cc",
    "exoplayer/exoplayer_bridge.h",
    "exoplayer/exoplayer_util.cc",
    "exoplayer/exoplayer_util.h",
    "exoplayer/exoplayer_worker_handler.cc",
    "exoplayer/exoplayer_worker_handler.h",
    "features_extension.cc",
    "features_extension.h",
    "file_internal.cc",
    "file_internal.h",
    "get_home_directory.cc",
    "graphics.cc",
    "graphics.h",
    "log.cc",
    "log_flush.cc",
    "log_format.cc",
    "log_internal.cc",
    "log_internal.h",
    "log_raw.cc",
    "max_media_codec_output_buffers_lookup_table.cc",
    "max_media_codec_output_buffers_lookup_table.h",
    "media_capabilities_cache.cc",
    "media_capabilities_cache.h",
    "media_codec_bridge.cc",
    "media_codec_bridge.h",
    "media_common.h",
    "media_decoder.cc",
    "media_decoder.h",
    "media_drm_bridge.cc",
    "media_drm_bridge.h",
    "media_get_audio_configuration.cc",
    "media_get_audio_output_count.cc",
    "media_get_initial_buffer_capacity.cc",
    "media_get_video_buffer_budget.cc",
    "media_is_audio_supported.cc",
    "media_is_buffer_pool_allocate_on_demand.cc",
    "media_is_supported.cc",
    "media_is_video_supported.cc",
    "microphone_impl.cc",
    "platform_info.cc",
    "platform_info.h",
    "platform_service.cc",
    "player_components_factory.cc",
    "player_create.cc",
    "player_destroy.cc",
    "player_get_maximum_number_of_samples_per_write.cc",
    "player_get_preferred_output_mode.cc",
    "player_set_bounds.cc",
    "player_set_max_video_input_size.cc",
    "player_set_max_video_input_size.h",
    "player_set_playback_rate.cc",

    # TODO: b/374300500 - posix_emu things should not be used on Android anymore
    # "posix_emu/dirent.cc",
    # "posix_emu/errno.cc",
    # "posix_emu/file.cc",
    # "posix_emu/pthread.cc",
    # "posix_emu/stat.cc",

    "runtime_resource_overlay.cc",
    "runtime_resource_overlay.h",
    "sanitizer_options.cc",

    # TODO: (cobalt b/392178584) clean up speech synthesis code.
    #"speech_synthesis_cancel.cc",
    #"speech_synthesis_internal.cc",
    #"speech_synthesis_is_supported.cc",
    #"speech_synthesis_speak.cc",
    "starboard_bridge.cc",
    "starboard_bridge.h",
    "system_get_extensions.cc",
    "system_get_locale_id.cc",
    "system_get_path.cc",
    "system_get_property.cc",
    "system_get_stack.cc",
    "system_has_capability.cc",
    "system_info_api.cc",
    "system_info_api.h",
    "system_platform_error.cc",
    "system_request_conceal.cc",
    "system_request_freeze_no_freezedone_callback.cc",
    "system_request_stop.cc",
    "text_to_speech_helper.cc",
    "text_to_speech_helper.h",
    "thread_create_priority.cc",
    "thread_get_id.cc",
    "thread_platform_android.cc",
    "time_zone_get_name.cc",
    "video_decoder.cc",
    "video_decoder.h",
    "video_frame_tracker.cc",
    "video_frame_tracker.h",
    "video_max_video_input_size.cc",
    "video_max_video_input_size.h",
    "video_render_algorithm.cc",
    "video_render_algorithm.h",
    "video_surface_texture_bridge.cc",
    "video_surface_texture_bridge.h",
    "video_window.cc",
    "video_window.h",

    #"window_create.cc",
    #"window_destroy.cc",
    #"window_get_diagonal_size_in_inches.cc",
    #"window_get_platform_handle.cc",
    #"window_get_size.cc",
    #"window_internal.h",
  ]

  sources += common_player_sources

  sources -= [
    "//starboard/shared/starboard/player/player_create.cc",
    "//starboard/shared/starboard/player/player_destroy.cc",
    "//starboard/shared/starboard/player/player_get_maximum_number_of_samples_per_write.cc",
    "//starboard/shared/starboard/player/player_get_preferred_output_mode_prefer_punchout.cc",
    "//starboard/shared/starboard/player/player_set_bounds.cc",
    "//starboard/shared/starboard/player/player_set_playback_rate.cc",
  ]

  configs += [
    "//starboard/build/config:starboard_implementation",
    ":starboard_platform_libs",
  ]

  public_deps = [
    ":starboard_base_symbolize",
    "//cobalt/android:jni_headers",
    "//cobalt/browser/client_hint_headers",
    "//cobalt/browser/h5vcc_accessibility:manager",
    "//cobalt/browser/h5vcc_runtime:deep_link_manager",
    "//starboard/common",
    "//starboard/shared/starboard/media:media_util",
    "//starboard/shared/starboard/player/filter:filter_based_player_sources",
  ]

  deps = [
    "//base",
    "//third_party/opus",
  ]

  if (sb_is_evergreen_compatible && current_toolchain == starboard_toolchain) {
    sources -= [
      "crash_handler.cc",
      "crash_handler.h",
    ]
    sources += [
      "//starboard/shared/starboard/crash_handler.cc",
      "//starboard/shared/starboard/crash_handler.h",
      "//starboard/shared/starboard/loader_app_metrics.cc",
      "//starboard/shared/starboard/loader_app_metrics.h",
    ]

    public_deps += [
      "//starboard/elf_loader:evergreen_config",
      "//starboard/loader_app:pending_restart",
    ]

    deps += [ "//starboard/crashpad_wrapper" ]
  }

  if (sb_evergreen_compatible_use_libunwind) {
    deps += [ "//third_party/llvm-project/libunwind:unwind_starboard" ]
  }
}

config("symbolize_config") {
  cflags = [
    "-Wno-unreachable-code-return",
    "-Wno-unused-but-set-variable",
  ]
}

static_library("starboard_base_symbolize") {
  sources = [
    "//base/third_party/symbolize/demangle.cc",
    "//base/third_party/symbolize/symbolize.cc",
  ]
  configs += [ ":symbolize_config" ]

  deps = [
    "//starboard:starboard_headers_only",
    "//starboard/common:common_headers_only",
  ]
  if (sb_is_evergreen_compatible && current_toolchain == starboard_toolchain) {
    public_deps = [ "//starboard/elf_loader:evergreen_info" ]
  }

  # TODO(cobalt, 377295011): enable dependency checking. A dependency on
  # //base/third_party/symbolize:symbolize is needed but introduces a duplicate
  # symbol error.
  check_includes = false
}

test("starboard_platform_tests") {
  testonly = true

  sources = media_tests_sources + player_tests_sources + [
              "//starboard/common/test_main.cc",
              "drm_session_id_mapper_test.cc",
              "media_capabilities_cache_test.cc",
              "model_year_test.cc",
              "player_get_preferred_output_mode_test.cc",
              "video_frame_tracker_test.cc",
            ]

  # Add starboard_feature_test to Android's starboard_platform_tests,
  # since Android is the only platform that includes starboard_feature.
  sources += [ "//starboard/shared/starboard/starboard_feature_test.cc" ]

  configs += [ "//starboard/build/config:starboard_implementation" ]

  deps = [
    ":starboard_platform",
    "//starboard:starboard_group",
    "//starboard/shared/starboard/drm:drm_test_helpers",
    "//starboard/shared/starboard/player/filter/testing:test_util",
    "//testing/gmock",
    "//testing/gtest",
  ]
}

java_cpp_enum("java_enums") {
  sources = [
    "exoplayer/exoplayer_bridge.h",
    "media_codec_bridge.h",
    "media_drm_bridge.h",
  ]
}
