# Copyright 2024 The Cobalt Authors. All Rights Reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

group("gn_all") {
  testonly = true

  # TODO(b/371589344): Fix android build configs.
  deps = [
    "//starboard($starboard_toolchain)",
    "//starboard/nplb",
  ]
  if (!is_android) {
    deps += [ ":cobalt" ]
  } else {
    # TODO(cobalt): reuse build rules below to define Android target.
    deps += [ "android:cobalt_apk" ]
  }
}

if (!is_android) {
  executable("cobalt") {
    testonly = true

    sources = [
      "cobalt.cc",
      "cobalt_content_browser_client.cc",
      "cobalt_content_browser_client.h",
      "cobalt_main_delegate.cc",
      "cobalt_main_delegate.h",
    ]

    defines = []

    deps = [
      "//content/public/app",
      "//content/shell:content_shell_app",
      "//content/shell:content_shell_lib",
      "//content/shell:pak",
      "//sandbox",
      "//third_party/blink/public/common",
    ]

    data_deps = [
      "//content/shell:pak",
      "//tools/v8_context_snapshot:v8_context_snapshot",
    ]

    data_deps += [ "//components/crash/core/app:chrome_crashpad_handler" ]

    configs += [ "//build/config/gcc:rpath_for_built_shared_libraries" ]
  }
}

static_library("common") {
  sources = [
    "user_agent_platform_info.cc",
    "user_agent_platform_info.h",
  ]

  deps = [
    "//base",
    "//starboard/common",
  ]
}

target(gtest_target_type, "common_unittests") {
  testonly = true

  sources = [ "user_agent_platform_info_test.cc" ]

  deps = [
    ":common",
    "//testing/gmock",
    "//testing/gtest",
  ]
}
