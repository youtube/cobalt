# Copyright 2024 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(is_win || is_mac || is_linux || is_chromeos)

source_set("commerce") {
  sources = [
    "add_to_comparison_table_sub_menu_model.h",
    "commerce_page_action_controller.h",
    "commerce_ui_tab_helper.h",
    "compare_sub_menu_model.h",
    "discounts_page_action_controller.h",
    "price_tracking_page_action_controller.h",
    "product_specifications_entry_point_controller.h",
    "product_specifications_page_action_controller.h",
    "ui_utils.h",
  ]
  public_deps = [
    "//base",
    "//chrome/browser/commerce",
    "//chrome/browser/ui/page_action:icon_type",
    "//chrome/browser/ui/tabs:features",
    "//chrome/browser/ui/tabs:tab_strip_model_observer",
    "//chrome/browser/ui/views/page_action",
    "//chrome/browser/ui/webui/commerce",
    "//components/commerce/core:commerce_types",
    "//components/commerce/core:shopping_service",
    "//components/commerce/core/compare",
    "//components/commerce/core/product_specifications",
    "//components/commerce/core/subscriptions",
    "//components/feature_engagement/public",
    "//components/image_fetcher/core",
    "//components/prefs",
    "//content/public/browser",
    "//ui/gfx",
  ]
  deps = [ "//ui/base/unowned_user_data" ]
}

source_set("impl") {
  sources = [
    "add_to_comparison_table_sub_menu_model.cc",
    "commerce_page_action_controller.cc",
    "commerce_ui_tab_helper.cc",
    "compare_sub_menu_model.cc",
    "discounts_page_action_controller.cc",
    "price_tracking_page_action_controller.cc",
    "product_specifications_entry_point_controller.cc",
    "product_specifications_page_action_controller.cc",
    "ui_utils.cc",
  ]

  public_deps = [ "//chrome/browser:browser_public_dependencies" ]

  deps = [
    ":commerce",
    "//chrome/browser/feature_engagement",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/ui:ui_features",
    "//chrome/browser/ui/browser_window",
    "//chrome/browser/ui/tabs:tab_strip",
    "//chrome/browser/ui/toasts",
    "//chrome/browser/ui/toasts/api:toasts",
    "//chrome/browser/ui/toolbar",
    "//chrome/browser/ui/views/frame:toolbar_button_provider",
    "//chrome/browser/ui/views/side_panel",
    "//chrome/common:constants",
    "//components/bookmarks/browser",
    "//components/commerce/core:commerce_constants",
    "//components/commerce/core:feature_list",
    "//components/commerce/core:feature_utils",
    "//components/commerce/core:metrics",
    "//components/commerce/core:pref_names",
    "//components/commerce/core:utils",
    "//components/commerce/core/mojom:mojo_bindings",
    "//components/strings",
    "//components/vector_icons",
    "//services/metrics/public/cpp:gen_ukm_builders",
    "//services/metrics/public/cpp:metrics_cpp",
    "//services/metrics/public/cpp:ukm_builders",
    "//ui/base",
    "//ui/base/metadata",
    "//ui/views",
    "//url",
  ]
}

source_set("test_support") {
  testonly = true
  sources = [
    "mock_commerce_ui_tab_helper.cc",
    "mock_commerce_ui_tab_helper.h",
  ]

  deps = [
    ":commerce",
    "//base",
    "//chrome/browser/ui/tabs:tabs_public",
    "//skia:skia_core_public_headers",
    "//testing/gmock",
    "//ui/views",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "commerce_ui_tab_helper_unittest.cc",
    "discounts_page_action_controller_unittest.cc",
    "price_tracking_page_action_controller_unittest.cc",
    "product_specifications_page_action_controller_unittest.cc",
  ]
  deps = [
    ":commerce",
    "//base/test:test_support",
    "//chrome/browser/ui/toasts",
    "//chrome/browser/ui/views/page_action:test_support",
    "//chrome/test:test_support",
    "//components/commerce/core:account_checker_test_support",
    "//components/commerce/core:feature_list",
    "//components/commerce/core:shopping_service_test_support",
    "//components/feature_engagement/test:test_support",
    "//components/image_fetcher/core:test_support",
    "//components/tabs:test_support",
    "//ui/base/unowned_user_data",
  ]
}

source_set("browser_tests") {
  testonly = true
  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
  sources = [
    "add_to_comparison_table_sub_menu_model_browsertest.cc",
    "product_specifications_entry_point_controller_browsertest.cc",
  ]
  deps = [
    ":commerce",
    "//chrome/browser/ui/browser_window",
    "//chrome/browser/ui/toasts",
    "//chrome/test:test_support",
    "//components/commerce/core:account_checker_test_support",
    "//components/commerce/core:shopping_service_test_support",
    "//components/commerce/core:utils",
    "//components/commerce/core/mojom:mojo_bindings",
  ]
}
