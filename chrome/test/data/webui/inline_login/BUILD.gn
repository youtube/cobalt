# Copyright 2020 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("../build_webui_tests.gni")

assert(is_chromeos_ash || is_win || is_mac)

build_webui_tests("build") {
  resource_path_prefix = "inline_login"

  files = [
    "inline_login_test.ts",
    "inline_login_test_util.ts",
  ]

  if (is_chromeos_ash) {
    files += [
      "arc_account_picker_page_test.ts",
      "inline_login_signin_blocked_by_policy_page_test.ts",
      "inline_login_welcome_page_test.ts",
    ]
  }

  ts_path_mappings = [
    "chrome://chrome-signin/gaia_auth_host/*|" +
        rebase_path("//chrome/browser/resources/gaia_auth_host/*",
                    target_gen_dir),
    "chrome://chrome-signin/*|" +
        rebase_path("$root_gen_dir/chrome/browser/resources/inline_login/tsc/*",
                    target_gen_dir),
  ]

  if (is_chromeos_ash) {
    ts_path_mappings += [
      "chrome://chrome-signin/arc_account_picker/*|" +
          rebase_path(
              "//chrome/browser/resources/chromeos/arc_account_picker/*",
              target_gen_dir),
      "chrome://webui-test/chromeos/arc_account_picker/*|" +
          rebase_path("//chrome/test/data/webui/chromeos/arc_account_picker/*",
                      target_gen_dir),
    ]
  }

  ts_deps = [
    "//chrome/browser/resources/inline_login:build_ts",
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources/js:build_ts",
  ]
}
