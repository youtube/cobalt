# Copyright 2023 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")

mojom("mojom") {
  sources = [
    "on_device_model.mojom",
    "on_device_model_service.mojom",
  ]
  public_deps = [
    "//mojo/public/mojom/base",
    "//sandbox/policy/mojom",
    "//skia/public/mojom",
  ]
  webui_module_path = "/"

  export_class_attribute_blink = "PLATFORM_EXPORT"
  export_define_blink = "BLINK_PLATFORM_IMPLEMENTATION=1"
  export_header_blink = "third_party/blink/renderer/platform/platform_export.h"
  generate_legacy_js_bindings = true
  generate_java = true

  cpp_typemaps = [
    {
      types = [
        {
          mojom = "on_device_model.mojom.Capabilities"
          cpp = "::on_device_model::Capabilities"
        },
        {
          mojom = "on_device_model.mojom.ModelAssets"
          cpp = "::on_device_model::ModelAssets"
          default_constructible = false
          move_only = true
        },
        {
          mojom = "on_device_model.mojom.ModelFile"
          cpp = "::on_device_model::ModelFile"
          default_constructible = false
          move_only = true
        },
        {
          mojom = "on_device_model.mojom.AdaptationAssets"
          cpp = "::on_device_model::AdaptationAssets"
          move_only = true
        },
        {
          mojom = "on_device_model.mojom.Token"
          cpp = "::ml::Token"
        },
        {
          mojom = "on_device_model.mojom.InputPiece"
          cpp = "::ml::InputPiece"
        },
        {
          mojom = "on_device_model.mojom.ModelBackendType"
          cpp = "::ml::ModelBackendType"
        },
        {
          mojom = "on_device_model.mojom.ModelPerformanceHint"
          cpp = "::ml::ModelPerformanceHint"
        },
      ]
      traits_headers = [
        "//services/on_device_model/ml/chrome_ml_types_traits.h",
        "//services/on_device_model/public/cpp/adaptation_assets_mojom_traits.h",
        "//services/on_device_model/public/cpp/capabilities_mojom_traits.h",
        "//services/on_device_model/public/cpp/model_assets_mojom_traits.h",
        "//services/on_device_model/public/cpp/model_file_mojom_traits.h",
      ]
      traits_sources =
          [ "//services/on_device_model/ml/chrome_ml_types_traits.cc" ]
      traits_public_deps = [
        "//services/on_device_model/ml:api",
        "//services/on_device_model/public/cpp:types",
      ]
    },
  ]
}
