# Copyright 2017 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
import("//build/config/compiler/compiler.gni")
import("//testing/test.gni")

source_set("lib") {
  sources = [
    "echo_service.cc",
    "echo_service.h",
  ]

  deps = [
    "//base",
    "//components/os_crypt/async/common",
    "//components/os_crypt/sync",
    "//services/service_manager/public/cpp",
    "//services/service_manager/public/mojom",
    "//services/test/echo/public/mojom",
  ]
}

if (is_win) {
  shared_library("echo_preload_library") {
    testonly = true

    sources = [
      "echo_preload_library.def",
      "echo_preload_library_win.cc",
    ]

    if (!is_asan && !use_clang_profiling) {
      no_default_deps = true
    }

    # Keep this a tiny dll as it does very little.
    assert_no_deps = [
      "//third_party/abseil-cpp:absl",
      "//third_party/perfetto:libperfetto",
    ]

    libs = [ "secur32.lib" ]
    ldflags = [ "/delayload:secur32.dll" ]
  }
}
