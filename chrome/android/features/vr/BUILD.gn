# Copyright 2019 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")
import("//build/config/locales.gni")
import("//chrome/common/features.gni")
import("//device/vr/buildflags/buildflags.gni")
import("//third_party/jni_zero/jni_zero.gni")
import("//tools/grit/grit_rule.gni")

assert(enable_vr)

java_strings_grd("java_strings_grd") {
  defines = chrome_grit_defines
  grd_file = "java/strings/android_chrome_vr_strings.grd"
  outputs = [ "values/android_chrome_vr_strings.xml" ] +
            process_file_template(
                android_bundle_locales_as_resources,
                [ "values-{{source_name_part}}/android_chrome_vr_strings.xml" ])
}

android_resources("java_resources") {
  sources = [
    "java/res/drawable-hdpi/vr_services.png",
    "java/res/drawable-mdpi/vr_services.png",
    "java/res/drawable-xhdpi/vr_services.png",
    "java/res/drawable-xxhdpi/vr_services.png",
    "java/res/drawable-xxxhdpi/vr_services.png",
  ]

  deps = [ ":java_strings_grd" ]
}

android_library("java") {
  srcjar_deps = [ ":split_jni_headers" ]
  sources = []
  if (enable_gvr_services) {
    sources += [
      "java/src/org/chromium/chrome/browser/vr/AndroidVSyncHelper.java",
      "java/src/org/chromium/chrome/browser/vr/OnDispatchTouchEventCallback.java",
      "java/src/org/chromium/chrome/browser/vr/VrCoreInstallUtils.java",
      "java/src/org/chromium/chrome/browser/vr/VrCoreVersionChecker.java",
      "java/src/org/chromium/chrome/browser/vr/VrDaydreamApi.java",
      "java/src/org/chromium/chrome/browser/vr/VrDelegateImpl.java",
      "java/src/org/chromium/chrome/browser/vr/VrDelegateProviderImpl.java",
      "java/src/org/chromium/chrome/browser/vr/VrShell.java",
      "java/src/org/chromium/chrome/browser/vr/VrShellDelegate.java",
      "java/src/org/chromium/chrome/browser/vr/VrWindowAndroid.java",
    ]
  } else {
    sources +=
        [ "dummy/java/src/org/chromium/chrome/browser/vr/VrShellDelegate.java" ]
  }

  deps = [
    ":java_resources",
    "//base:base_java",
    "//build/android:build_java",
    "//cc:cc_java",
    "//chrome/android:chrome_java",
    "//chrome/browser/feedback/android:java",
    "//chrome/browser/flags:java",
    "//chrome/browser/fullscreen/android:java",
    "//chrome/browser/preferences:java",
    "//chrome/browser/profiles/android:java",
    "//chrome/browser/tab:java",
    "//chrome/browser/tabmodel:java",
    "//chrome/browser/ui/android/omnibox:util_java",
    "//chrome/browser/ui/android/page_info:java",
    "//chrome/browser/ui/messages/android:java",
    "//chrome/browser/util:java",
    "//components/browser_ui/modaldialog/android:java",
    "//components/browser_ui/notifications/android:java",
    "//components/browser_ui/widget/android:java",
    "//components/embedder_support/android:content_view_java",
    "//components/external_intents/android:java",
    "//components/infobars/core:infobar_enums_java",
    "//components/messages/android:java",
    "//components/page_info/android:java",
    "//components/policy/android:policy_java",
    "//content/public/android:content_java",
    "//third_party/androidx:androidx_annotation_annotation_java",
    "//third_party/androidx:androidx_lifecycle_lifecycle_runtime_java",
    "//third_party/gvr-android-sdk:gvr_common_java",
    "//third_party/jni_zero:jni_zero_java",
    "//ui/android:ui_full_java",
    "//ui/android:ui_utils_java",
    "//url:gurl_java",
  ]

  if (enable_gvr_services) {
    deps += [ "//device/vr:java" ]
  }

  resources_package = "org.chromium.chrome.vr"
}

generate_jni("split_jni_headers") {
  sources = [
    "java/src/org/chromium/chrome/browser/vr/AndroidVSyncHelper.java",
    "java/src/org/chromium/chrome/browser/vr/VrCoreInstallUtils.java",
    "java/src/org/chromium/chrome/browser/vr/VrShell.java",
    "java/src/org/chromium/chrome/browser/vr/VrShellDelegate.java",
  ]
  split_name = "vr"
}
