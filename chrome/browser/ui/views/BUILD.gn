# Copyright 2014 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//build/config/ui.gni")
import("//ui/views/features.gni")

assert(toolkit_views)

component("views") {
  output_name = "browser_ui_views"
  sources = [
    "chrome_views_export.h",
    "chrome_widget_sublevel.h",
    "event_utils.cc",
    "event_utils.h",
    "tab_icon_view_model.h",
    "tabs/tab_strip_observer.cc",
    "tabs/tab_strip_observer.h",
    "tabs/tab_strip_types.h",
  ]

  configs += [ "//build/config/compiler:wexit_time_destructors" ]

  defines = [ "CHROME_VIEWS_IMPLEMENTATION" ]

  public_deps = [ "//chrome/browser:browser_public_dependencies" ]

  deps = [
    "//base",
    "//build:branding_buildflags",
    "//build:chromeos_buildflags",
    "//chrome/app:command_ids",
    "//chrome/app:generated_resources",
    "//components/keep_alive_registry",
    "//components/lens",
    "//components/lens:buildflags",
    "//components/vector_icons",
    "//content/public/browser",
    "//printing/buildflags",
    "//services/screen_ai/buildflags",
    "//skia",
    "//ui/base",
    "//ui/events",
    "//ui/gfx",
    "//ui/gfx/geometry",
    "//ui/views",
    "//url",
  ]

  if (is_chromeos_ash) {
    deps += [
      "//ash",
      "//ash/components/arc",
      "//components/arc",
    ]
  }
}

source_set("test_support") {
  testonly = true
  public = [ "global_media_controls/media_dialog_ui_for_test.h" ]
  sources = [ "global_media_controls/media_dialog_ui_for_test.cc" ]
  deps = [
    ":views",
    "//chrome/browser/media/router:test_support",
    "//chrome/browser/ui",
    "//chrome/browser/ui/global_media_controls",
    "//chrome/browser/ui/views/toolbar",
    "//chrome/test:test_support_ui",
    "//components/global_media_controls:global_media_controls",
    "//components/media_message_center:media_message_center",
    "//components/media_router/browser:test_support",
    "//testing/gtest",
    "//ui/base:test_support",
    "//ui/views",
    "//ui/views:test_support",
  ]
}
