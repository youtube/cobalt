# Copyright 2021 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//mojo/public/tools/bindings/mojom.gni")

assert(is_chromeos_ash,
       "Quick Pair protocols (e.g. Fast Pair) are ash-chrome only")

mojom("mojom") {
  sources = [
    "fast_pair_data_parser.mojom",
    "quick_pair_service.mojom",
  ]

  public_deps = [ "//sandbox/policy/mojom" ]

  cpp_typemaps = [
    {
      types = [
        {
          mojom = "ash.quick_pair.mojom.DecryptedResponse"
          cpp = "::ash::quick_pair::DecryptedResponse"
        },
        {
          mojom = "ash.quick_pair.mojom.DecryptedPasskey"
          cpp = "::ash::quick_pair::DecryptedPasskey"
        },
        {
          mojom = "ash.quick_pair.mojom.MessageType"
          cpp = "::ash::quick_pair::FastPairMessageType"
        },
        {
          mojom = "ash.quick_pair.mojom.NotDiscoverableAdvertisement"
          cpp = "::ash::quick_pair::NotDiscoverableAdvertisement"
        },
      ]
      traits_headers = [
        "//chromeos/ash/services/quick_pair/public/mojom/fast_pair_traits.h",
      ]
      traits_sources = [
        "//chromeos/ash/services/quick_pair/public/mojom/fast_pair_traits.cc",
      ]
      traits_public_deps = [ "//chromeos/ash/services/quick_pair/public/cpp" ]
    },
  ]
}
