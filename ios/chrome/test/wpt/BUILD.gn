# Copyright 2019 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/apple/tweak_info_plist.gni")
import("//build/config/ios/ios_sdk.gni")
import("//build/config/ios/rules.gni")
import("//ios/build/chrome_build.gni")
import("//ios/chrome/test/xcuitest/ios_chrome_xcuitest_app_host.gni")
import("//ios/public/provider/chrome/browser/build_config.gni")
import("//testing/test.gni")

script_test("wpt_tests_ios") {
  script = "//third_party/blink/tools/run_wpt_tests.py"
  args = [
    "--product",
    "chrome_ios",
    "--target",
    "Release",
    "--webdriver-arg=--os=16.0",
    "--no-restart-on-unexpected",
    "--retry-unexpected=0",

    # TODO(crbug.com/1351820): Enable more tests to run.
    "/css/css-transforms/",
  ]
  deps = [ ":all_tests" ]
  data = [
    "//third_party/blink/tools/run_wpt_tests.py",
    "//third_party/wpt_tools/",
    "//third_party/blink/tools/blinkpy/",
    "//build/skia_gold_common/",
    "//third_party/blink/web_tests/external/",
    "//third_party/blink/web_tests/wpt_internal/",
    "//third_party/blink/web_tests/resources/testdriver-vendor.js",
    "//third_party/blink/web_tests/wptrunner.blink.ini",
    "//third_party/blink/web_tests/FlagSpecificConfig",
    "//third_party/blink/web_tests/SmokeTests/",
    "//.vpython3",
  ]
  data_deps = [
    ":tools",
    "//testing:test_scripts_shared",
    "//third_party/catapult/third_party/typ:typ",
    "//tools/imagediff",
  ]
}

group("tools") {
  data = [
    "tools/",
    "//ios/build/bots/scripts/",
    "//ios/testing/plugin",
  ]
}

group("all_tests") {
  testonly = true
  deps = [
    ":ios_cwt_chromedriver_tests",
    ":ios_cwt_chromedriver_tests_module",
  ]
}

copy("copy_clusterfuzz_scripts") {
  sources = [
    "//ios/build/bots/scripts",
    "//ios/chrome/test/wpt/tools",
    "//ios/testing/plugin",

    # Variations test utilities used by variations_runner script.
    "//testing/scripts/variations_seed_access_helper.py",
  ]
  outputs =
      [ "$root_build_dir/{{source_root_relative_dir}}/{{source_file_part}}" ]
}

group("ios_chrome_clusterfuzz_asan_build") {
  testonly = true
  deps = [
    ":all_tests",
    ":copy_clusterfuzz_scripts",
  ]
}

ios_chrome_xcuitest_app_host("ios_cwt_chromedriver_tests") {
  deps = [
    ":app_support",

    # Use the chromium implementation of the provider API.
    "//ios/chrome/browser/providers:chromium_providers",
  ]
  bundle_deps = [ "//ios/third_party/gtx:gtx+bundle" ]
}

ios_xcuitest_test("ios_cwt_chromedriver_tests_module") {
  xcode_test_application_name = "ios_cwt_chromedriver_tests"

  deps = [ ":cwt_chromedriver_tests" ]
  data_deps = [ ":ios_cwt_chromedriver_tests" ]
  bundle_deps = [
    "//ios/chrome/app/resources",
    "//ios/third_party/gtx:gtx+bundle",
    "//third_party/icu:icudata",
  ]

  # Those frameworks are force-linked via //base/test:test_support, so
  # ensure the framework bundles are packaged in the test runner and
  # not the test module.
  runner_only_bundle_deps = [ "//base/test:test_trace_processor+bundle" ]
}

source_set("shared_helper_headers") {
  testonly = true
  sources = [ "cwt_webdriver_app_interface.h" ]
  public_deps = [ "//base" ]
}

source_set("cwt_constants") {
  testonly = true
  sources = [
    "cwt_constants.cc",
    "cwt_constants.h",
  ]
}

source_set("app_support") {
  testonly = true
  configs += [ "//build/config/compiler:enable_arc" ]

  sources = [
    "cwt_stderr_logger.h",
    "cwt_stderr_logger.mm",
    "cwt_tests_hook.mm",
    "cwt_webdriver_app_interface.mm",
  ]

  deps = [
    ":cwt_constants",
    "//base",
    "//base/test:test_support",
    "//ios/chrome/app:app_internal",
    "//ios/chrome/app:tests_hook",
    "//ios/chrome/browser/main:public",
    "//ios/chrome/browser/tabs:tabs",
    "//ios/chrome/browser/web_state_list",
    "//ios/chrome/test/app:test_support",
    "//ios/testing:nserror_support",
    "//ios/third_party/edo",
    "//ios/web/public/js_messaging",
    "//ios/web/public/test",
    "//ui/gfx",
  ]

  public_deps = [ ":shared_helper_headers" ]
}

source_set("cwt_chromedriver_tests") {
  testonly = true
  configs += [
    "//build/config/compiler:enable_arc",
    "//build/config/ios:xctest_config",
  ]

  sources = [
    "cwt_chrome_driver.mm",
    "cwt_request_handler.h",
    "cwt_request_handler.mm",
  ]

  deps = [
    ":cwt_constants",
    ":shared_helper_headers",
    "//components/version_info:version_info",
    "//ios/third_party/edo",
    "//net:test_support",
  ]

  frameworks = [ "UIKit.framework" ]
}
