# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//testing/test.gni")
import("//third_party/protobuf/proto_library.gni")

source_set("core") {
  sources = [ "session_proto_storage.h" ]

  deps = [ "//components/leveldb_proto" ]

  public_deps = [ "//third_party/leveldatabase" ]
}

source_set("session_proto_db") {
  sources = [ "session_proto_db.h" ]

  deps = [
    ":core",
    "//base:base",
    "//components/commerce/core:persisted_state_db_content_proto",
    "//components/keyed_service/core",
    "//components/leveldb_proto",
    "//third_party/leveldatabase",
  ]
}

# TODO(crbug.com/40250069) Make tests more general and run on iOS non-blink
# trybot.
if (use_blink) {
  source_set("unit_tests") {
    testonly = true
    sources = [ "session_proto_db_unittest.cc" ]

    deps = [
      ":core",
      ":session_proto_db",
      ":session_proto_db_test_proto",
      "//base:base",
      "//base/test:test_support",
      "//components/commerce/core:persisted_state_db_content_proto",
      "//components/leveldb_proto:test_support",
      "//content/test:test_support",
      "//testing/gmock",
      "//testing/gtest",
      "//third_party/leveldatabase",
      "//third_party/protobuf:protobuf_lite",
    ]
  }
}

proto_library("session_proto_db_test_proto") {
  sources = [ "session_proto_db_test_proto.proto" ]
}
