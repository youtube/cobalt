# Copyright 2020 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//chromecast/chromecast.gni")

cast_source_set("display_settings") {
  sources = [
    "brightness_animation.cc",
    "brightness_animation.h",
    "color_temperature_animation.cc",
    "color_temperature_animation.h",
    "screen_power_controller.h",
  ]

  deps = [
    "//base",
    "//chromecast/browser:display_configurator",
    "//chromecast/graphics",
    "//chromecast/ui:display_settings_public",
    "//skia",
    "//ui/compositor",
    "//ui/display/types",
    "//ui/gfx/animation",
  ]

  if (use_aura) {
    sources += [
      "gamma_configurator.cc",
      "gamma_configurator.h",
    ]

    deps += [
      ":screen_power_controller_aura",
      "//ui/aura",
    ]
  } else {
    deps += [ ":screen_power_controller_default" ]
  }
}

cast_source_set("screen_power_controller_default") {
  sources = [
    "screen_power_controller.h",
    "screen_power_controller_default.cc",
    "screen_power_controller_default.h",
  ]

  deps = [ "//base" ]
}

cast_source_set("screen_power_controller_aura") {
  sources = [
    "screen_power_controller.h",
    "screen_power_controller_aura.cc",
    "screen_power_controller_aura.h",
  ]

  deps = [ "//base" ]
}

test("cast_display_settings_unittests") {
  testonly = true

  sources = []

  deps = [
    "//base",
    "//base/test:run_all_unittests",
    "//base/test:test_support",
    "//testing/gmock",
    "//testing/gtest",
  ]

  if (use_aura) {
    sources += [ "screen_power_controller_aura_unittest.cc" ]

    deps += [ ":screen_power_controller_aura" ]
  }
}
