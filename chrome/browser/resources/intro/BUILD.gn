# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//components/signin/features.gni")
import("//ui/webui/resources/tools/build_webui.gni")

assert(is_chromeos_lacros || enable_dice_support)

build_webui("build") {
  grd_prefix = "intro"

  static_files = [ "intro.html" ]

  if (enable_dice_support) {
    static_files += [
      "images/cloud_upload.svg",
      "images/devices.svg",
    ]

    if (!is_chrome_branded) {
      static_files += [ "images/security.svg" ]
    }
    web_component_files = [
      "dice_app.ts",
      "sign_in_promo.ts",
    ]
  }

  if (is_chromeos_lacros) {
    static_files += [ "images/lacros_intro_banner.svg" ]
    web_component_files = [ "lacros_app.ts" ]
  }

  non_web_component_files = [ "browser_proxy.ts" ]

  ts_definitions = [ "//tools/typescript/definitions/chrome_send.d.ts" ]
  ts_composite = true
  ts_deps = [
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources/cr_elements:build_ts",
    "//ui/webui/resources/js:build_ts",
  ]
}
