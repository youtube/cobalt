# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//chromeos/dbus/config/use_real_dbus_clients.gni")
import("//components/metrics/generate_allowlist_from_histograms_file.gni")
import("//extensions/buildflags/buildflags.gni")

generate_allowlist_from_histograms_file("webui_url_hashes") {
  namespace = "webui_metrics"
  input_xml_file = "//tools/metrics/histograms/enums.xml"
  tag = "enum"
  output_file = "webui_url_hashes.h"
  allow_list_name = "WebUIUrlHashes"
}

source_set("configs") {
  sources = [
    "chrome_untrusted_web_ui_configs.cc",
    "chrome_untrusted_web_ui_configs.h",
    "chrome_web_ui_configs.cc",
    "chrome_web_ui_configs.h",
  ]

  configs += [ "//build/config/compiler:wexit_time_destructors" ]

  if (!is_android) {
    public_deps = [ "//chrome/browser/ui/webui/commerce" ]
  }

  deps = [
    ":webui",
    "//chrome/browser/optimization_guide:optimization_guide",
    "//chrome/browser/ui",
    "//chrome/browser/ui/webui/safe_browsing",
    "//components/compose:buildflags",
    "//components/lens:buildflags",
    "//components/security_interstitials/content:security_interstitial_page",
    "//content/public/browser",
    "//extensions/buildflags:buildflags",
    "//printing/buildflags",
    "//skia",
    "//third_party/abseil-cpp:absl",
  ]

  if (is_win || is_mac || is_linux || is_chromeos) {
    deps += [
      "//chrome/browser/ui/lens",
      "//chrome/browser/ui/views/side_panel",
    ]

    if (!is_chromeos) {
      deps += [ "//chrome/browser/ui/webui/whats_new" ]
    }
  }

  if (is_chromeos) {
    deps += [
      "//chrome/browser/ui/webui/ash/config",
      "//chromeos/constants",
    ]
  }
}

source_set("webui_util") {
  sources = [
    "webui_util.cc",
    "webui_util.h",
  ]

  public_deps = [
    "//base",
    "//ui/base",
  ]

  deps = [
    "//build:chromeos_buildflags",
    "//content/public/browser",
    "//content/public/common",
    "//services/network/public/mojom",
    "//ui/resources",
  ]

  if (!is_android) {
    sources += [
      "webui_embedding_context.cc",
      "webui_embedding_context.h",
    ]

    deps += [
      "//chrome/browser/ui/browser_window:browser_window",
      "//chrome/browser/ui/tabs:tabs_public",
    ]
  }
}

source_set("webui") {
  sources = [
    "constrained_web_dialog_ui.cc",
    "constrained_web_dialog_ui.h",
  ]

  sources += get_target_outputs(":webui_url_hashes")

  public_deps = [ "//content/public/browser" ]

  deps = [
    ":webui_url_hashes",
    "//chrome/common",
    "//extensions/buildflags",
  ]
  if (enable_extensions) {
    deps += [ "//chrome/browser/extensions" ]
  }
  if (!is_android) {
    deps += [ "//ui/web_dialogs" ]
  }
}
