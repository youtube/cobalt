# Copyright 2023 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//build/config/features.gni")
import("//testing/libfuzzer/fuzzer_test.gni")
import("//third_party/libprotobuf-mutator/fuzzable_proto_library.gni")

source_set("affiliation") {
  sources = [
    "affiliated_match_helper.cc",
    "affiliated_match_helper.h",
    "affiliation_backend.cc",
    "affiliation_backend.h",
    "affiliation_database.cc",
    "affiliation_database.h",
    "affiliation_fetch_throttler.cc",
    "affiliation_fetch_throttler.h",
    "affiliation_fetch_throttler_delegate.h",
    "affiliation_fetcher_base.cc",
    "affiliation_fetcher_base.h",
    "affiliation_fetcher_delegate.cc",
    "affiliation_fetcher_delegate.h",
    "affiliation_fetcher_factory.h",
    "affiliation_fetcher_factory_impl.cc",
    "affiliation_fetcher_factory_impl.h",
    "affiliation_fetcher_interface.h",
    "affiliation_service.h",
    "affiliation_service_impl.cc",
    "affiliation_service_impl.h",
    "affiliation_utils.cc",
    "affiliation_utils.h",
    "facet_manager.cc",
    "facet_manager.h",
    "facet_manager_host.h",
    "hash_affiliation_fetcher.cc",
    "hash_affiliation_fetcher.h",
    "lookup_affiliation_response_parser.cc",
    "lookup_affiliation_response_parser.h",
  ]
  deps = [
    ":affiliation_proto",
    "//base",
    "//components/keyed_service/core",
    "//components/password_manager/core/browser:password_form",
    "//components/password_manager/core/browser:password_store_interface",
    "//components/password_manager/core/browser:sql_builder",
    "//components/password_manager/core/browser/features:password_features",
    "//components/prefs",
    "//components/url_formatter",
    "//components/variations/net",
    "//google_apis",
    "//net",
    "//services/network/public/cpp",
    "//sql",
    "//url",
  ]
  configs += [ "//build/config/compiler:wexit_time_destructors" ]
}

source_set("affiliation_fetching") {
  sources = [
    "affiliations_prefetcher.cc",
    "affiliations_prefetcher.h",
  ]
  deps = [
    ":affiliation",
    "//components/password_manager/core/browser",
    "//components/password_manager/core/browser:password_form",
    "//components/password_manager/core/browser/features:password_features",
    "//components/password_manager/core/common:features",
    "//net",
    "//url",
  ]
  configs += [ "//build/config/compiler:wexit_time_destructors" ]
}

fuzzable_proto_library("affiliation_proto") {
  sources = [ "affiliation_api.proto" ]
}

source_set("test_support") {
  testonly = true
  sources = [
    "fake_affiliation_api.cc",
    "fake_affiliation_api.h",
    "fake_affiliation_fetcher.cc",
    "fake_affiliation_fetcher.h",
    "fake_affiliation_service.cc",
    "fake_affiliation_service.h",
    "mock_affiliated_match_helper.cc",
    "mock_affiliated_match_helper.h",
    "mock_affiliation_consumer.cc",
    "mock_affiliation_consumer.h",
    "mock_affiliation_fetcher.cc",
    "mock_affiliation_fetcher.h",
    "mock_affiliation_fetcher_delegate.cc",
    "mock_affiliation_fetcher_delegate.h",
    "mock_affiliation_fetcher_factory.cc",
    "mock_affiliation_fetcher_factory.h",
    "mock_affiliation_service.cc",
    "mock_affiliation_service.h",
  ]
  deps = [
    ":affiliation",
    "//base",
    "//components/password_manager/core/browser:password_form",
    "//services/network:test_support",
    "//testing/gmock",
    "//testing/gtest",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "affiliated_match_helper_unittest.cc",
    "affiliation_backend_unittest.cc",
    "affiliation_database_unittest.cc",
    "affiliation_fetch_throttler_unittest.cc",
    "affiliation_fetcher_factory_impl_unittest.cc",
    "affiliation_service_impl_unittest.cc",
    "affiliation_utils_unittest.cc",
    "facet_manager_unittest.cc",
    "hash_affiliation_fetcher_unittest.cc",
  ]
  deps = [
    ":affiliation",
    ":affiliation_proto",
    ":test_support",
    "//base",
    "//base/test:test_support",
    "//components/password_manager/core/browser:password_form",
    "//components/password_manager/core/browser:password_store_interface",
    "//components/password_manager/core/common",
    "//components/password_manager/core/common:features",
    "//components/variations:test_support",
    "//services/network:test_support",
    "//services/network/public/mojom",
    "//sql:test_support",
    "//testing/gmock",
    "//testing/gtest",
    "//third_party/sqlite",
    "//url",
  ]
}

source_set("affiliation_fetching_unittests") {
  testonly = true
  sources = [ "affiliations_prefetcher_unittest.cc" ]
  deps = [
    ":affiliation",
    ":affiliation_fetching",
    ":test_support",
    "//base",
    "//components/password_manager/core/browser:browser",
    "//components/password_manager/core/browser:test_support",
    "//services/network:test_support",
    "//testing/gmock",
    "//testing/gtest",
  ]
}

if (use_fuzzing_engine_with_lpm) {
  fuzzer_test("lookup_affiliation_response_parser_fuzzer") {
    sources = [ "lookup_affiliation_response_parser_fuzzer.cc" ]
    deps = [
      ":affiliation",
      ":affiliation_proto",
      "//base:base",
      "//base:i18n",
      "//third_party/libprotobuf-mutator",
    ]
  }
}
