# Copyright 2020 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//ui/webui/resources/tools/build_webui.gni")
import("//ui/webui/webui_features.gni")

assert(!is_chromeos_ash)

build_webui("build") {
  grd_prefix = "profile_picker"

  static_files = [ "profile_picker.html" ]

  web_component_files = [
    "profile_card_menu.ts",
    "profile_card.ts",
    "profile_creation_flow/profile_type_choice.ts",
    "profile_picker_app.ts",
    "profile_picker_main_view.ts",
    "profile_switch.ts",
  ]

  if (is_chromeos_lacros) {
    web_component_files +=
        [ "profile_creation_flow/account_selection_lacros.ts" ]
  }

  non_web_component_files = [
    "drag_drop_reorder_tile_list_delegate.ts",
    "ensure_lazy_loaded.ts",
    "lazy_load.ts",
    "manage_profiles_browser_proxy.ts",
    "navigation_mixin.ts",
    "policy_helper.ts",
    "profile_picker.ts",
  ]

  icons_html_files = [ "icons.html" ]

  css_files = [
    "profile_creation_flow/profile_creation_shared.css",
    "profile_picker_shared.css",
  ]

  ts_composite = true
  ts_definitions = [
    "//tools/typescript/definitions/chrome_send.d.ts",
    "//tools/typescript/definitions/metrics_private.d.ts",
  ]
  ts_deps = [
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources/cr_components/customize_themes:build_ts",
    "//ui/webui/resources/cr_elements:build_ts",
    "//ui/webui/resources/js:build_ts",
  ]

  optimize = optimize_webui
  if (optimize) {
    optimize_webui_host = "profile-picker"
    optimize_webui_in_files = [
      "profile_picker.js",
      "lazy_load.js",
    ]
    optimize_webui_excludes =
        [ "chrome://resources/mojo/skia/public/mojom/skcolor.mojom-webui.js" ]
  }
}
