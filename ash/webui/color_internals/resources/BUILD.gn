# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(is_chromeos_ash, "Color Internals is ash-chrome only")

ts_library("color_internals_ts") {
  root_dir = "."
  out_dir = "$target_gen_dir/tsc"
  composite = true
  in_files = [
    "index.ts",
    "wallpaper_colors.ts",
    "utils.ts",
  ]
  deps = [
    "../mojom:mojom_ts",
    "//ui/webui/resources/cr_components/color_change_listener:build_ts",
    "//ui/webui/resources/mojo:build_ts",
  ]

  # Allows TSC to check the generated mojom-webui/ directory when it encounters
  # imports.
  path_mappings =
      [ "/*|" + rebase_path("$root_gen_dir/mojom-webui/*", target_gen_dir) ]
}

generate_grd("build_cros_styles_grdp") {
  grd_prefix = "webui"
  out_grd = "$target_gen_dir/${grd_prefix}_cros_styles_resources.grdp"
  public_deps = [ "//ui/chromeos/styles:color_internals_tokens_json" ]
  input_files_base_dir = rebase_path("$root_gen_dir/", root_build_dir)
  input_files = [ "ui/chromeos/styles/color_internals_tokens.json" ]
  resource_path_rewrites = [ "ui/chromeos/styles/color_internals_tokens.json|chromeos/colors/color_internals_tokens.json" ]
}

generate_grd("build_grd") {
  grd_prefix = "ash_color_internals"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"

  grdp_files = [
    "$target_gen_dir/webui_cros_styles_resources.grdp",
    "$target_gen_dir/../mojom/mojom_webui_resources.grdp",
  ]

  input_files = [ "index.html" ]
  input_files_base_dir = rebase_path(".", "//")

  # color_internals_ts.manifest is generated by :color_internals_ts and adds
  # all of the in_files to the grd.
  manifest_files = filter_include(get_target_outputs(":color_internals_ts"),
                                  [ "*.manifest" ])

  deps = [
    ":build_cros_styles_grdp",
    ":color_internals_ts",
    "../mojom:mojom_webui_grdp",
  ]
}
