# Copyright 2019 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")

if (is_android) {
  import("//build/config/android/rules.gni")
}

source_set("identity_manager") {
  sources = [
    "access_token_fetcher.cc",
    "access_token_fetcher.h",
    "access_token_info.h",
    "access_token_restriction.cc",
    "access_token_restriction.h",
    "account_capabilities.cc",
    "account_capabilities.h",
    "account_info.cc",
    "account_info.h",
    "account_managed_status_finder.cc",
    "account_managed_status_finder.h",
    "accounts_cookie_mutator.h",
    "accounts_in_cookie_jar_info.cc",
    "accounts_in_cookie_jar_info.h",
    "accounts_mutator.h",
    "device_accounts_synchronizer.h",
    "diagnostics_provider.h",
    "identity_manager.cc",
    "identity_manager.h",
    "identity_manager_builder.cc",
    "identity_manager_builder.h",
    "identity_mutator.cc",
    "identity_mutator.h",
    "identity_utils.cc",
    "identity_utils.h",
    "load_credentials_state.h",
    "primary_account_access_token_fetcher.cc",
    "primary_account_access_token_fetcher.h",
    "primary_account_change_event.cc",
    "primary_account_change_event.h",
    "primary_account_mutator.h",
    "scope_set.h",
    "set_accounts_in_cookie_result.h",
  ]

  configs += [ "//build/config/compiler:wexit_time_destructors" ]

  public_deps = [
    ":tribool",
    "//base",
    "//components/keyed_service/core",
    "//components/signin/public/base",
    "//components/signin/public/base:signin_buildflags",
    "//google_apis",
    "//ui/gfx",
  ]

  deps = [
    "//build:chromeos_buildflags",
    "//components/image_fetcher/core",
    "//components/plus_addresses:features",
    "//components/prefs",
    "//components/signin/internal/identity_manager",
    "//components/signin/public/webdata",
    "//services/network/public/cpp",
  ]

  if (is_chromeos_ash) {
    deps += [ "//components/user_manager" ]
  }

  if (is_ios) {
    deps += [ "ios" ]
  }

  if (is_android) {
    deps += [ "//components/signin/public/android:jni_headers" ]
  }

  allow_circular_includes_from = [
    # This target is a pair with internal/identity_manager. They always go
    # together and include headers from each other.
    "//components/signin/internal/identity_manager",
  ]
}

if (is_android) {
  java_cpp_enum("primary_account_mutator_enum_javagen") {
    sources = [ "primary_account_mutator.h" ]
  }
}

source_set("tribool") {
  sources = [
    "tribool.cc",
    "tribool.h",
  ]
  configs += [ "//build/config/compiler:wexit_time_destructors" ]
  deps = [ "//base" ]
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "access_token_fetcher_unittest.cc",
    "access_token_restriction_unittest.cc",
    "account_capabilities_unittest.cc",
    "account_info_unittest.cc",
    "account_managed_status_finder_unittest.cc",
    "accounts_cookie_mutator_unittest.cc",
    "accounts_in_cookie_jar_info_unittest.cc",
    "accounts_mutator_unittest.cc",
    "diagnostics_provider_unittest.cc",
    "identity_manager_builder_unittest.cc",
    "identity_manager_unittest.cc",
    "identity_test_environment_unittest.cc",
    "identity_utils_unittest.cc",
    "primary_account_access_token_fetcher_unittest.cc",
    "primary_account_change_event_unittest.cc",
    "primary_account_mutator_unittest.cc",
    "test_accounts_unittest.cc",
  ]

  deps = [
    ":identity_manager",
    ":test_support",
    "//base",
    "//base/test:test_support",
    "//build:chromeos_buildflags",
    "//components/image_fetcher/core:test_support",
    "//components/prefs",
    "//components/prefs:test_support",
    "//components/signin/internal/identity_manager",
    "//components/signin/internal/identity_manager:test_support",
    "//components/signin/public/base",
    "//components/signin/public/base:test_support",
    "//components/signin/public/identity_manager",
    "//components/sync_preferences:test_support",
    "//google_apis",
    "//services/network:test_support",
    "//testing/gmock",
    "//testing/gtest",
  ]

  if (is_ios) {
    deps += [ "//components/signin/public/identity_manager/ios:test_support" ]
  }

  if (is_chromeos) {
    deps += [
      "//components/account_manager_core",
      "//components/account_manager_core:test_support",
    ]
  }

  if (is_chromeos_ash) {
    deps += [ "//chromeos/ash/components/account_manager" ]
  }
}

source_set("test_support") {
  testonly = true

  sources = [
    "account_capabilities_test_mutator.cc",
    "account_capabilities_test_mutator.h",
    "identity_test_environment.cc",
    "identity_test_environment.h",
    "identity_test_utils.cc",
    "identity_test_utils.h",
    "test_accounts.cc",
    "test_accounts.h",
    "test_identity_manager_observer.cc",
    "test_identity_manager_observer.h",
  ]

  public_deps = [
    "//base",
    "//components/signin/public/base",
    "//components/signin/public/identity_manager",
    "//google_apis",
  ]

  deps = [
    "//build:chromeos_buildflags",
    "//components/image_fetcher/core:test_support",
    "//components/signin/internal/identity_manager",
    "//components/signin/internal/identity_manager:test_support",
    "//components/signin/public/base:test_support",
    "//components/sync_preferences:test_support",
    "//testing/gtest",
  ]

  if (is_chromeos_ash) {
    deps += [ "//chromeos/ash/components/account_manager" ]
  }

  if (is_chromeos_lacros) {
    deps += [ "//components/account_manager_core" ]
  }

  if (is_android) {
    deps += [ "//components/signin/public/android:test_support_jni_headers" ]
  }
}
