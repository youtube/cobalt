# Copyright 2019 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//chrome/common/features.gni")
import("//components/signin/features.gni")
import("//ui/webui/resources/tools/build_webui.gni")

assert(!is_android)

build_webui("build") {
  grd_prefix = "signin"

  static_files = [
    # The shared images are used on the first run experience, sync confirmation
    # and the profile picker.
    "images/shared_dialog_illustration.svg",
    "images/shared_dialog_illustration_dark.svg",
    "images/shared_left_banner.svg",
    "images/shared_left_banner_dark.svg",
    "images/shared_right_banner.svg",
    "images/shared_right_banner_dark.svg",
    "profile_customization/images/profile_customization_illustration.svg",
    "profile_customization/images/profile_customization_illustration_dark.svg",
    "sync_confirmation/sync_confirmation.html",
    "sync_confirmation/sync_disabled_confirmation.html",
    "sync_confirmation/sync_loading_confirmation.html",
    "managed_user_profile_notice/images/data_handling.svg",
    "managed_user_profile_notice/images/enrollment_failure.svg",
    "managed_user_profile_notice/images/enrollment_success.svg",
    "managed_user_profile_notice/images/enrollment_timeout.svg",
    "managed_user_profile_notice/images/enrollment_failure_dark.svg",
    "managed_user_profile_notice/images/enrollment_success_dark.svg",
    "managed_user_profile_notice/images/enrollment_timeout_dark.svg",
  ]
  if (!is_chromeos_ash) {
    static_files += [
      "managed_user_profile_notice/managed_user_profile_notice.html",
      "profile_customization/profile_customization.html",
      "signin_email_confirmation/signin_email_confirmation.html",
      "signin_error/signin_error.html",
      "signin_reauth/images/account_passwords_reauth_illustration_dark.svg",
      "signin_reauth/images/account_passwords_reauth_illustration.svg",
      "signin_reauth/signin_reauth.html",
    ]
  }
  if (enable_dice_support) {
    static_files += [
      "dice_web_signin_intercept/chrome_signin/chrome_signin.html",
      "dice_web_signin_intercept/dice_web_signin_intercept.html",
      "dice_web_signin_intercept/images/split_header.svg",
    ]
  }

  non_web_component_files = [
    "sync_confirmation/sync_confirmation_app.html.ts",
    "sync_confirmation/sync_confirmation_app.ts",
    "sync_confirmation/sync_confirmation_browser_proxy.ts",
    "sync_confirmation/sync_confirmation.ts",
    "sync_confirmation/sync_disabled_confirmation_app.html.ts",
    "sync_confirmation/sync_disabled_confirmation_app.ts",
  ]

  if (!is_chromeos_ash) {
    non_web_component_files += [
      "managed_user_profile_notice/legacy_managed_user_profile_notice_app.html.ts",
      "managed_user_profile_notice/legacy_managed_user_profile_notice_app.ts",
      "managed_user_profile_notice/managed_user_profile_notice_app.html.ts",
      "managed_user_profile_notice/managed_user_profile_notice_app.ts",
      "managed_user_profile_notice/managed_user_profile_notice_browser_proxy.ts",
      "managed_user_profile_notice/managed_user_profile_notice_data_handling.html.ts",
      "managed_user_profile_notice/managed_user_profile_notice_data_handling.ts",
      "managed_user_profile_notice/managed_user_profile_notice_disclosure.html.ts",
      "managed_user_profile_notice/managed_user_profile_notice_disclosure.ts",
      "managed_user_profile_notice/managed_user_profile_notice_value_prop.html.ts",
      "managed_user_profile_notice/managed_user_profile_notice_value_prop.ts",
      "managed_user_profile_notice/managed_user_profile_notice_state.html.ts",
      "managed_user_profile_notice/managed_user_profile_notice_state.ts",
      "profile_customization/profile_customization_app.html.ts",
      "profile_customization/profile_customization_app.ts",
      "profile_customization/profile_customization_browser_proxy.ts",
      "signin_email_confirmation/signin_email_confirmation_app.html.ts",
      "signin_email_confirmation/signin_email_confirmation_app.ts",
      "signin_error/signin_error_app.html.ts",
      "signin_error/signin_error_app.ts",
      "signin_error/signin_error.ts",
      "signin_reauth/signin_reauth_app.html.ts",
      "signin_reauth/signin_reauth_app.ts",
      "signin_reauth/signin_reauth_browser_proxy.ts",
    ]
  }
  if (enable_dice_support) {
    non_web_component_files += [
      "dice_web_signin_intercept/chrome_signin/chrome_signin_app.html.ts",
      "dice_web_signin_intercept/chrome_signin/chrome_signin_app.ts",
      "dice_web_signin_intercept/dice_web_signin_intercept_app.html.ts",
      "dice_web_signin_intercept/dice_web_signin_intercept_app.ts",
      "dice_web_signin_intercept/dice_web_signin_intercept_browser_proxy.ts",
    ]
  }

  icons_html_files = [ "icons.html" ]

  css_files = [
    "signin_shared.css",
    "signin_vars.css",
    "sync_confirmation/sync_confirmation_app.css",
    "sync_confirmation/sync_disabled_confirmation_app.css",
    "tangible_sync_style_shared.css",
  ]

  if (!is_chromeos_ash) {
    css_files += [
      "managed_user_profile_notice/legacy_managed_user_profile_notice_app.css",
      "managed_user_profile_notice/managed_user_profile_notice_app.css",
      "managed_user_profile_notice/managed_user_profile_notice_data_handling.css",
      "managed_user_profile_notice/managed_user_profile_notice_disclosure.css",
      "managed_user_profile_notice/managed_user_profile_notice_state.css",
      "managed_user_profile_notice/managed_user_profile_notice_value_prop.css",
      "profile_customization/profile_customization_app.css",
      "signin_email_confirmation/signin_email_confirmation_app.css",
      "signin_error/signin_error_app.css",
      "signin_reauth/signin_reauth_app.css",
    ]
  }
  if (enable_dice_support) {
    css_files += [
      "dice_web_signin_intercept/chrome_signin/chrome_signin_app.css",
      "dice_web_signin_intercept/dice_web_signin_intercept_app.css",
    ]
  }

  html_to_wrapper_template = "detect"

  ts_composite = true
  ts_definitions = [ "//tools/typescript/definitions/chrome_send.d.ts" ]
  ts_deps = [
    "//third_party/lit/v3_0:build_ts",
    "//ui/webui/resources/cr_components/localized_link:build_ts",
    "//ui/webui/resources/cr_components/theme_color_picker:build_ts",
    "//ui/webui/resources/cr_elements:build_ts",
    "//ui/webui/resources/js:build_ts",
  ]

  ts_path_mappings = [
    "/icons.html.js|" +
        rebase_path("$target_gen_dir/preprocessed/icons.html.ts",
                    target_gen_dir),
    "/signin_shared.css.js|" +
        rebase_path("$target_gen_dir/preprocessed/signin_shared.css.ts",
                    target_gen_dir),
    "/signin_vars.css.js|" +
        rebase_path("$target_gen_dir/preprocessed/signin_vars.css.ts",
                    target_gen_dir),
    "/tangible_sync_style_shared.css.js|" +
        rebase_path(
            "$target_gen_dir/preprocessed/tangible_sync_style_shared.css.ts",
            target_gen_dir),
  ]

  webui_context_type = "trusted"
}
