# Copyright 2018 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ozone.gni")
import("//build/config/ui.gni")

assert(ozone_platform_drm || ozone_platform_wayland || ozone_platform_x11)

source_set("drm") {
  sources = [
    "drm_util_linux.cc",
    "drm_util_linux.h",
  ]

  deps = [
    "//base:base",
    "//build/config/linux/libdrm",
    "//ui/gfx:buffer_types",
  ]
}

source_set("gbm") {
  sources = [
    "gbm_buffer.h",
    "gbm_defines.h",
    "gbm_device.h",
    "gbm_util.cc",
    "gbm_util.h",
    "gbm_wrapper.cc",
    "gbm_wrapper.h",
    "scoped_gbm_device.cc",
    "scoped_gbm_device.h",
  ]

  deps = [
    ":drm",
    "//base:base",
    "//build/config/linux/libdrm",
    "//skia",
    "//third_party/minigbm",
    "//ui/gfx:buffer_types",
    "//ui/gfx:memory_buffer",
    "//ui/gfx/geometry:geometry",
  ]
}

if (ozone_platform_x11) {
  component("gpu_memory_buffer_support_x11") {
    sources = [
      "gpu_memory_buffer_support_x11.cc",
      "gpu_memory_buffer_support_x11.h",
    ]
    deps = [
      ":drm",
      ":gbm",
      "//base",
      "//skia",
      "//ui/gfx:buffer_types",
      "//ui/gfx:memory_buffer",
      "//ui/gfx/x",
    ]
    defines = [ "IS_GBM_SUPPORT_X11_IMPL" ]
  }
}

source_set("test_support") {
  testonly = true

  sources = [
    "test/mock_gbm_device.cc",
    "test/mock_gbm_device.h",
  ]

  deps = [
    ":drm",
    ":gbm",
    "//base",
    "//build/config/linux/libdrm",
    "//skia",
    "//testing/gtest",
  ]
}
