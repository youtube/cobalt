# Copyright 2019 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/browser/buildflags.gni")
if (is_android) {
  import("//build/config/android/config.gni")
  import("//build/config/android/rules.gni")
}

source_set("share") {
  sources = [
    "default_ranking.cc",
    "default_ranking.h",
    "share_attempt.cc",
    "share_attempt.h",
    "share_history.cc",
    "share_history.h",
    "share_ranking.cc",
    "share_ranking.h",
  ]

  configs += [ "//build/config/compiler:wexit_time_destructors" ]

  deps = [
    "//base",
    "//chrome/browser/favicon",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/share/proto:proto",
    "//chrome/services/qrcode_generator/public/cpp",
    "//chrome/services/qrcode_generator/public/mojom",
    "//components/leveldb_proto:leveldb_proto",
    "//components/shared_highlighting/core/common",
    "//content/public/browser",
    "//skia",
    "//ui/gfx",
    "//ui/snapshot",
  ]

  if (is_android) {
    sources += [
      "bitmap_download_request.cc",
      "default_ranking_android.cc",
      "editor_screenshot_task.cc",
      "link_to_text_bridge.cc",
      "qr_code_generation_request.cc",
      "qr_code_generation_request.h",
    ]
    deps += [
      ":jni_headers",
      "//chrome/browser/share/android:jni_headers",
      "//components/history/core/browser:browser",
      "//components/ukm/content:content",
      "//ui/android",
      "//url:gurl_android",
    ]
  }
}

if (is_android) {
  android_library("java") {
    sources = [
      "android/java/src/org/chromium/chrome/browser/share/ChromeShareExtras.java",
      "android/java/src/org/chromium/chrome/browser/share/ShareDelegate.java",
      "android/java/src/org/chromium/chrome/browser/share/ShareHistoryBridge.java",
      "android/java/src/org/chromium/chrome/browser/share/ShareRankingBridge.java",
      "android/java/src/org/chromium/chrome/browser/share/share_sheet/ChromeOptionShareCallback.java",
    ]

    annotation_processor_deps = [ "//base/android/jni_generator:jni_processor" ]

    deps = [
      ":jni_headers",
      "//base:base_java",
      "//base:jni_java",
      "//build/android:build_java",
      "//chrome/browser/preferences:java",
      "//chrome/browser/profiles/android:java",
      "//chrome/browser/tab:java",
      "//components/browser_ui/share/android:java",
      "//content/public/android:content_java",
      "//third_party/androidx:androidx_annotation_annotation_java",
      "//url:gurl_java",
    ]
  }

  generate_jni("jni_headers") {
    sources = [
      "android/java/src/org/chromium/chrome/browser/share/ShareHistoryBridge.java",
      "android/java/src/org/chromium/chrome/browser/share/ShareRankingBridge.java",
    ]
  }
}
