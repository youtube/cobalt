# Copyright 2019 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chromecast/chromecast.gni")

assert(!is_fuchsia)

source_set("external_service") {
  public = [
    "external_connector.h",
    "external_connector_impl.h",
    "external_service.h",
  ]
  sources = [
    "external_connector_impl.cc",
    "external_service.cc",
  ]
  public_deps = [
    "//base",
    "//chromecast/external_mojo/public/mojom",
    "//chromecast/mojo",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/system",
  ]
  deps = [
    "//chromecast/external_mojo/broker_service",
    "//mojo/core/embedder",
    "//mojo/public/cpp/platform",
    "//services/service_manager/public/cpp",
  ]
}

source_set("test_support") {
  testonly = true

  sources = [
    "fake_external_connector.cc",
    "fake_external_connector.h",
  ]

  deps = [
    ":external_service",
    "//base",
    "//chromecast/external_mojo/public/mojom:test_support",
    "//mojo/public/cpp/bindings",
  ]
}

source_set("process_setup") {
  sources = [
    "process_setup.cc",
    "process_setup.h",
  ]
  deps = [
    "//base",
    "//base:base_static",
    "//chromecast:chromecast_buildflags",
    "//chromecast/base:chromecast_switches",
  ]

  if (!is_fuchsia) {
    sources += [ "crash_reporter_client.h" ]

    if (use_system_crash_handler) {
      sources += [ "crash_reporter_system.cc" ]
      deps += [ "//third_party/crashpad/crashpad/client" ]
    } else {
      sources += [ "crash_reporter_builtin.cc" ]
      deps += [
        "//chromecast/crash",
        "//components/crash/core/app",
      ]
    }
  }
}

source_set("tracing_client_hdr") {
  sources = [ "tracing_client.h" ]
}

source_set("tracing_client_dummy") {
  sources = [
    "tracing_client_dummy.cc",
    "tracing_client_dummy.h",
  ]
  deps = [ ":tracing_client_hdr" ]
}

source_set("perfetto_tracing_client") {
  sources = [ "perfetto_tracing_client.cc" ]
  public = [ "perfetto_tracing_client.h" ]
  deps = [
    ":external_service",
    ":tracing_client_hdr",
    "//base",
    "//chromecast:chromecast_buildflags",
    "//mojo/public/cpp/bindings",
    "//services/tracing:lib",
    "//services/tracing/public/cpp",
    "//services/tracing/public/cpp:traced_process",
    "//services/tracing/public/mojom",
    "//third_party/perfetto:libperfetto",
  ]
}

group("tracing_client") {
  public_deps = [ ":tracing_client_hdr" ]
  if (enable_perfetto_tracing) {
    public_deps += [ ":perfetto_tracing_client" ]
  } else {
    public_deps += [ ":tracing_client_dummy" ]
  }
}

source_set("service_process") {
  sources = [ "service_process.h" ]
}

source_set("standalone_service_main") {
  sources = [ "standalone_service_main.cc" ]
  deps = [
    ":external_service",
    ":process_setup",
    ":tracing_client",
    "//base",
    "//chromecast/base:chromecast_switches",
    "//chromecast/external_mojo/public/cpp:common",
    "//chromecast/external_mojo/public/cpp:external_mojo_broker",
    "//mojo/core/embedder",
  ]
  public_deps = [ ":service_process" ]
}

source_set("chromium_service") {
  sources = [
    "chromium_service.cc",
    "chromium_service.h",
  ]
  deps = [
    ":external_service",
    "//base",
    "//chromecast/external_mojo/public/mojom",
    "//mojo/public/cpp/bindings",
    "//services/service_manager/public/cpp",
    "//services/service_manager/public/mojom",
  ]
}

source_set("util") {
  public = [ "reconnecting_remote.h" ]
  public_deps = [
    ":external_service",
    "//base",
    "//mojo/public/cpp/bindings",
  ]
}

executable("standalone_mojo_broker") {
  sources = [ "standalone_mojo_broker.cc" ]
  deps = [
    ":external_service",
    ":process_setup",
    ":tracing_client",
    "//base",
    "//chromecast/external_mojo/public/cpp:common",
    "//chromecast/external_mojo/public/cpp:external_mojo_broker",
    "//mojo/core/embedder",
  ]
}
