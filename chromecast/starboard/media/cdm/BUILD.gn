# Copyright 2024 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chromecast/chromecast.gni")
import("//chromecast/starboard/starboard.gni")

cast_source_set("starboard_decryptor_cast") {
  public = [ "starboard_decryptor_cast.h" ]
  sources = [ "starboard_decryptor_cast.cc" ]

  public_deps = [
    ":starboard_drm_wrapper",
    "//base",
    "//chromecast/media/cdm",
    "//media",
  ]
  deps = [
    ":starboard_drm_key_tracker",
    "//chromecast/media/base",
    "//chromecast/starboard/chromecast/starboard_adapter",
    "//chromecast/starboard/media/media:starboard_api_wrapper",
    "//google_apis",
  ]
}

cast_source_set("starboard_drm_wrapper") {
  public = [ "starboard_drm_wrapper.h" ]
  sources = [ "starboard_drm_wrapper.cc" ]
  public_deps = [
    "//base",
    "//chromecast/common:timing_tracker",
    "//chromecast/starboard/media/media:starboard_api_wrapper",
  ]
  deps = [ "//chromecast/starboard/chromecast/starboard_adapter" ]
}

cast_source_set("starboard_drm_key_tracker") {
  public = [ "starboard_drm_key_tracker.h" ]
  sources = [ "starboard_drm_key_tracker.cc" ]

  public_deps = [ "//base" ]
}

cast_source_set("mocks") {
  testonly = true
  public = [ "mock_starboard_drm_wrapper_client.h" ]
  sources = [ "mock_starboard_drm_wrapper_client.cc" ]
  public_deps = [
    ":starboard_drm_wrapper",
    "//testing/gmock",
  ]
}

test("starboard_cdm_unittests") {
  sources = [
    "starboard_decryptor_cast_test.cc",
    "starboard_drm_key_tracker_test.cc",
    "starboard_drm_wrapper_test.cc",
  ]
  deps = [
    ":mocks",
    ":starboard_decryptor_cast",
    ":starboard_drm_key_tracker",
    ":starboard_drm_wrapper",
    "//base",
    "//base/test:run_all_unittests",
    "//base/test:test_support",
    "//chromecast/starboard/media/media:mock_starboard_api_wrapper",
    "//chromecast/starboard/media/media:starboard_api_wrapper",
    "//media",
    "//testing/gmock",
    "//testing/gtest",
    "//url",
  ]
}
