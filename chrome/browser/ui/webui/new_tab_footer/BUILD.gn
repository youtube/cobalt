# Copyright 2025 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")

assert(!is_android)

source_set("new_tab_footer") {
  sources = [
    "footer_context_menu.h",
    "new_tab_footer_handler.h",
    "new_tab_footer_helper.h",
    "new_tab_footer_ui.h",
  ]
  public_deps = [
    ":mojo_bindings",
    "//chrome/browser/search/background",
    "//chrome/browser/ui/webui/customize_buttons:mojo_bindings",
    "//chrome/browser/ui/webui/top_chrome",
    "//components/policy/core/browser",
    "//components/prefs",
    "//components/user_education/webui",
    "//extensions/browser",
    "//ui/menus",
    "//ui/webui",
    "//ui/webui/resources/cr_components/help_bubble:mojo_bindings",
  ]
}

source_set("impl") {
  sources = [
    "footer_context_menu.cc",
    "new_tab_footer_handler.cc",
    "new_tab_footer_helper.cc",
    "new_tab_footer_ui.cc",
  ]
  public_deps = [ "//chrome/browser:browser_public_dependencies" ]
  deps = [
    ":new_tab_footer",
    "//chrome/browser:browser_process",
    "//chrome/browser/extensions",
    "//chrome/browser/profiles",
    "//chrome/browser/resources/new_tab_footer:resources_grit",
    "//chrome/browser/resources/new_tab_shared:resources_grit",
    "//chrome/browser/search",
    "//chrome/browser/ui/browser_window",
    "//chrome/browser/ui/views/side_panel",
    "//chrome/browser/ui/webui:webui_util",
    "//chrome/browser/ui/webui/customize_buttons",
    "//chrome/common",
    "//components/search",
    "//content/public/browser",
    "//ui/base",
  ]
}

source_set("test_support") {
  testonly = true
  sources = [
    "mock_new_tab_footer_document.cc",
    "mock_new_tab_footer_document.h",
  ]
  public_deps = [
    ":new_tab_footer",
    "//base/test:test_support",
    "//chrome/browser/ui:ui_features",
    "//chrome/test:test_support",
  ]
}

source_set("browser_tests") {
  testonly = true
  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
  sources = [
    "footer_context_menu_browsertest.cc",
    "new_tab_footer_handler_browsertest.cc",
  ]
  deps = [
    ":test_support",
    "//chrome/browser/ui/webui:webui_util",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [ "new_tab_footer_handler_unittest.cc" ]
  deps = [ ":test_support" ]
}

mojom("mojo_bindings") {
  sources = [ "new_tab_footer.mojom" ]
  webui_module_path = "/"

  public_deps = [
    "//ui/gfx/geometry/mojom",
    "//url/mojom:url_mojom_gurl",
  ]
}
