# Copyright 2014 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")

static_library("net") {
  sources = [
    "variations_command_line.cc",
    "variations_command_line.h",
    "variations_flags.cc",
    "variations_flags.h",
    "variations_http_headers.cc",
    "variations_http_headers.h",
  ]

  public_deps = [
    "//components/variations",
    "//net",
    "//services/network/public/cpp",
    "//services/network/public/cpp:cpp_base",
    "//url",
  ]

  deps = [
    "//base",
    "//components/google/core/common",
    "//components/variations:variations_mojom",
    "//components/variations/field_trial_config",
    "//components/variations/proto",
  ]

  if (!is_chromeos) {
    deps += [ "//third_party/boringssl" ]
  }

  if (use_blink) {
    sources += [
      "variations_url_loader_throttle.cc",
      "variations_url_loader_throttle.h",
    ]
    deps += [
      "//components/variations:variations_mojom",
      "//third_party/blink/public/common",
    ]
    public_deps += [ ":omnibox_autofocus_http_headers" ]
  }
}

if (use_blink) {
  component("omnibox_autofocus_http_headers") {
    output_name = "omnibox_autofocus_http_headers"

    defines = [ "IS_OMNIBOX_AUTOFOCUS_HTTP_HEADERS_IMPL" ]

    sources = [
      "omnibox_autofocus_http_headers.cc",
      "omnibox_autofocus_http_headers.h",
      "omnibox_autofocus_url_loader_throttle.cc",
      "omnibox_autofocus_url_loader_throttle.h",
    ]

    deps = [
      "//base",
      "//components/google/core/common",
      "//net",
      "//services/network/public/cpp",
      "//services/network/public/cpp:cpp_base",
      "//third_party/blink/public/common",
      "//url",
    ]
  }
}
