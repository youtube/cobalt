# Copyright 2021 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//testing/libfuzzer/fuzzer_test.gni")
import("//testing/test.gni")

assert(is_chromeos, "Non-Chrome-OS builds must not depend on //chromeos")

component("onc") {
  output_name = "chromeos_onc"
  defines = [ "IS_CHROMEOS_ONC_IMPL" ]
  deps = [
    "//components/crx_file",
    "//components/device_event_log",
    "//components/onc",
    "//crypto",
    "//net",
  ]
  sources = [
    "certificate_scope.cc",
    "certificate_scope.h",
    "onc_mapper.cc",
    "onc_mapper.h",
    "onc_parsed_certificates.cc",
    "onc_parsed_certificates.h",
    "onc_signature.cc",
    "onc_signature.h",
    "onc_utils.cc",
    "onc_utils.h",
    "onc_validator.cc",
    "onc_validator.h",
    "variable_expander.cc",
    "variable_expander.h",
  ]
}

source_set("test_support") {
  testonly = true
  deps = [
    "//base",
    "//components/onc",
    "//testing/gtest",
  ]
  data = [ "//chromeos/components/test/data/onc/" ]
  sources = [
    "onc_test_utils.cc",
    "onc_test_utils.h",
  ]
}

source_set("unit_tests") {
  testonly = true
  deps = [
    ":onc",
    ":test_support",
    "//base",
    "//base/test:test_support",
    "//components/onc",
    "//net",
    "//testing/gtest",
  ]
  sources = [
    "onc_parsed_certificates_unittest.cc",
    "onc_utils_unittest.cc",
    "onc_validator_unittest.cc",
    "variable_expander_unittest.cc",
  ]
}

fuzzer_test("variable_expander_fuzzer") {
  sources = [ "variable_expander_fuzzer.cc" ]
  seed_corpus =
      "//chromeos/components/test/data/onc/variable_expander/fuzzer_corpus"
  dict = "//chromeos/components/test/data/onc/variable_expander/fuzzer.dict"
  deps = [
    ":onc",
    "//base/test:test_support",
  ]
}

fuzzer_test("onc_validator_fuzzer") {
  sources = [ "onc_validator_fuzzer.cc" ]
  deps = [
    ":onc",
    "//base",
  ]
}
