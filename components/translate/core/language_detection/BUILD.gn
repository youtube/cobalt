# Copyright 2014 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")
import("//components/optimization_guide/features.gni")

static_library("chinese_script_classifier") {
  sources = [
    "chinese_script_classifier.cc",
    "chinese_script_classifier.h",
  ]

  deps = [
    "//base",
    "//third_party/icu",
  ]
}

static_library("language_detection") {
  sources = [
    "embedding_lookup.cc",
    "embedding_lookup.h",
    "language_detection_model.cc",
    "language_detection_model.h",
    "language_detection_resolver.h",
    "language_detection_util.cc",
    "language_detection_util.h",
    "ngram_hash.cc",
    "ngram_hash.h",
    "ngram_hash_ops_utils.cc",
    "ngram_hash_ops_utils.h",
    "quantization_utils.cc",
    "quantization_utils.h",
  ]

  deps = [
    ":chinese_script_classifier",
    "//base",
    "//components/language/core/common",
    "//components/optimization_guide/core:features",
    "//components/translate/core/common",
    "//third_party/cld_3/src/src:cld_3",
    "//third_party/flatbuffers",
    "//third_party/icu",
    "//third_party/smhasher:murmurhash2",
    "//third_party/tflite",
    "//third_party/tflite:tflite_public_headers",
    "//third_party/tflite_support",
    "//third_party/tflite_support:tflite_support_proto",
    "//third_party/utf",
    "//url",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [ "chinese_script_classifier_test.cc" ]
  if (build_with_tflite_lib) {
    sources += [
      "language_detection_model_unittest.cc",
      "language_detection_util_unittest.cc",
      "ngram_hash_ops_utils_unittest.cc",
      "quantization_utils_unittest.cc",
    ]
  }

  deps = [
    ":chinese_script_classifier",
    ":language_detection",
    "//base",
    "//base/test:test_support",
    "//components/translate/core/common",
    "//testing/gtest",
    "//third_party/icu",
    "//url",
  ]
  if (build_with_tflite_lib) {
    deps += [ ":unit_tests_bundle_data" ]
  }
}

bundle_data("unit_tests_bundle_data") {
  visibility = [ ":unit_tests" ]
  testonly = true
  sources = [ "//components/test/data/translate/valid_model.tflite" ]
  outputs = [ "{{bundle_resources_dir}}/" +
              "{{source_root_relative_dir}}/{{source_file_part}}" ]
}
