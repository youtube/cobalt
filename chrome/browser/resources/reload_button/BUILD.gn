# Copyright 2025 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//ui/webui/resources/tools/build_webui.gni")

assert(!is_android)

build_webui("build") {
  grd_prefix = "reload_button"

  static_files = [
    "reload_button.html",
    "reload_button.css",
  ]

  ts_files = [
    "app.ts",
    "app.html.ts",
    "browser_proxy.ts",
    "metrics_recorder.ts",
  ]
  css_files = [ "app.css" ]

  webui_context_type = "trusted"
  optimize = optimize_webui
  if (optimize) {
    optimize_webui_host = "reload-button.top-chrome"
    optimize_webui_in_files = [ "app.js" ]
    optimize_webui_external_paths =
        [ "chrome://reload-button.top-chrome|" +
          rebase_path(
              "$root_gen_dir/chrome/browser/resources/reload_button/tsc",
              root_build_dir) ]
  }

  ts_definitions = [
    "//tools/typescript/definitions/chrome_send.d.ts",
    "//tools/typescript/definitions/chrome_timeticks.d.ts",
    "//tools/typescript/definitions/metrics_private.d.ts",
    "//tools/typescript/definitions/pending.d.ts",
  ]

  ts_deps = [
    "//third_party/lit/v3_0:build_ts",
    "//ui/webui/resources/cr_components/color_change_listener:build_ts",
    "//ui/webui/resources/cr_elements:build_ts",
    "//ui/webui/resources/js:build_ts",
    "//ui/webui/resources/mojo:build_ts",
  ]

  mojo_files_deps =
      [ "//chrome/browser/ui/webui/reload_button:mojo_bindings_ts__generator" ]

  mojo_files = [ "$root_gen_dir/chrome/browser/ui/webui/reload_button/reload_button.mojom-webui.ts" ]
}
