# Copyright 2020 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//third_party/protobuf/proto_library.gni")

assert(is_chromeos_ash, "Non-ChromeOS builds cannot depend on //chromeos/ash")

component("arc") {
  output_name = "ash_dbus_arc"
  defines = [ "IS_ASH_DBUS_ARC_IMPL" ]

  public_deps = [ ":arc_proto" ]

  deps = [
    ":arcvm_data_migrator_proto_lib",
    "//base",
    "//chromeos/dbus/common",
    "//dbus",
  ]

  sources = [
    "arc_appfuse_provider_client.cc",
    "arc_appfuse_provider_client.h",
    "arc_camera_client.cc",
    "arc_camera_client.h",
    "arc_keymaster_client.cc",
    "arc_keymaster_client.h",
    "arc_keymint_client.cc",
    "arc_keymint_client.h",
    "arc_midis_client.cc",
    "arc_midis_client.h",
    "arc_obb_mounter_client.cc",
    "arc_obb_mounter_client.h",
    "arcvm_data_migrator_client.cc",
    "arcvm_data_migrator_client.h",
    "fake_arc_appfuse_provider_client.cc",
    "fake_arc_appfuse_provider_client.h",
    "fake_arc_camera_client.cc",
    "fake_arc_camera_client.h",
    "fake_arc_keymaster_client.cc",
    "fake_arc_keymaster_client.h",
    "fake_arc_keymint_client.cc",
    "fake_arc_keymint_client.h",
    "fake_arc_midis_client.cc",
    "fake_arc_midis_client.h",
    "fake_arc_obb_mounter_client.cc",
    "fake_arc_obb_mounter_client.h",
    "fake_arcvm_data_migrator_client.cc",
    "fake_arcvm_data_migrator_client.h",
  ]
}

static_library("arc_proto") {
  public_deps = [ ":arc_proto_lib" ]
  all_dependent_configs = [ ":arc_proto_config" ]
}

config("arc_proto_config") {
  include_dirs = [ "${root_gen_dir}/chromeos/ash/components/dbus" ]
}

proto_library("arc_proto_lib") {
  sources = [ "//third_party/cros_system_api/dbus/arc/arc.proto" ]
  proto_out_dir = "chromeos/ash/components/dbus/arc"
}

proto_library("arcvm_data_migrator_proto_lib") {
  sources = [ "//third_party/cros_system_api/dbus/arcvm_data_migrator/arcvm_data_migrator.proto" ]
  proto_out_dir = "chromeos/ash/components/dbus/arcvm_data_migrator"
}
