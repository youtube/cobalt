# Copyright 2024 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(is_win || is_mac || is_linux || is_chromeos || is_android)

source_set("file_system_access") {
  sources = [
    "file_system_access_dangerous_file_dialog.h",
    "file_system_access_dialogs.h",
    "file_system_access_permission_dialog.h",
    "file_system_access_restricted_directory_dialog.h",
    "file_system_access_ui_helpers.h",
  ]
  public_deps = [
    "//base",
    "//chrome/browser/file_system_access",
    "//content/public/browser",
  ]
}

source_set("impl") {
  sources = [
    "file_system_access_dangerous_file_dialog.cc",
    "file_system_access_dialogs.cc",
    "file_system_access_permission_dialog.cc",
    "file_system_access_restricted_directory_dialog.cc",
    "file_system_access_ui_helpers.cc",
  ]
  deps = [
    ":file_system_access",
    "//chrome/browser/ui:url_identity",
  ]
  public_deps = [ "//chrome/browser:browser_public_dependencies" ]
}

if (!is_android) {
  source_set("browser_tests") {
    testonly = true
    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
    sources = [
      "file_system_access_dangerous_file_dialog_browsertest.cc",
      "file_system_access_permission_dialog_browsertest.cc",
      "file_system_access_restricted_directory_dialog_browsertest.cc",
    ]
    deps = [
      ":file_system_access",
      "//base/test:test_support",
      "//chrome/test:test_support",
      "//components/permissions",
      "//content/test:test_support",
      "//url",
    ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "file_system_access_dangerous_file_dialog_unittest.cc",
    "file_system_access_permission_dialog_unittest.cc",
    "file_system_access_restricted_directory_dialog_unittest.cc",
    "file_system_access_ui_helpers_unittest.cc",
  ]
  deps = [
    ":file_system_access",
    "//base/test:test_support",
    "//components/permissions",
    "//content/public/browser",
    "//ui/base:test_support",
    "//url",
  ]
}
