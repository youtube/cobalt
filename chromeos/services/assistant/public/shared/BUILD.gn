# Copyright 2020 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chrome_build.gni")
import("//chromeos/ash/components/assistant/assistant.gni")

# C++ headers and sources that can be shared by assistant internal and
# external directories.
component("shared") {
  output_name = "assistant_service_shared"

  defines = [ "IS_ASSISTANT_SERVICE_SHARED_IMPL" ]

  sources = [
    "constants.h",
    "utils.h",
  ]

  if (enable_cros_libassistant) {
    sources += [ "//chromeos/assistant/internal/constants.cc" ]
  } else {
    sources += [ "constants.cc" ]
  }

  deps = [ "//base:base" ]
}

source_set("new_entry_point_constants") {
  if (enable_cros_libassistant) {
    deps = [ "//chromeos/assistant/internal:new_entry_point_constants" ]
  } else {
    deps = [ ":new_entry_point_constants_placeholder" ]
  }

  public_deps = [ ":new_entry_point_constants_interface" ]
}

source_set("new_entry_point_constants_placeholder") {
  sources = [ "new_entry_point_constants.cc" ]
  deps = [
    ":new_entry_point_constants_interface",
    "//chrome/app/theme:theme_resources_grit",
  ]
}

source_set("new_entry_point_constants_interface") {
  sources = [ "new_entry_point_constants.h" ]

  deps = [ "//base" ]
}
