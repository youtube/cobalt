# Copyright 2021 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/css_to_wrapper.gni")
import("//tools/polymer/html_to_wrapper.gni")
import("//tools/typescript/ts_definitions.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(is_chromeos_ash)

# The directory where most files will be processed into,
# and where ts_library inputs from.
preprocess_folder = "preprocessed"

# The directory where ts_library outputs to.
out_folder = "tsc"

# The directory where HTML and CSS files will be processed into.
# Ultimately, files in this directory will be processed into $preprocess_folder.
html_css_preprocess_folder = "preprocessed_html_css_wrapper_files"

non_web_component_files = [
  "js/accelerator_lookup_manager.ts",
  "js/fake_data.ts",
  "js/fake_shortcut_provider.ts",
  "js/mojo_interface_provider.ts",
  "js/router.ts",
  "js/search/fake_shortcut_search_handler.ts",
  "js/search/search_result_bolding.ts",
  "js/search/shortcut_search_handler.ts",
  "js/shortcut_types.ts",
  "js/shortcut_utils.ts",
]

web_component_files = [
  "js/accelerator_edit_dialog.ts",
  "js/accelerator_edit_view.ts",
  "js/accelerator_row.ts",
  "js/accelerator_subsection.ts",
  "js/accelerator_view.ts",
  "js/bottom_nav_content.ts",
  "js/input_key.ts",
  "js/search/search_box.ts",
  "js/search/search_result_row.ts",
  "js/shortcut_customization_app.ts",
  "js/shortcut_input.ts",
  "js/shortcuts_page.ts",
  "js/text_accelerator.ts",
]

html_files = []
foreach(f, web_component_files) {
  html_files += [ string_replace(f, ".ts", ".html") ]
}

icons_html_files = [ "common/icons.html" ]

html_wrapper_files = []
foreach(f, html_files + icons_html_files) {
  html_wrapper_files += [ f + ".js" ]
}

js_files = web_component_files + non_web_component_files

# Files that are passed as input to css_to_wrapper().
css_files = [ "css/shortcut_customization_shared.css" ]

# Files that are generated by css_to_wrapper().
css_wrapper_files = []
foreach(f, css_files) {
  css_wrapper_files += [ f + ".js" ]
}

static_resource_files = [
  "app_icon_192.png",
  "index.html",
]

css_to_wrapper("css_wrapper_files") {
  in_files = css_files
  use_js = true
  out_folder = "$target_gen_dir/$html_css_preprocess_folder"
}

html_to_wrapper("html_to_wrapper_files") {
  in_files = html_files + icons_html_files
  use_js = true
  out_folder = "$target_gen_dir/$html_css_preprocess_folder"
}

preprocess_if_expr("preprocess") {
  in_folder = "."
  out_folder = "$target_gen_dir/$preprocess_folder"
  in_files = js_files
}

preprocess_if_expr("preprocess_generated") {
  deps = [
    ":css_wrapper_files",
    ":html_to_wrapper_files",
  ]
  in_folder = "$target_gen_dir/$html_css_preprocess_folder"
  out_folder = "$target_gen_dir/$preprocess_folder"
  in_files = html_wrapper_files + css_wrapper_files
}

# This template is used to reduce redundancy when copying generated files from
# $root_gen_dir/mojom-webui/ into this project's preprocess directory.
# Note that $in_dir is used in the output so that the output directory structure
# matches the generated mojom-webui/ structure. This is necessary because
# shortcut_customization.mojom-webui.ts depends on
# accelerator_info.mojom-webui.ts via a relative import in the generated code.
template("copy_generated_mojo") {
  forward_variables_from(invoker, [ "deps" ])

  copy(target_name) {
    in_dir = invoker.in_dir
    out_dir = in_dir
    if (defined(invoker.out_dir)) {
      out_dir = invoker.out_dir
    }
    sources = []
    foreach(file, invoker.mojo_files) {
      sources += [ "$root_gen_dir/$in_dir/$file" ]
    }
    outputs = [ "$target_gen_dir/$preprocess_folder/mojom-webui/$out_dir/{{source_file_part}}" ]
  }
}

copy_generated_mojo("copy_shortcuts_mojo") {
  deps = [ "//ash/webui/shortcut_customization_ui/mojom:mojom_ts__generator" ]
  in_dir = "ash/webui/shortcut_customization_ui/mojom"
  mojo_files = [ "shortcut_customization.mojom-webui.ts" ]
}

copy_generated_mojo("copy_shortcuts_search_mojo") {
  deps = [ "//ash/webui/shortcut_customization_ui/backend/search:mojo_bindings_ts__generator" ]
  in_dir = "ash/webui/shortcut_customization_ui/backend/search"
  mojo_files = [ "search.mojom-webui.ts" ]
}

copy_generated_mojo("copy_accelerator_info_mojo") {
  deps = [ "//ash/public/mojom:accelerator_info_ts__generator" ]
  in_dir = "ash/public/mojom"
  out_dir = ""
  mojo_files = [ "accelerator_info.mojom-webui.ts" ]
}

copy_generated_mojo("copy_accelerator_configuration_mojo") {
  deps = [ "//ash/public/mojom:accelerator_configuration_ts__generator" ]
  in_dir = "ash/public/mojom"
  out_dir = ""
  mojo_files = [ "accelerator_configuration.mojom-webui.ts" ]
}

copy_generated_mojo("copy_accelerator_keys_mojo") {
  deps = [ "//ash/public/mojom:accelerator_keys_ts__generator" ]
  in_dir = "ash/public/mojom"
  out_dir = ""
  mojo_files = [ "accelerator_keys.mojom-webui.ts" ]
}

ts_library("build_ts") {
  composite = true
  root_dir = "$target_gen_dir/$preprocess_folder"
  out_dir = "$target_gen_dir/$out_folder"
  tsconfig_base = "//tools/typescript/tsconfig_base_polymer.json"
  in_files =
      js_files + css_wrapper_files + html_wrapper_files +
      rebase_path(get_target_outputs(":copy_shortcuts_mojo"),
                  root_dir,
                  root_build_dir) +
      rebase_path(get_target_outputs(":copy_shortcuts_search_mojo"),
                  root_dir,
                  root_build_dir) +
      rebase_path(get_target_outputs(":copy_accelerator_info_mojo"),
                  root_dir,
                  root_build_dir) +
      rebase_path(get_target_outputs(":copy_accelerator_configuration_mojo"),
                  root_dir,
                  root_build_dir) +
      rebase_path(get_target_outputs(":copy_accelerator_keys_mojo"),
                  root_dir,
                  root_build_dir)

  deps = [
    "//ash/webui/common/resources:build_ts",
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources/cr_components/color_change_listener:build_ts",
    "//ui/webui/resources/cr_elements:build_ts",
    "//ui/webui/resources/js:build_ts",
    "//ui/webui/resources/mojo:build_ts",
  ]

  extra_deps = [
    ":copy_accelerator_configuration_mojo",
    ":copy_accelerator_info_mojo",
    ":copy_accelerator_keys_mojo",
    ":copy_shortcuts_mojo",
    ":copy_shortcuts_search_mojo",
    ":preprocess",
    ":preprocess_generated",
    "//ash/webui/common/resources:copy_checked_in_dts_files",
  ]
}

mojo_grdp_file = "$target_gen_dir/mojo.grdp"

generate_grd("build_grd") {
  input_files_base_dir = rebase_path(".", "//")
  input_files = static_resource_files

  deps = [ ":build_ts" ]
  manifest_files =
      filter_include(get_target_outputs(":build_ts"), [ "*_manifest.json" ])
  grd_prefix = "ash_shortcut_customization_app"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"
}

generate_grd("build_mojo_grdp") {
  deps = [
    "//ash/public/mojom:accelerator_configuration_ts__generator",
    "//ash/public/mojom:accelerator_info_ts__generator",
    "//ash/public/mojom:accelerator_keys_ts__generator",
    "//ash/webui/shortcut_customization_ui/backend/search:mojo_bindings_ts__generator",
    "//ash/webui/shortcut_customization_ui/mojom:mojom_ts__generator",
  ]

  input_files_base_dir = rebase_path("$root_gen_dir/", root_build_dir)
  input_files = [
    "mojom-webui/ash/public/mojom/accelerator_configuration.mojom-webui.ts",
    "mojom-webui/ash/public/mojom/accelerator_info.mojom-webui.ts",
    "mojom-webui/ash/public/mojom/accelerator_keys.mojom-webui.ts",
    "mojom-webui/ash/webui/shortcut_customization_ui/backend/search/search.mojom-webui.ts",
    "mojom-webui/ash/webui/shortcut_customization_ui/mojom/shortcut_customization.mojom-webui.ts",
  ]

  grd_prefix = "ash_shortcut_customization_app_mojo"
  out_grd = mojo_grdp_file
}
