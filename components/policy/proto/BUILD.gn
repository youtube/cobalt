# Copyright 2014 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//third_party/protobuf/proto_library.gni")

# The proto files need to be a component to avoid duplicate symbols the way the
# dependency structure is currently. Since proto_library generates a source
# set, here we link the result into a component.
component("proto") {
  output_name = "policy_proto"
  public_deps = [ ":proto_internal" ]

  # Make all direct and indirect depedendencies include the config generated by
  # the proto_internal target below. This config adds include paths needed to
  # compile the generated protobuf headers.
  all_dependent_configs = [ ":proto_internal_config" ]
}

proto_library("policy_common_definitions_compile_proto") {
  visibility = [
    ":proto_internal",
    ":proto_internal_gen",
    "//chrome/browser/privacy:chrome_settings_gen",
    "//chrome/browser/privacy:chrome_settings",
    "//components/policy:chrome_settings_proto_generated_compile_proto_gen",
    "//components/policy:chrome_settings_proto_generated_compile_proto",
    "//components/policy:cloud_policy_proto_generated_compile_proto_gen",
    "//components/policy:cloud_policy_proto_generated_compile_proto",
    "//components/policy:generated",
  ]

  sources = [ "policy_common_definitions.proto" ]

  proto_out_dir = "components/policy/proto"
}

proto_library("proto_internal") {
  visibility = [ ":proto" ]

  sources = [
    "device_management_backend.proto",
    "policy_signing_key.proto",
    "secure_connect.proto",
  ]

  if (is_chromeos_ash) {
    sources += [
      "chrome_device_policy.proto",
      "install_attributes.proto",
    ]
  }

  if (!is_android && !is_ios) {
    sources += [ "chrome_extension_policy.proto" ]
  }

  extra_configs =
      [ "//third_party/private_membership:private_membership_config" ]

  import_dirs = [
    "//third_party/private_membership/src",
    "//third_party/shell-encryption/src",
    ".",
  ]

  deps = [
    ":policy_common_definitions_compile_proto",
    "//third_party/private_membership:private_membership_proto",
  ]

  cc_generator_options = "dllexport_decl=POLICY_PROTO_EXPORT:"
  cc_include = "components/policy/proto/policy_proto_export.h"
  component_build_force_source_set = true
  defines = [ "POLICY_PROTO_COMPILATION" ]
}
