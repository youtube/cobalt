# Copyright 2024 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(is_win || is_mac || is_linux || is_chromeos || is_android)

source_set("ai") {
  sources = [
    "ai_assistant.h",
    "ai_context_bound_object.h",
    "ai_context_bound_object_set.h",
    "ai_create_on_device_session_task.h",
    "ai_data_keyed_service.h",
    "ai_data_keyed_service_factory.h",
    "ai_manager_keyed_service.h",
    "ai_manager_keyed_service_factory.h",
    "ai_on_device_model_component_observer.h",
    "ai_rewriter.h",
    "ai_summarizer.h",
    "ai_utils.h",
    "ai_writer.h",
    "features.cc",
    "features.h",
  ]
  public_deps = [
    "//base",
    "//chrome/browser:browser_process",
    "//chrome/browser/optimization_guide",
    "//chrome/browser/profiles:profile",
    "//components/component_updater:component_updater",
    "//components/optimization_guide/core",
    "//components/update_client:update_client",
    "//content/public/browser",
  ]
}

source_set("impl") {
  sources = [
    "ai_assistant.cc",
    "ai_context_bound_object_set.cc",
    "ai_create_on_device_session_task.cc",
    "ai_data_keyed_service.cc",
    "ai_data_keyed_service_factory.cc",
    "ai_manager_keyed_service.cc",
    "ai_manager_keyed_service_factory.cc",
    "ai_on_device_model_component_observer.cc",
    "ai_rewriter.cc",
    "ai_summarizer.cc",
    "ai_utils.cc",
    "ai_writer.cc",
  ]
  public_deps = [
    "//chrome/browser:browser_public_dependencies",
    "//chrome/browser/content_extraction:content_extraction",
    "//chrome/browser/ui:ui",
  ]
  deps = [
    ":ai",
    "//components/site_engagement/content:content",
    "//skia",
  ]
  if (!is_android) {
    deps += [
      "//chrome/browser/ui/tabs:tab_group",
      "//chrome/browser/ui/tabs:tab_strip",
    ]
  }
}

source_set("test_support") {
  testonly = true
  sources = [
    "ai_test_utils.cc",
    "ai_test_utils.h",
  ]
  public_deps = [
    "//chrome/browser/optimization_guide",
    "//chrome/browser/optimization_guide:test_support",
    "//chrome/test:test_support",
    "//testing/gmock:gmock",
    "//testing/gtest:gtest",
  ]
  deps = [ ":ai" ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "ai_assistant_unittest.cc",
    "ai_manager_keyed_service_unittest.cc",
    "ai_rewriter_unittest.cc",
    "ai_summarizer_unittest.cc",
    "ai_writer_unittest.cc",
  ]
  deps = [
    ":ai",
    ":test_support",
    "//components/optimization_guide/core:test_support",
  ]
}
