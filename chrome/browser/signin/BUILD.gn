# Copyright 2021 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//components/signin/features.gni")

source_set("identity_manager_provider") {
  sources = [
    "identity_manager_provider.cc",
    "identity_manager_provider.h",
  ]
  deps = [ "//base" ]
}

# TODO(crbug.com/364667553): complete modularization of //c/b/signin.
# At the moment, it's hard to complete it at once as it pulls deps, which
# result in circular deps. Modularization of others to resolve that is
# non-trivial and is hard to complete at once.
source_set("signin") {
  sources = []
  public_deps = []

  if (enable_dice_support) {
    sources = [
      "dice_web_signin_interceptor.h",
      "web_signin_interceptor.h",
    ]
    public_deps += [
      "//base",
      "//chrome/browser/search_engine_choice",
      "//chrome/browser/ui/webui/signin:signin_utils",
      "//components/keyed_service/core",
      "//components/policy/core/browser",
      "//components/search_engines",
      "//components/signin/public/base",
      "//components/signin/public/identity_manager",
      "//google_apis",
      "//skia",
    ]
  }
}

source_set("impl") {
  sources = []
  public_deps = [ "//chrome/browser:browser_public_dependencies" ]
  deps = [ ":signin" ]

  if (enable_dice_support) {
    sources = [
      "dice_web_signin_interceptor.cc",
      "web_signin_interceptor.cc",
    ]
    deps += [
      "//base",
      "//base:i18n",
      "//chrome/browser:browser_process",
      "//chrome/browser/new_tab_page/chrome_colors:generate_chrome_colors_info",
      "//chrome/browser/profiles:profile_util",
      "//chrome/browser/search_engine_choice",
      "//chrome/browser/search_engines",
      "//chrome/browser/themes",
      "//chrome/browser/ui:browser_list",
      "//chrome/browser/ui:ui_features",
      "//chrome/browser/ui/signin",
      "//chrome/common:channel_info",
      "//chrome/common:constants",
      "//chrome/common/themes:autogenerated_theme_util",
      "//components/password_manager/core/browser",
      "//components/password_manager/core/common",
      "//components/policy:generated",
      "//components/policy/core/browser",
      "//components/policy/core/common",
      "//components/pref_registry",
      "//components/prefs",
      "//components/search_engines",
      "//components/signin/public/base",
      "//components/signin/public/identity_manager",
      "//google_apis",
      "//ui/base",
    ]
  }
}
