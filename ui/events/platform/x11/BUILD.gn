# Copyright 2014 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//build/config/ozone.gni")
import("//build/config/ui.gni")

assert(ozone_platform_x11)

component("x11") {
  output_name = "x11_events_platform"

  sources = [
    "x11_event_source.cc",
    "x11_event_source.h",
    "x11_hotplug_event_handler.cc",
    "x11_hotplug_event_handler.h",
  ]

  defines = [ "EVENTS_IMPLEMENTATION" ]

  public_deps = [
    "//ui/events",
    "//ui/events:events_base",
    "//ui/events/devices",
    "//ui/events/devices/x11",
    "//ui/events/keycodes:x11",
    "//ui/events/platform",
    "//ui/events/x",
    "//ui/gfx",
    "//ui/gfx/x",
  ]

  deps = [
    "//base",
    "//build:chromeos_buildflags",
    "//ui/base:features",
    "//ui/events/keycodes:x11",
  ]

  if (is_chromeos_ash) {
    deps += [ "//ui/events/ozone" ]
  }

  if (use_glib) {
    sources += [
      "x11_event_watcher_glib.cc",
      "x11_event_watcher_glib.h",
    ]
    configs += [ "//build/config/linux:glib" ]
  } else {
    sources += [
      "x11_event_watcher_fdwatch.cc",
      "x11_event_watcher_fdwatch.h",
    ]
  }

  if (use_ozone) {
    deps += [ "//ui/base:features" ]
  }
}
