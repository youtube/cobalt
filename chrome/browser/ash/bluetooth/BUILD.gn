# Copyright 2024 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/chromeos/crosier_tests.gni")
import("//build/config/chromeos/ui_mode.gni")

assert(is_chromeos_ash)

static_library("bluetooth") {
  sources = [
    "bluetooth_log_controller.cc",
    "bluetooth_log_controller.h",
    "debug_logs_manager.cc",
    "debug_logs_manager.h",
    "debug_logs_manager_factory.cc",
    "debug_logs_manager_factory.h",
    "hats_bluetooth_revamp_trigger_impl.cc",
    "hats_bluetooth_revamp_trigger_impl.h",
  ]
  deps = [
    "//chrome/browser/ash/profiles",
    "//chromeos/ash/components/dbus/upstart",
    "//components/device_event_log",
    "//components/session_manager/core",
  ]
  public_deps = [
    "//ash",
    "//base",
    "//chrome/browser:browser_public_dependencies",
    "//chrome/browser/ash/hats",
    "//chrome/browser/profiles",
    "//chrome/browser/ui/webui/bluetooth_internals:mojo_bindings",
    "//device/bluetooth",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "bluetooth_log_controller_unittest.cc",
    "debug_logs_manager_unittest.cc",
  ]
  deps = [
    ":bluetooth",
    "//ash/constants",
    "//base/test:test_support",
    "//chromeos/ash/components/dbus/upstart",
    "//components/account_id",
    "//components/prefs:test_support",
    "//components/user_manager:test_support",
    "//dbus",
    "//device/bluetooth",
  ]
}

source_set("browser_tests") {
  testonly = true

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

  sources = [ "hats_bluetooth_revamp_trigger_impl_browsertest.cc" ]

  deps = [
    ":bluetooth",
    "//ash/constants",
    "//ash/public/cpp",
    "//base/test:test_support",
    "//chrome/browser/ash/hats",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/ui",
    "//chrome/common",
    "//chrome/test:test_support",
    "//chromeos/ash/components/network",
    "//components/prefs",
    "//components/session_manager/core",
    "//content/test:test_support",
  ]
}

if (is_chromeos_device) {
  crosier_tests("integration_tests") {
    sources = [ "bluetooth_integration_test.cc" ]

    sources_metadata = [ "bluetooth_integration_test.yaml" ]

    deps = [
      "//ash",
      "//base",
      "//base/test:test_support",
      "//chrome/browser/ui",
      "//chrome/test:ash_integration_test_support",
      "//chrome/test:test_support",
      "//chrome/test/base/ash/interactive/bluetooth:test_support",
      "//dbus",
      "//device/bluetooth",
      "//testing/gtest",
      "//ui/base:test_support",
      "//ui/views",
    ]
  }
}
