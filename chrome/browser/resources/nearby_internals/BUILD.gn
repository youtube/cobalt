# Copyright 2020 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/css_to_wrapper.gni")
import("//tools/polymer/html_to_wrapper.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(is_chromeos_ash)

preprocess_folder = "$target_gen_dir/preprocessed"
preprocess_manifest = "preprocessed_manifest.json"
preprocess_gen_manifest = "preprocessed_gen_manifest.json"

web_component_files = [
  "contact_object.js",
  "contact_tab.js",
  "cross_device_internals.js",
  "http_message_object.js",
  "http_tab.js",
  "log_object.js",
  "logging_tab.js",
  "log_types.js",
  "nearby_internals.js",
  "ui_trigger_list_object.js",
  "np_list_object.js",
  "ui_trigger_tab.js",
]

# Files that are passed as input to html_to_wrapper().
html_files = []
foreach(f, web_component_files) {
  html_files += [ string_replace(f, ".js", ".html") ]
}

# Files that are generated by html_to_wrapper().
html_wrapper_files = []
foreach(f, html_files) {
  html_wrapper_files += [ f + ".js" ]
}

css_files = [ "shared_style.css" ]

# Files that are generated by css_to_wrapper().
css_wrapper_files = []
foreach(f, css_files) {
  css_wrapper_files += [ f + ".js" ]
}

generate_grd("build_grd") {
  grd_prefix = "nearby_internals"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"
  input_files = [ "index.html" ]
  input_files_base_dir = rebase_path(".", "//")

  deps = [
    ":preprocess",
    ":preprocess_html_css_files",
  ]
  manifest_files = [
    "$target_gen_dir/$preprocess_manifest",
    "$target_gen_dir/$preprocess_gen_manifest",
  ]
}

preprocess_if_expr("preprocess") {
  out_folder = preprocess_folder
  out_manifest = "$target_gen_dir/$preprocess_manifest"
  in_files = [
               "nearby_contact_browser_proxy.js",
               "nearby_http_browser_proxy.js",
               "cross_device_logs_browser_proxy.js",
               "nearby_presence_browser_proxy.js",
               "nearby_prefs_browser_proxy.js",
               "nearby_ui_trigger_browser_proxy.js",
               "types.js",
             ] + web_component_files
}

preprocess_if_expr("preprocess_html_css_files") {
  deps = [
    ":css_wrapper_files",
    ":html_wrapper_files",
  ]
  in_folder = target_gen_dir
  out_folder = preprocess_folder
  out_manifest = "$target_gen_dir/$preprocess_gen_manifest"
  in_files = html_wrapper_files + css_wrapper_files
}

grit("resources") {
  defines = chrome_grit_defines

  # These arguments are needed since the grd is generated at build time.
  enable_input_discovery_for_gn_analyze = false
  source = "$target_gen_dir/nearby_internals_resources.grd"
  deps = [ ":build_grd" ]

  outputs = [
    "grit/nearby_internals_resources.h",
    "grit/nearby_internals_resources_map.cc",
    "grit/nearby_internals_resources_map.h",
    "nearby_internals_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
}

js_type_check("closure_compile") {
  is_polymer3 = true
  deps = [
    ":contact_object",
    ":contact_tab",
    ":cross_device_internals",
    ":cross_device_logs_browser_proxy",
    ":http_message_object",
    ":http_tab",
    ":log_object",
    ":log_types",
    ":logging_tab",
    ":nearby_contact_browser_proxy",
    ":nearby_http_browser_proxy",
    ":nearby_internals",
    ":nearby_prefs_browser_proxy",
    ":nearby_presence_browser_proxy",
    ":nearby_ui_trigger_browser_proxy",
    ":np_list_object",
    ":types",
    ":ui_trigger_list_object",
    ":ui_trigger_tab",
  ]
}

js_library("cross_device_internals") {
  deps = [
    ":cross_device_logs_browser_proxy",
    ":log_object",
    ":log_types",
    ":logging_tab",
    ":nearby_presence_browser_proxy",
    ":np_list_object",
    ":types",
    ":ui_trigger_list_object",
    "//ash/webui/common/resources:cr.m",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("nearby_internals") {
  deps = [
    ":contact_object",
    ":contact_tab",
    ":cross_device_logs_browser_proxy",
    ":http_message_object",
    ":http_tab",
    ":log_object",
    ":logging_tab",
    ":nearby_contact_browser_proxy",
    ":nearby_http_browser_proxy",
    ":nearby_prefs_browser_proxy",
    ":nearby_ui_trigger_browser_proxy",
    ":np_list_object",
    ":types",
    ":ui_trigger_list_object",
    ":ui_trigger_tab",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("log_types") {
  deps = [
    ":types",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("logging_tab") {
  deps = [
    ":cross_device_logs_browser_proxy",
    ":log_object",
    ":nearby_prefs_browser_proxy",
    ":types",
    "//ash/webui/common/resources:web_ui_listener_behavior",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("log_object") {
  deps = [
    ":cross_device_logs_browser_proxy",
    ":types",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("cross_device_logs_browser_proxy") {
  deps = [
    ":types",
    "//ash/webui/common/resources:cr.m",
    "//ash/webui/common/resources:cr_deprecated",
  ]
}

js_library("nearby_prefs_browser_proxy") {
  deps = [
    ":types",
    "//ash/webui/common/resources:cr_deprecated",
  ]
}

js_library("nearby_http_browser_proxy") {
  deps = [
    ":types",
    "//ash/webui/common/resources:cr.m",
    "//ash/webui/common/resources:cr_deprecated",
  ]
}

js_library("http_tab") {
  deps = [
    ":http_message_object",
    ":nearby_http_browser_proxy",
    ":types",
    "//ash/webui/common/resources:cr.m",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("http_message_object") {
  deps = [
    ":nearby_http_browser_proxy",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("nearby_ui_trigger_browser_proxy") {
  deps = [
    ":types",
    "//ash/webui/common/resources:cr.m",
    "//ash/webui/common/resources:cr_deprecated",
  ]
  externs_list = [ "$externs_path/chrome_send.js" ]
}

js_library("ui_trigger_tab") {
  deps = [
    ":cross_device_internals",
    ":nearby_ui_trigger_browser_proxy",
    ":np_list_object",
    ":types",
    ":ui_trigger_list_object",
    "//ash/webui/common/resources:cr.m",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("ui_trigger_list_object") {
  deps = [
    ":nearby_ui_trigger_browser_proxy",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("np_list_object") {
  deps = [
    ":nearby_presence_browser_proxy",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("types") {
}

js_library("contact_tab") {
  deps = [
    ":contact_object",
    ":nearby_contact_browser_proxy",
    ":types",
    "//ash/webui/common/resources:cr.m",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("contact_object") {
  deps = [
    ":types",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("nearby_contact_browser_proxy") {
  deps = [
    ":types",
    "//ash/webui/common/resources:cr.m",
    "//ash/webui/common/resources:cr_deprecated",
  ]
}

js_library("nearby_presence_browser_proxy") {
  deps = [
    ":types",
    "//ash/webui/common/resources:cr_deprecated",
  ]
}

html_to_wrapper("html_wrapper_files") {
  out_folder = target_gen_dir
  in_files = html_files
  use_js = true
}

css_to_wrapper("css_wrapper_files") {
  out_folder = target_gen_dir
  in_files = css_files
  use_js = true
}
