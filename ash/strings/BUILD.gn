# Copyright 2014 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chrome_build.gni")
import("//build/config/chromeos/ui_mode.gni")
import("//build/config/locales.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/repack.gni")

assert(is_chromeos_ash)

grit("strings") {
  source = "../ash_strings.grd"
  defines = [ "is_chrome_branded=$is_chrome_branded" ]
  outputs = [ "grit/ash_strings.h" ] +
            process_file_template(all_chrome_locales,
                                  [ "ash_strings_{{source_name_part}}.pak" ])
}

# Creates locale-specific pak files with strings needed for ash_unittests, etc.
template("repack_one_locale_ash") {
  locale = invoker.locale
  output = invoker.output

  repack(target_name) {
    # Each input pak file should also have a deps line for completeness.
    sources = [
      "$root_gen_dir/ash/strings/ash_strings_${locale}.pak",
      "$root_gen_dir/chromeos/strings/chromeos_strings_${locale}.pak",
      "$root_gen_dir/components/strings/components_strings_${locale}.pak",
      "$root_gen_dir/device/bluetooth/strings/bluetooth_strings_${locale}.pak",
      "$root_gen_dir/ui/chromeos/strings/ui_chromeos_strings_${locale}.pak",
      "$root_gen_dir/ui/strings/app_locale_settings_${locale}.pak",
      "$root_gen_dir/ui/strings/ax_strings_${locale}.pak",
      "$root_gen_dir/ui/strings/ui_strings_${locale}.pak",
    ]

    deps = [
      "//ash/strings",
      "//chromeos/strings",
      "//components/strings",
      "//device/bluetooth/strings",
      "//ui/chromeos/strings",
      "//ui/strings:app_locale_settings",
      "//ui/strings:ax_strings",
      "//ui/strings:ui_strings",
    ]

    if (is_chrome_branded) {
      sources += [ "$root_gen_dir/chromeos/ash/resources/internal/strings/ash_internal_strings_${locale}.pak" ]
      deps += [ "//chromeos/ash/resources/internal/strings" ]
    }
  }
}

# Creates a pak file containing en-US strings for ash_unittests, etc.
repack_one_locale_ash("ash_test_strings") {
  output = "$root_build_dir/ash_test_strings.pak"
  locale = "en-US"
}
