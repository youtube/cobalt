# Copyright 2018 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//gpu/vulkan/features.gni")

assert(enable_vulkan)

component("init") {
  output_name = "vulkan_init"

  sources = [
    "vulkan_factory.cc",
    "vulkan_factory.h",
  ]

  defines = [ "IS_VULKAN_INIT_IMPL" ]

  public_deps = [
    "//base",
    "//gpu/vulkan",
  ]

  deps = []
  if (is_android) {
    deps += [ "//gpu/vulkan/android" ]
  }
  if (use_ozone) {
    deps += [
      "//ui/base:features",
      "//ui/ozone",
    ]
  }
  if (is_win) {
    deps += [ "//gpu/vulkan/win32" ]
  }
  if (is_apple) {
    deps += [ "//gpu/vulkan/mac" ]
  }
}

component("skia") {
  output_name = "vulkan_init_skia"

  sources = [
    "gr_vk_memory_allocator_impl.cc",
    "gr_vk_memory_allocator_impl.h",
  ]

  defines = [ "IS_VULKAN_INIT_IMPL" ]

  public_deps = [
    "//base",
    "//gpu/vulkan",
    "//skia",
  ]

  deps = [ "//third_party/vulkan_memory_allocator" ]
}
