# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/protobuf/proto_library.gni")

source_set("screen_ai") {
  sources = [
    "screen_ai_ax_tree_serializer.cc",
    "screen_ai_ax_tree_serializer.h",
    "screen_ai_library_wrapper.cc",
    "screen_ai_library_wrapper.h",
    "screen_ai_service_impl.cc",
    "screen_ai_service_impl.h",
  ]

  public_deps = [ "//components/services/screen_ai/proto:proto_convertors" ]

  deps = [
    "//components/services/screen_ai/proto",
    "//components/services/screen_ai/public/cpp:utilities",
    "//components/services/screen_ai/public/mojom",
    "//content/public/browser",
    "//mojo/public/cpp/bindings",
    "//sandbox/policy",
    "//services/metrics/public/cpp:ukm_builders",
    "//skia",
    "//ui/accessibility:accessibility",
  ]
}

if (is_linux || is_chromeos) {
  source_set("screen_ai_sandbox_hook") {
    sources = [
      "sandbox/screen_ai_sandbox_hook_linux.cc",
      "sandbox/screen_ai_sandbox_hook_linux.h",
    ]

    deps = [
      "//base",
      "//components/component_updater",
      "//components/services/screen_ai/public/cpp:utilities",
      "//sandbox/linux:sandbox_services",
      "//ui/accessibility:ax_base",
    ]

    public_deps = [ "//sandbox/policy" ]
  }
}

group("test_support_data") {
  visibility = [ ":test_support" ]
  testonly = true

  data = [ "$root_gen_dir/components/services/screen_ai/proto/view_hierarchy.descriptor" ]

  deps = [
    "//components/services/screen_ai/proto:view_hierarchy_proto_descriptor",
  ]
}

source_set("test_support") {
  testonly = true

  sources = [
    "proto/test_proto_loader.cc",
    "proto/test_proto_loader.h",
  ]

  data_deps = [ ":test_support_data" ]

  deps = [
    "//base",
    "//third_party/protobuf:protobuf_full",
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "public/cpp/screen_ai_install_state_unittest.cc",
    "screen_ai_ax_tree_serializer_unittest.cc",
    "screen_ai_service_impl_unittest.cc",
  ]

  # TODO(https://crbug.com/1278249): Enable after the protobuf issue is fixed.
  if (!is_win) {
    sources += [
      "proto/main_content_extractor_proto_convertor_unittest.cc",
      "proto/visual_annotator_proto_convertor_unittest.cc",
    ]
  }

  data = [ "//components/test/data/screen_ai/" ]
  deps = [
    ":screen_ai",
    "//base/test:test_support",
    "//components/services/screen_ai/proto",
    "//components/services/screen_ai/public/cpp:screen_ai_install_state",
    "//components/ukm:test_support",
    "//testing/gtest",
    "//ui/accessibility:test_support",
  ]

  if (!is_win) {
    deps += [ ":test_support" ]
  }
}
