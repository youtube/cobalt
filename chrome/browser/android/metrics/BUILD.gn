# Copyright 2018 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/rules.gni")

  _jni_sources = [ "//chrome/android/java/src/org/chromium/chrome/browser/metrics/AndroidSessionDurationsServiceState.java" ]

  _test_jni_sources = [ "javatests/src/org/chromium/chrome/browser/metrics/util/UkmUtilsForTest.java" ]

  static_library("ukm_utils_for_test") {
    testonly = true
    sources = [ "ukm_utils_for_test.cc" ]
    deps = [
      "//base",
      "//chrome/browser:browser_process",
      "//chrome/browser/android/metrics:test_jni_headers",
      "//components/metrics_services_manager:metrics_services_manager",
      "//components/ukm",
    ]
  }

  generate_jni("jni_headers") {
    sources = _jni_sources
  }

  generate_jni("test_jni_headers") {
    testonly = true
    sources = _test_jni_sources
  }

  android_library("ukm_java_test_support") {
    testonly = true
    sources = _test_jni_sources
    deps = [ "//base:jni_java" ]
    annotation_processor_deps = [ "//base/android/jni_generator:jni_processor" ]
  }

  android_library("ukm_javatests") {
    testonly = true
    sources =
        [ "javatests/src/org/chromium/chrome/browser/metrics/UkmTest.java" ]

    deps = [
      ":ukm_java_test_support",
      "//base:base_java_test_support",
      "//chrome/android:chrome_java",
      "//chrome/browser/flags:java",
      "//chrome/browser/profiles/android:java",
      "//chrome/browser/signin/services/android:java",
      "//chrome/browser/tab:java",
      "//chrome/test/android:chrome_java_integration_test_support",
      "//components/browsing_data/core:java",
      "//components/metrics:metrics_java",
      "//content/public/test/android:content_java_test_support",
      "//third_party/androidx:androidx_test_monitor_java",
      "//third_party/androidx:androidx_test_runner_java",
      "//third_party/junit",
    ]
  }
}
