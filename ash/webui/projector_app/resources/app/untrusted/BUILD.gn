# Copyright 2021 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/typescript/webui_ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(is_chromeos, "Projector is ChromeOS only")

mojo_files = [
  "ash/webui/projector_app/mojom/untrusted_projector.mojom-webui.ts",
  "ash/webui/projector_app/public/mojom/projector_types.mojom-webui.ts",
]

preprocess_if_expr("copy_mojo_ts") {
  visibility = [ ":build_ts" ]
  in_folder = root_gen_dir
  in_files = mojo_files
  deps = [
    "//ash/webui/projector_app/mojom:projector_mojo_bindings_ts__generator",
    "//ash/webui/projector_app/public/mojom:projector_mojo_bindings_ts__generator",
  ]
  out_folder = "$target_gen_dir/preprocessed"
}

webui_ts_library("build_ts") {
  root_dir = "$target_gen_dir/preprocessed"
  out_dir = "$target_gen_dir/tsc"

  in_files = mojo_files

  deps = [ "//ui/webui/resources/mojo:build_ts" ]
  extra_deps = [ ":copy_mojo_ts" ]
}

generate_grd("mojom_grdp") {
  grd_prefix = "projector_mojo_bindings_webui"
  out_grd = "$target_gen_dir/projector_mojo_bindings_webui_resources.grdp"
  deps = [ ":build_ts" ]
  input_files = [
    "ash/webui/projector_app/mojom/untrusted_projector.mojom-webui.js",
    "ash/webui/projector_app/public/mojom/projector_types.mojom-webui.js",
  ]
  input_files_base_dir = rebase_path("$target_gen_dir/tsc", root_build_dir)
}

generate_grd("build_untrusted_grd") {
  input_files = [
    "index.html",
    "launch.js",
    "sandboxed_load_time_data.js",
    "untrusted_app_comm_factory.js",
    "untrusted_projector_browser_proxy.js",
    "assets/icon_16.png",
    "assets/icon_32.png",
    "assets/icon_48.png",
    "assets/icon_64.png",
    "assets/icon_96.png",
    "assets/icon_128.png",
    "assets/icon_192.png",
    "assets/icon_256.png",
  ]

  input_files_base_dir = rebase_path(".", "//")
  grd_prefix = "ash_projector_app_untrusted"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"

  deps = [ ":mojom_grdp" ]
  grdp_files =
      [ "$target_gen_dir/projector_mojo_bindings_webui_resources.grdp" ]
}
