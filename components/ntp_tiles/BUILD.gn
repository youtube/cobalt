# Copyright 2016 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//components/supervised_user/buildflags.gni")
import("//extensions/buildflags/buildflags.gni")
import("//mojo/public/tools/bindings/mojom.gni")

if (is_android) {
  import("//build/config/android/rules.gni")
}

source_set("pref_names") {
  sources = [ "pref_names.h" ]
  public_deps = [ "//build:branding_buildflags" ]
}

static_library("ntp_tiles") {
  sources = [
    "constants.cc",
    "constants.h",
    "custom_links_manager.h",
    "custom_links_manager_impl.cc",
    "custom_links_manager_impl.h",
    "custom_links_store.cc",
    "custom_links_store.h",
    "custom_links_util.h",
    "enterprise/enterprise_shortcut.cc",
    "enterprise/enterprise_shortcut.h",
    "enterprise/enterprise_shortcuts_manager.h",
    "features.cc",
    "features.h",
    "icon_cacher.h",
    "icon_cacher_impl.cc",
    "icon_cacher_impl.h",
    "metrics.cc",
    "metrics.h",
    "most_visited_sites.cc",
    "most_visited_sites.h",
    "ntp_tile.cc",
    "ntp_tile.h",
    "ntp_tile_impression.cc",
    "ntp_tile_impression.h",
    "popular_sites.h",
    "popular_sites_impl.cc",
    "popular_sites_impl.h",
    "section_type.h",
    "switches.cc",
    "switches.h",
    "tile_source.h",
    "tile_title_source.h",
    "tile_type.h",
    "tile_visual_type.h",
    "webui/ntp_tiles_internals_message_handler.cc",
    "webui/ntp_tiles_internals_message_handler.h",
    "webui/ntp_tiles_internals_message_handler_client.cc",
    "webui/ntp_tiles_internals_message_handler_client.h",
  ]

  public_deps = [
    "//base",
    "//components/history/core/browser",
    "//components/ntp_tiles/webui/resources",
  ]
  deps = [
    ":mojo_bindings",
    ":pref_names",
    "//build:branding_buildflags",
    "//components/favicon/core",
    "//components/favicon_base",
    "//components/google/core/common",
    "//components/image_fetcher/core",
    "//components/pref_registry",
    "//components/prefs",
    "//components/resources",
    "//components/search",
    "//components/search_engines",
    "//components/strings",
    "//components/supervised_user/core/common:buildflags",
    "//components/url_formatter",
    "//components/variations",
    "//components/variations/service",
    "//components/webapps/common",
    "//extensions/buildflags",
    "//services/data_decoder/public/cpp",
    "//services/network/public/cpp",
    "//third_party/re2",
    "//ui/base",
  ]

  if (is_ios) {
    sources += [
      "country_code_ios.h",
      "country_code_ios.mm",
    ]
  }

  if (enable_extensions) {
    deps += [ "//extensions/common:common_constants" ]
  }

  if (enable_supervised_users) {
    deps += [ "//components/supervised_user/core/browser" ]
  }

  if (is_linux || is_win || is_mac || is_chromeos) {
    sources += [
      "enterprise/enterprise_shortcuts_manager_impl.cc",
      "enterprise/enterprise_shortcuts_manager_impl.h",
      "enterprise/enterprise_shortcuts_store.cc",
      "enterprise/enterprise_shortcuts_store.h",
      "enterprise/ntp_shortcuts_policy_handler.cc",
      "enterprise/ntp_shortcuts_policy_handler.h",
    ]
    deps += [
      "//components/policy/core/browser",
      "//components/policy/core/common",
    ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "custom_links_manager_impl_unittest.cc",
    "custom_links_store_unittest.cc",
    "icon_cacher_impl_unittest.cc",
    "metrics_unittest.cc",
    "most_visited_sites_unittest.cc",
    "popular_sites_impl_unittest.cc",
  ]

  deps = [
    ":ntp_tiles",
    ":pref_names",
    "//base/test:test_support",
    "//build:branding_buildflags",
    "//components/favicon/core",
    "//components/favicon/core:history_implementation",
    "//components/favicon_base",
    "//components/history/core/test",
    "//components/image_fetcher/core",
    "//components/image_fetcher/core:test_support",
    "//components/pref_registry",
    "//components/prefs",
    "//components/supervised_user/core/common:buildflags",
    "//components/sync_preferences:test_support",
    "//components/webapps/common",
    "//extensions/buildflags",
    "//net:test_support",
    "//services/data_decoder/public/cpp:test_support",
    "//services/network:test_support",
    "//services/network/public/cpp",
    "//testing/gmock",
    "//testing/gtest",
    "//ui/base",
    "//ui/base:test_support",
    "//ui/gfx:test_support",
  ]

  if (enable_supervised_users) {
    deps += [ "//components/supervised_user/core/browser" ]
  }

  if (is_linux || is_win || is_mac || is_chromeos) {
    sources += [
      "enterprise/enterprise_shortcuts_manager_impl_unittest.cc",
      "enterprise/enterprise_shortcuts_store_unittest.cc",
      "enterprise/ntp_shortcuts_policy_handler_unittest.cc",
    ]
    deps += [ "//components/policy/core/browser:test_support" ]

    # TODO(crbug.com/40031409): Fix code that adds exit-time destructors and
    # enable the diagnostic by removing this line.
    configs += [ "//build/config/compiler:no_exit_time_destructors" ]
  }
}

if (is_android) {
  java_cpp_enum("ntp_tiles_enums_java") {
    sources = [
      "section_type.h",
      "tile_title_source.h",
      "tile_visual_type.h",
    ]
  }
}

mojom("mojo_bindings") {
  if (is_android) {
    generate_java = true
  }
  sources = [
    "tile_source.mojom",
    "tile_type.mojom",
  ]
  webui_module_path = "chrome://resources/mojo/components/ntp_tiles/"
}
