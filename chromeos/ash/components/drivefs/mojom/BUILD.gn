# Copyright 2018 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//mojo/public/tools/bindings/mojom.gni")

assert(is_chromeos_ash)

mojom_component("mojom") {
  sources = [
    "drivefs.mojom",
    "fake_drivefs_launcher.mojom",
    "notifications.mojom",
  ]

  public_deps = [
    "//chromeos/components/drivefs/mojom",
    "//mojo/public/mojom/base",
  ]

  output_prefix = "drivefs_mojom"
  macro_prefix = "DRIVEFS_MOJOM"

  cpp_typemaps = [
    {
      types = [
        {
          mojom = "drivefs.mojom.FileError"
          cpp = "::drive::FileError"
        },
      ]
      traits_headers =
          [ "//chromeos/ash/components/drivefs/drivefs_mojom_traits.h" ]
      traits_sources =
          [ "//chromeos/ash/components/drivefs/drivefs_mojom_traits.cc" ]
      traits_public_deps = [
        "//base",
        "//components/drive",
      ]
    },
  ]
}

mojom_component("pinning_manager_types") {
  sources = [ "pinning_manager_types.mojom" ]

  output_prefix = "pinning_manager_types_mojom"
  macro_prefix = "PINNING_MANAGER_TYPES_MOJOM"

  webui_module_path = "/"
}
