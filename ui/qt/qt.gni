# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromecast_build.gni")
import("//build/config/sanitizers/sanitizers.gni")
import("//build/config/sysroot.gni")

declare_args() {
  # TODO(https://crbug.com/1424435): Allow QT in MSAN builds once QT is
  # added to the instrumented libraries.
  use_qt = is_linux && !is_castos && !is_msan
}

declare_args() {
  if (!use_sysroot && use_qt) {
    moc_qt5_path = ""
  }
}

declare_args() {
  use_qt6 = use_qt && use_sysroot
}

declare_args() {
  if (!use_sysroot && use_qt6) {
    moc_qt6_path = ""
  }
}

# use_qt6 => use_qt
assert(!use_qt6 || use_qt)
