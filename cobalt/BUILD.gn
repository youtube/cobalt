# Copyright 2024 The Cobalt Authors. All Rights Reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/linux/strip_binary.gni")
import("//build/toolchain/toolchain.gni")
import("//cobalt/build/modular_executable.gni")
import("//testing/test.gni")
import("//ui/base/ui_features.gni")

if (!toolchain_for_rust_host_build_tools) {
  assert(!cobalt_is_release_build || is_official_build,
         "Release builds are only available in official builds.")
}

if (is_ios) {
  import("//build/config/apple/mobile_config.gni")
}

# This arg temporarily enables or disables the updater_sandbox target.
# It's disabled on CI by default due to missing libcurl pkg. To enable it
# locally, set it to true before building the target.
declare_args() {
  build_updater_sandbox = false

  if (is_ios && target_platform == "tvos") {
    # tvOS: Default app bundle identifier.
    cobalt_bundle_identifier = "dev.cobalt.tvos"

    # tvOS: Default Info.plist path for the bundle.
    cobalt_info_plist = "//cobalt/shell/app/tvos-Info.plist"
  }
}

group("gn_all") {
  testonly = true

  # TODO(b/371589344): Fix android build configs.
  deps = [ "//starboard:starboard_group" ]
  if (!cobalt_is_release_build) {
    deps += [ "//starboard/nplb" ]
  }
  if (is_android) {
    # TODO(cobalt): reuse build rules below to define Android target.
    deps += [ "android:cobalt_apk" ]
  } else if (is_ios && target_platform == "tvos") {
    deps += [ ":cobalt" ]
  } else if (!is_cobalt_hermetic_build) {
    deps += [ ":strip_cobalt_binary" ]
  }
  if (is_cobalt_hermetic_build) {
    deps += [
      ":cobalt_loader",
      "//starboard/elf_loader:elf_loader_sandbox($starboard_toolchain)",
      "//starboard/elf_loader:elf_loader_sys_sandbox($starboard_toolchain)",
      "//starboard/loader_app:loader_app($starboard_toolchain)",
    ]
  }
  if (is_starboard && build_updater_sandbox) {
    deps += [ "//chrome/updater/cobalt:updater_sandbox" ]
  }
  if (use_crashpad) {
    deps += [
      "//third_party/crashpad/crashpad/handler:crashpad_handler($native_target)",
      "//third_party/crashpad/crashpad/tools:crashpad_database_util($native_target)",
    ]
  }
}

group("cobalt_browsertests") {
  testonly = true

  deps = [ "testing:cobalt_browsertests" ]
}

if (is_ios && target_platform == "tvos") {
  ios_app_bundle("cobalt") {
    testonly = true

    sources = [ "app/tvos/main.mm" ]

    deps = [
      ":common",
      "//base",
      "//cobalt/browser/tvos/intents:yt_intents",
      "//cobalt/shell:cobalt_shell_app",
      "//cobalt/shell:cobalt_shell_lib",
      "//content/public/app",
      "//starboard:starboard_group",
    ]
    bundle_deps = [
      ":cobalt_shell_framework_resources",
      "shell/app/ios/resources",
    ]

    intents_target = "//cobalt/browser/tvos/intents:yt_intents"

    data_deps = [ "//tools/v8_context_snapshot:v8_context_snapshot" ]

    bundle_identifier = cobalt_bundle_identifier
    entitlements_path = "//cobalt/shell/app/tvos-entitlements.plist"
    info_plist = cobalt_info_plist
  }

  bundle_data("cobalt_shell_framework_resources") {
    testonly = true
    sources = [ "$root_out_dir/cobalt_shell.pak" ]

    public_deps = [ "//cobalt/shell:pak" ]

    outputs = [ "{{bundle_resources_dir}}/{{source_file_part}}" ]
  }
} else if (!is_android) {
  if (is_cobalt_hermetic_build) {
    cobalt_target_type = "modular_executable"
  } else {
    cobalt_target_type = "executable"
  }
  target(cobalt_target_type, "cobalt") {
    testonly = true

    sources = [ "app/cobalt.cc" ]

    configs += [ "//build/config/gcc:rpath_for_built_shared_libraries" ]

    deps = [
      ":common",
      "//base",
      "//cobalt/browser/h5vcc_accessibility:manager",
      "//cobalt/browser/h5vcc_runtime:deep_link_manager",
      "//cobalt/shell:cobalt_shell_app",
      "//cobalt/shell:cobalt_shell_lib",
      "//content/public/app",
      "//services/device/time_zone_monitor",
      "//starboard:starboard_headers_only",
    ]

    if (is_cobalt_hermetic_build) {
      # Needed for third_party/musl/src/starboard/internal/hwcap_impl.h
      deps += [ "//third_party/musl:headers" ]
    }
    if (is_starboard) {
      deps += [ "//ui/ozone/platform/starboard:starboard" ]
    } else {
      # Web test support only built for the linux-x64x11-no-starboard target.
      deps += [
        "//content/web_test:web_test_browser",
        "//content/web_test:web_test_common",
        "//content/web_test:web_test_renderer",
      ]
    }

    data_deps = [
      "//cobalt/shell:pak",
      "//tools/v8_context_snapshot:v8_context_snapshot",
    ]
  }

  if (!is_cobalt_hermetic_build) {
    strip_binary("strip_cobalt_binary") {
      binary_input = "$root_out_dir/cobalt"
      deps = [ ":cobalt" ]
      testonly = true
    }
  }
}

source_set("common") {
  testonly = true

  sources = [
    "app/cobalt_main_delegate.cc",
    "app/cobalt_main_delegate.h",
    "app/cobalt_switch_defaults.cc",
    "app/cobalt_switch_defaults.h",
  ]

  if (is_starboard) {
    sources += [ "app/cobalt_switch_defaults_starboard.cc" ]
  } else if (is_ios && target_platform == "tvos") {
    sources += [ "app/cobalt_switch_defaults_tvos.cc" ]
  }

  if (use_evergreen) {
    sources += [
      "browser/loader_app_metrics.cc",
      "browser/loader_app_metrics.h",
    ]
  }

  public_deps = [ "//cobalt/browser" ]

  deps = [
    "//base",
    "//cobalt/browser:switches",
    "//cobalt/gpu",
    "//cobalt/renderer",
    "//cobalt/shell:cobalt_shell_app",
    "//cobalt/shell:cobalt_shell_lib",
    "//cobalt/shell:pak",
    "//cobalt/utility",
    "//components/memory_system:memory_system",
    "//content/public/app",
    "//gpu/command_buffer/service",
    "//media",
    "//sandbox/policy",
    "//starboard:starboard_headers_only",
    "//third_party/blink/public/common",
    "//ui/gl",
  ]

  if (use_ozone) {
    deps += [ "//ui/ozone:ozone_switches" ]
  }

  if (is_starboard) {
    deps += [ "//ui/ozone/platform/starboard:starboard" ]
  }
}

action("cobalt_build_info") {
  script = "build/build_info.py"
  outputs = [
    "$root_gen_dir/cobalt/cobalt_build_id.h",
    "$root_gen_dir/build_info.json",
    "$root_gen_dir/licenses_cobalt.txt",
    "$root_gen_dir/licenses_cobalt.spdx",
  ]
  args = [
    rebase_path(outputs[0], root_build_dir),
    rebase_path(outputs[1], root_build_dir),
    rebase_path(outputs[2], root_build_dir),
    rebase_path(outputs[3], root_build_dir),
  ]
}

test("cobalt_unittests") {
  testonly = true

  sources = [
    "//cobalt/browser/client_hint_headers/cobalt_header_value_provider_test.cc",
    "//cobalt/browser/client_hint_headers/cobalt_trusted_header_client_test.cc",
    "//cobalt/browser/client_hint_headers/cobalt_trusted_url_loader_header_client_test.cc",
    "//cobalt/browser/cobalt_secure_navigation_throttle_unittest.cc",
    "//cobalt/browser/cobalt_web_contents_observer_unittest.cc",
    "//cobalt/browser/command_line_logger_unittest.cc",
    "//cobalt/browser/experiments/experiment_config_manager_unittest.cc",
    "//cobalt/browser/global_features_unittest.cc",
    "//cobalt/browser/h5vcc_runtime/deep_link_manager_unittest.cc",
    "//cobalt/browser/metrics/cobalt_enabled_state_provider_test.cc",
    "//cobalt/browser/metrics/cobalt_metrics_log_uploader_test.cc",
    "//cobalt/browser/metrics/cobalt_metrics_service_client_test.cc",
    "//cobalt/browser/metrics/cobalt_metrics_services_manager_client_test.cc",
    "//cobalt/browser/user_agent/user_agent_platform_info_test.cc",
    "//cobalt/shell/browser/h5vcc_scheme_url_loader_factory_unittest.cc",
  ]

  if (is_starboard) {
    sources += [
      "//cobalt/app/cobalt_switch_defaults_starboard_test.cc",
      "//cobalt/shell/common/device_authentication_test.cc",
    ]
  }

  if (!is_android) {
    sources += [ "//cobalt/shell/browser/splash_screen_unittest.cc" ]
  }

  public_deps = [ "//third_party/zlib/google:compression_utils" ]

  deps = [
    ":common",
    "//base/test:run_all_unittests",
    "//base/test:test_support",
    "//cobalt/browser:command_line_logger",
    "//cobalt/browser:global_features",
    "//cobalt/browser:metrics",
    "//cobalt/browser/client_hint_headers",
    "//cobalt/browser/constants:cobalt_experiment_names",
    "//cobalt/browser/h5vcc_metrics/public/mojom",
    "//cobalt/browser/h5vcc_runtime:deep_link_manager",
    "//cobalt/browser/user_agent",
    "//cobalt/common/libc:libc_test",
    "//cobalt/shell/browser/migrate_storage_record:unittest",
    "//components/metrics",
    "//components/prefs",
    "//components/prefs:test_support",
    "//components/variations",
    "//components/variations/service",
    "//mojo/core/embedder",
    "//net/traffic_annotation:test_support",
    "//services/network/public/cpp",
    "//testing/gmock",
    "//testing/gtest",
    "//third_party/metrics_proto",
  ]

  # All configs that provide definitions for switch constants.
  deps += [
    "//cobalt/browser:switches",

    # TODO(cobalt b/375241103) This dependency is large in scope. It would be
    # better to find more well-scoped targets to depend on to provide the
    # switch constants definitions.
    "//cobalt/shell:cobalt_shell_lib",
    "//content/browser:for_content_tests",
    "//content/public/common:switches",
    "//content/test:test_support",
    "//gpu/config",
    "//sandbox/policy:policy",
    "//ui/events/devices",
  ]

  if (is_android) {
    deps += [
      "//base:base_java_test_support",
      "//content/public/android:content_java",
      "//ui/android:ui_java",
    ]
  }

  if (use_aura) {
    deps += [ "//ui/aura" ]
  }

  if (use_ozone) {
    deps += [ "//ui/ozone:ozone_switches" ]
  }
}
