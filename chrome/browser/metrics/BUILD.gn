# Copyright 2017 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//components/metrics/generate_expired_histograms_array.gni")
import("//components/metrics/generate_histograms_variants_allowlist.gni")

source_set("test_support") {
  testonly = true
  sources = [
    "testing/metrics_reporting_pref_helper.cc",
    "testing/metrics_reporting_pref_helper.h",
    "testing/sync_metrics_test_utils.cc",
    "testing/sync_metrics_test_utils.h",
  ]

  deps = [
    "//base",
    "//build:chromeos_buildflags",
    "//chrome/browser",
    "//chrome/test:sync_integration_test_support",
    "//components/signin/public/identity_manager",
    "//components/sync:test_support",
    "//third_party/metrics_proto",
  ]

  if (is_android) {
    deps += [ "//content/public/browser" ]
  }
}

generate_expired_histograms_array("expired_histograms_array") {
  namespace = "chrome_metrics"
  header_filename = "expired_histograms_array.h"
  major_branch_date_filepath = "//chrome/MAJOR_BRANCH_DATE"
  milestone_filepath = "//chrome/VERSION"
}

generate_histograms_variants_allowlist("bubble_histograms_variant") {
  namespace = "chrome_metrics"
  input_xml_file = "//tools/metrics/histograms/metadata/views/histograms.xml"
  header_filename = "bubble_histograms_variant.h"
  source_filename = "bubble_histograms_variant.cc"
  allow_list_name = "BubbleName"
}
