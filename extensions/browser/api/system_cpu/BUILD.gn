# Copyright 2016 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//extensions/buildflags/buildflags.gni")

assert(enable_extensions_core,
       "Cannot depend on extensions because enable_extensions_core=false.")
assert(!is_fuchsia, "Fuchsia does not support extensions.")

source_set("system_cpu") {
  sources = [
    "cpu_info_provider.cc",
    "cpu_info_provider.h",
    "system_cpu_api.cc",
    "system_cpu_api.h",
  ]

  configs += [ "//build/config/compiler:wexit_time_destructors" ]

  public_deps = [
    "//base",
    "//extensions/browser:browser_sources",
    "//extensions/browser/api/system_info",
    "//extensions/common/api",
  ]

  if (is_chromeos) {
    deps = [ "//chromeos/system" ]
  }

  if (is_mac) {
    sources += [ "cpu_info_provider_mac.cc" ]
  }

  if (is_linux || is_chromeos || enable_desktop_android_extensions) {
    sources += [ "cpu_info_provider_linux.cc" ]
  }

  if (is_win) {
    sources += [ "cpu_info_provider_win.cc" ]
  }
}
