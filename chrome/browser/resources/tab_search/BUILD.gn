# Copyright 2020 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//ui/webui/resources/tools/build_webui.gni")
import("//ui/webui/resources/tools/generate_grd.gni")
import("//ui/webui/webui_features.gni")

assert(is_linux || is_chromeos || is_win || is_mac || is_fuchsia)

preprocess_folder = "preprocessed"

build_webui("build") {
  grd_prefix = "tab_search"

  static_files = [
    "alert_indicators/tab_audio_muting_rounded.svg",
    "alert_indicators/tab_audio_rounded.svg",
    "alert_indicators/tab_media_recording.svg",
    "tab_search.html",
  ]

  web_component_files = [
    "app.ts",
    "infinite_list.ts",
    "tab_organization_failure.ts",
    "tab_organization_in_progress.ts",
    "tab_organization_not_started.ts",
    "tab_organization_page.ts",
    "tab_organization_results.ts",
    "tab_search_group_item.ts",
    "tab_search_item.ts",
    "tab_search_page.ts",
  ]

  non_web_component_files = [
    "bimap.ts",
    "fuzzy_search.ts",
    "tab_data.ts",
    "tab_group_color_helper.ts",
    "tab_search_api_proxy.ts",
    "tab_search_sync_browser_proxy.ts",
    "tab_search.ts",
    "tab_search_utils.ts",
    "title_item.ts",
  ]

  css_files = [
    "tab_group_shared_vars.css",
    "tab_organization_shared_style.css",
  ]

  mojo_files_deps = [
    "//chrome/browser/ui/webui/tab_search:mojo_bindings_ts__generator",
    "//chrome/browser/ui/webui/tabs:mojo_bindings_ts__generator",
    "//components/tab_groups/public/mojom:mojo_bindings_ts__generator",
  ]
  mojo_files = [
    "$root_gen_dir/chrome/browser/ui/webui/tab_search/tab_search.mojom-webui.ts",
    "$root_gen_dir/chrome/browser/ui/webui/tabs/tabs.mojom-webui.ts",
    "$root_gen_dir/components/tab_groups/public/mojom/tab_group_types.mojom-webui.ts",
  ]

  ts_composite = true
  ts_definitions = [
    "//tools/typescript/definitions/metrics_private.d.ts",
    "$target_gen_dir/$preprocess_folder/fuse.d.ts",
  ]
  ts_deps = [
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources/cr_components/color_change_listener:build_ts",
    "//ui/webui/resources/cr_elements:build_ts",
    "//ui/webui/resources/js:build_ts",
    "//ui/webui/resources/mojo:build_ts",
  ]
  ts_extra_deps = [
    ":copy_fuse",
    ":copy_fuse_dts",
    ":copy_fuse_dts_tsc",
  ]

  extra_grdp_deps = [ ":build_fuse_grdp" ]
  extra_grdp_files = [ "$target_gen_dir/fuse_resources.grdp" ]

  optimize = optimize_webui
  if (optimize) {
    optimize_webui_host = "tab-search.top-chrome"
    optimize_webui_in_files = [ "tab_search.js" ]
    optimize_webui_excludes = [
      "chrome://resources/js/metrics_reporter/metrics_reporter.js",
      "chrome://resources/mojo/mojo/public/mojom/base/time.mojom-webui.js",
      "chrome://resources/mojo/mojo/public/mojom/base/token.mojom-webui.js",
      "chrome://resources/mojo/url/mojom/url.mojom-webui.js",
      "fuse.js",
    ]
  }
}

generate_grd("build_fuse_grdp") {
  grd_prefix = "tab_search"
  out_grd = "$target_gen_dir/fuse_resources.grdp"
  input_files = [ "fuse.js" ]
  input_files_base_dir =
      rebase_path("$target_gen_dir/$preprocess_folder", root_build_dir)
  deps = [ ":copy_fuse" ]
}

copy("copy_fuse") {
  sources = [ "//third_party/fusejs/dist/fuse.basic.esm.min.js" ]
  outputs = [ "$target_gen_dir/$preprocess_folder/fuse.js" ]
}

copy("copy_fuse_dts") {
  sources = [ "//third_party/fusejs/dist/fuse.d.ts" ]
  outputs = [ "$target_gen_dir/$preprocess_folder/{{source_file_part}}" ]
}

# Work-around for https://github.com/microsoft/TypeScript/issues/47035, so
# that the manually provided fuse.d.ts file prevails.
copy("copy_fuse_dts_tsc") {
  sources = [ "//third_party/fusejs/dist/fuse.d.ts" ]
  outputs = [ "$target_gen_dir/tsc/{{source_file_part}}" ]
}
