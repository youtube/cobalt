# Copyright 2024 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//testing/test.gni")
import("//third_party/protobuf/proto_library.gni")

source_set("unit_tests") {
  testonly = true
  deps = [
    "//components/ip_protection/common:ip_protection_config_http_unittest",
    "//components/ip_protection/common:ip_protection_core_impl_unittest",
    "//components/ip_protection/common:ip_protection_data_types_unittest",
    "//components/ip_protection/common:ip_protection_proxy_config_direct_fetcher_unittest",
    "//components/ip_protection/common:ip_protection_proxy_config_manager_impl_unittest",
    "//components/ip_protection/common:ip_protection_proxy_delegate_unittest",
    "//components/ip_protection/common:ip_protection_token_manager_impl_unittest",
    "//components/ip_protection/common:masked_domain_list_manager_unittest",
    "//components/ip_protection/common:mock_blind_sign_auth",
    "//components/ip_protection/common:url_matcher_with_bypass_unittest",

    # Select UMA as the implementation of `ip_protection::Telemetry()`.
    "//components/ip_protection/common:ip_protection_telemetry_uma",
  ]

  if (is_android) {
    deps += [ "//components/ip_protection/android:blind_sign_message_android_impl_unittest" ]
  }
}

# A unit test binary that only runs tests for this component. This is useful
# during development, but these tests are typically run as part of
# `//components:components_unittests`.
test("components_ip_protection_unittests") {
  sources = [ "test/run_all_unittests.cc" ]
  deps = [
    "//base",
    "//base/test:test_support",
    "//components/ip_protection:unit_tests",
    "//mojo/core/embedder",
    "//testing/gtest",
  ]
}

proto_library("get_proxy_config_proto") {
  sources = [ "get_proxy_config.proto" ]
}

if (is_android) {
  import("//build/config/android/rules.gni")

  proto_java_library("get_proxy_config_proto_java") {
    sources = [ "get_proxy_config.proto" ]
    proto_path = "./"
  }
}
