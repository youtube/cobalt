# Copyright 2016 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//media/media_options.gni")
import("//testing/test.gni")
import("//third_party/protobuf/proto_library.gni")

source_set("remoting_sender") {
  sources = [
    "courier_renderer_factory.cc",
    "courier_renderer_factory.h",
    "metrics.cc",
    "metrics.h",
    "renderer_controller.cc",
    "renderer_controller.h",
  ]

  deps = [
    "//base",
    "//media",
    "//media/mojo/common",
    "//media/mojo/mojom:remoting",
    "//mojo/public/cpp/bindings",
    "//ui/gfx",
    "//url",
  ]

  public_deps = []

  if (enable_media_remoting_rpc) {
    sources += [
      "courier_renderer.cc",
      "courier_renderer.h",
      "demuxer_stream_adapter.cc",
      "demuxer_stream_adapter.h",
      "triggers.h",
    ]

    # Consumers of the CourierRenderer implicitly take a dependency on the
    # generated remoting.pb.h file.
    public_deps += [ "//media/cast/openscreen:remoting_utils" ]
  }
}

source_set("remoting_device_capability") {
  sources = [
    "device_capability_checker.cc",
    "device_capability_checker.h",
  ]

  deps = [
    "//base",
    "//media",
  ]
}

source_set("remoting_renderer") {
  sources = [
    "receiver.cc",
    "receiver.h",
    "receiver_controller.cc",
    "receiver_controller.h",
    "remoting_renderer_factory.cc",
    "remoting_renderer_factory.h",
    "stream_provider.cc",
    "stream_provider.h",
  ]

  deps = [
    "//media/mojo/common:common",
    "//media/mojo/mojom:remoting",
    "//third_party/openscreen/src/cast/streaming:common",
  ]

  # Consumers of the ReceiverController implicitly take a dependency on the
  # generated remoting.pb.h file.
  public_deps = [ "//media/cast/openscreen:remoting_utils" ]
}

source_set("media_remoting_tests") {
  testonly = true
  sources = [
    "device_capability_checker_unittest.cc",
    "fake_remoter.cc",
    "fake_remoter.h",
    "mock_receiver_controller.cc",
    "mock_receiver_controller.h",
    "receiver_unittest.cc",
    "renderer_controller_unittest.cc",
    "stream_provider_unittest.cc",
    "test_utils.cc",
    "test_utils.h",
  ]

  deps = [
    ":remoting_device_capability",
    ":remoting_renderer",
    ":remoting_sender",
    "//base",
    "//base/test:test_support",
    "//media:test_support",
    "//media/cast/openscreen:decoder_buffer_reader",
    "//media/cast/openscreen:remoting_utils",
    "//media/mojo/common",
    "//media/mojo/mojom:remoting",
    "//testing/gmock",
    "//testing/gtest",
    "//url",
  ]

  if (enable_media_remoting_rpc) {
    sources += [
      "courier_renderer_unittest.cc",
      "demuxer_stream_adapter_unittest.cc",
      "end2end_test_renderer.cc",
      "end2end_test_renderer.h",
      "fake_media_resource.cc",
      "fake_media_resource.h",
      "integration_test.cc",
      "metrics_unittest.cc",
    ]

    deps += [
      "//media/test:pipeline_integration_test_base",
      "//services/service_manager/public/cpp:cpp",
      "//ui/gfx:test_support",
      "//ui/gfx/geometry",
    ]
  }
}

test("media_remoting_unittests") {
  data = [ "//media/test/data/" ]
  deps = [
    ":media_remoting_tests",
    "//media/test:run_all_unittests",
  ]
}
