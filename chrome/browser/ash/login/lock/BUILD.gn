# Copyright 2024 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/chromeos/crosier_tests.gni")
import("//build/config/chromeos/ui_mode.gni")

assert(is_chromeos_ash)

static_library("lock") {
  sources = [
    "screen_locker.cc",
    "screen_locker.h",
    "views_screen_locker.cc",
    "views_screen_locker.h",
  ]

  public_deps = [
    "//ash/public/cpp",
    "//base",
    "//chrome/browser:browser_public_dependencies",
    "//chrome/browser/ash/lock_screen_apps",
    "//chromeos/ash/components/login/auth",
    "//chromeos/ash/components/login/auth/public:authpublic",
    "//chromeos/ash/components/login/auth/public:challenge_response_key",
    "//chromeos/dbus/power",
    "//components/account_id",
    "//components/session_manager:base",
    "//components/user_manager",
    "//mojo/public/cpp/bindings",
    "//services/device/public/mojom",
    "//ui/base",
    "//ui/base/ime/ash",
  ]

  deps = [
    "//ash/constants",
    "//base:i18n",
    "//chrome/app:generated_resources",
    "//chrome/browser:browser_process",
    "//chrome/browser:resources",
    "//chrome/browser/ash/child_accounts",
    "//chrome/browser/ash/extensions/autotest_private",
    "//chrome/browser/ash/extensions/file_manager",
    "//chrome/browser/ash/input_method",
    "//chrome/browser/ash/login",
    "//chrome/browser/ash/login/quick_unlock",
    "//chrome/browser/ash/login/session",
    "//chrome/browser/ash/profiles",
    "//chrome/browser/ash/system",
    "//chrome/browser/extensions",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/ui/ash/login",
    "//chrome/common:constants",
    "//chrome/common:non_code_constants",
    "//chromeos/ash/components/browser_context_helper",
    "//chromeos/ash/components/dbus/biod:biod_proto",
    "//chromeos/ash/components/dbus/session_manager",
    "//chromeos/ash/components/login/session",
    "//components/password_manager/core/browser:hash_password_manager",
    "//components/prefs",
    "//components/session_manager/core",
    "//components/user_manager:common",
    "//content/public/browser",
    "//google_apis",
    "//services/device/public/mojom:mojom_shared",
    "//services/device/public/mojom:mojom_shared_cpp_sources",
    "//ui/display",
    "//ui/gfx",
    "//url",
  ]

  allow_circular_includes_from = [
    "//chrome/browser/ash/child_accounts",
    "//chrome/browser/ash/extensions/autotest_private",
    "//chrome/browser/ash/extensions/file_manager",
    "//chrome/browser/ash/input_method",
    "//chrome/browser/ash/login",
    "//chrome/browser/ash/login/session",
    "//chrome/browser/extensions",
    "//chrome/browser/ui/ash/login",
  ]
}

static_library("test_support") {
  testonly = true

  sources = [
    "screen_locker_tester.cc",
    "screen_locker_tester.h",
  ]

  public_deps = [
    "//base",
    "//components/session_manager/core",
  ]

  deps = [
    ":lock",
    "//ash/public/cpp",
    "//chromeos/ash/components/login/auth",
    "//chromeos/ash/components/login/auth/public:authpublic",
    "//components/session_manager:base",
    "//content/public/browser",
    "//content/test:test_support",
    "//testing/gtest",
  ]
}

if (is_chromeos_device) {
  crosier_tests("integration_tests") {
    sources = [ "lock_screen_integration_test.cc" ]

    sources_metadata = [ "lock_screen_integration_test.yaml" ]

    deps = [
      "//ash",
      "//ash:test_support",
      "//ash/constants",
      "//chrome/browser",
      "//chrome/browser/ash/login/lock:test_support",
      "//chrome/browser/profiles:profile",
      "//chrome/test:ash_integration_test_support",
      "//chrome/test:test_support",
      "//chromeos/ash/components/dbus/session_manager",
      "//components/user_manager",
    ]
  }
}

source_set("browser_tests") {
  testonly = true

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

  sources = [
    "fingerprint_unlock_browsertest.cc",
    "lock_screen_browsertest.cc",
    "screen_locker_browsertest.cc",
  ]

  deps = [
    ":lock",
    ":test_support",
    "//ash",
    "//ash/constants",
    "//ash/public/cpp",
    "//base",
    "//base/test:test_support",
    "//chrome/browser",
    "//chrome/browser/ash/login:test_support",
    "//chrome/browser/ash/login/quick_unlock",
    "//chrome/browser/ash/login/test:test_support",
    "//chrome/browser/ash/policy/core:test_support",
    "//chrome/browser/ui",
    "//chrome/browser/ui/ash/login",
    "//chrome/test:test_support_ui",
    "//chromeos/ash/components/dbus/biod",
    "//chromeos/ash/components/dbus/session_manager",
    "//chromeos/ash/components/settings",
    "//components/prefs",
    "//components/session_manager/core",
    "//components/user_manager",
    "//content/test:test_support",
    "//testing/gmock",
    "//testing/gtest",
    "//ui/base/ime/ash",
    "//ui/compositor",
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [ "screen_locker_unittest.cc" ]

  deps = [
    ":lock",
    "//ash:test_support",
    "//ash/public/cpp",
    "//base",
    "//chrome/browser",
    "//chrome/browser/ash/lock_screen_apps",
    "//chrome/browser/ash/login/users:test_support",
    "//chrome/browser/ash/settings:test_support",
    "//chrome/browser/ui/ash/assistant",
    "//chrome/browser/ui/ash/login",
    "//chrome/browser/ui/ash/session",
    "//chrome/browser/ui/ash/session:test_support",
    "//chrome/common:non_code_constants",
    "//chrome/test:test_support",
    "//chromeos/ash/components/cryptohome",
    "//chromeos/ash/components/dbus/biod",
    "//chromeos/ash/components/dbus/concierge",
    "//chromeos/ash/components/dbus/userdataauth",
    "//chromeos/ash/components/install_attributes:test_support",
    "//chromeos/ash/components/login/login_state",
    "//chromeos/ash/components/login/session",
    "//chromeos/ash/components/system",
    "//chromeos/dbus/tpm_manager",
    "//components/account_id",
    "//components/keyed_service/core",
    "//components/session_manager/core",
    "//components/user_manager",
    "//content/test:test_support",
    "//device/bluetooth",
    "//testing/gtest",
    "//ui/base:test_support",

    # Tests from subdirectories:
    "online_reauth:unit_tests",
  ]
}
