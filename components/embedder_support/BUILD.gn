# Copyright 2019 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//device/vr/buildflags/buildflags.gni")

static_library("embedder_support") {
  sources = [
    "pref_names.h",
    "switches.cc",
    "switches.h",
  ]
}

static_library("metrics") {
  sources = [
    "metrics/process_memory_metrics_emitter.cc",
    "metrics/process_memory_metrics_emitter.h",
    "metrics/tab_footprint_aggregator.cc",
    "metrics/tab_footprint_aggregator.h",
  ]

  deps = [
    ":embedder_support",
    "//base",
    "//components/metrics",
    "//components/performance_manager",
    "//components/services/paint_preview_compositor/public/mojom",
    "//content/public/browser",
    "//content/public/common",
    "//media/mojo/mojom",
    "//services/metrics/public/cpp:ukm_builders",
    "//services/network/public/mojom",
    "//services/resource_coordinator/public/cpp/memory_instrumentation",
    "//url",
  ]
}

static_library("content_settings") {
  sources = [
    "content_settings_utils.cc",
    "content_settings_utils.h",
  ]

  deps = [
    "//components/content_settings/browser",
    "//components/content_settings/core/browser",
    "//components/content_settings/core/browser:cookie_settings",
    "//content/public/browser",
    "//net",
    "//third_party/blink/public/mojom:mojom_platform_headers",
    "//url",
  ]
}

static_library("permissions") {
  sources = [
    "permission_context_utils.cc",
    "permission_context_utils.h",
  ]

  deps = [
    "//components/background_sync",
    "//components/permissions",
    "//device/vr/buildflags",
    "//services/device/public/cpp:device_features",
    "//services/device/public/cpp/geolocation:buildflags",
  ]

  if (enable_vr) {
    deps += [ "//device/vr/public/cpp:features" ]
  }
}

static_library("user_agent") {
  sources = [
    "user_agent_utils.cc",
    "user_agent_utils.h",
  ]

  deps = [
    ":embedder_support",
    "//build:branding_buildflags",
    "//build/util:chromium_git_revision",
    "//components/policy/core/common",
    "//components/prefs",
    "//components/version_info",
    "//net",
    "//third_party/blink/public/common:headers",
    "//ui/base",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "origin_trials/component_updater_utils_unittest.cc",
    "origin_trials/origin_trial_policy_impl_unittest.cc",
    "origin_trials/origin_trials_settings_storage_unittest.cc",
    "user_agent_utils_unittest.cc",
  ]

  deps = [
    ":user_agent",
    "//base",
    "//base/test:test_support",
    "//build:branding_buildflags",
    "//components/component_updater/installer_policies",
    "//components/embedder_support",
    "//components/embedder_support/origin_trials",
    "//components/embedder_support/origin_trials:test_support",
    "//components/prefs",
    "//components/prefs:test_support",
    "//components/version_info:version_info",
    "//content/public/common",
    "//mojo/core/embedder:embedder",
    "//net:net",
    "//net/traffic_annotation:test_support",
    "//services/network:test_support",
    "//testing/gtest",
    "//third_party/blink/public/common:headers",
    "//third_party/blink/public/mojom:mojom_platform_headers",
    "//third_party/re2",
  ]

  if (is_android) {
    sources += [
      "android/util/android_stream_reader_url_loader_unittest.cc",
      "android/util/input_stream_reader_unittest.cc",
      "android/util/input_stream_unittest.cc",
      "android/util/web_resource_response_unittest.cc",
    ]

    deps += [
      "android:native_j_unittests_jni_headers",
      "android:native_java_unittests_java",
      "android:util",
    ]
  }
}
