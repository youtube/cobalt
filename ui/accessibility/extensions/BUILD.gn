# Copyright 2017 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//chrome/test/base/js2gtest.gni")
import("//testing/test.gni")
import("//tools/grit/grit_rule.gni")

group("extensions") {
  deps = [
    ":caretbrowsing",
    ":colorenhancer",
  ]
  if (is_chromeos_ash) {
    deps += [ "chromevoxclassic:chromevox" ]
  }
}

group("extension_tests") {
  testonly = true
  if (is_chromeos_ash) {
    deps = [
      ":caretbrowsing_tests",
      ":colorenhancer_tests",
      ":highcontrast_tests",
      "chromevoxclassic:chromevox_tests",
    ]
  }
}

locale_files = [
  "_locales/am/messages.json",
  "_locales/ar/messages.json",
  "_locales/bg/messages.json",
  "_locales/bn/messages.json",
  "_locales/ca/messages.json",
  "_locales/cs/messages.json",
  "_locales/da/messages.json",
  "_locales/de/messages.json",
  "_locales/el/messages.json",
  "_locales/en_GB/messages.json",
  "_locales/en/messages.json",
  "_locales/es/messages.json",
  "_locales/es_419/messages.json",
  "_locales/et/messages.json",
  "_locales/fa/messages.json",
  "_locales/fi/messages.json",
  "_locales/fil/messages.json",
  "_locales/fr/messages.json",
  "_locales/gu/messages.json",
  "_locales/he/messages.json",
  "_locales/hi/messages.json",
  "_locales/hr/messages.json",
  "_locales/hu/messages.json",
  "_locales/id/messages.json",
  "_locales/it/messages.json",
  "_locales/ja/messages.json",
  "_locales/kn/messages.json",
  "_locales/ko/messages.json",
  "_locales/lt/messages.json",
  "_locales/lv/messages.json",
  "_locales/ml/messages.json",
  "_locales/mr/messages.json",
  "_locales/ms/messages.json",
  "_locales/nl/messages.json",
  "_locales/nb/messages.json",
  "_locales/pl/messages.json",
  "_locales/pt_BR/messages.json",
  "_locales/pt_PT/messages.json",
  "_locales/ro/messages.json",
  "_locales/ru/messages.json",
  "_locales/sk/messages.json",
  "_locales/sl/messages.json",
  "_locales/sr/messages.json",
  "_locales/sv/messages.json",
  "_locales/sw/messages.json",
  "_locales/ta/messages.json",
  "_locales/te/messages.json",
  "_locales/th/messages.json",
  "_locales/tr/messages.json",
  "_locales/uk/messages.json",
  "_locales/vi/messages.json",
  "_locales/zh_CN/messages.json",
  "_locales/zh_TW/messages.json",

  # Pseudolocales
  "_locales/ar_XB/messages.json",
  "_locales/en_XA/messages.json",
]

#
# Color Enhancer
#

group("colorenhancer") {
  deps = [
    ":colorenhancer_copy",
    ":colorenhancer_strings",
  ]
}

grit("colorenhancer_strings") {
  source = "strings/accessibility_extensions_strings.grd"
  outputs = locale_files
  output_dir = "$root_out_dir/colorenhancer"
  resource_ids = ""
}

colorenhancer_files = [
  "colorenhancer/manifest.json",
  "colorenhancer/src/background.js",
  "colorenhancer/src/common.js",
  "colorenhancer/src/cvd.js",
  "colorenhancer/src/cvd_type.js",
  "colorenhancer/src/matrix.js",
  "colorenhancer/src/popup.html",
  "colorenhancer/src/popup.js",
  "colorenhancer/src/storage.js",
  "colorenhancer/res/cvd-128.png",
  "colorenhancer/res/cvd-16.png",
  "colorenhancer/res/cvd-19.png",
  "colorenhancer/res/cvd-38.png",
  "colorenhancer/res/cvd-48.png",
  "colorenhancer/res/setup.css",
]

copy("colorenhancer_copy") {
  sources = colorenhancer_files
  outputs = [ "$root_out_dir/{{source_target_relative}}" ]
}

if (is_chromeos_ash) {
  webui_test_deps = [ "//chrome/test:browser_tests_runner" ]

  test("colorenhancer_tests") {
    deps = [ ":colorenhancer_js_webui_tests" ]
    deps += webui_test_deps
    data = js2gtest_js_libraries
  }

  js2gtest("colorenhancer_js_webui_tests") {
    test_type = "webui"
    sources = [
      "colorenhancer/src/matrix_test.js",
      "colorenhancer/src/storage_test.js",
    ]
    gen_include_files = [
      "colorenhancer/src/cvd_type.js",
      "colorenhancer/src/matrix.js",
      "colorenhancer/src/storage.js",
      "webstore_extension_test_base.js",
      "//chrome/browser/resources/chromeos/accessibility/common/testing/mock_storage.js",
    ]
    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
  }
}

#
# Caret Browsing
#

group("caretbrowsing") {
  deps = [
    ":caretbrowsing_copy",
    ":caretbrowsing_strings",
  ]
}

grit("caretbrowsing_strings") {
  source = "strings/accessibility_extensions_strings.grd"
  outputs = locale_files
  output_dir = "$root_out_dir/caretbrowsing"
  resource_ids = ""
}

caretbrowsing_files = [
  "caretbrowsing/background.js",
  "caretbrowsing/caret_128.png",
  "caretbrowsing/caret_16.png",
  "caretbrowsing/caret_19_on.png",
  "caretbrowsing/caret_19.png",
  "caretbrowsing/caret_48.png",
  "caretbrowsing/caretbrowsing.css",
  "caretbrowsing/caretbrowsing.js",
  "caretbrowsing/increase_brightness.png",
  "caretbrowsing/manifest.json",
  "caretbrowsing/node_util.js",
  "caretbrowsing/options.html",
  "caretbrowsing/options.js",
  "caretbrowsing/selection_util.js",
  "caretbrowsing/storage.js",
  "caretbrowsing/traverse_util.js",
  "//third_party/accessibility-audit/axs_testing.js",
]

copy("caretbrowsing_copy") {
  sources = caretbrowsing_files
  outputs = [ "$root_out_dir/caretbrowsing/{{source_file_part}}" ]
}

if (is_chromeos_ash) {
  test("caretbrowsing_tests") {
    deps = [ ":caretbrowsing_webui_js_tests" ]
    deps += webui_test_deps

    data = js2gtest_js_libraries
  }

  js2gtest("caretbrowsing_webui_js_tests") {
    test_type = "webui"
    sources = [
      "caretbrowsing/node_util_test.js",
      "caretbrowsing/selection_util_test.js",
      "caretbrowsing/storage_test.js",
    ]
    gen_include_files = [
      "caretbrowsing/node_util.js",
      "caretbrowsing/selection_util.js",
      "caretbrowsing/storage.js",
      "caretbrowsing/traverse_util.js",
      "testing/webstore_extension_test_base.js",
      "//chrome/browser/resources/chromeos/accessibility/common/testing/callback_helper.js",
      "//chrome/browser/resources/chromeos/accessibility/common/testing/mock_storage.js",
    ]

    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
  }

  test("highcontrast_tests") {
    deps = [ ":highcontrast_webui_js_tests" ]
    deps += webui_test_deps

    data = js2gtest_js_libraries
  }

  js2gtest("highcontrast_webui_js_tests") {
    test_type = "webui"
    sources = [ "highcontrast/storage_test.js" ]
    gen_include_files = [
      "highcontrast/storage.js",
      "testing/webstore_extension_test_base.js",
      "//chrome/browser/resources/chromeos/accessibility/common/testing/callback_helper.js",
      "//chrome/browser/resources/chromeos/accessibility/common/testing/mock_storage.js",
    ]

    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
  }
}
