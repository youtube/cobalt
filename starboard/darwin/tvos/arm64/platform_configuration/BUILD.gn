# Copyright 2021 The Cobalt Authors. All Rights Reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//starboard/build/config/mac/xcode_tools_path.gni")

config("platform_configuration") {
  configs = [
    "//starboard/build/config/sabi",
    "//starboard/darwin/tvos/shared/platform_configuration",
  ]

  cflags = []
  ldflags = []
  asmflags = []
  arflags = []

  if (is_gold) {
    cflags += [ "-fembed-bitcode" ]
    asmflags += [ "-fembed-bitcode" ]
  } else {
    cflags += [ "-fembed-bitcode-marker" ]
    asmflags += [ "-fembed-bitcode-marker" ]
  }

  sysroot_path = "$xcode_tools_path/Platforms/AppleTVOS.platform/Developer/SDKs/AppleTVOS.sdk"
  common_flags = [
    "-isysroot",
    sysroot_path,
  ]
  if (is_clang) {
    common_flags += [
      "-arch",
      "arm64",
    ]
  }
  cflags += common_flags
  ldflags += common_flags
  asmflags += common_flags
  arflags += [
    "-arch_only",
    "arm64",
    "-syslibroot",
    sysroot_path,
  ]
}
