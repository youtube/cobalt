# Copyright 2023 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/buildflag_header.gni")
import("//build/config/chromeos/ui_mode.gni")
import("//chromeos/ash/components/scalable_iph/scalable_iph.gni")

assert(is_chromeos_ash)

# TODO(crbug.com/367479298): Clean up the dependencies and visibilities.

source_set("config") {
  sources = [
    "config.cc",
    "config.h",
  ]
  deps = [
    ":constants",
    ":scalable_iph_delegate",
    "//ash/constants",
    "//base",
    "//components/feature_engagement/public",
  ]
}

source_set("constants") {
  sources = [
    "scalable_iph_constants.cc",
    "scalable_iph_constants.h",
  ]
}

source_set("scalable_iph") {
  sources = [
    "scalable_iph.cc",
    "scalable_iph.h",
    "scalable_iph_factory.cc",
    "scalable_iph_factory.h",
  ]

  deps = [
    ":config",
    ":constants",
    ":iph_session",
    ":logger",
    ":scalable_iph_delegate",
    "//ash/constants:constants",
    "//base",
    "//chromeos/ash/components/browser_context_helper",
    "//components/feature_engagement/public",
    "//components/keyed_service/content",
    "//components/keyed_service/core",
  ]
}

source_set("iph_session") {
  sources = [
    "iph_session.cc",
    "iph_session.h",
  ]

  deps = [
    ":constants",
    "//base",
    "//components/feature_engagement/public",
  ]
}

source_set("scalable_iph_delegate") {
  sources = [
    "scalable_iph_delegate.cc",
    "scalable_iph_delegate.h",
  ]

  deps = [
    ":constants",
    ":iph_session",
  ]

  public_deps = [ "//base" ]
}

source_set("logger") {
  sources = [
    "logger.cc",
    "logger.h",
  ]

  deps = [
    "//ash/constants",
    "//base",
  ]
}

buildflag_header("buildflags") {
  header = "buildflags.h"

  flags = [ "ENABLE_CROS_SCALABLE_IPH=$enable_cros_scalable_iph" ]
}

source_set("unit_tests") {
  testonly = true

  sources = [ "scalable_iph_unittest.cc" ]

  deps = [
    ":constants",
    ":logger",
    ":scalable_iph",
    ":scalable_iph_delegate",
    "//chrome/browser/ash/scalable_iph:test_support",
    "//components/feature_engagement/public",
    "//testing/gmock",
    "//testing/gtest",
  ]
}
