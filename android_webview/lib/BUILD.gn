# Copyright 2019 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/config.gni")
import("//components/spellcheck/spellcheck_build_features.gni")

source_set("lib") {
  # External code should depend on ":android_webview" instead.
  visibility = [ "//android_webview" ]
  sources = [
    "aw_main_delegate.cc",
    "aw_main_delegate.h",
    "webview_jni_onload.cc",
    "webview_jni_onload.h",
  ]
  defines = [ "IS_ANDROID_WEBVIEW_IMPL" ]

  deps = [
    "//android_webview/browser",
    "//android_webview/browser/gfx",
    "//android_webview/common",
    "//android_webview/gpu",
    "//android_webview/js_sandbox/service:js_sandbox",
    "//android_webview/renderer",
    "//base",
    "//cc/base",
    "//components/autofill/core/common",
    "//components/crash/core/common",
    "//components/embedder_support",
    "//components/gwp_asan/common:crash_keys",
    "//components/memory_system",
    "//components/metrics",
    "//components/payments/content/android",
    "//components/safe_browsing/android:safe_browsing_api_handler",
    "//components/services/heap_profiling/public/cpp",
    "//components/spellcheck:buildflags",
    "//components/stylus_handwriting/android",
    "//components/variations",
    "//components/variations:variations_associated_data",
    "//components/version_info",
    "//components/version_info/android:channel_getter",
    "//components/viz/common",
    "//content/public/app",
    "//content/public/browser",
    "//content/public/common",
    "//content/public/common:content_descriptor_keys",
    "//device/base",
    "//gin",
    "//gpu/command_buffer/service",
    "//gpu/config",
    "//media",
    "//media:media_buildflags",
    "//third_party/blink/public/common:headers",
    "//tools/v8_context_snapshot:buildflags",
    "//ui/base",
    "//ui/events:gesture_detection",
  ]

  if (enable_spellcheck) {
    deps += [ "//components/spellcheck/common" ]
  }
}

source_set("webview_entry_point") {
  deps = [
    "//android_webview",
    "//base",
    "//base:jni_onload",
  ]
  if (enable_javaless_renderers) {
    deps += [ "//content/app:javaless_renderer" ]
  }
  sources = [ "webview_entry_point.cc" ]
}
