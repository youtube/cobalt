# Copyright 2017 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//components/enterprise/buildflags/buildflags.gni")
import("//printing/buildflags/buildflags.gni")

static_library("print_compositor") {
  sources = [
    "print_compositor_impl.cc",
    "print_compositor_impl.h",
  ]

  deps = [
    "//base",
    "//components/crash/core/common:crash_key",
    "//components/discardable_memory/client",
    "//components/enterprise/buildflags",
    "//content/public/utility",
    "//printing/common",
    "//printing/mojom",
    "//third_party/blink/public:blink_headers",
    "//ui/accessibility",
  ]

  if (is_win) {
    deps += [
      "//content/public/child",
      "//printing:printing_base",
    ]
  }

  if (enterprise_watermark) {
    deps += [ "//components/enterprise/watermarking" ]
  }

  public_deps = [
    "//components/enterprise/buildflags",
    "//components/services/print_compositor/public/cpp",
    "//components/services/print_compositor/public/mojom",
    "//printing/buildflags",
    "//skia",
  ]
}

if (enable_printing) {
  source_set("unit_tests") {
    testonly = true
    sources = [ "print_compositor_impl_unittest.cc" ]

    deps = [
      ":print_compositor",
      "//base/test:test_support",
      "//components/crash/core/common:crash_key",
      "//components/enterprise/buildflags",
      "//components/services/print_compositor/public/cpp",
      "//testing/gmock",
      "//testing/gtest",
    ]

    if (enterprise_watermark) {
      data = [ "//components/services/print_compositor/test/data" ]

      deps += [
        "//cc:test_support",
        "//components/enterprise/watermarking",
      ]
    }
  }
}
