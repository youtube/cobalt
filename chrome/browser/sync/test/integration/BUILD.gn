# Copyright 2025 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/rules.gni")
} else {
  import("//build/config/ui.gni")
  import("//chrome/common/features.gni")
  import("//printing/buildflags/buildflags.gni")
}

testonly = true

source_set("sync_integration_tests_impl") {
  configs += [ "//build/config:precompiled_headers" ]
  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
  deps = [
    "//chrome/browser/plus_addresses",
    "//chrome/browser/prefs",
    "//chrome/browser/reading_list",
    "//chrome/browser/sync",
    "//chrome/browser/sync",
    "//chrome/browser/ui:browser_navigator_params_headers",
    "//chrome/browser/ui:ui_features",
    "//chrome/test:browser_tests_runner",
    "//chrome/test:sync_integration_test_support",
    "//chrome/test:test_support",
    "//components/bookmarks/browser",
    "//components/data_sharing/public",
    "//components/password_manager/core/browser/sharing",
    "//components/plus_addresses:test_support",
    "//components/plus_addresses/resources/strings",
    "//components/plus_addresses/webdata",
    "//components/reading_list/core:test_support",
    "//components/saved_tab_groups/test_support",
    "//components/sync:test_support",
    "//components/sync_bookmarks",
    "//components/sync_device_info:test_support",
    "//components/sync_preferences:common_syncable_prefs_database",
    "//components/undo",
    "//components/version_info",
  ]

  sources = [
    "single_client_autofill_profile_sync_test.cc",
    "single_client_bookmarks_sync_test.cc",
    "single_client_collaboration_group_sync_test.cc",
    "single_client_contact_info_sync_test.cc",
    "single_client_custom_passphrase_sync_test.cc",
    "single_client_device_info_sync_test.cc",
    "single_client_history_delete_directives_sync_test.cc",
    "single_client_history_sync_test.cc",
    "single_client_offer_sync_test.cc",
    "single_client_plus_address_setting_sync_test.cc",
    "single_client_plus_address_sync_test.cc",
    "single_client_preferences_sync_test.cc",
    "single_client_reading_list_sync_test.cc",
    "single_client_shared_tab_group_data_sync_test.cc",
    "single_client_standalone_transport_sync_test.cc",
    "single_client_sync_invalidations_test.cc",
    "sync_exponential_backoff_test.cc",

    # TODO(crbug.com/393119606): After enough of the implementation is ready
    # (CL #N), add integration tests. Careful, sync_integration_tests runs in CQ
    # but android_sync_integration_tests do not.
    # TODO(crbug.com/397767033): After enough of the implementation is ready
    # (CL #N), add integration tests. Careful, sync_integration_tests runs in CQ
    # but android_sync_integration_tests do not.
  ]
  data = [ "//chrome/test/data/sync/" ]

  if (is_android) {
    sources += [
      "//chrome/test/android/browsertests_apk/android_browsertests_jni_onload.cc",
      "single_client_web_apks_sync_test.cc",
    ]

    # Defined here so it's shared between android_sync_integration_tests (upstream)
    # and android_live_sync_integration_tests (downstream).
    deps += [
      "//chrome:chrome_android_core",
      "//chrome/browser/android/webapk:webapk_sources",
      "//chrome/browser/ui:ui_features",
      "//chrome/test:chrome_test_launcher",
      "//chrome/test:test_support_ui_android",
      "//components/browser_sync:switches",
      "//components/saved_tab_groups/public",
      "//content/test:android_test_message_pump_support",
      "//testing/android/native_test:native_test_support",
    ]

    data += [
      "$root_gen_dir/chrome/android/chrome_apk_paks/chrome_100_percent.pak",
      "$root_gen_dir/chrome/android/chrome_apk_paks/locales/en-US.pak",
      "$root_gen_dir/chrome/android/chrome_apk_paks/resources.pak",
      "$root_gen_dir/components/components_resources.pak",
      "$root_gen_dir/components/dev_ui_components_resources.pak",
    ]
  } else {  # !is_android
    sources += [
      "apps_sync_test_base.cc",
      "apps_sync_test_base.h",
      "enable_disable_test.cc",
      "local_sync_test.cc",
      "migration_test.cc",
      "password_manager_sync_test.cc",
      "single_client_app_settings_sync_test.cc",
      "single_client_common_sync_test.cc",
      "single_client_extension_apps_sync_test.cc",
      "single_client_extensions_sync_test.cc",
      "single_client_incoming_password_sharing_invitation_test.cc",
      "single_client_nigori_sync_test.cc",
      "single_client_outgoing_password_sharing_invitation_test.cc",
      "single_client_password_sharing_policy_test.cc",
      "single_client_passwords_sync_test.cc",
      "single_client_polling_sync_test.cc",
      "single_client_product_specifications_sync_test.cc",
      "single_client_saved_tab_groups_sync_test.cc",
      "single_client_search_engines_sync_test.cc",
      "single_client_secondary_account_sync_test.cc",
      "single_client_send_tab_to_self_sync_test.cc",
      "single_client_sessions_sync_test.cc",
      "single_client_sharing_message_sync_test.cc",
      "single_client_themes_sync_test.cc",
      "single_client_user_consents_sync_test.cc",
      "single_client_user_events_sync_test.cc",
      "single_client_valuables_sync_test.cc",
      "single_client_wallet_credential_sync_test.cc",
      "single_client_wallet_sync_test.cc",
      "single_client_web_apps_sync_generated_icon_fix_sync_test.cc",
      "single_client_web_apps_sync_test.cc",
      "single_client_webauthn_credentials_sync_test.cc",
      "sync_auth_test.cc",
      "sync_errors_test.cc",

      # Tests with two clients can't run on Android.
      "two_client_app_settings_sync_test.cc",
      "two_client_autocomplete_sync_test.cc",
      "two_client_autofill_sync_test.cc",
      "two_client_bookmarks_sync_test.cc",
      "two_client_contact_info_sync_test.cc",
      "two_client_custom_passphrase_sync_test.cc",
      "two_client_extension_apps_sync_test.cc",
      "two_client_extension_settings_sync_test.cc",
      "two_client_extensions_sync_test.cc",
      "two_client_history_sync_test.cc",
      "two_client_passwords_sync_test.cc",
      "two_client_power_bookmarks_sync_test.cc",
      "two_client_preferences_sync_test.cc",
      "two_client_search_engines_sync_test.cc",
      "two_client_send_tab_to_self_sync_test.cc",
      "two_client_sessions_sync_test.cc",
      "two_client_shared_tab_group_data_sync_test.cc",
      "two_client_themes_sync_test.cc",
      "two_client_user_events_sync_test.cc",
      "two_client_wallet_credential_sync_test.cc",
      "two_client_wallet_sync_test.cc",
      "two_client_web_apps_bmo_sync_test.cc",
      "two_client_web_apps_generated_icon_fix_test.cc",
      "two_client_web_apps_integration_test.cc",
      "two_client_web_apps_integration_test_base.cc",
      "two_client_web_apps_integration_test_base.h",
      "two_client_web_apps_sync_test.cc",
      "two_client_webauthn_credentials_sync_test.cc",
      "web_apps_sync_test_base.cc",
      "web_apps_sync_test_base.h",
    ]
    if (!is_mac) {
      # Dictionary sync is disabled on Mac.
      sources += [
        "single_client_dictionary_sync_test.cc",
        "two_client_dictionary_sync_test.cc",
      ]
    }
    if (is_win || is_mac || is_linux) {
      sources += [ "two_client_web_apps_integration_test_mac_win_linux.cc" ]
    }
    if (is_win || is_linux) {
      sources += [ "two_client_web_apps_integration_test_win_linux.cc" ]
    }
    if (is_mac) {
      sources += [ "two_client_web_apps_integration_test_mac.cc" ]
    }
    if (is_win) {
      sources += [ "two_client_web_apps_integration_test_win.cc" ]
    }
    if (is_chromeos) {
      sources += [ "two_client_web_apps_integration_test_cros.cc" ]
    }
    if (!is_chromeos) {
      sources += [
        "select_type_and_migrate_local_data_items_when_active_sync_test.cc",
      ]
    }

    deps += [
      "//chrome:packed_resources",
      "//chrome:resources",
      "//chrome:strings",
      "//chrome/app:chrome_dll_resources",
      "//chrome/app:command_ids",
      "//chrome/browser/affiliations",
      "//chrome/browser/apps/link_capturing",
      "//chrome/browser/autofill",
      "//chrome/browser/browsing_data:constants",
      "//chrome/browser/metrics/desktop_session_duration",
      "//chrome/browser/search_engines",
      "//chrome/browser/themes",
      "//chrome/browser/ui:ui_features",
      "//chrome/browser/web_applications:features",
      "//chrome/browser/web_applications:web_applications_test_support",
      "//chrome/common",
      "//chrome/renderer",
      "//chrome/test:test_support_ui",
      "//components/app_constants",
      "//components/bookmarks/test",
      "//components/browser_sync",
      "//components/commerce/core:feature_list",
      "//components/consent_auditor",
      "//components/favicon/core",
      "//components/history/content/browser",
      "//components/history/core/common",
      "//components/power_bookmarks/common:test_support",
      "//components/power_bookmarks/core",
      "//components/reading_list/core",
      "//components/saved_tab_groups/internal:tab_group_sync_bridge",
      "//components/search_engines",
      "//components/send_tab_to_self",
      "//components/spellcheck/common",
      "//components/sync",
      "//components/trusted_vault",
      "//components/trusted_vault:test_support",
      "//components/webapps/browser",
      "//components/webapps/common",
      "//components/webauthn/core/browser",
      "//components/webauthn/core/browser:passkey_model",
      "//testing/gmock",
      "//testing/gtest",
      "//third_party/blink/public:blink",
      "//third_party/icu",
      "//third_party/leveldatabase",
    ]

    data_deps = [
      "//testing:test_scripts_shared",
      "//testing/buildbot/filters:sync_integration_tests_filters",
      "//third_party/angle:includes",
    ]

    if (is_linux || is_chromeos || is_win) {
      data_deps += [ "//chrome:packed_resources" ]
    }

    if (is_mac) {
      data_deps += [
        "//chrome",
        "//chrome:chrome_framework",
      ]
    }
    if (is_win) {
      deps += [
        "//chrome:other_version",
        "//third_party/wtl",
        "//ui/resources",
      ]
    }
    if (is_chromeos) {
      sources += [
        "single_client_app_list_sync_test.cc",
        "single_client_arc_package_sync_test.cc",
        "single_client_cookies_sync_test.cc",
        "single_client_os_preferences_sync_test.cc",
        "single_client_printers_sync_test.cc",
        "single_client_wifi_configuration_sync_test.cc",
        "single_client_workspace_desk_sync_test.cc",
        "two_client_app_list_sync_test.cc",
        "two_client_arc_package_sync_test.cc",
        "two_client_os_preferences_sync_test.cc",
        "two_client_printers_sync_test.cc",
        "two_client_workspace_desk_sync_test.cc",
      ]
      deps += [
        "//chrome/browser/ash/app_list",
        "//chrome/browser/ash/app_list/arc",
        "//chrome/browser/ash/app_list/test:test_support",
        "//chrome/browser/ash/crosapi",
        "//chrome/browser/ash/floating_sso",
        "//chrome/browser/ash/floating_sso:test_support",
        "//chrome/browser/ash/printing",
        "//chrome/browser/ash/sync",
        "//chrome/browser/ash/system_web_apps/test_support",
        "//chromeos/ash/components/browser_context_helper",
        "//chromeos/ash/components/dbus/shill",
        "//chromeos/ash/components/sync_wifi",
        "//chromeos/ash/components/sync_wifi:test_support",
        "//chromeos/printing",
        "//chromeos/services/network_config/public/cpp",
        "//components/desks_storage",
      ]
    }

    if (toolkit_views) {
      deps += [ "//ui/views" ]
    }
    if (enable_printing) {
      deps += [ "//printing" ]
    }
    if (enable_glic) {
      deps += [
        "//chrome/browser/glic",
        "//chrome/browser/glic/test_support",
      ]
    }
    data += [
      "//chrome/test/data/password/",
      "//chrome/test/data/webapps_integration/",
      "//chrome/test/data/web_apps/",
      "//chrome/test/data/banners/",
      "//net/tools/testserver/",
      "//third_party/pywebsocket3/src/mod_pywebsocket/",
    ]
  }
}

if (is_android) {
  java_group("sync_integration_tests_java") {
    deps = [
      "//chrome/android:delegate_public_impl_java",
      "//chrome/test:android_browsertests_assets",
      "//chrome/test:android_browsertests_java",
      "//chrome/test:sync_integration_test_support_java",
      "//chrome/test:test_support_java",
    ]
  }
}
