# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chrome_build.gni")
import("//build/config/chromeos/ui_mode.gni")
import("//chromeos/ash/components/assistant/assistant.gni")
import("//chromeos/ash/components/scalable_iph/scalable_iph.gni")
import("//tools/grit/grit_rule.gni")

assert(is_chromeos_ash)

grit("resources") {
  source = "ash_resources.grd"

  outputs = [
    "grit/ash_resources.h",
    "grit/ash_resources_map.cc",
    "grit/ash_resources_map.h",
    "ash_resources.pak",
  ]
  output_dir = "$root_gen_dir/chromeos/ash"

  defines = [
    "enable_cros_libassistant=$enable_cros_libassistant",
    "enable_cros_scalable_iph=$enable_cros_scalable_iph",
    "is_chrome_branded=$is_chrome_branded",
  ]

  grit_flags = [
    "-E",
    "mojom_root=" + rebase_path(root_gen_dir, root_build_dir),
  ]

  deps = [
    "//chromeos/ash/components/language_packs/public/mojom:mojom_js",
    "//chromeos/ash/components/multidevice/mojom:mojom_js",
    "//chromeos/ash/services/device_sync/public/mojom:mojom_js",
    "//chromeos/ash/services/multidevice_setup/public/mojom:mojom_js",
  ]
}
