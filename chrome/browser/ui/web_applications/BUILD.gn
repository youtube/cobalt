# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")

source_set("unit_tests") {
  testonly = true

  sources = [
    "app_browser_controller_unittest.cc",
    "share_target_utils_unittest.cc",
    "web_app_launch_utils_unittest.cc",
  ]

  if (is_chromeos) {
    sources += [ "sub_apps_install_dialog_controller_unittest.cc" ]
  }

  deps = [
    "//chrome/browser",
    "//chrome/browser/apps/app_service",
    "//chrome/browser/ui",
    "//chrome/browser/web_applications",
    "//chrome/browser/web_applications:web_applications_test_support",
    "//chrome/browser/web_applications:web_applications_unit_tests",
    "//chrome/test:test_support",
    "//components/services/app_service",
    "//components/webapps/common",
    "//content/test:test_support",
    "//storage/browser:test_support",
  ]
}

source_set("browser_tests") {
  testonly = true

  sources = [
    "app_browser_controller_browsertest.cc",
    "commands/launch_web_app_command_browsertest.cc",
    "create_shortcut_browsertest.cc",
    "pwa_mixed_content_browsertest.cc",
    "status_bar_browsertest.cc",
    "sub_apps_service_impl_browsertest.cc",
    "web_app_browsertest.cc",
    "web_app_dark_mode_browsertest.cc",
    "web_app_engagement_browsertest.cc",
    "web_app_file_handling_browsertest.cc",
    "web_app_launch_handler_browsertest.cc",
    "web_app_launch_prevent_close_browsertest.cc",
    "web_app_menu_model_browsertest.cc",
    "web_app_metrics_browsertest.cc",
    "web_app_navigate_browsertest.cc",
    "web_app_ui_manager_impl_browsertest.cc",
    "web_app_uninstall_browsertest.cc",
  ]

  if (!is_chromeos_lacros) {
    sources += [
      # Tests are not valid on Lacros as web apps are only enabled in the main
      # profile which can never be deleted.
      "web_app_link_capturing_browsertest.cc",
      "web_app_profile_deletion_browsertest.cc",
    ]
  }

  if (is_chromeos_ash) {
    sources += [
      "app_browser_controller_browsertest_chromeos.cc",
      "sub_apps_install_dialog_controller_browsertest.cc",
      "web_app_guest_session_browsertest_chromeos.cc",
    ]
  }

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

  deps = [
    "//chrome/app:command_ids",
    "//chrome/browser:browser_themes",
    "//chrome/browser:theme_properties",
    "//chrome/browser/apps/app_service",
    "//chrome/browser/apps/app_service:app_registry_cache_waiter",
    "//chrome/browser/apps/app_service:test_support",
    "//chrome/browser/apps/link_capturing",
    "//chrome/browser/browsing_data:constants",
    "//chrome/browser/devtools",
    "//chrome/browser/web_applications:web_applications_test_support",
    "//chrome/browser/web_applications/app_service:test_support",
    "//chrome/test:test_support",
    "//chrome/test:test_support_ui",
    "//components/embedder_support",
    "//components/page_load_metrics/browser:test_support",
    "//components/services/app_service",
    "//components/site_engagement/content",
    "//components/webapps/browser",
    "//components/webapps/common",
  ]

  if (is_chromeos_ash) {
    deps += [
      "//chrome/browser/ash:test_support",
      "//chrome/browser/ash/system_web_apps/test_support",
      "//chrome/browser/ui/ash/system_web_apps",
      "//components/session_manager/core:core",
      "//components/user_manager:user_manager",
    ]
  }

  if (is_chromeos) {
    deps += [
      "//chrome/browser/metrics/structured:features",
      "//chrome/browser/metrics/structured:test_support",
      "//chromeos/constants",
      "//components/metrics/structured:structured_events",
    ]
  }
}

# On Lacros, these browser tests require Ash to be running, as they rely on
# web apps being registered with the App Service. Runs in the "lacros_chrome_browsertests_run_in_series" target.
source_set("app_service_browser_tests") {
  testonly = true

  sources = [
    "launch_web_app_browsertest.cc",
    "web_app_badging_browsertest.cc",
    "web_app_protocol_handling_browsertest.cc",
    "web_app_tab_restore_browsertest.cc",
    "web_app_url_handling_browsertest.cc",
    "web_app_window_controls_overlay_browsertest.cc",
  ]

  if (is_chromeos) {
    sources += [
      "share_to_target_browsertest.cc",
      "web_share_target_browsertest.cc",
    ]
  }

  if (is_chromeos_lacros) {
    sources += [
      "lacros_web_app_browsertest.cc",
      "lacros_web_app_shelf_browsertest.cc",
      "sub_apps_install_dialog_controller_browsertest.cc",
      "web_app_link_capturing_browsertest.cc",
    ]
  }

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

  deps = [
    "//chrome/app:command_ids",
    "//chrome/browser/apps/app_service",
    "//chrome/browser/apps/app_service:app_registry_cache_waiter",
    "//chrome/browser/apps/link_capturing",
    "//chrome/browser/browsing_data:constants",
    "//chrome/browser/ui/web_applications/diagnostics:app_service_browser_tests",
    "//chrome/browser/web_applications",
    "//chrome/browser/web_applications:web_applications_test_support",
    "//chrome/test:test_support",
    "//chrome/test:test_support_ui",
    "//components/app_constants",
    "//components/embedder_support",
    "//components/page_load_metrics/browser:test_support",
    "//components/services/app_service",
    "//components/webapps/browser:test_support",
    "//components/webapps/common",
  ]

  if (is_chromeos_lacros) {
    deps += [
      "//chrome/test:lacros_test_support_ui",
      "//chromeos/constants",
      "//chromeos/lacros",
      "//chromeos/lacros:test_support",
    ]
  }
}
