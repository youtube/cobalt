# Copyright 2023 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")
import("//third_party/jni_zero/jni_zero.gni")

generate_jni("jni_headers") {
  sources = [ "java/src/org/chromium/components/environment_integrity/IntegrityServiceBridge.java" ]
}

java_cpp_enum("enums_java") {
  sources = [ "integrity_service_bridge.h" ]
}

static_library("android") {
  sources = [
    "android_environment_integrity_data_manager.cc",
    "android_environment_integrity_data_manager.h",
    "android_environment_integrity_data_storage.cc",
    "android_environment_integrity_data_storage.h",
    "android_environment_integrity_service.cc",
    "android_environment_integrity_service.h",
    "integrity_service_bridge.cc",
    "integrity_service_bridge.h",
  ]
  deps = [
    ":jni_headers",
    "//base",
    "//content/public/browser",
    "//mojo/public/cpp/bindings",
    "//sql",
    "//third_party/blink/public/common:headers",
    "//third_party/blink/public/mojom:mojom_core",
    "//third_party/sqlite",
    "//url",
  ]
}

android_library("java") {
  sources = [
    "java/src/org/chromium/components/environment_integrity/IntegrityException.java",
    "java/src/org/chromium/components/environment_integrity/IntegrityServiceBridge.java",
    "java/src/org/chromium/components/environment_integrity/IntegrityServiceBridgeDelegate.java",
    "java/src/org/chromium/components/environment_integrity/IntegrityServiceBridgeDelegateImpl.java",
  ]
  deps = [
    "//base:base_java",
    "//third_party/android_deps:com_google_code_findbugs_jsr305_java",
    "//third_party/android_deps:guava_android_java",
    "//third_party/androidx:androidx_annotation_annotation_java",
    "//third_party/androidx:androidx_concurrent_concurrent_futures_java",
    "//third_party/androidx:androidx_core_core_java",
    "//third_party/jni_zero:jni_zero_java",
  ]
  srcjar_deps = [
    ":enums_java",
    ":jni_headers",
  ]

  jar_excluded_patterns = [
    "*/IntegrityServiceBridgeDelegateImpl.class",
    "*/IntegrityServiceBridgeDelegateImpl\$*.class",
  ]
}

android_library("integrity_service_bridge_public_impl_java") {
  sources = [ "java/src/org/chromium/components/environment_integrity/IntegrityServiceBridgeDelegateImpl.java" ]
  deps = [
    ":java",
    "//third_party/android_deps:guava_android_java",
    "//third_party/androidx:androidx_concurrent_concurrent_futures_java",
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "android_environment_integrity_data_manager_unittest.cc",
    "android_environment_integrity_data_storage_unittest.cc",
    "android_environment_integrity_service_unittest.cc",
  ]

  deps = [
    ":android",
    "//base/test:test_support",
    "//content/test:test_support",
    "//sql",
    "//sql:test_support",
    "//testing/gmock",
    "//testing/gtest",
    "//third_party/blink/public/common:headers",
    "//url",
  ]
}

robolectric_library("junit") {
  testonly = true
  sources = [
    "junit/src/org/chromium/components/environment_integrity/IntegrityServiceBridgeDelegateImplTest.java",
    "junit/src/org/chromium/components/environment_integrity/IntegrityServiceBridgeTest.java",
  ]
  deps = [
    ":integrity_service_bridge_public_impl_java",
    ":java",
    "//base:base_java",
    "//base:base_java_test_support",
    "//base:base_junit_test_support",
    "//base/test:test_support_java",
    "//third_party/android_deps:guava_android_java",
    "//third_party/jni_zero:jni_zero_java",
    "//third_party/junit",
    "//third_party/mockito:mockito_java",
  ]
}
