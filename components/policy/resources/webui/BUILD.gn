# Copyright 2023 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//ui/webui/resources/tools/build_webui.gni")

build_webui("build") {
  grd_prefix = "policy"

  static_files = [
    "policy.html",
    "policy.css",
  ]

  if (is_android || is_ios) {
    static_files += [ "logs/policy_logs.html" ]
  }

  # Files holding a Custom element definition AND have an equivalent .html file.
  web_component_files = [
    "policy_conflict.ts",
    "policy_row.ts",
    "policy_precedence_row.ts",
    "policy_table.ts",
    "status_box.ts",
  ]

  non_web_component_files = [
    "policy_base.ts",
    "policy.ts",
  ]
  if (is_android || is_ios) {
    non_web_component_files += [
      "logs/types.ts",
      "logs/policy_logs.ts",
    ]
  }

  ts_composite = true
  ts_definitions = [ "//tools/typescript/definitions/chrome_send.d.ts" ]
  html_to_wrapper_template = "native"
  ts_tsconfig_base = "tsconfig_base.json"

  ts_deps = [
    "//ui/webui/resources/cr_elements:build_ts",
    "//ui/webui/resources/js:build_ts",
  ]

  grit_output_dir = "$root_gen_dir/components"
}
