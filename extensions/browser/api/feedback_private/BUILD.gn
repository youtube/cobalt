# Copyright 2017 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//extensions/buildflags/buildflags.gni")

assert(enable_extensions,
       "Cannot depend on extensions because enable_extensions=false.")

source_set("feedback_private") {
  sources = [
    "feedback_private_api.cc",
    "feedback_private_api.h",
    "feedback_private_delegate.h",
    "feedback_service.cc",
    "feedback_service.h",
  ]

  configs += [ "//build/config/compiler:wexit_time_destructors" ]

  public_deps = [
    "//base",
    "//build:chromeos_buildflags",
    "//components/feedback",
    "//extensions/browser:browser_sources",
    "//extensions/browser/api:extensions_api_client",
    "//extensions/common/api",
    "//services/data_decoder/public/cpp",
    "//ui/gfx/geometry",
  ]

  deps = [
    "//components/account_id",
    "//components/embedder_support:user_agent",
    "//components/feedback/content",
    "//components/variations/net",
    "//content/public/browser",
    "//extensions/browser/api/feedback_private/proto",
    "//extensions/common",
    "//google_apis",
    "//net",
    "//services/network/public/cpp",
    "//services/network/public/mojom:url_loader_base",
    "//third_party/boringssl",
  ]

  if (!is_chromeos) {
    deps += [ "//components/feedback/redaction_tool" ]
  } else {
    sources += [
      "access_rate_limiter.cc",
      "access_rate_limiter.h",
      "log_source_access_manager.cc",
      "log_source_access_manager.h",
      "log_source_resource.cc",
      "log_source_resource.h",
    ]

    public_deps += [
      "//chromeos/ash/components/dbus/debug_daemon",
      "//components/feedback/redaction_tool",
    ]

    deps += [
      "//ash/public/cpp",
      "//chromeos/ash/components/cryptohome",
      "//chromeos/ash/components/dbus/cryptohome:cryptohome_proto",
      "//chromeos/ash/services/assistant/public/cpp",
    ]
  }
}
