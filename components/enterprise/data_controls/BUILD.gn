# Copyright 2023 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//components/enterprise/buildflags/buildflags.gni")
import("//third_party/protobuf/proto_library.gni")

assert(enterprise_data_controls)

source_set("features") {
  sources = [ "features.cc" ]
  public = [ "features.h" ]
  public_deps = [ "//base" ]
}

source_set("data_controls") {
  sources = [
    "action_context.cc",
    "action_context.h",
    "and_condition.cc",
    "and_condition.h",
    "attributes_condition.cc",
    "attributes_condition.h",
    "condition.h",
    "data_controls_policy_handler.cc",
    "data_controls_policy_handler.h",
    "dlp_histogram_helper.cc",
    "dlp_histogram_helper.h",
    "dlp_rules_manager_base.h",
    "not_condition.cc",
    "not_condition.h",
    "or_condition.cc",
    "or_condition.h",
    "prefs.cc",
    "prefs.h",
    "rule.cc",
    "rule.h",
    "rules_service.cc",
    "rules_service.h",
    "verdict.cc",
    "verdict.h",
  ]

  deps = [
    ":features",
    "//base",
    "//components/keyed_service/core",
    "//components/policy/core/browser",
    "//components/policy/core/common:common_constants",
    "//components/prefs",
    "//components/strings",
    "//components/url_matcher",
    "//url",
  ]

  if (is_chromeos) {
    sources += [
      "component.cc",
      "component.h",
    ]
    deps += [ "//chromeos/dbus/dlp:dlp_proto" ]
  }
}

source_set("test_support") {
  testonly = true
  sources = [
    "test_utils.cc",
    "test_utils.h",
  ]
  public_deps = [
    ":data_controls",
    "//base",
    "//components/policy/core/common:common_constants",
    "//components/prefs",
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "and_condition_unittest.cc",
    "attributes_condition_unittest.cc",
    "not_condition_unittest.cc",
    "or_condition_unittest.cc",
    "rule_unittest.cc",
    "verdict_unittest.cc",
  ]

  deps = [
    ":data_controls",
    ":test_support",
    "//base",
    "//base/test:test_support",
    "//testing/gtest",
  ]
}

proto_library("dlp_policy_event_proto") {
  sources = [ "dlp_policy_event.proto" ]
}
