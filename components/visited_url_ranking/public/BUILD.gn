# Copyright 2024 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/config.gni")
  import("//build/config/android/rules.gni")
  import("//third_party/jni_zero/jni_zero.gni")
}

source_set("public") {
  sources = [
    "decoration.cc",
    "decoration.h",
    "features.cc",
    "features.h",
    "fetch_options.cc",
    "fetch_options.h",
    "fetch_result.cc",
    "fetch_result.h",
    "fetcher_config.cc",
    "fetcher_config.h",
    "tab_metadata.cc",
    "tab_metadata.h",
    "url_grouping/group_suggestions.cc",
    "url_grouping/group_suggestions.h",
    "url_grouping/group_suggestions_delegate.h",
    "url_grouping/group_suggestions_service.h",
    "url_grouping/tab_event_tracker.h",
    "url_visit.cc",
    "url_visit.h",
    "url_visit_aggregates_transformer.h",
    "url_visit_data_fetcher.h",
    "url_visit_schema.cc",
    "url_visit_schema.h",
    "url_visit_util.cc",
    "url_visit_util.h",
    "visited_url_ranking_service.h",
  ]

  deps = [
    "//base",
    "//components/history/core/browser",
    "//components/keyed_service/core",
    "//components/segmentation_platform/public",
    "//components/strings",
    "//components/sync_device_info",
    "//components/url_deduplication",
    "//services/metrics/public/cpp:metrics_cpp",
    "//ui/base",
    "//url",
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [ "url_visit_util_unittest.cc" ]

  deps = [
    ":public",
    ":test_support",
    "//base",
    "//base/test:test_support",
    "//components/bookmarks/browser:browser",
    "//components/history/core/browser",
    "//components/segmentation_platform/public",
    "//components/segmentation_platform/public:test_support",
    "//components/sessions",
    "//components/sync_sessions",
    "//testing/gmock",
    "//testing/gtest",
  ]
}

source_set("test_support") {
  testonly = true

  sources = [
    "test_support.cc",
    "test_support.h",
    "testing/mock_visited_url_ranking_service.cc",
    "testing/mock_visited_url_ranking_service.h",
  ]

  deps = [
    ":public",
    "//base",
    "//components/history/core/browser",
    "//testing/gmock",
    "//testing/gtest",
  ]
}

if (is_android) {
  android_library("public_java") {
    srcjar_deps = [ ":group_suggestions_generated_enums" ]

    sources = [
      "url_grouping/android/java/src/org/chromium/components/visited_url_ranking/url_grouping/GroupSuggestion.java",
      "url_grouping/android/java/src/org/chromium/components/visited_url_ranking/url_grouping/GroupSuggestions.java",
      "url_grouping/android/java/src/org/chromium/components/visited_url_ranking/url_grouping/GroupSuggestionsService.java",
      "url_grouping/android/java/src/org/chromium/components/visited_url_ranking/url_grouping/UserResponseMetadata.java",
    ]

    deps = [
      "//base:base_java",
      "//base:callback_java",
      "//third_party/androidx:androidx_annotation_annotation_java",
      "//third_party/jni_zero:jni_zero_java",
      "//ui/android:ui_java",
      "//url:url_java",
    ]
  }

  generate_jni("jni_headers") {
    sources = [
      "url_grouping/android/java/src/org/chromium/components/visited_url_ranking/url_grouping/GroupSuggestion.java",
      "url_grouping/android/java/src/org/chromium/components/visited_url_ranking/url_grouping/GroupSuggestions.java",
      "url_grouping/android/java/src/org/chromium/components/visited_url_ranking/url_grouping/UserResponseMetadata.java",
    ]
  }

  java_cpp_enum("group_suggestions_generated_enums") {
    sources = [
      "url_grouping/group_suggestions_delegate.h",
      "url_grouping/tab_event_tracker.h",
    ]
  }
}
