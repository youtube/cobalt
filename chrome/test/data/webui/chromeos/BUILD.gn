# Copyright 2020 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/test/base/ash/js2gtest.gni")
import("//chrome/test/include_js_tests.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/typescript/webui_ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")
import("../build_webui_tests.gni")

assert(is_chromeos)

if (include_js2gtest_tests) {
  js2gtest("interactive_ui_tests_js_webui") {
    test_type = "webui"

    sources = [ "cr_focus_row_behavior_interactive_test.js" ]

    gen_include_files = [
      "polymer_browser_test_base.js",
      "polymer_interactive_ui_test.js",
    ]

    deps = [ "//chrome/browser/ui" ]
    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
  }

  js2gtest("browser_tests_js_webui") {
    test_type = "webui"

    # Javascript sources. These are combined with the .cc files in the GYP
    # build and are handled by a rule, but in the GN build they're in a
    # separate action so need to be separated out.
    sources = [
      "account_manager/account_manager_browsertest.js",
      "ash_common/i18n_process_test.js",
      "assertions.js",
      "async_gen.js",
      "borealis_installer/borealis_installer_browsertest.js",
      "certificate_manager_dialog_browsertest.js",
      "cloud_upload/cloud_upload_dialog_browsertest.js",
      "cloud_upload/connect_onedrive_browsertest.js",
      "cloud_upload/file_handler_page_browsertest.js",
      "cloud_upload/move_confirmation_page_browsertest.js",
      "cr_components_chromeos_v3_browsertest.js",
      "emoji_picker/emoji_picker_browsertest.js",
      "internet_config_dialog_browsertest.js",
      "internet_detail_dialog_browsertest.js",
      "js2gtest_browsertest.js",
      "network_browsertest.js",
      "office_fallback/office_fallback_browsertest.js",
      "set_time_dialog/set_time_dialog_browsertest.js",
      "sys_internals/sys_internals_browsertest.js",
    ]

    gen_include_files = [ "polymer_browser_test_base.js" ]

    deps = [
      "//build:branding_buildflags",
      "//build/config/coverage:buildflags",
      "//chrome/browser/ui",
      "//crypto:buildflags",
      "//services/network/public/cpp",
      "//skia",
    ]

    data = [ "//ui/webui/resources/js/load_time_data_deprecated.js" ]

    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
  }

  js2gtest("browser_tests_js_mojo_webui") {
    test_type = "mojo_webui"
    sources = [
      "crostini_installer_browsertest.js",
      "crostini_upgrader_browsertest.js",
      "manage_mirrorsync/manage_mirrorsync_browsertest.js",
      "nearby_share/nearby_browsertest.js",
      "nearby_share/shared/nearby_shared_v3_browsertest.js",
    ]
    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
  }
}

js_type_check("closure_compile_local") {
  is_polymer3 = true
  closure_flags = default_closure_args + mojom_js_args + [
                    "js_module_root=" + rebase_path("//chrome/test/data/webui/",
                                                    root_build_dir),
                    "js_module_root=./gen/chrome/test/data/webui/",
                  ]
  deps = [
    ":chai_assert",
    ":mock_controller.m",
    ":test_browser_proxy",
    ":test_util",
  ]
}

js_library("chai_assert") {
  externs_list = [ "//third_party/closure_compiler/externs/chai-3.5.js" ]
}

js_library("mock_controller.m") {
}

js_library("test_browser_proxy") {
  deps = [ "//ash/webui/common/resources:promise_resolver" ]
}

js_library("test_util") {
  deps = [ "//ash/webui/common/resources:event_target" ]
}

group("closure_compile") {
  public_deps = [
    ":closure_compile_local",
    "account_manager:closure_compile",
    "ash_common:closure_compile",
  ]
}

build_webui_tests("build") {
  ts_composite = true
  ts_tsconfig_base = "tsconfig_base.json"
  files = [
    "chai_assert.js",
    "cr_focus_row_behavior_test.ts",
    "crostini_installer_app_test.js",
    "crostini_upgrader_app_test.js",
    "fake_network_config_mojom.js",
    "fake_passpoint_service_mojom.ts",
    "internet_config_dialog_test.ts",
    "internet_detail_dialog_test.ts",
    "mocha_adapter.js",
    "mock_controller.js",
    "mock_controller.m.js",
    "mojo_webui_test_support.ts",
    "test_browser_proxy.js",
    "test_util.js",

    # Subfolder files. Move these files to their dedicated ts_library()
    # subfolder targets.
    "account_manager/account_migration_welcome_test.js",
    "account_manager/test_account_manager_browser_proxy.js",
    "bluetooth/bluetooth_base_page_test.ts",
    "bluetooth/bluetooth_battery_icon_percentage_test.ts",
    "bluetooth/bluetooth_device_battery_info_test.ts",
    "bluetooth/bluetooth_icon_test.ts",
    "bluetooth/bluetooth_pairing_confirm_code_page_test.ts",
    "bluetooth/bluetooth_pairing_device_item_test.ts",
    "bluetooth/bluetooth_pairing_device_selection_page_test.ts",
    "bluetooth/bluetooth_pairing_enter_code_page_test.ts",
    "bluetooth/bluetooth_pairing_request_code_page_test.ts",
    "bluetooth/bluetooth_pairing_ui_test.ts",
    "bluetooth/bluetooth_spinner_page_test.ts",
    "bluetooth/fake_bluetooth_config.ts",
    "bluetooth/fake_bluetooth_discovery_delegate.ts",
    "bluetooth/fake_device_pairing_handler.ts",
    "bluetooth/fake_hid_preserving_bluetooth_state_controller.ts",
    "cellular_setup/activation_code_page_test.ts",
    "cellular_setup/base_page_test.ts",
    "cellular_setup/button_bar_test.ts",
    "cellular_setup/cellular_setup_test.ts",
    "cellular_setup/confirmation_code_page_test.ts",
    "cellular_setup/esim_flow_ui_test.ts",
    "cellular_setup/fake_barcode_detector.ts",
    "cellular_setup/fake_cellular_setup_delegate.ts",
    "cellular_setup/fake_cellular_setup_remote.ts",
    "cellular_setup/fake_esim_manager_remote.ts",
    "cellular_setup/fake_media_devices.ts",
    "cellular_setup/final_page_test.ts",
    "cellular_setup/mock_metrics_private.ts",
    "cellular_setup/profile_discovery_list_page_test.ts",
    "cellular_setup/provisioning_page_test.ts",
    "cellular_setup/psim_flow_ui_test.ts",
    "cellular_setup/setup_loading_page_test.ts",
    "multidevice_setup/integration_test.js",
    "multidevice_setup/setup_succeeded_page_test.js",
    "multidevice_setup/start_setup_page_test.js",
    "network/apn_list_item_test.ts",
    "network/apn_list_test.ts",
    "network/apn_selection_dialog_list_item_test.ts",
    "network/apn_selection_dialog_test.ts",
    "network/cr_policy_network_behavior_mojo_tests.js",
    "network/cr_policy_network_indicator_mojo_tests.js",
    "network/cr_policy_strings.js",
    "network/network_apnlist_test.js",
    "network/network_choose_mobile_test.ts",
    "network/network_config_element_behavior_test.js",
    "network/network_config_input_test.ts",
    "network/network_config_select_test.js",
    "network/network_config_test.ts",
    "network/network_config_toggle_test.js",
    "network/network_config_vpn_test.ts",
    "network/network_config_wifi_test.ts",
    "network/network_icon_test.js",
    "network/network_ip_config_test.ts",
    "network/network_list_item_test.js",
    "network/network_list_test.js",
    "network/network_nameservers_test.ts",
    "network/network_password_input_test.ts",
    "network/network_property_list_mojo_test.js",
    "network/network_proxy_exclusions_test.ts",
    "network/network_proxy_input_test.ts",
    "network/network_proxy_test.ts",
    "network/network_select_test.ts",
    "network/network_siminfo_test.ts",
    "network/sim_lock_dialogs_test.ts",
    "network/test_utils.ts",
    "network_health/fake_network_diagnostics_routines.ts",
    "network_health/network_diagnostics_test.ts",
    "network_health/network_health_test_utils.ts",
    "network_health/routine_group_test.ts",
    "set_time_dialog/set_time_dialog_test.js",
    "set_time_dialog/test_set_time_browser_proxy.js",
    "sys_internals/all_tests.js",
    "sys_internals/api_test.js",
    "sys_internals/line_chart/data_series_test.js",
    "sys_internals/line_chart/line_chart_test.js",
    "sys_internals/line_chart/menu_test.js",
    "sys_internals/line_chart/scrollbar_test.js",
    "sys_internals/line_chart/sub_chart_test.js",
    "sys_internals/line_chart/unit_label_test.js",
    "sys_internals/page_drawer_test.js",
    "sys_internals/page_infopage_test.js",
    "sys_internals/page_switch_test.js",
    "sys_internals/page_unit_test.js",
    "sys_internals/test_util.js",
    "traffic_counters/traffic_counters_test.ts",
  ]

  mojo_files = [ "$target_gen_dir/web_ui_test.mojom-webui.ts" ]
  mojo_files_deps = [ ":web_ui_test_bindings_ts__generator" ]

  ts_definitions = [
    "//tools/typescript/definitions/chrome_send.d.ts",
    "//tools/typescript/definitions/metrics_private.d.ts",
    "//tools/typescript/definitions/pending.d.ts",
  ]

  ts_path_mappings = [
    "*/strings.m.js|" +
        rebase_path("//tools/typescript/definitions/strings.d.ts",
                    target_gen_dir),
    "chrome://internet-detail-dialog/*|" + rebase_path(
            "${root_gen_dir}/chrome/browser/resources/chromeos/internet_detail_dialog/tsc/*",
            target_gen_dir),
    "chrome://internet-config-dialog/*|" + rebase_path(
            "${root_gen_dir}/chrome/browser/resources/chromeos/internet_config_dialog/tsc/*",
            target_gen_dir),
    "chrome://webui-test/*|" +
        rebase_path("$root_gen_dir/chrome/test/data/webui/tsc/*",
                    target_gen_dir),
  ]

  ts_deps = [
    "//ash/webui/common/resources:build_ts",
    "//ash/webui/common/resources/cellular_setup:build_ts",
    "//ash/webui/common/resources/cr_elements:build_ts",
    "//chrome/browser/resources/chromeos/internet_config_dialog:build_ts",
    "//chrome/browser/resources/chromeos/internet_detail_dialog:build_ts",
    "//chrome/test/data/webui:build_ts",
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources/js:build_ts",
    "//ui/webui/resources/mojo:build_ts",
  ]
  ts_extra_deps = [ "//ash/webui/common/resources:generate_definitions" ]
}

mojom("web_ui_test_bindings") {
  sources = [ "web_ui_test.mojom" ]
  webui_module_path = "/"

  # Some tests in ash/webui/ are using js2gtest's
  # `test_type = "mojo_lite_webui"` which uses the old -lite bindings.
  generate_legacy_js_bindings = true
}

generate_grd("build_web_ui_test_mojo_lite_grdp") {
  testonly = true
  grd_prefix = "webui_chromeos_test"
  out_grd = "$target_gen_dir/web_ui_test_mojo_lite_resources.grdp"
  input_files_base_dir = rebase_path(target_gen_dir, root_build_dir)
  input_files = [ "web_ui_test.mojom-lite.js" ]
  deps = [ ":web_ui_test_bindings_js__generator" ]
  resource_path_prefix = "chromeos"
}
