# Copyright 2021 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//chrome/test/base/ash/js2gtest.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(is_chromeos_ash, "System Extensions Internals is ash-chrome only")

static_library("system_extensions_internals_ui") {
  sources = [
    "system_extensions_internals_ui.cc",
    "system_extensions_internals_ui.h",
    "url_constants.cc",
    "url_constants.h",
  ]

  deps = [
    "//ash/constants:constants",
    "//ash/webui/resources:system_extensions_internals_resources",
    "//ash/webui/system_extensions_internals_ui/mojom",
    "//content/public/browser",
    "//ui/webui",
  ]
}

js_type_check("closure_compile") {
  deps = [ ":system_extensions_internals" ]
  closure_flags = default_closure_args + mojom_js_args
}

js_library("system_extensions_internals") {
  sources = [
    "resources/index.js",
    "resources/page_handler.js",
  ]
  externs_list =
      [ "//ash/webui/web_applications/externs/file_handling.externs.js" ]
  deps = [ "//ash/webui/system_extensions_internals_ui/mojom:mojom_webui_js" ]
}

js2gtest("browser_tests_js") {
  test_type = "mojo_lite_webui"

  sources = [ "test/system_extensions_internals_ui_browsertest.js" ]

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
}

grd_prefix = "ash_system_extensions_internals"

mojo_grdp = "$target_gen_dir/system_extensions_internals_mojo_resources.grdp"

generate_grd("build_mojo_grdp") {
  grd_prefix = grd_prefix
  out_grd = mojo_grdp

  deps = [ "//ash/webui/system_extensions_internals_ui/mojom:mojom_webui_js" ]

  # Flatten out the dependency tree of your mojom and add generated bindings
  # file here.
  input_files = [ "ash/webui/system_extensions_internals_ui/mojom/system_extensions_internals_ui.mojom-webui.js" ]

  input_files_base_dir =
      rebase_path("$root_gen_dir/mojom-webui", "$root_build_dir")
}

generate_grd("build_grd") {
  input_files_base_dir = rebase_path("resources", "//")
  input_files = [
    "index.html",
    "index.js",
    "page_handler.js",
  ]

  grd_prefix = grd_prefix
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"
  deps = [ ":build_mojo_grdp" ]
  grdp_files = [ mojo_grdp ]
}
