import("//build/config/android/config.gni")
import("//build/config/android/rules.gni")
import("//starboard/build/buildflags.gni")
import("//third_party/icu/config.gni")
import("//third_party/jni_zero/jni_zero.gni")

cobalt_manifest = "$target_gen_dir/cobalt_manifest/AndroidManifest.xml"

jinja_template("cobalt_manifest") {
  input = "//cobalt/shell/android/shell_apk/AndroidManifest.xml.jinja2"
  output = cobalt_manifest
  variables = [
    "manifest_package=dev.cobalt.coat",
    "build_type=$build_type",
  ]
}

generate_jni("jni_headers") {
  sources = [
    "apk/app/src/main/java/dev/cobalt/coat/AudioPermissionRequester.java",
    "apk/app/src/main/java/dev/cobalt/coat/CaptionSettings.java",
    "apk/app/src/main/java/dev/cobalt/coat/CobaltService.java",
    "apk/app/src/main/java/dev/cobalt/coat/CobaltSystemConfigChangeReceiver.java",
    "apk/app/src/main/java/dev/cobalt/coat/CobaltTextToSpeechHelper.java",
    "apk/app/src/main/java/dev/cobalt/coat/PlatformError.java",
    "apk/app/src/main/java/dev/cobalt/coat/ResourceOverlay.java",
    "apk/app/src/main/java/dev/cobalt/coat/StarboardBridge.java",
    "apk/app/src/main/java/dev/cobalt/coat/javabridge/HTMLMediaElementExtension.java",
    "apk/app/src/main/java/dev/cobalt/media/AudioOutputManager.java",
    "apk/app/src/main/java/dev/cobalt/media/AudioTrackBridge.java",
    "apk/app/src/main/java/dev/cobalt/media/MediaCodecBridge.java",
    "apk/app/src/main/java/dev/cobalt/media/MediaCodecBridgeBuilder.java",
    "apk/app/src/main/java/dev/cobalt/media/MediaCodecUtil.java",
    "apk/app/src/main/java/dev/cobalt/media/MediaDrmBridge.java",
    "apk/app/src/main/java/dev/cobalt/media/VideoFrameReleaseTimeHelper.java",
    "apk/app/src/main/java/dev/cobalt/media/VideoSurfaceTexture.java",
    "apk/app/src/main/java/dev/cobalt/media/VideoSurfaceView.java",
    "apk/app/src/main/java/dev/cobalt/util/DisplayUtil.java",
  ]

  if (!use_evergreen) {
    sources += [
      "apk/app/src/main/java/dev/cobalt/browser/CobaltInterfaceRegistrar.java",
    ]
  }
}

generate_jni("browser_jni_headers") {
  sources = [
    "apk/app/src/main/java/dev/cobalt/browser/CobaltContentBrowserClient.java",
  ]
}

android_library("cobalt_browser_java") {
  sources = [
    "apk/app/src/main/java/dev/cobalt/browser/CobaltContentBrowserClient.java",
  ]
  srcjar_deps = [ ":browser_jni_headers" ]
  deps = [ "//third_party/jni_zero:jni_zero_java" ]
}

android_library("cobalt_main_java") {
  testonly = true
  resources_package = "dev.cobalt.coat"
  deps = [
    ":cobalt_browser_java",
    "//base:base_java",
    "//base:process_launcher_java",
    "//base/version_info/android:version_constants_java",
    "//build/android:build_java",
    "//cobalt/shell/android:cobalt_shell_java",
    "//cobalt/shell/android:cobalt_shell_java_resources",
    "//cobalt/shell/android:cobalt_shell_jni_headers",
    "//components/embedder_support/android:view_java",
    "//content/public/android:content_java",
    "//media/capture/video/android:capture_java",
    "//net/android:net_java",
    "//services/media_session/public/cpp/android:media_session_java",
    "//services/media_session/public/mojom:mojom_java",
    "//third_party/android_deps:com_google_code_findbugs_jsr305_java",
    "//third_party/android_deps:google_play_services_ads_identifier_java",
    "//third_party/android_deps:google_play_services_basement_java",
    "//third_party/androidx:androidx_annotation_annotation_java",
    "//third_party/androidx:androidx_core_core_java",
    "//third_party/androidx:androidx_customview_customview_java",
    "//third_party/androidx:androidx_media_media_java",
    "//third_party/jni_zero:jni_zero_java",
    "//ui/android:ui_java",
    "//url:gurl_java",
  ]

  if (!use_evergreen) {
    deps += [
      "//cobalt/browser/crash_annotator/public/mojom:mojom_java",
      "//mojo/public/java:base_java",
      "//mojo/public/java:bindings_java",
      "//mojo/public/java:system_java",
      "//services/service_manager/public/java:service_manager_java",
    ]
  }

  srcjar_deps = [
    ":jni_headers",
    "//starboard/android/shared:java_enums",
  ]

  sources = [
    "apk/app/src/main/java/dev/cobalt/coat/AdvertisingId.java",
    "apk/app/src/main/java/dev/cobalt/coat/ArtworkDownloader.java",
    "apk/app/src/main/java/dev/cobalt/coat/ArtworkDownloaderDefault.java",
    "apk/app/src/main/java/dev/cobalt/coat/ArtworkLoader.java",
    "apk/app/src/main/java/dev/cobalt/coat/AudioPermissionRequester.java",
    "apk/app/src/main/java/dev/cobalt/coat/CaptionSettings.java",
    "apk/app/src/main/java/dev/cobalt/coat/CobaltA11yHelper.java",
    "apk/app/src/main/java/dev/cobalt/coat/CobaltActivity.java",
    "apk/app/src/main/java/dev/cobalt/coat/CobaltConnectivityDetector.java",
    "apk/app/src/main/java/dev/cobalt/coat/CobaltHttpHelper.java",
    "apk/app/src/main/java/dev/cobalt/coat/CobaltMediaSession.java",
    "apk/app/src/main/java/dev/cobalt/coat/CobaltService.java",
    "apk/app/src/main/java/dev/cobalt/coat/CobaltSystemConfigChangeReceiver.java",
    "apk/app/src/main/java/dev/cobalt/coat/CobaltTextToSpeechHelper.java",
    "apk/app/src/main/java/dev/cobalt/coat/CommandLineOverrideHelper.java",
    "apk/app/src/main/java/dev/cobalt/coat/CrashContext.java",
    "apk/app/src/main/java/dev/cobalt/coat/CrashContextUpdateHandler.java",
    "apk/app/src/main/java/dev/cobalt/coat/ErrorDialog.java",
    "apk/app/src/main/java/dev/cobalt/coat/NetworkStatus.java",
    "apk/app/src/main/java/dev/cobalt/coat/NullCobaltFactory.java",
    "apk/app/src/main/java/dev/cobalt/coat/PlatformError.java",
    "apk/app/src/main/java/dev/cobalt/coat/ResourceOverlay.java",
    "apk/app/src/main/java/dev/cobalt/coat/StarboardBridge.java",
    "apk/app/src/main/java/dev/cobalt/coat/VolumeStateReceiver.java",
    "apk/app/src/main/java/dev/cobalt/coat/javabridge/AmatiDeviceInspector.java",
    "apk/app/src/main/java/dev/cobalt/coat/javabridge/CobaltJavaScriptAndroidObject.java",
    "apk/app/src/main/java/dev/cobalt/coat/javabridge/CobaltJavaScriptInterface.java",
    "apk/app/src/main/java/dev/cobalt/coat/javabridge/H5vccPlatformService.java",
    "apk/app/src/main/java/dev/cobalt/coat/javabridge/HTMLMediaElementExtension.java",
    "apk/app/src/main/java/dev/cobalt/libraries/services/clientloginfo/ClientLogInfo.java",
    "apk/app/src/main/java/dev/cobalt/libraries/services/clientloginfo/ClientLogInfoModule.java",
    "apk/app/src/main/java/dev/cobalt/media/AudioOutputManager.java",
    "apk/app/src/main/java/dev/cobalt/media/AudioTrackBridge.java",
    "apk/app/src/main/java/dev/cobalt/media/Log.java",
    "apk/app/src/main/java/dev/cobalt/media/MediaCodecBridge.java",
    "apk/app/src/main/java/dev/cobalt/media/MediaCodecBridgeBuilder.java",
    "apk/app/src/main/java/dev/cobalt/media/MediaCodecCapabilitiesLogger.java",
    "apk/app/src/main/java/dev/cobalt/media/MediaCodecUtil.java",
    "apk/app/src/main/java/dev/cobalt/media/MediaDrmBridge.java",
    "apk/app/src/main/java/dev/cobalt/media/MediaFormatBuilder.java",
    "apk/app/src/main/java/dev/cobalt/media/VideoDecoderCache.java",
    "apk/app/src/main/java/dev/cobalt/media/VideoFrameReleaseTimeHelper.java",
    "apk/app/src/main/java/dev/cobalt/media/VideoSurfaceTexture.java",
    "apk/app/src/main/java/dev/cobalt/media/VideoSurfaceView.java",
    "apk/app/src/main/java/dev/cobalt/util/AssetLoader.java",
    "apk/app/src/main/java/dev/cobalt/util/DisplayUtil.java",
    "apk/app/src/main/java/dev/cobalt/util/Holder.java",
    "apk/app/src/main/java/dev/cobalt/util/IsEmulator.java",
    "apk/app/src/main/java/dev/cobalt/util/JavaSwitches.java",
    "apk/app/src/main/java/dev/cobalt/util/Log.java",
    "apk/app/src/main/java/dev/cobalt/util/SynchronizedHolder.java",
    "apk/app/src/main/java/dev/cobalt/util/SystemPropertiesHelper.java",
  ]

  if (!use_evergreen) {
    sources += [
      "apk/app/src/main/java/dev/cobalt/browser/CobaltInterfaceRegistrar.java",
      "apk/app/src/main/java/dev/cobalt/browser/crashannotator/CrashAnnotatorImplFactory.java",
      "apk/app/src/main/java/dev/cobalt/browser/crashannotator/CrashAnnotatorImplFirstParty.java",
    ]
  }
}

android_library("cobalt_apk_java") {
  testonly = true
  resources_package = "dev.cobalt.coat"

  deps = [
    ":cobalt_main_java",
    "//base:base_java",
  ]

  sources = [
    "apk/app/src/app/java/dev/cobalt/app/CobaltApplication.java",
    "apk/app/src/app/java/dev/cobalt/app/MainActivity.java",
  ]
}

android_assets("coat_assets") {
  sources = [
    "apk/app/src/app/assets/not_empty.txt",
    "apk/app/src/app/assets/test/not_empty.txt",
    "apk/app/src/app/assets/web/cobalt_logo_1024.png",
  ]
  disable_compression = true
}

# TODO(b/450024477): just a shared library once jni migration is complete
shared_library_with_jni("libchrobalt") {
  # TODO(b/375655377): remove testonly
  testonly = true
  deps = [
    "//cobalt/browser",
    "//cobalt/gpu",
    "//cobalt/renderer",
    "//cobalt/utility",
    "//gpu/command_buffer/service",

    # TODO: what can be removed in the dependencies?
    "//cobalt/shell:cobalt_shell_app",
    "//cobalt/shell:cobalt_shell_lib",
    "//cobalt/shell:pak",
    "//components/crash/content/browser",
    "//components/memory_system",
    "//media",
    "//skia",
    "//third_party/blink/public/common",
  ]

  # Explicit dependency required for JNI registration to be able to
  # find the native side functions.
  if (is_component_build) {
    deps += [
      "//device/gamepad",
      "//media/midi",
    ]
  }
  sources = [
    "//cobalt/app/cobalt_main_delegate.cc",
    "//cobalt/app/cobalt_main_delegate.h",
    "cobalt_library_loader.cc",
  ]
  configs -= [ "//build/config/android:hide_all_but_jni_onload" ]
  configs += [ "//build/config/android:hide_all_but_jni" ]
  if (is_official_build) {
    configs += [ "//build/config/compiler:strip_debug" ]
  }
  java_targets = [ ":cobalt_apk" ]
}

android_apk("cobalt_apk") {
  testonly = true
  apk_name = "Cobalt"
  android_manifest = cobalt_manifest
  android_manifest_dep = ":cobalt_manifest"
  deps = [
    ":coat_assets",
    ":cobalt_apk_java",
    "//cobalt/shell/android:cobalt_shell_assets",
    "//cobalt/shell/android:cobalt_shell_java",
    "//components/metrics:metrics_java",
    "//content/public/android:content_java",
    "//media/capture/video/android:capture_java",
    "//net/android:net_java",
    "//third_party/mesa_headers",
    "//ui/android:ui_java",
  ]
  shared_libraries = [ ":libchrobalt" ]
  srcjar_deps = [ ":libchrobalt__jni_registration" ]
  include_size_info = is_official_build
}

robolectric_binary("cobalt_coat_junit_tests") {
  sources = [
    "apk/app/src/test/java/dev/cobalt/coat/ArtworkDownloaderDefaultTest.java",
    "apk/app/src/test/java/dev/cobalt/coat/ArtworkLoaderTest.java",
    "apk/app/src/test/java/dev/cobalt/coat/CobaltActivityTest.java",
    "apk/app/src/test/java/dev/cobalt/coat/CobaltServiceTest.java",
    "apk/app/src/test/java/dev/cobalt/coat/CommandLineOverrideHelperTest.java",
    "apk/app/src/test/java/dev/cobalt/coat/MockShellManagerNatives.java",
    "apk/app/src/test/java/dev/cobalt/coat/ShellManagerTest.java",
  ]
  deps = [
    ":cobalt_apk_java",
    ":cobalt_main_java",
    "//base:base_java",
    "//base:base_java_test_support",
    "//base:base_junit_test_support",
    "//cobalt/shell/android:cobalt_shell_java",
    "//components/embedder_support/android:view_java",
    "//content/public/android:content_java",
    "//services/media_session/public/cpp/android:media_session_java",
    "//third_party/androidx:androidx_test_runner_java",
    "//third_party/google-truth:google_truth_java",
    "//third_party/junit",
    "//url:gurl_java",
  ]
}
