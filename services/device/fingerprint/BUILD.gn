# Copyright 2017 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//build/config/features.gni")

component("fingerprint") {
  visibility = [ "//services/device:*" ]

  sources = [
    "fingerprint.h",
    "fingerprint_export.h",
  ]

  defines = [ "SERVICES_DEVICE_FINGERPRINT_IMPLEMENTATION" ]

  configs += [ "//build/config/compiler:wexit_time_destructors" ]

  deps = [
    "//base",
    "//mojo/public/cpp/bindings",
  ]

  if (is_chromeos_ash) {
    sources += [
      "fingerprint_chromeos.cc",
      "fingerprint_chromeos.h",
    ]
    deps += [
      "//chromeos/ash/components/dbus/biod",
      "//dbus",
      "//third_party/protobuf:protobuf_lite",
    ]
  } else {
    sources += [ "fingerprint_default.cc" ]
  }

  public_deps = [ "//services/device/public/mojom" ]
}
