# Copyright 2016 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//ios/build/chrome_build.gni")

source_set("default_browser_scene_agent") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "default_browser_scene_agent.h",
    "default_browser_scene_agent.mm",
  ]
  deps = [
    "//base",
    "//ios/chrome/app/application_delegate:app_state_header",
    "//ios/chrome/browser/default_browser:utils",
    "//ios/chrome/browser/promos_manager",
    "//ios/chrome/browser/promos_manager:constants",
    "//ios/chrome/browser/shared/coordinator/scene:observing_scene_agent",
    "//ios/chrome/browser/shared/coordinator/scene:scene_state_header",
    "//ios/chrome/browser/shared/public/commands",
    "//ios/chrome/browser/shared/public/features",
    "//ios/chrome/browser/signin",
    "//ios/chrome/browser/ui/default_promo",
  ]
  frameworks = [ "UIKit.framework" ]
}

source_set("incognito_blocker_scene_agent") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "incognito_blocker_scene_agent.h",
    "incognito_blocker_scene_agent.mm",
  ]
  deps = [
    "//base",
    "//ios/chrome/browser/shared/coordinator/scene:scene_state_header",
  ]
  frameworks = [ "UIKit.framework" ]
}

source_set("scene") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "ui_blocker_scene_agent.h",
    "ui_blocker_scene_agent.mm",
  ]

  deps = [
    "//base",
    "//ios/chrome/app:blocking_scene_commands",
    "//ios/chrome/app/application_delegate:app_state_header",
    "//ios/chrome/browser/shared/coordinator/scene:observing_scene_agent",
    "//ios/chrome/browser/shared/public/commands",
    "//ios/chrome/browser/ui/blocking_overlay",
  ]
  public_deps = []

  frameworks = [ "UIKit.framework" ]
}

source_set("main") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "browser_view_wrangler.h",
    "browser_view_wrangler.mm",
    "bvc_container_view_controller.h",
    "bvc_container_view_controller.mm",
    "wrangled_browser.h",
    "wrangled_browser.mm",
  ]
  deps = [
    "//base",
    "//components/translate/core/browser",
    "//ios/chrome/app:mode",
    "//ios/chrome/app/application_delegate:app_state_header",
    "//ios/chrome/app/resources:base_scene_storyboard",
    "//ios/chrome/app/resources:launchscreen_xib",
    "//ios/chrome/browser/app_launcher",
    "//ios/chrome/browser/application_context",
    "//ios/chrome/browser/autofill:autofill_internal",
    "//ios/chrome/browser/browser_state",
    "//ios/chrome/browser/browsing_data",
    "//ios/chrome/browser/crash_report:crash_report_internal",
    "//ios/chrome/browser/device_sharing",
    "//ios/chrome/browser/download",
    "//ios/chrome/browser/main",
    "//ios/chrome/browser/reading_list",
    "//ios/chrome/browser/sessions",
    "//ios/chrome/browser/sessions:restoration_agent",
    "//ios/chrome/browser/sessions:serialisation",
    "//ios/chrome/browser/sessions:session_service",
    "//ios/chrome/browser/shared/coordinator/chrome_coordinator",
    "//ios/chrome/browser/shared/coordinator/scene:scene_state_browser_agent",
    "//ios/chrome/browser/shared/public/commands",
    "//ios/chrome/browser/snapshots",
    "//ios/chrome/browser/store_kit",
    "//ios/chrome/browser/tabs",
    "//ios/chrome/browser/tabs:tabs_internal",
    "//ios/chrome/browser/tabs/inactive_tabs",
    "//ios/chrome/browser/tabs/inactive_tabs:features",
    "//ios/chrome/browser/ui/autofill",
    "//ios/chrome/browser/ui/browser_container",
    "//ios/chrome/browser/ui/browser_view",
    "//ios/chrome/browser/ui/download",
    "//ios/chrome/browser/ui/gestures",
    "//ios/chrome/browser/ui/incognito_reauth:incognito_reauth_scene_agent",
    "//ios/chrome/browser/ui/page_info:coordinator",
    "//ios/chrome/browser/ui/print",
    "//ios/chrome/browser/ui/qr_scanner:coordinator",
    "//ios/chrome/browser/ui/reading_list",
    "//ios/chrome/browser/ui/recent_tabs",
    "//ios/chrome/browser/ui/settings/sync",
    "//ios/chrome/browser/ui/settings/sync/utils",
    "//ios/chrome/browser/ui/snackbar",
    "//ios/chrome/browser/ui/tabs:constants",
    "//ios/chrome/browser/ui/thumb_strip",
    "//ios/chrome/browser/ui/thumb_strip:feature_flags",
    "//ios/chrome/browser/ui/thumb_strip:public",
    "//ios/chrome/browser/url_loading:url_loading_params_header",
    "//ios/chrome/browser/web",
    "//ios/chrome/browser/web:tab_helper_delegates",
    "//ios/chrome/browser/web:web_internal",
    "//ios/chrome/browser/web_state_list",
    "//ios/chrome/common/ui/colors",
    "//net",
  ]

  public_deps =
      [ "//ios/chrome/browser/shared/coordinator/scene:scene_state_header" ]

  # TODO(crbug.com/1103553): remove circular dependency.
  allow_circular_includes_from = [ "//ios/chrome/browser/ui/settings/sync" ]

  frameworks = [ "UIKit.framework" ]
}

source_set("unit_tests") {
  configs += [ "//build/config/compiler:enable_arc" ]
  testonly = true
  sources = [
    "browser_view_wrangler_unittest.mm",
    "default_browser_scene_agent_unittest.mm",
    "incognito_blocker_scene_agent_unittest.mm",
  ]
  deps = [
    ":default_browser_scene_agent",
    ":incognito_blocker_scene_agent",
    ":main",
    "//base",
    "//base/test:test_support",
    "//components/bookmarks/test",
    "//components/sync_preferences:sync_preferences",
    "//components/sync_preferences:test_support",
    "//ios/chrome/app:app_internal",
    "//ios/chrome/app/application_delegate:app_state_header",
    "//ios/chrome/app/application_delegate:application_delegate_internal",
    "//ios/chrome/app/application_delegate:test_support",
    "//ios/chrome/app/startup",
    "//ios/chrome/browser/application_context",
    "//ios/chrome/browser/bookmarks",
    "//ios/chrome/browser/browser_state:test_support",
    "//ios/chrome/browser/default_browser:test_support",
    "//ios/chrome/browser/default_browser:utils",
    "//ios/chrome/browser/favicon",
    "//ios/chrome/browser/history",
    "//ios/chrome/browser/main",
    "//ios/chrome/browser/main:test_support",
    "//ios/chrome/browser/prefs:browser_prefs",
    "//ios/chrome/browser/prefs:pref_names",
    "//ios/chrome/browser/prefs:pref_names",
    "//ios/chrome/browser/prerender",
    "//ios/chrome/browser/promos_manager:constants",
    "//ios/chrome/browser/promos_manager:features",
    "//ios/chrome/browser/promos_manager:test_support",
    "//ios/chrome/browser/search_engines",
    "//ios/chrome/browser/sessions:restoration_agent",
    "//ios/chrome/browser/sessions:scene_util_test_support",
    "//ios/chrome/browser/sessions:test_support",
    "//ios/chrome/browser/shared/coordinator/scene",
    "//ios/chrome/browser/shared/coordinator/scene:scene_state_browser_agent",
    "//ios/chrome/browser/shared/coordinator/scene/test",
    "//ios/chrome/browser/shared/public/features",
    "//ios/chrome/browser/signin",
    "//ios/chrome/browser/signin:fake_system_identity",
    "//ios/chrome/browser/signin:fake_system_identity_manager",
    "//ios/chrome/browser/signin:signin_util",
    "//ios/chrome/browser/signin:test_support",
    "//ios/chrome/browser/sync",
    "//ios/chrome/browser/tabs",
    "//ios/chrome/browser/tabs/inactive_tabs:features",
    "//ios/chrome/browser/ui/browser_view",
    "//ios/chrome/test:block_cleanup_test",
    "//ios/chrome/test:test_support",
    "//ios/testing:block_swizzler",
    "//ios/web/public/test",
    "//testing/gtest",
    "//third_party/ocmock",
  ]
}
