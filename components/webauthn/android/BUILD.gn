# Copyright 2021 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")
import("//third_party/jni_zero/jni_zero.gni")

generate_jni("jni_headers") {
  sources = [
    "java/src/org/chromium/components/webauthn/CredManSupportProvider.java",
    "java/src/org/chromium/components/webauthn/Fido2Api.java",
    "java/src/org/chromium/components/webauthn/Fido2CredentialRequest.java",
    "java/src/org/chromium/components/webauthn/InternalAuthenticator.java",
    "java/src/org/chromium/components/webauthn/WebAuthnBrowserBridge.java",
  ]
}

java_cpp_enum("java_enums_srcjar") {
  sources = [ "cred_man_support.h" ]
  visibility = [ ":*" ]
}

android_library("java") {
  srcjar_deps = [
    ":java_enums_srcjar",
    ":jni_headers",
  ]
  sources = [
    "java/src/org/chromium/components/webauthn/AuthenticatorFactory.java",
    "java/src/org/chromium/components/webauthn/AuthenticatorImpl.java",
    "java/src/org/chromium/components/webauthn/AuthenticatorIncognitoConfirmationBottomsheet.java",
    "java/src/org/chromium/components/webauthn/Barrier.java",
    "java/src/org/chromium/components/webauthn/CredManHelper.java",
    "java/src/org/chromium/components/webauthn/CredManMetricsHelper.java",
    "java/src/org/chromium/components/webauthn/CredManSupportProvider.java",
    "java/src/org/chromium/components/webauthn/CredManUiModeRecommender.java",
    "java/src/org/chromium/components/webauthn/Fido2Api.java",
    "java/src/org/chromium/components/webauthn/Fido2ApiCall.java",
    "java/src/org/chromium/components/webauthn/Fido2ApiCallHelper.java",
    "java/src/org/chromium/components/webauthn/Fido2CredentialRequest.java",
    "java/src/org/chromium/components/webauthn/FidoErrorResponseCallback.java",
    "java/src/org/chromium/components/webauthn/GetAssertionResponseCallback.java",
    "java/src/org/chromium/components/webauthn/GetMatchingCredentialIdsResponseCallback.java",
    "java/src/org/chromium/components/webauthn/InternalAuthenticator.java",
    "java/src/org/chromium/components/webauthn/IsUvpaaResponseCallback.java",
    "java/src/org/chromium/components/webauthn/MakeCredentialResponseCallback.java",
    "java/src/org/chromium/components/webauthn/WebAuthnBrowserBridge.java",
    "java/src/org/chromium/components/webauthn/WebAuthnCredentialDetails.java",
  ]

  jar_excluded_patterns = [ "*/CredManUiModeRecommender.class" ]

  deps = [
    ":java_resources",
    "$google_play_services_package:google_play_services_base_java",
    "$google_play_services_package:google_play_services_basement_java",
    "$google_play_services_package:google_play_services_tasks_java",
    "//base:base_java",
    "//build/android:build_java",
    "//components/browser_ui/bottomsheet/android:java",
    "//components/externalauth/android:java",
    "//components/payments/content/android:feature_list_java",
    "//components/version_info/android:version_constants_java",
    "//content/public/android:content_java",
    "//content/public/common:common_java",
    "//mojo/public/java:bindings_java",
    "//mojo/public/java:system_java",
    "//mojo/public/mojom/base:base_java",
    "//net/android:net_java",
    "//services/device/public/java:device_feature_list_java",
    "//services/service_manager/public/java:service_manager_java",
    "//third_party/androidx:androidx_annotation_annotation_experimental_java",
    "//third_party/androidx:androidx_annotation_annotation_java",
    "//third_party/androidx:androidx_core_core_java",
    "//third_party/blink/public/mojom:android_mojo_bindings_java",
    "//third_party/jni_zero:jni_zero_java",
    "//ui/android:ui_no_recycler_view_java",
    "//url:gurl_java",
    "//url/mojom:url_mojom_gurl_java",
  ]

  resources_package = "org.chromium.components.webauthn"
}
android_library("delegate_public_java") {
  sources = [
    "java/src/org/chromium/components/webauthn/CredManUiModeRecommender.java",
  ]
  deps = [ ":java" ]
}

android_library("test_support_java") {
  testonly = true
  sources = [
    "java/src/org/chromium/components/webauthn/Fido2ApiTestHelper.java",
    "java/src/org/chromium/components/webauthn/MockFido2CredentialRequest.java",
  ]
  deps = [
    ":java",
    "//base:base_java_test_support",
    "//components/payments/content/android:feature_list_java",
    "//components/payments/mojom:mojom_java",
    "//content/public/android:content_java",
    "//mojo/public/mojom/base:base_java",
    "//third_party/android_deps:guava_android_java",
    "//third_party/androidx:androidx_annotation_annotation_java",
    "//third_party/blink/public/mojom:android_mojo_bindings_java",
    "//third_party/jni_zero:jni_zero_java",
    "//third_party/junit:junit",
    "//url:gurl_java",
    "//url/mojom:url_mojom_gurl_java",
    "//url/mojom:url_mojom_origin_java",
  ]
}

source_set("android") {
  sources = [
    "cred_man_support.h",
    "fido2api_native_android.cc",
    "fido2credentialrequest_native_android.cc",
    "internal_authenticator_android.cc",
    "internal_authenticator_android.h",
    "webauthn_browser_bridge.cc",
    "webauthn_browser_bridge.h",
    "webauthn_client_android.cc",
    "webauthn_client_android.h",
    "webauthn_cred_man_delegate.cc",
    "webauthn_cred_man_delegate.h",
    "webauthn_cred_man_delegate_factory.cc",
    "webauthn_cred_man_delegate_factory.h",
  ]
  deps = [
    ":jni_headers",
    "//base",
    "//components/cbor",
    "//components/webauthn/core/browser",
    "//components/webauthn/json",
    "//content/public/browser",
    "//device/fido",
    "//third_party/blink/public/mojom:android_mojo_bindings_blink",
    "//third_party/boringssl",
    "//url",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "webauthn_cred_man_delegate_factory_unittest.cc",
    "webauthn_cred_man_delegate_unittest.cc",
  ]
  deps = [
    ":android",
    "//base/test:test_support",
    "//content/public/browser",
    "//content/test:test_support",
    "//testing/gmock",
    "//testing/gtest",
  ]
}

android_resources("java_resources") {
  sources = [
    "java/res/drawable/webauthn_incognito_confirmation_icon.xml",
    "java/res/layout/webauthn_incognito_confirmation.xml",
  ]
  deps = [
    "//components/browser_ui/bottomsheet/android:java_resources",
    "//components/strings:components_strings_grd",
  ]
}

robolectric_library("junit") {
  sources = [ "junit/src/org/chromium/components/webauthn/AuthenticatorIncognitoConfirmationBottomsheetTest.java" ]
  deps = [
    ":java",
    "//base:base_java",
    "//base:base_java_test_support",
    "//base:base_junit_test_support",
    "//components/browser_ui/bottomsheet/android:java",
    "//components/payments/content/android:service_java",
    "//components/payments/mojom:mojom_java",
    "//components/url_formatter/android:url_formatter_java",
    "//content/public/android:content_java",
    "//mojo/public/java:system_java",
    "//third_party/android_deps:material_design_java",
    "//third_party/androidx:androidx_collection_collection_java",
    "//third_party/blink/public/mojom:android_mojo_bindings_java",
    "//third_party/jni_zero:jni_zero_java",
    "//third_party/junit",
    "//third_party/mockito:mockito_java",
    "//ui/android:ui_no_recycler_view_java",
    "//url:gurl_java",
    "//url:gurl_junit_test_support",
    "//url/mojom:url_mojom_gurl_java",
    "//url/mojom:url_mojom_origin_java",
  ]
}
