# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")

source_set("configs") {
  sources = [
    "chrome_untrusted_web_ui_configs.cc",
    "chrome_untrusted_web_ui_configs.h",
    "chrome_untrusted_web_ui_configs_common.cc",
    "chrome_untrusted_web_ui_configs_common.h",
    "chrome_web_ui_configs.cc",
    "chrome_web_ui_configs.h",
  ]

  configs += [ "//build/config/compiler:wexit_time_destructors" ]

  deps = [
    "//components/lens:buildflags",
    "//content/public/browser",
    "//printing/buildflags:buildflags",
    "//skia",
    "//third_party/abseil-cpp:absl",
  ]

  if (is_android) {
    sources += [
      "chrome_untrusted_web_ui_configs_android.cc",
      "chrome_untrusted_web_ui_configs_android.h",
    ]
  }

  if (is_mac || is_win || is_linux || is_chromeos || is_fuchsia) {
    sources += [
      "chrome_untrusted_web_ui_configs_desktop.cc",
      "chrome_untrusted_web_ui_configs_desktop.h",
    ]

    deps += [
      "//build:branding_buildflags",
      "//chrome/browser/companion/core",
      "//chrome/browser/companion/core/mojom:mojo_bindings",
    ]
  }

  # Includes Ash Chrome and Lacros Chrome
  if (is_chromeos) {
    sources += [
      "chromeos/chrome_untrusted_web_ui_configs_chromeos.cc",
      "chromeos/chrome_untrusted_web_ui_configs_chromeos.h",
      "chromeos/chrome_web_ui_configs_chromeos.cc",
      "chromeos/chrome_web_ui_configs_chromeos.h",
    ]
    deps += [ "//build:chromeos_buildflags" ]
  }

  if (is_chromeos_ash) {
    deps += [
      "//ash/constants",
      "//ash/webui/color_internals:color_internals",
      "//ash/webui/demo_mode_app_ui",
      "//ash/webui/eche_app_ui",
      "//ash/webui/face_ml_app_ui",
      "//ash/webui/file_manager:file_manager_untrusted_ui",
      "//ash/webui/firmware_update_ui:firmware_update_ui",
      "//ash/webui/guest_os_installer:guest_os_installer",
      "//ash/webui/help_app_ui",
      "//ash/webui/os_feedback_ui",
      "//ash/webui/shortcut_customization_ui",
      "//ash/webui/system_extensions_internals_ui:system_extensions_internals_ui",
      "//chrome/browser/ash",
    ]
    if (!is_official_build) {
      deps += [ "//ash/webui/sample_system_web_app_ui" ]
    }
  }
}
