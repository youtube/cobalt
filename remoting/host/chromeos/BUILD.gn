# Copyright 2016 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//remoting/build/config/remoting_build.gni")

source_set("chromeos") {
  sources = [
    "ash_mojom_video_consumer.cc",
    "ash_mojom_video_consumer.h",
    "ash_proxy.cc",
    "ash_proxy.h",
    "clipboard_aura.cc",
    "clipboard_aura.h",
    "frame_sink_desktop_capturer.cc",
    "frame_sink_desktop_capturer.h",
    "message_box.cc",
    "message_box.h",
    "mouse_cursor_monitor_aura.cc",
    "mouse_cursor_monitor_aura.h",
    "point_transformer.cc",
    "point_transformer.h",
    "skia_bitmap_desktop_frame.cc",
    "skia_bitmap_desktop_frame.h",
  ]

  public_deps = [
    "//ash",
    "//build:chromeos_buildflags",
    "//cc",
    "//components/prefs",
    "//components/viz/host",
    "//gpu/command_buffer/common",
    "//remoting/protocol",
    "//services/viz/privileged/mojom/compositing",
    "//skia",
    "//third_party/webrtc_overrides:webrtc_component",
    "//ui/aura",
    "//ui/base/cursor",
    "//ui/compositor",
    "//ui/events",
    "//ui/views",
  ]

  deps = [
    ":features",
    "//remoting/base:base",
    "//remoting/host:clipboard",
    "//ui/base/cursor/mojom:cursor_type",
    "//ui/display/manager",
    "//ui/wm",
  ]
}

# ChromeOS enterprise specific stuff that is also accessed from other areas
# like //remoting/host/it2me.
# Split out in its own target to prevent cyclic dependencies.
source_set("enterprise_support") {
  sources = [ "chromeos_enterprise_params.h" ]
}

source_set("browser_interop") {
  sources = [
    "browser_interop.cc",
    "browser_interop.h",
  ]

  deps = [
    "//chrome/browser:browser_process",
    "//content/public/browser:browser",
    "//remoting/base:base",
    "//remoting/host",
  ]
}

source_set("remoting_service") {
  sources = [
    "file_session_storage.cc",
    "file_session_storage.h",
    "remote_support_host_ash.cc",
    "remote_support_host_ash.h",
    "remoting_service.cc",
    "remoting_service.h",
    "session_id.h",
    "session_storage.h",
  ]

  public_deps = [
    ":enterprise_support",
    "//remoting/host/it2me:chrome_os_host",
    "//remoting/host/it2me:common",
  ]

  configs += [ "//remoting/build/config:version" ]

  deps = [
    ":browser_interop",
    ":features",
    "//chrome/common:constants",
    "//mojo/public/cpp/bindings",
    "//remoting/base:base",
    "//remoting/host",
    "//remoting/host/mojom:mojom",
  ]
}

static_library("host_event_reporter_impl") {
  sources = [
    "host_event_reporter_impl.cc",
    "host_event_reporter_impl.h",
  ]
  public_deps = [
    "//base",
    "//chrome/browser/policy/messaging_layer/proto:crd_event_proto",
    "//components/reporting/proto:record_constants",
    "//remoting/host:common",
    "//remoting/protocol",
  ]
}

# All unit tests are built as part of the //remoting/host:unit_tests target
# and must be one target named "unit_tests".
source_set("reporter_unit_tests") {
  testonly = true
  sources = [ "host_event_reporter_impl_unittest.cc" ]
  deps = [
    ":host_event_reporter_impl",
    "//base",
    "//base/test:test_support",
    "//remoting/host:common",
    "//testing/gmock",
    "//testing/gtest",
  ]
}

# The host portions of the remoting unit tests.
source_set("unit_tests") {
  testonly = true

  sources = [
    "clipboard_aura_unittest.cc",
    "file_session_storage_unittest.cc",
    "frame_sink_desktop_capturer_unittest.cc",
    "remote_support_host_ash_unittest.cc",
    "remoting_service_unittest.cc",
    "scoped_fake_ash_proxy.cc",
    "scoped_fake_ash_proxy.h",
  ]

  configs += [ "//remoting/build/config:version" ]

  deps = [
    ":browser_interop",
    ":chromeos",
    ":features",
    ":remoting_service",
    "//base/test:test_support",
    "//components/policy/core/common:test_support",
    "//components/viz/service",
    "//remoting/host:common",
    "//remoting/host:test_support",
    "//remoting/host/it2me:common",
    "//remoting/proto",
    "//remoting/resources",
    "//services/network:test_support",
    "//skia",
    "//testing/gmock",
    "//testing/gtest",
    "//ui/display/manager",
  ]
}

source_set("features") {
  sources = [
    "features.cc",
    "features.h",
  ]
  deps = [ "//base" ]
}
