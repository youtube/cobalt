# Copyright 2023 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/protobuf/proto_library.gni")

source_set("test_support") {
  testonly = true
  sources = [
    "browser_state_management.cc",
    "browser_state_management.h",
    "kids_chrome_management_test_utils.cc",
    "kids_chrome_management_test_utils.h",
    "kids_management_api_server_mock.cc",
    "kids_management_api_server_mock.h",
    "supervised_user_signin_test_utils.cc",
    "supervised_user_signin_test_utils.h",
    "supervised_user_url_filter_test_utils.cc",
    "supervised_user_url_filter_test_utils.h",
    "testutil_proto_fetcher.cc",
    "testutil_proto_fetcher.h",
  ]

  deps = [
    "//base",
    "//base/test:test_support",
    "//components/content_settings/core/browser",
    "//components/prefs",
    "//components/safe_search_api",
    "//components/signin/public/identity_manager",
    "//components/signin/public/identity_manager:test_support",
    "//components/supervised_user/core/browser",
    "//components/supervised_user/core/browser:fetcher",
    "//components/supervised_user/core/browser:fetcher_config",
    "//components/supervised_user/core/browser/proto",
    "//components/supervised_user/core/common",
    "//net:test_support",
    "//net/traffic_annotation",
    "//services/network/public/cpp",
    "//url",
  ]
}

# Repository of test accounts is only supported for platforms that utilize end-to-end tests
if (is_win || is_ios || is_linux) {
  source_set("account_repository") {
    testonly = true
    sources = [
      "account_repository.cc",
      "account_repository.h",
    ]

    deps = [
      "//base",
      "//components/supervised_user/core/browser/proto",
    ]
  }
  source_set("account_repository_unit_tests") {
    testonly = true

    sources = [ "account_repository_unittest.cc" ]

    deps = [
      ":account_repository",
      "//base",
      "//testing/gmock",
      "//testing/gtest",
    ]

    if (is_chrome_branded) {
      sources += [ "account_repository_internal_unittest.cc" ]
      data = [
        "//chrome/browser/internal/resources/supervised_user/ios_test_accounts.json",
        "//chrome/browser/internal/resources/supervised_user/linux_test_accounts.json",
        "//chrome/browser/internal/resources/supervised_user/win_test_accounts.json",
      ]
    }
  }
}
