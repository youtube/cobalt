# Copyright 2019 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//android_webview/system_webview_apk_tmpl.gni")
import("//build/config/android/config.gni")
import("//build/config/android/rules.gni")

android_library("webengine_java_tests") {
  testonly = true
  sources = [
    "src/org/chromium/webengine/test/AllowedOriginsTest.java",
    "src/org/chromium/webengine/test/CookieManagerTest.java",
    "src/org/chromium/webengine/test/ExecuteScriptTest.java",
    "src/org/chromium/webengine/test/ExternalIntentsTest.java",
    "src/org/chromium/webengine/test/FaviconTest.java",
    "src/org/chromium/webengine/test/FullscreenCallbackTest.java",
    "src/org/chromium/webengine/test/PostMessageTest.java",
    "src/org/chromium/webengine/test/StatePersistenceTest.java",
    "src/org/chromium/webengine/test/TabManagerTest.java",
    "src/org/chromium/webengine/test/TabNavigationControllerTest.java",
    "src/org/chromium/webengine/test/TabPrerenderTest.java",
    "src/org/chromium/webengine/test/WebEngineTest.java",
    "src/org/chromium/webengine/test/WebFragmentTest.java",
    "src/org/chromium/webengine/test/WebSandboxTest.java",
  ]
  deps = [
    ":webengine_instrumentation_test_java",
    ":webengine_java_test_support",
    "//base:base_java",
    "//base:base_java_test_support",
    "//components/browser_ui/share/android:java",
    "//components/safe_browsing/android:safe_browsing_java",
    "//content/public/android:content_java",
    "//content/public/test/android:content_java_test_support",
    "//net/android:net_java_test_support",
    "//third_party/android_deps:com_google_guava_listenablefuture_java",
    "//third_party/android_deps:guava_android_java",
    "//third_party/androidx:androidx_activity_activity_java",
    "//third_party/androidx:androidx_annotation_annotation_java",
    "//third_party/androidx:androidx_appcompat_appcompat_java",
    "//third_party/androidx:androidx_core_core_java",
    "//third_party/androidx:androidx_fragment_fragment_java",
    "//third_party/androidx:androidx_test_core_java",
    "//third_party/androidx:androidx_test_monitor_java",
    "//third_party/androidx:androidx_test_runner_java",
    "//third_party/blink/public/common:common_java",
    "//third_party/hamcrest:hamcrest_core_java",
    "//third_party/junit:junit",
    "//weblayer/public/java:webengine_java",
    "//weblayer/public/javatests:weblayer_public_javatests",
  ]
}

_webengine_instrumentation_test_strings =
    "$target_gen_dir/instrumentation_test_apk/res/values/strings.xml"

jinja_template("webengine_instrumentation_test_strings") {
  input = "instrumentation_test_apk/res_template/values/strings.xml"
  output = _webengine_instrumentation_test_strings
}

android_resources("webengine_instrumentation_resources") {
  sources = [
    "instrumentation_test_apk/res/layout/main.xml",
    "instrumentation_test_apk/res/values/styles.xml",
    _webengine_instrumentation_test_strings,
  ]
  deps = [ ":webengine_instrumentation_test_strings" ]
}

android_library("webengine_instrumentation_test_java") {
  testonly = true
  resources_package = "org.chromium.webengine.test.instrumentation_test_apk"

  sources = [ "instrumentation_test_apk/src/org/chromium/webengine/test/instrumentation_test_apk/InstrumentationActivity.java" ]

  deps = [
    ":webengine_instrumentation_resources",
    "//third_party/android_deps:com_google_guava_listenablefuture_java",
    "//third_party/androidx:androidx_appcompat_appcompat_java",
    "//third_party/androidx:androidx_fragment_fragment_java",
    "//weblayer/public/java:webengine_java",
  ]
}

_webengine_instrumentation_apk_android_manifest =
    "$target_gen_dir/instrumentation_test_apk/AndroidManifest.xml"

jinja_template("webengine_instrumentation_apk_manifest") {
  input = "instrumentation_test_apk/AndroidManifest.xml"
  output = _webengine_instrumentation_apk_android_manifest

  variables = [ "browser_process_mode=local" ]
}

android_apk("webengine_instrumentation_test_apk") {
  testonly = true

  apk_name = "WebengineInstrumentationTest"
  android_manifest = _webengine_instrumentation_apk_android_manifest
  android_manifest_dep = ":webengine_instrumentation_apk_manifest"

  deps = [
    ":webengine_instrumentation_test_java",
    "//weblayer/public/java:service_provider_java",
    "//weblayer/shell/android:webengine_service_provider_resources",
  ]

  # Test runner does not support having "additional apks" that are incremental.
  never_incremental = true

  target_sdk_version = 28
}

android_library("webengine_java_test_support") {
  testonly = true
  sources = [
    "src/org/chromium/webengine/test/DigitalAssetLinksServerRule.java",
    "src/org/chromium/webengine/test/InstrumentationActivityTestRule.java",
    "src/org/chromium/webengine/test/WebEngineActivityTestRule.java",
    "src/org/chromium/webengine/test/WebEngineJUnit4ClassRunner.java",
  ]
  deps = [
    ":webengine_instrumentation_test_java",
    "//base:base_java",
    "//base:base_java_test_support",
    "//content/public/test/android:content_java_test_support",
    "//net/android:net_java_test_support",
    "//third_party/android_deps:com_google_guava_listenablefuture_java",
    "//third_party/androidx:androidx_annotation_annotation_java",
    "//third_party/androidx:androidx_fragment_fragment_java",
    "//third_party/androidx:androidx_test_monitor_java",
    "//third_party/junit:junit",
    "//ui/android:ui_java_test_support",
    "//weblayer/public/java:webengine_java",
  ]
}

android_library("external_intents_apk_java") {
  sources = [ "external_intents/src/org/chromium/webengine/test/external/intents/ExternalIntentsTestActivity.java" ]
}

android_apk("external_intents_apk") {
  apk_name = "external_intents_apk"
  android_manifest = "external_intents/AndroidManifest.xml"

  deps = [
    ":external_intents_apk_java",
    "//weblayer/public/java:webengine_java",
  ]
}

template("webengine_instrumentation") {
  instrumentation_test_apk(target_name) {
    forward_variables_from(invoker, "*")

    android_manifest = "AndroidManifest.xml"

    if (!defined(additional_apks)) {
      additional_apks = []
    }
    additional_apks += [ "//net/android:net_test_support_apk" ]
    data = [
      "//content/test/data/android/authenticator.html",
      "//weblayer/test/data/",
    ]

    # The instrumentation test should not be obfuscated otherwise the shell cannot be loaded.
    proguard_enabled = false
  }
}

# Runs the instrumentation tests loading the implementation from the WebLayer
# support library.
webengine_instrumentation("webengine_support_instrumentation_test_apk") {
  apk_name = "WebEngineSupportInstrumentationTest"
  apk_under_test = ":webengine_instrumentation_test_apk"
  additional_apks = [
    "//weblayer/shell/android:weblayer_support_apk",
    ":external_intents_apk",
  ]

  deps = [ ":webengine_java_tests" ]
}
