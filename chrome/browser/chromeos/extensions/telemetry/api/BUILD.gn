# Copyright 2021 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/chromeos/extensions/api/api_sources.gni")
import("//extensions/buildflags/buildflags.gni")
import("//tools/json_schema_compiler/json_schema_api.gni")

assert(enable_extensions,
       "Cannot depend on extensions because enable_extensions=false.")

function_registration("api_registration") {
  sources = chromeos_system_extensions_api_schema_sources
  impl_dir = "//chrome/browser/chromeos/extensions/telemetry/api"
  configs = [ "//build/config:precompiled_headers" ]
  bundle_name = "ChromeOS"
  root_namespace = chromeos_system_extensions_api_root_namespace
  schema_include_rules = chromeos_system_extensions_api_schema_include_rules

  deps = [
    "diagnostics",
    "events",
    "management",
    "telemetry",
    "//base",
    "//extensions/common",
  ]
}

source_set("keyed_service_factory") {
  sources = [
    "telemetry_extension_api_browser_context_keyed_service_factories.cc",
    "telemetry_extension_api_browser_context_keyed_service_factories.h",
  ]

  deps = [
    "events:event_manager",
    "routines:routine_manager",
  ]
}

source_set("browser_tests") {
  testonly = true

  deps = [
    "common:browser_tests",
    "diagnostics:browser_tests",
    "events:browser_tests",
    "management:browser_tests",
    "routines:browser_tests",
    "telemetry:browser_tests",
  ]
}

source_set("extensions_unittests") {
  testonly = true

  deps = [ "events:extensions_unittests" ]
}

source_set("unit_tests") {
  testonly = true

  deps = [
    "common:unit_tests",
    "diagnostics:unit_tests",
    "events:unit_tests",
    "routines:unit_tests",
    "telemetry:unit_tests",
  ]
}
