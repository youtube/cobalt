# Copyright 2017 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/buildflag_header.gni")
import("//build/config/chrome_build.gni")

declare_args() {
  # Whether software reporter is enabled for this build. This defaults to true
  # for Chrome branded builds, but can be disabled manually if a Chrome branded
  # build is required without necessarily needing the software reporter.
  enable_software_reporter = is_chrome_branded
}

source_set("constants") {
  sources = [
    "constants.cc",
    "constants.h",
    "result_codes.h",
  ]
  public_deps = [ ":buildflags" ]
}

buildflag_header("buildflags") {
  header = "buildflags.h"
  flags = [ "ENABLE_SOFTWARE_REPORTER=$enable_software_reporter" ]
}
