# Copyright 2018 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//ui/webui/resources/tools/build_webui.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(!is_chromeos_ash && !is_android)

if (is_chrome_branded) {
  generate_grd("build_icons_grdp") {
    grd_prefix = "welcome_images"
    out_grd = "$target_gen_dir/icon_resources.grdp"
    input_files = [
      "module_icons/add_bookmarks.svg",
      "module_icons/pick_a_background.svg",
      "module_icons/set_default_dark.svg",
      "module_icons/set_default_light.svg",
      "ntp_thumbnails/art.jpg",
      "ntp_thumbnails/cityscape.jpg",
      "ntp_thumbnails/earth.jpg",
      "ntp_thumbnails/geometric_shapes.jpg",
      "ntp_thumbnails/landscape.jpg",
      "set_default_dark.svg",
      "set_default_light.svg",
    ]
    input_files_base_dir =
        rebase_path("//chrome/app/theme/google_chrome/welcome/", "//")
    resource_path_prefix = "images"
  }
}

build_webui("build") {
  grd_prefix = "welcome"

  static_files = [
    "images/background_svgs/bookmarks_background.svg",
    "images/background_svgs/bookmarks_foreground.svg",
    "images/background_svgs/devices_check.svg",
    "images/background_svgs/devices.svg",
    "images/background_svgs/hexagon.svg",
    "images/background_svgs/lozenge.svg",
    "images/background_svgs/password_field.svg",
    "images/background_svgs/password.svg",
    "images/background_svgs/square.svg",
    "images/background_svgs/streamer_circle.svg",
    "images/background_svgs/streamer_line.svg",
    "images/background_svgs/triangle.svg",
    "welcome.html",
    "welcome.css",
  ]

  if (is_chrome_branded) {
    # Additional static files that need to be passed separately since they have
    # a different |input_files_base_dir|
    extra_grdp_deps = [ ":build_icons_grdp" ]
    extra_grdp_files = [ "$target_gen_dir/icon_resources.grdp" ]
  }

  non_web_component_files = [
    "landing_view.html.ts",
    "landing_view_proxy.ts",
    "landing_view.ts",
    "navigation_mixin.ts",
    "router.ts",
    "signin_view.html.ts",
    "signin_view_proxy.ts",
    "signin_view.ts",
    "welcome_app.html.ts",
    "welcome_app.ts",
    "welcome_browser_proxy.ts",

    "google_apps/google_app_proxy.ts",
    "google_apps/google_apps_metrics_proxy.ts",
    "google_apps/nux_google_apps.html.ts",
    "google_apps/nux_google_apps.ts",
    "ntp_background/ntp_background_metrics_proxy.ts",
    "ntp_background/ntp_background_proxy.ts",
    "ntp_background/nux_ntp_background.html.ts",
    "ntp_background/nux_ntp_background.ts",
    "set_as_default/nux_set_as_default.html.ts",
    "set_as_default/nux_set_as_default_proxy.ts",
    "set_as_default/nux_set_as_default.ts",
    "shared/bookmark_proxy.ts",
    "shared/module_metrics_proxy.ts",
    "shared/nux_types.ts",
    "shared/onboarding_background.html.ts",
    "shared/onboarding_background.ts",
    "shared/step_indicator.html.ts",
    "shared/step_indicator.ts",
  ]

  # Files that are passed as input to css_to_wrapper().
  css_files = [
    "google_apps/nux_google_apps.css",
    "landing_view.css",
    "ntp_background/nux_ntp_background.css",
    "set_as_default/nux_set_as_default.css",
    "shared/action_link_style.css",
    "shared/animations.css",
    "shared/chooser_shared.css",
    "shared/navi_colors.css",
    "shared/onboarding_background.css",
    "shared/splash_pages_shared.css",
    "shared/step_indicator.css",
    "signin_view.css",
    "welcome_app.css",
  ]

  icons_html_files = [ "shared/icons.html" ]

  ts_composite = true
  ts_definitions = [
    "//tools/typescript/definitions/bookmarks.d.ts",
    "//tools/typescript/definitions/chrome_event.d.ts",
    "//tools/typescript/definitions/chrome_send.d.ts",
    "//tools/typescript/definitions/metrics_private.d.ts",
  ]
  ts_deps = [
    "//third_party/lit/v3_0:build_ts",
    "//ui/webui/resources/cr_elements:build_ts",
    "//ui/webui/resources/js:build_ts",
  ]

  html_to_wrapper_template = "detect"
  webui_context_type = "trusted"
}
