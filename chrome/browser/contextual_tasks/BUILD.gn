# Copyright 2025 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")
import("//third_party/protobuf/proto_library.gni")

# This target is only for public headers for classes used outside this directory (i.e. services and factories).
source_set("contextual_tasks") {
  sources = [
    "ai_mode_context_library_converter.h",
    "contextual_tasks_context_controller.h",
    "contextual_tasks_context_controller_factory.h",
    "contextual_tasks_service_factory.h",
  ]

  if (!is_android) {
    sources += [
      "active_task_context_provider.h",
      "active_task_context_provider_impl.h",
      "contextual_tasks_context_service.h",
      "contextual_tasks_context_service_factory.h",
    ]
  }

  deps = [
    "//base",
    "//components/tabs:public",
  ]

  public_deps = [
    "//chrome/browser/profiles:profile",
    "//components/contextual_tasks/public",
    "//components/keyed_service/core",
  ]
  if (!is_android) {
    public_deps += [
      ":mojo_bindings",
      "//chrome/browser/passage_embeddings",
      "//components/passage_embeddings:passage_embeddings_types",
    ]
  }
}

# This target is for all non-public files (all .c - including factories and services - and non-public .h).
source_set("impl") {
  visibility = [
    ":*",
    "//chrome/browser",
  ]

  sources = [
    "ai_mode_context_library_converter.cc",
    "contextual_tasks_context_controller.cc",
    "contextual_tasks_context_controller_factory.cc",
    "contextual_tasks_context_controller_impl.cc",
    "contextual_tasks_context_controller_impl.h",
    "contextual_tasks_service_factory.cc",
  ]
  if (!is_android) {
    sources += [
      "active_task_context_provider_impl.cc",
      "contextual_tasks_context_service.cc",
      "contextual_tasks_context_service_factory.cc",
      "contextual_tasks_signal_utils.cc",
      "contextual_tasks_signal_utils.h",
      "tab_strip_context_decorator.cc",
      "tab_strip_context_decorator.h",
    ]
  }

  deps = [
    ":contextual_tasks",
    "//chrome/browser/autocomplete:aim_eligibility_service",
    "//chrome/browser/favicon",
    "//chrome/browser/history",
    "//chrome/browser/optimization_guide",
    "//chrome/browser/page_content_annotations",
    "//chrome/browser/signin",
    "//chrome/browser/sync",
    "//chrome/common:channel_info",
    "//components/contextual_search:public",
    "//components/contextual_tasks/internal",
    "//components/contextual_tasks/public",
    "//components/favicon/core",
    "//components/history/core/browser",
    "//components/keyed_service/core",
    "//components/omnibox/browser",
    "//components/sessions:session_id",
    "//components/sync/model",
    "//content/public/browser",
    "//third_party/lens_server_proto:lens_overlay_proto",
  ]
  if (!is_android) {
    deps += [
      ":ui",
      "//chrome/browser:browser_public_dependencies",
      "//chrome/browser/contextual_search",
      "//chrome/browser/page_content_annotations:extraction_service",
      "//chrome/browser/passage_embeddings",
      "//chrome/browser/ui",
      "//chrome/browser/ui:browser_list",
      "//chrome/browser/ui/browser_window",
      "//chrome/browser/ui/tabs:tab_strip",
      "//components/contextual_search:public",
      "//components/optimization_guide/core:model_execution",
      "//components/optimization_guide/proto:optimization_guide_proto",
      "//components/tabs:public",
      "//components/url_deduplication",
      "//components/visited_url_ranking/public",
    ]
  }
}

if (!is_android) {
  source_set("ui") {
    sources = [
      "contextual_tasks_composebox_handler.h",
      "contextual_tasks_internals_page_handler.h",
      "contextual_tasks_navigation_throttle.h",
      "contextual_tasks_page_handler.h",
      "contextual_tasks_side_panel_coordinator.h",
      "contextual_tasks_ui.h",
      "contextual_tasks_ui_service.h",
      "contextual_tasks_ui_service_factory.h",
      "task_info_delegate.h",
    ]

    public_deps = [
      ":contextual_tasks",
      ":mojo_bindings",
      "//base",
      "//chrome/browser/contextual_search",
      "//chrome/browser/ui/browser_window",
      "//chrome/browser/ui/tabs:tab_strip",
      "//chrome/browser/ui/views/side_panel",
      "//chrome/browser/ui/webui/cr_components/composebox",
      "//chrome/browser/ui/webui/searchbox",
      "//chrome/browser/ui/webui/top_chrome",
      "//components/contextual_tasks/public:feature_list",
      "//content/public/browser",
      "//content/public/common",
      "//third_party/lens_server_proto:lens_overlay_proto",
      "//ui/webui",
      "//ui/webui/resources/cr_components/composebox:mojo_bindings",
      "//url",
    ]
  }

  source_set("ui_impl") {
    sources = [
      "contextual_tasks_composebox_handler.cc",
      "contextual_tasks_internals_page_handler.cc",
      "contextual_tasks_navigation_throttle.cc",
      "contextual_tasks_page_handler.cc",
      "contextual_tasks_side_panel_coordinator.cc",
      "contextual_tasks_ui.cc",
      "contextual_tasks_ui_service.cc",
      "contextual_tasks_ui_service_factory.cc",
    ]

    deps = [
      ":ui",
      "//base",
      "//chrome/browser:global_features",
      "//chrome/browser/autocomplete:aim_eligibility_service",
      "//chrome/browser/profiles:profile",
      "//chrome/browser/resources/contextual_tasks:resources",
      "//chrome/browser/signin",
      "//chrome/browser/ui",
      "//chrome/browser/ui/browser_window",
      "//chrome/browser/ui/contextual_search",
      "//chrome/browser/ui/lens",
      "//chrome/browser/ui/omnibox",
      "//chrome/browser/ui/tabs:tab_strip",
      "//chrome/browser/ui/views/interaction",
      "//chrome/browser/ui/webui",
      "//chrome/browser/ui/webui:webui_util",
      "//chrome/browser/ui/webui/cr_components/searchbox",
      "//components/application_locale_storage",
      "//components/contextual_tasks/public:feature_list",
      "//components/keyed_service/core",
      "//components/lens",
      "//components/omnibox/browser:mojo_bindings",
      "//google_apis",
      "//third_party/lens_server_proto:lens_overlay_proto",
      "//third_party/protobuf:protobuf_lite",
      "//url",
    ]
  }

  mojom("mojo_bindings") {
    sources = [
      "contextual_tasks.mojom",
      "contextual_tasks_internals.mojom",
      "contextual_tasks_types.mojom",
    ]
    webui_module_path = "/"
    public_deps = [
      "//mojo/public/mojom/base",
      "//ui/webui/resources/cr_components/composebox:mojo_bindings",
      "//url/mojom:url_mojom_gurl",
    ]
  }
}

source_set("test_support") {
  testonly = true

  sources = [
    "mock_contextual_tasks_context_controller.cc",
    "mock_contextual_tasks_context_controller.h",
  ]

  deps = [
    ":contextual_tasks",
    "//testing/gmock",
  ]
}

if (!is_android) {
  source_set("browser_tests") {
    testonly = true
    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
    sources = [
      "contextual_tasks_context_service_browsertest.cc",
      "contextual_tasks_ui_browsertest.cc",
    ]

    deps = [
      ":contextual_tasks",
      ":test_support",
      ":ui",
      "//base",
      "//base/test:test_support",
      "//chrome/browser:browser_process",
      "//chrome/browser/optimization_guide",
      "//chrome/browser/optimization_guide:test_support",
      "//chrome/browser/page_content_annotations",
      "//chrome/browser/page_content_annotations:extraction_service",
      "//chrome/browser/profiles:profile",
      "//chrome/browser/signin",
      "//chrome/browser/ui",
      "//chrome/test:test_support",
      "//chrome/test:test_support_ui",
      "//components/contextual_tasks/public:feature_list",
      "//components/optimization_guide/core:model_execution_test_support",
      "//components/page_content_annotations/core",
      "//components/page_content_annotations/core:test_support",
      "//components/passage_embeddings",
      "//components/passage_embeddings:test_support",
      "//components/signin/public/base",
      "//components/signin/public/identity_manager",
      "//components/signin/public/identity_manager:test_support",
      "//content/test:test_support",
      "//google_apis",
      "//testing/gmock",
      "//testing/gtest",
    ]
  }
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "ai_mode_context_library_converter_unittest.cc",
    "contextual_tasks_context_controller_factory_unittest.cc",
    "contextual_tasks_context_controller_impl_unittest.cc",
    "contextual_tasks_service_factory_unittest.cc",
  ]

  if (!is_android) {
    sources += [
      "contextual_tasks_composebox_handler_unittest.cc",
      "contextual_tasks_context_service_factory_unittest.cc",
      "contextual_tasks_side_panel_coordinator_unittest.cc",
      "contextual_tasks_signal_utils_unittest.cc",
      "contextual_tasks_ui_service_unittest.cc",
      "contextual_tasks_ui_unittest.cc",
      "tab_strip_context_decorator_unittest.cc",
    ]
  }

  deps = [
    ":contextual_tasks",
    ":impl",
    ":test_support",
    "//base",
    "//base/test:test_support",
    "//chrome/browser/sync",
    "//chrome/test:test_support",
    "//components/contextual_search:public",
    "//components/contextual_tasks/public",
    "//content/test:test_support",
    "//services/network/public/cpp",
    "//testing/gmock",
    "//testing/gtest",
    "//third_party/lens_server_proto:lens_overlay_proto",
  ]

  if (!is_android) {
    deps += [
      ":ui",
      ":ui_impl",
      "//chrome/browser/contextual_search",
      "//chrome/browser/search_engines",
      "//chrome/browser/ui/contextual_search",
      "//chrome/browser/ui/tabs:test_support",
      "//chrome/browser/ui/webui/searchbox:searchbox_test_utils",
      "//components/contextual_search:public",
      "//components/contextual_search:test_support",
      "//components/passage_embeddings",
      "//components/tabs:test_support",
      "//components/variations:test_support",
      "//chrome/browser/ui/webui:webui_util",
    ]
  }
}

source_set("interactive_ui_tests") {
  testonly = true
  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

  sources = []

  if (!is_android) {
    sources += [
      "contextual_tasks_side_panel_coordinator_interactive_uitest.cc",
      "contextual_tasks_ui_service_interactive_uitest.cc",
    ]
  }

  deps = [
    ":contextual_tasks",
    ":impl",
    "//base",
    "//base/test:test_support",
    "//chrome/test:test_support",
    "//components/contextual_tasks/public",
    "//content/test:test_support",
    "//services/network/public/cpp",
    "//testing/gmock",
    "//testing/gtest",
  ]

  if (!is_android) {
    deps += [ ":ui" ]
  }
}
