# Copyright 2019 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/config.gni")
import("//build/config/android/rules.gni")
import("//third_party/jni_zero/jni_zero.gni")

static_library("internal") {
  sources = [
    "compositor_view_impl.cc",
    "compositor_view_impl.h",
    "thin_webview.cc",
    "thin_webview.h",
  ]

  deps = [
    ":jni_headers",
    "//cc/slim",
    "//skia",
  ]

  public_deps = [
    "//base",
    "//components/embedder_support/android:web_contents_delegate",
    "//components/thin_webview",
    "//content/public/browser",
    "//ui/android",
  ]
}

android_library("internal_java") {
  srcjar_deps = [ ":jni_headers" ]
  sources = [
    "java/src/org/chromium/components/thinwebview/internal/CompositorViewImpl.java",
    "java/src/org/chromium/components/thinwebview/internal/ThinWebViewImpl.java",
  ]

  deps = [
    "//base:base_java",
    "//build/android:build_java",
    "//components/embedder_support/android:web_contents_delegate_java",
    "//components/thin_webview:java",
    "//content/public/android:content_java",
    "//third_party/androidx:androidx_annotation_annotation_java",
    "//third_party/jni_zero:jni_zero_java",
    "//ui/android:ui_java",
  ]
}

generate_jni("jni_headers") {
  visibility = [ ":*" ]
  sources = [
    "java/src/org/chromium/components/thinwebview/internal/CompositorViewImpl.java",
    "java/src/org/chromium/components/thinwebview/internal/ThinWebViewImpl.java",
  ]
}
