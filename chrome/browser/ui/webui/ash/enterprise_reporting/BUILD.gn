# Copyright 2023 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")

assert(is_chromeos_ash, "Enterprise Reporting WebUI is ChromeOS Ash only.")

mojom("mojo_bindings") {
  sources = [ "enterprise_reporting.mojom" ]
  webui_module_path = "/"
}

static_library("enterprise_reporting") {
  sources = [
    "enterprise_reporting_page_handler.cc",
    "enterprise_reporting_page_handler.h",
    "enterprise_reporting_ui.cc",
    "enterprise_reporting_ui.h",
    "history_converter.cc",
    "history_converter.h",
  ]

  public_deps = [
    ":mojo_bindings",
    "//ash/webui/common:chrome_os_webui_config",
    "//base",
    "//chrome/browser:browser_public_dependencies",
    "//chrome/common",
    "//chromeos/dbus/missive:history_tracker",
    "//components/reporting/proto:health_proto",
    "//components/reporting/proto:status_proto",
    "//content/public/common",
    "//mojo/public/cpp/bindings",
    "//ui/webui",
  ]

  deps = [
    "//ash/constants",
    "//chrome/browser:browser_process",
    "//chrome/browser/ash/policy/core",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/profiles:profile_util",
    "//chrome/browser/resources/chromeos/enterprise_reporting:resources",
    "//chrome/browser/ui/webui:webui_util",
    "//components/reporting/util:status",
    "//components/version_info",
    "//content/public/browser",
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [ "history_converter_unittest.cc" ]

  deps = [
    ":enterprise_reporting",
    ":mojo_bindings",
    "//components/reporting/proto:health_proto",
    "//components/reporting/proto:record_constants",
    "//components/reporting/proto:status_proto",
    "//components/reporting/util:status",
    "//testing/gmock",
    "//testing/gtest",
  ]
}
