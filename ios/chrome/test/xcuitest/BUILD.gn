# Copyright 2021 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ios/rules.gni")
import("//ios/chrome/test/xcuitest/ios_chrome_xcuitest_app_host.gni")

ios_chrome_xcuitest_app_host("ios_chrome_xcuitests") {
  use_default_test_hooks = true
  deps = [
    # Use the chromium implementation of the provider API.
    "//ios/chrome/browser/providers:chromium_providers",
  ]
}

ios_xcuitest_test("ios_chrome_device_check_xcuitests_module") {
  xcode_test_application_name = "ios_chrome_xcuitests"
  deps = [ ":device_check_xcuitests" ]
  data_deps = [ ":ios_chrome_xcuitests" ]

  # Those frameworks are force-linked via //base/test:test_support, so
  # ensure the framework bundles are packaged in the test runner and
  # not the test module.
  runner_only_bundle_deps = [ "//base/test:test_trace_processor+bundle" ]
}

# This target is only used in routinely device checks.
source_set("device_check_xcuitests") {
  configs += [ "//build/config/ios:xctest_config" ]
  testonly = true
  sources = [ "device_check_xctest.mm" ]
  deps = [
    "//base/test:test_support",
    "//ios/chrome/browser/ui/content_suggestions:content_suggestions_constant",
    "//ios/testing:system_alert_handler",
  ]
  frameworks = [ "UIKit.framework" ]
}

source_set("hooks") {
  testonly = true
  sources = [ "xcuitests_hook.mm" ]
  deps = [
    "//components/signin/internal/identity_manager",
    "//ios/chrome/app:tests_hook",
  ]
}
