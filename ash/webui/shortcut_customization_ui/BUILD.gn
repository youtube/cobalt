# Copyright 2021 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chrome_build.gni")
import("//build/config/chromeos/ui_mode.gni")

assert(is_chromeos_ash, "Shortcut Customization App is ash-chrome only")

static_library("shortcut_customization_ui") {
  sources = [
    "shortcut_customization_app_ui.cc",
    "shortcut_customization_app_ui.h",
    "shortcuts_app_manager.cc",
    "shortcuts_app_manager.h",
    "shortcuts_app_manager_factory.cc",
    "shortcuts_app_manager_factory.h",
  ]

  public_deps = [ ":url_constants" ]

  deps = [
    "//ash",
    "//ash/constants",
    "//ash/webui/common:trusted_types_util",
    "//ash/webui/common/backend",
    "//ash/webui/common/mojom",
    "//ash/webui/shortcut_customization_ui/backend",
    "//ash/webui/shortcut_customization_ui/backend/search:mojo_bindings",
    "//ash/webui/shortcut_customization_ui/mojom",
    "//ash/webui/shortcut_customization_ui/resources:resources",
    "//ash/webui/system_apps/public:system_web_app_config",
    "//build:branding_buildflags",
    "//chromeos/ash/components/local_search_service/public/cpp",
    "//chromeos/strings",
    "//components/keyed_service/content:content",
    "//components/user_prefs",
    "//content/public/browser",
    "//ui/resources:webui_resources_grd_grit",
    "//ui/webui",
  ]

  if (is_chrome_branded) {
    deps += [ "//chromeos/ash/resources/internal/strings" ]
  }
}

# Url constants pulled out to enable depending on production url constants in
# browser tests.
source_set("url_constants") {
  sources = [
    "url_constants.cc",
    "url_constants.h",
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "backend/accelerator_configuration_provider_unittest.cc",
    "backend/accelerator_layout_table_unittest.cc",
    "backend/search/search_concept_registry_unittest.cc",
    "backend/search/search_handler_unittest.cc",
    "shortcuts_app_manager_unittest.cc",
  ]

  deps = [
    ":shortcut_customization_ui",
    "//ash",
    "//ash:test_support",
    "//ash/public/cpp",
    "//ash/public/mojom",
    "//ash/webui/shortcut_customization_ui/backend",
    "//ash/webui/shortcut_customization_ui/backend/search:mojo_bindings",
    "//ash/webui/shortcut_customization_ui/mojom",
    "//base/test:test_support",
    "//chromeos/ash/components:test_support",
    "//chromeos/ash/components/local_search_service/public/cpp:cpp",
    "//chromeos/ash/components/local_search_service/public/mojom",
    "//chromeos/ash/components/local_search_service/public/mojom:mojom",
    "//content/test:test_support",
    "//device/udev_linux:test_support",
    "//mojo/public/cpp/bindings:bindings",
    "//testing/gtest",
    "//ui/base:test_support",
    "//ui/base/ime/ash",
    "//ui/events/ash",
    "//ui/events/devices",
    "//ui/events/devices:test_support",
  ]
}
