# Copyright 2023 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/apple/compile_entitlements.gni")
import("//build/apple/tweak_info_plist.gni")
import("//build/config/ios/rules.gni")
import("//ios/build/chrome_build.gni")
import("//ios/build/config.gni")

tweak_info_plist("info_plist") {
  info_plist = "Info.plist"
}

compile_entitlements("entitlements") {
  substitutions = [
    "IOS_BUNDLE_ID_PREFIX=$ios_app_bundle_id_prefix",
    "CHROMIUM_BUNDLE_ID=$ios_chromium_bundle_id",
  ]
  entitlements_templates =
      [ "entitlements/external/intents_extension.entitlements" ]
  output_name = "$target_gen_dir/intents_extension.appex.entitlements"
}

ios_appex_bundle("appex") {
  output_name = "intents_extension"
  deps = [ "//ios/chrome/intents_extension:intents" ]

  bundle_deps_filter = [ "//third_party/icu:icudata" ]
  assert_no_deps = ios_extension_assert_no_deps

  entitlements_target = ":entitlements"
  info_plist_target = ":info_plist"
  bundle_identifier = "$ios_chromium_bundle_id.IntentsExtension"
}
