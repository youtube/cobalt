# Copyright 2021 The Cobalt Authors. All Rights Reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

config("compiler_flags") {
  cflags = []
  cflags_c = []
  cflags_cc = []
  defines = [
    "WARN_UNUSED_RESULT=",
    "ALLOW_UNUSED_TYPE=",
    "BASE_HASH_NAMESPACE=std",
    "BASE_HASH_USE_HASH_STRUCT",
  ]
  ldflags = [ "-Wl,--error-limit=0" ]

  if (is_debug) {
    cflags += [ "-O0" ]
  } else if (is_devel) {
    cflags += [ "-O2" ]
  } else {
    cflags += [ "-gline-tables-only" ]
  }

  if (is_clang) {
    ldflags += [ "-fuse-ld=lld" ]
    cflags += [
      "-Werror",
      "-fcolor-diagnostics",

      # Warn for implicit type conversions that may change a value.
      "-Wconversion",

      # Warns on switches on enums that cover all enum values but
      # also contain a default: branch. Chrome is full of that.
      "-Wno-covered-switch-default",

      # protobuf uses hash_map.
      "-Wno-deprecated",

      # Don"t warn about the "struct foo f = {0};" initialization pattern.
      "-Wno-missing-field-initializers",

      # Do not warn for implicit sign conversions.
      "-Wno-sign-conversion",
      "-fno-strict-aliasing",  # See http://crbug.com/32204

      # Triggered by the COMPILE_ASSERT macro.
      "-Wno-unused-local-typedef",

      # Do not warn if a function or variable cannot be implicitly
      # instantiated.
      "-Wno-undefined-var-template",

      # Do not warn about an implicit exception spec mismatch.
      "-Wno-implicit-exception-spec-mismatch",

      # Do not warn about unused function params.
      "-Wno-unused-parameter",
    ]
    if (sb_is_modular && !sb_is_evergreen) {
      # If we're building with cobalt toolchain and native linker, we need visibility.
      cflags += [ "-fvisibility=default" ]
    } else {
      # Default visibility to hidden, to enable dead stripping.
      cflags += [ "-fvisibility=hidden" ]
    }

    if (is_gold) {
      cflags += [
        # Don't generate an eh_frame and eh_frame_hdr section.
        "-fno-asynchronous-unwind-tables",
      ]
    }
  }

  if (is_clang_16) {
    cflags += [
      # Do not warn about an implicit conversion from int to float
      "-Wno-implicit-int-float-conversion",

      # Do not warn about an implicit conversion from int to char16
      "-Wno-implicit-int-conversion",

      # Do not remove null pointer checks.
      "-fno-delete-null-pointer-checks",
    ]
  }

  if (!cobalt_fastbuild && (is_debug || is_devel)) {
    cflags += [ "-g" ]
  }

  defines += [
    # By default, <EGL/eglplatform.h> pulls in some X11 headers that have some
    # nasty macros (|Status|, for example) that conflict with Chromium base.
    "MESA_EGL_NO_X11_HEADERS",
  ]
  cflags_c += [
    # Limit to C99. This allows Linux to be a canary build for any
    # C11 features that are not supported on some platforms' compilers.
    "-std=c99",
  ]
  cflags_cc = [ "-std=c++17" ]

  if (use_asan) {
    cflags += [
      "-fsanitize=address",
      "-fno-omit-frame-pointer",
    ]
    ldflags += [
      "-fsanitize=address",

      # Force linking of the helpers in sanitizer_options.cc
      "-Wl,-u_sanitizer_options_link_helper",
    ]
    defines += [ "ADDRESS_SANITIZER" ]
    if (asan_symbolizer_path != "") {
      defines += [ "ASAN_SYMBOLIZER_PATH=\"${asan_symbolizer_path}\"" ]
    }
  } else if (use_tsan) {
    cflags += [
      "-fsanitize=thread",
      "-fno-omit-frame-pointer",
    ]
    ldflags += [ "-fsanitize=thread" ]
    defines += [ "THREAD_SANITIZER" ]
  }
}

config("platform_configuration") {
  defines = []
  ldflags = []
  libs = [
    "asound",
    "dl",
    "pthread",
    "rt",
  ]

  if (!sb_is_modular || current_toolchain != cobalt_toolchain) {
    ldflags += [ "-static-libstdc++" ]
  }

  defines += [
    # Defined to get format macro constants from <inttypes.h>.
    "__STDC_FORMAT_MACROS",
    "COBALT_LINUX",

    # Enable GNU extensions to get prototypes like ffsl.
    "_GNU_SOURCE=1",
  ]

  if (is_debug) {
    defines += [
      "_GLIBCXX_DEBUG",
      "_LIBCPP_DEBUG=1",
    ]
    configs = [ "//build/config/compiler:rtti" ]
  } else if (is_devel) {
    defines += [
      "_GLIBCXX_DEBUG",
      "_LIBCPP_DEBUG=0",
    ]
    configs = [ "//build/config/compiler:rtti" ]
  }
}

config("libraries") {
  ldflags = [ "-Wl,--wrap=eglSwapBuffers" ]
  libs = [
    "X11",
    "Xcomposite",
    "Xrender",
  ]
}

config("speed") {
  cflags = [ "-O2" ]

  if (is_qa || is_gold) {
    cflags += [
      # Compile symbols in separate sections
      "-ffunction-sections",
      "-fdata-sections",
    ]
  }
}

config("size") {
  cflags = [ "-Os" ]
  if (is_qa || is_gold) {
    cflags += [
      # Compile symbols in separate sections
      "-ffunction-sections",
      "-fdata-sections",
    ]
  }
}

config("pedantic_warnings") {
  cflags = [
    "-Wall",
    "-Wextra",
    "-Wunreachable-code",
  ]
}

config("no_pedantic_warnings") {
  cflags = [
    # 'this' pointer cannot be NULL...pointer may be assumed
    # to always convert to true.
    "-Wno-undefined-bool-conversion",

    # Skia doesn't use overrides.
    "-Wno-inconsistent-missing-override",

    # Do not warn for implicit type conversions that may change a value.
    "-Wno-conversion",

    # shifting a negative signed value is undefined
    "-Wno-shift-negative-value",

    # Width of bit-field exceeds width of its type- value will be truncated
    "-Wno-bitfield-width",
    "-Wno-undefined-var-template",
  ]
  extra_should_be_removed = [
    "-Wno-invalid-noreturn",
    "-Wno-sign-compare",
    "-Wno-unused-function",
    "-Wno-unreachable-code-break",
    "-Wno-tautological-unsigned-zero-compare",
    "-Wno-c++98-compat-extra-semi",
    "-Wno-extra-semi",
    "-Wno-array-parameter",
    "-Wno-unused-variable",
    "-Wno-implicit-fallthrough",
    "-Wno-unused-private-field",
    "-Wno-deprecated-copy",
    "-Wno-macro-redefined",
    "-Wno-unreachable-code-return",
    "-Wno-reorder-ctor",
    "-Wno-unused-local-typedef",
    "-Wno-missing-braces",
    "-Wno-error=missing-braces",
    "-Wno-error=unused-local-typedef",
    "-Wno-error=reorder-ctor",
    "-Wno-error=unreachable-code-return",
    "-Wno-error=sign-compare",
    "-Wno-error=c++98-compat-extra-semi",
    "-Wno-error=shadow",
    "-Wno-error=implicit-fallthrough",
    "-Wno-error=unused-variable",
    "-Wno-error=unused-private-field",
    "-Wno-error=unreachable-code-break",
    "-Wno-error=unused-label",
    "-Wno-error=extra-semi",
    "-Wno-error=unused-but-set-variable",
    "-Wno-error=array-parameter",
    "-Wno-error=unused-but-set-variable",
    "-Wno-error=deprecated-copy",
    "-Wno-error=unused-function",
    "-Wno-mismatched-tags",
    "-Wno-bitwise-instead-of-logical",
    "-Wno-unused-but-set-parameter",
    "-Wno-self-assign-overloaded",
    "-Wno-pessimizing-move",
    "-Wno-ignored-qualifiers",
    "-Wno-unreachable-code-loop-increment",
    "-Wno-c++20-extensions",
    "-Wno-unreachable-code",
    "-Wno-null-pointer-subtraction",
    "-Wno-self-assign",
    "-Wno-shadow",
    "-Wno-sometimes-uninitialized",
    "-Wno-delete-non-abstract-non-virtual-dtor",
  ]
  cflags += extra_should_be_removed
}
