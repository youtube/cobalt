# Copyright 2018 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//net/features.gni")
import("//testing/libfuzzer/fuzzer_test.gni")

static_library("http_server") {
  public = []

  # This target is being deprecated. Use //services/network/public/cpp/server
  # instead.
  visibility = [
    ":net_http_server_fuzzer_deps",
    ":tests",
    "//chrome/browser/devtools",
    "//chrome/test/chromedriver/*",
    "//content/browser",
    "//net:net_web_socket_encoder_fuzzer_deps",
  ]

  friend = [
    ":net_http_server_fuzzer",
    "//net:net_web_socket_encoder_fuzzer",
    ":tests",
    "//chrome/browser/devtools",
    "//chrome/test/chromedriver/*",
    "//content/browser",
  ]

  if (enable_websockets) {
    sources = [
      "http_connection.cc",
      "http_connection.h",
      "http_server.cc",
      "http_server.h",
      "http_server_request_info.cc",
      "http_server_request_info.h",
      "http_server_response_info.cc",
      "http_server_response_info.h",
      "web_socket.cc",
      "web_socket.h",
      "web_socket_encoder.cc",
      "web_socket_encoder.h",
    ]
    configs += [ "//build/config/compiler:wexit_time_destructors" ]
    deps = [
      "//base",
      "//net",
    ]
  }
}

source_set("tests") {
  testonly = true
  if (enable_websockets) {
    sources = [
      "http_connection_unittest.cc",
      "http_server_response_info_unittest.cc",
      "http_server_unittest.cc",
      "web_socket_encoder_unittest.cc",
    ]
    deps = [
      ":http_server",
      "//base",
      "//net",
      "//net:test_support",
      "//testing/gtest",
    ]
  }
}

if (enable_websockets && use_fuzzing_engine) {
  # This is a separate target because //net/server:http_server
  # wants to limit its visibility to a select number of target, but the
  # "fuzzer_test" template expands on iOS to many targets that need to
  # have the "deps" information propagated in order to support "fat"
  # binaries.
  source_set("net_http_server_fuzzer_deps") {
    testonly = true
    visibility = [
      ":net_http_server_fuzzer",
      ":net_http_server_fuzzer_executable",
      ":run_net_http_server_fuzzer",
    ]
    public_deps = [
      ":http_server",
      "//base",
      "//net",
      "//net:net_fuzzer_test_support",
      "//net:test_support",
    ]
  }

  fuzzer_test("net_http_server_fuzzer") {
    sources = [ "http_server_fuzzer.cc" ]
    deps = [ ":net_http_server_fuzzer_deps" ]
    dict = "//net/data/fuzzer_dictionaries/net_http_server_fuzzer.dict"
    seed_corpus = "//net/data/fuzzer_data/http_server_requests/"
  }
}
