# Copyright 2025 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")
import("//extensions/buildflags/buildflags.gni")
import("//third_party/jni_zero/jni_zero.gni")

source_set("android") {
  sources = [
    "extensions_url_override_registry_manager.cc",
    "extensions_url_override_registry_manager.h",
    "extensions_url_override_state_tracker.h",
  ]

  deps = [
    ":jni_headers",
    "//base",
    "//chrome/browser/flags:flags_android",
    "//chrome/browser/profiles:profile",
    "//content/public/browser",
    "//extensions/buildflags",
    "//third_party/jni_zero",
  ]

  if (enable_extensions_core) {
    sources += [
      "extensions_url_override_state_tracker_impl.cc",
      "extensions_url_override_state_tracker_impl.h",
    ]

    deps += [
      "//chrome/browser/extensions",
      "//extensions/browser",
      "//extensions/common",
    ]
  }
}

generate_jni("jni_headers") {
  sources = [ "java/src/org/chromium/chrome/browser/extensions/ExtensionsUrlOverrideRegistryManager.java" ]
}

android_library("java") {
  srcjar_deps = [ ":jni_headers" ]
  resources_package = "org.chromium.chrome.browser.extensions"
  sources = [
    "java/src/org/chromium/chrome/browser/extensions/ExtensionsUrlOverrideRegistryManager.java",
    "java/src/org/chromium/chrome/browser/extensions/ExtensionsUrlOverrideRegistryManagerFactory.java",
  ]

  deps = [
    "//base:base_java",
    "//build/android:build_java",
    "//chrome/browser/profiles/android:java",
    "//chrome/browser/url_constants/android:java",
    "//components/embedder_support/android:util_java",
    "//third_party/androidx:androidx_annotation_annotation_java",
    "//third_party/jni_zero:jni_zero_java",
  ]
}
