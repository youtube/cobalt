# Copyright 2016 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//build/config/ui.gni")
import("//skia/features.gni")
import("//testing/test.gni")
import("//ui/gl/features.gni")

source_set("main") {
  output_name = "viz_main_impl"

  defines = []

  sources = [
    "viz_compositor_thread_runner.h",
    "viz_compositor_thread_runner_impl.cc",
    "viz_compositor_thread_runner_impl.h",
    "viz_main_impl.cc",
    "viz_main_impl.h",
  ]

  deps = [
    "//components/discardable_memory/client",
    "//components/discardable_memory/public/mojom",
    "//components/ui_devtools",
    "//components/viz/service",
    "//gpu/command_buffer/service:gles2",
    "//gpu/ipc/common",
    "//gpu/ipc/service",
    "//ipc",
    "//mojo/public/cpp/system",
    "//services/metrics/public/cpp:metrics_cpp",
    "//services/metrics/public/mojom",
    "//services/service_manager/public/cpp",
    "//services/viz/privileged/mojom",
    "//ui/gfx:memory_buffer",
    "//ui/gl/init",
  ]

  public_deps = [ "//services/network/public/mojom" ]

  if (use_ozone) {
    deps += [ "//ui/ozone:ozone" ]
  }

  if (is_chromeos_ash) {
    deps += [ "//media/gpu" ]
  }
}
