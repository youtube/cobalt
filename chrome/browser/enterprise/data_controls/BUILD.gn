# Copyright 2023 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//components/enterprise/buildflags/buildflags.gni")

assert(enterprise_data_controls)

source_set("data_controls") {
  sources = [
    "chrome_dlp_rules_manager.cc",
    "chrome_dlp_rules_manager.h",
    "dlp_reporting_manager.cc",
    "dlp_reporting_manager.h",
    "dlp_rules_manager_base.h",
  ]

  public_deps = [
    "//components/enterprise/data_controls",
    "//components/enterprise/data_controls:features",
  ]

  deps = [
    "//base",
    "//chrome/browser/profiles:profile",
    "//components/enterprise/data_controls:dlp_policy_event_proto",
    "//components/keyed_service/core",
    "//components/policy/core/browser",
    "//components/prefs",
    "//components/reporting/client:report_queue",
    "//components/reporting/client:report_queue_configuration",
    "//components/reporting/client:report_queue_factory",
    "//components/reporting/proto:record_constants",
    "//components/reporting/util:status",
    "//components/url_matcher",
    "//third_party/abseil-cpp:absl",
    "//url",
  ]
  if (is_chromeos) {
    deps += [ "//chromeos/startup" ]
  }
  if (is_chromeos_ash) {
    deps += [ "//components/user_manager" ]
  }
}

source_set("test_support") {
  testonly = true
  sources = [
    "dlp_reporting_manager_test_helper.cc",
    "dlp_reporting_manager_test_helper.h",
  ]
  public_deps = [
    ":data_controls",
    "//base",
    "//base/test:test_support",
    "//components/account_id",
    "//components/enterprise/data_controls:dlp_policy_event_proto",
    "//components/prefs",
    "//components/reporting/client:test_support",
    "//components/reporting/util:status",
    "//content/test:test_support",
    "//testing/gmock",
    "//testing/gtest",
    "//third_party/abseil-cpp:absl",
  ]
  if (is_chromeos) {
    public_deps += [ "//chromeos/startup" ]
  }
  if (is_chromeos_ash) {
    public_deps += [ "//components/user_manager" ]
  }
}

source_set("unit_tests") {
  testonly = true

  deps = [
    ":data_controls",
    ":test_support",
    "//components/enterprise/data_controls:unit_tests",
  ]
  if (is_chromeos_ash) {
    deps += [ "//chrome/browser/ash:test_support" ]
  }
  if (is_chromeos_lacros) {
    deps += [ "//chromeos/lacros" ]
  }
}
