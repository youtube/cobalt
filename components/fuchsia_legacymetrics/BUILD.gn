# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(is_fuchsia)

visibility = []

source_set("fuchsia_legacymetrics") {
  visibility += [
    ":unit_tests",
    "//chromecast/internal/*",
    "//fuchsia_web/webengine/*",
  ]
  sources = [
    "legacymetrics_client.cc",
    "legacymetrics_histogram_flattener.cc",
    "legacymetrics_histogram_flattener.h",
    "legacymetrics_user_event_recorder.cc",
    "legacymetrics_user_event_recorder.h",
  ]
  public = [ "legacymetrics_client.h" ]
  deps = [ "//base" ]
  public_deps = [ "//third_party/fuchsia-sdk/sdk/fidl/fuchsia.legacymetrics:fuchsia.legacymetrics_hlcpp" ]
  friend = [ ":unit_tests" ]  # For access to private headers.
}

source_set("unit_tests") {
  testonly = true
  visibility += [ "//components:components_unittests__exec" ]
  sources = [
    "legacymetrics_client_unittest.cc",
    "legacymetrics_histogram_flattener_unittest.cc",
    "legacymetrics_user_event_recorder_unittest.cc",
  ]
  public_deps = [
    ":fuchsia_legacymetrics",
    "//base",
    "//base/test:test_support",
    "//testing/gmock",
    "//testing/gtest",
  ]
}
