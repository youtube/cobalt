# Copyright 2025 The Cobalt Authors. All Rights Reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/chromecast_build.gni")
import("//build/config/ui.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//starboard/build/buildflags.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/repack.gni")

# TODO(crbug.com/1336055, spang): Investigate using shell_views with cast builds as
# true.
shell_use_toolkit_views = toolkit_views && !is_castos

# Web test support only built for the linux-x64x11-no-starboard target.
support_web_tests = !is_android && !is_starboard

# TODO(b/384748332): Changes below are for removing dependency on content shell
config("cobalt_shell_lib_warnings") {
  if (is_clang) {
    # TODO(thakis): Remove this once http://crbug.com/383820 is figured out
    cflags = [ "-Wno-nonnull" ]
  }
}

source_set("url_constants") {
  sources = [ "common/url_constants.h" ]
}

source_set("android_shell_descriptors") {
  sources = []
  public_deps = [ "//content/public/common:content_descriptors" ]
  if (is_android) {
    sources += [ "android/shell_descriptors.h" ]
  }
}

group("cobalt_shell_lib_deps") {
  visibility = [ "//cobalt/shell:*" ]

  public_deps = [
    ":android_shell_descriptors",
    ":embed_loader_resources_as_header_files",
    ":embed_polyfilled_javascript",
    ":resources",
    "//base",
    "//base:base_static",
    "//base/third_party/dynamic_annotations",
    "//cc/base",
    "//cobalt/android/oom_intervention",
    "//cobalt/shell/browser/migrate_storage_record",
    "//components/cdm/renderer",
    "//components/crash/content/browser",
    "//components/crash/core/app",
    "//components/custom_handlers",
    "//components/js_injection/browser:browser",
    "//components/keyed_service/content",
    "//components/metrics",
    "//components/metrics:net",
    "//components/network_hints/browser:browser",
    "//components/network_hints/renderer",
    "//components/network_session_configurator/common",
    "//components/performance_manager",
    "//components/prefs",
    "//components/url_formatter",
    "//components/variations",
    "//components/variations/service",
    "//components/web_cache/renderer",
    "//content:content_resources",
    "//content:dev_ui_content_resources",
    "//content/public/app",
    "//content/public/browser",
    "//content/public/gpu",
    "//media",
    "//media/mojo:buildflags",
    "//net",
    "//net:net_resources",
    "//ppapi/buildflags",
    "//services/network:network_service",
    "//services/network/public/cpp",
    "//third_party/blink/public:blink",
    "//third_party/blink/public:image_resources",
    "//third_party/blink/public:resources",
    "//third_party/blink/public/strings",
    "//third_party/blink/public/strings:accessibility_strings",
    "//ui/gfx",
    "//ui/gfx/geometry",
    "//url",
    "//v8",
  ]

  if (!is_android) {
    public_deps += [
      "//ui/base",
      "//ui/base/clipboard",
      "//ui/base/ime/init",
      "//ui/platform_window",
    ]
  }

  if (enable_cast_renderer) {
    public_deps += [ "//media/mojo/services" ]
  }

  if (is_linux) {
    public_deps += [ "//ui/linux:linux_ui_factory" ]
  }

  if (is_ios) {
    public_deps += [
      "//services/tracing/public/cpp",
      "//services/tracing/public/mojom:mojom",
      "//ui/shell_dialogs",
    ]
  }

  if (is_android) {
    public_deps += [
      "//cc/slim",
      "//cobalt/shell/android:cobalt_shell_jni_headers",
      "//ui/android",
    ]
  }

  if (use_aura) {
    public_deps += [
      "//ui/aura",
      "//ui/events",
      "//ui/wm",
    ]

    if (shell_use_toolkit_views) {
      public_deps += [
        "//ui/color:color_headers",
        "//ui/native_theme",
        "//ui/resources",
        "//ui/views/controls/webview",
      ]
    }
  }

  if (shell_use_toolkit_views) {
    public_deps += [ "//ui/views" ]
  }

  if (is_linux) {
    public_deps += [ "//build/config/freetype" ]
  }

  if (use_ozone) {
    public_deps += [ "//ui/ozone" ]
  }

  if (use_starboard_media) {
    public_deps += [ "//starboard:starboard_group" ]
    if (is_android) {
      public_deps += [ "//starboard/android/shared:starboard_platform" ]
    }
  }

  if (is_androidtv) {
    public_deps += [ "//cobalt/browser:hang_watcher_delegate" ]
  }
}

static_library("cobalt_shell_switches") {
  sources = [
    "common/shell_switches.cc",
    "common/shell_switches.h",
  ]
  deps = [ "//base" ]
}

static_library("cobalt_shell_lib") {
  testonly = true
  public_deps = [
    ":cobalt_shell_lib_deps",
    ":cobalt_shell_switches",
    ":url_constants",
  ]
  sources = [
    "browser/h5vcc_scheme_url_loader_factory.cc",
    "browser/h5vcc_scheme_url_loader_factory.h",
    "browser/shell.cc",
    "browser/shell.h",
    "browser/shell_browser_context.cc",
    "browser/shell_browser_context.h",
    "browser/shell_browser_main_parts.cc",
    "browser/shell_browser_main_parts.h",
    "browser/shell_content_browser_client.cc",
    "browser/shell_content_browser_client.h",
    "browser/shell_content_index_provider.cc",
    "browser/shell_content_index_provider.h",
    "browser/shell_devtools_bindings.cc",
    "browser/shell_devtools_bindings.h",
    "browser/shell_devtools_frontend.cc",
    "browser/shell_devtools_frontend.h",
    "browser/shell_devtools_manager_delegate.cc",
    "browser/shell_devtools_manager_delegate.h",
    "browser/shell_javascript_dialog.h",
    "browser/shell_javascript_dialog_manager.cc",
    "browser/shell_javascript_dialog_manager.h",
    "browser/shell_paths.cc",
    "browser/shell_paths.h",
    "browser/shell_platform_data_aura.cc",
    "browser/shell_platform_data_aura.h",
    "browser/shell_platform_delegate.cc",
    "browser/shell_platform_delegate.h",
    "browser/shell_speech_recognition_manager_delegate.cc",
    "browser/shell_speech_recognition_manager_delegate.h",
    "browser/shell_web_contents_view_delegate.h",
    "browser/shell_web_contents_view_delegate_creator.h",
    "browser/splash_screen_web_contents_delegate.cc",
    "browser/splash_screen_web_contents_delegate.h",
    "browser/splash_screen_web_contents_observer.cc",
    "browser/splash_screen_web_contents_observer.h",
    "common/shell_content_client.cc",
    "common/shell_content_client.h",
    "renderer/shell_content_renderer_client.cc",
    "renderer/shell_content_renderer_client.h",
  ]

  if (is_android) {
    sources += [
      "android/content_view_render_view.cc",
      "android/content_view_render_view.h",
      "android/shell_manager.cc",
      "android/shell_manager.h",
      "browser/shell_platform_delegate_android.cc",
      "browser/shell_web_contents_view_delegate_android.cc",
    ]
  }

  if (is_ios) {
    sources += [
      "browser/shell_file_select_helper.cc",
      "browser/shell_file_select_helper.h",
      "browser/shell_platform_delegate_ios.mm",
      "browser/shell_web_contents_view_delegate_ios.mm",
    ]
    configs += [ "//build/config/compiler:enable_arc" ]
  }

  configs += [
    ":cobalt_shell_lib_warnings",
    "//build/config:precompiled_headers",
  ]

  defines = [
    "CONTENT_SHELL_VERSION=\"1.0.0\"",
    "CONTENT_SHELL_MAJOR_VERSION=\"1\"",
  ]

  if (shell_use_toolkit_views) {
    # All cobalt_shell code should use this define instead of TOOLKIT_VIEWS,
    # since any transitive dependency on //ui/views from another component will
    # cause TOOLKIT_VIEWS to be defined, even when cobalt_shell does not want
    # to use it internally. See https://crbug.com/1218907.
    defines += [ "SHELL_USE_TOOLKIT_VIEWS=1" ]
  }

  # This is to support our dependency on //content/browser.
  # See comment at the top of //content/BUILD.gn for why this is disabled in
  # component builds.
  if (is_component_build) {
    check_includes = false
  }

  if (use_aura) {
    if (shell_use_toolkit_views) {
      sources += [
        "browser/cobalt_views_delegate.cc",
        "browser/cobalt_views_delegate.h",
        "browser/shell_platform_delegate_views.cc",
        "browser/shell_web_contents_view_delegate_views.cc",
      ]
    } else {
      sources += [
        "browser/shell_platform_delegate_aura.cc",
        "browser/shell_web_contents_view_delegate_aura.cc",
      ]
    }
  } else {
    sources -= [
      "browser/shell_platform_data_aura.cc",
      "browser/shell_platform_data_aura.h",
    ]
  }
}

# This component provides a ContentMainDelegate for Content Shell and derived
# applications. This delegate is the central place that creates interfaces for
# each type of process (browser, renderer, etc). This implementation of
# ContentMainDelegate will create either production or test-based
# implementations.
#
# This component needs to be linked into every process in a Content Shell-based
# application that wants to use ShellMainDelegate.
#
# TODO(danakj): This component will depend on {renderer,browser}/web_test. The
# cobalt_shell_lib component will not, to avoid circular deps, as web_test
# inherits from things in cobalt_shell_lib.
static_library("cobalt_shell_app") {
  testonly = true
  sources = [
    "app/shell_crash_reporter_client.cc",
    "app/shell_crash_reporter_client.h",
    "app/shell_main_delegate.cc",
    "app/shell_main_delegate.h",
  ]
  public_deps = [ ":cobalt_shell_app_deps" ]
  if (is_ios) {
    sources += [
      "app/ios/shell_application_ios.h",
      "app/ios/shell_application_ios.mm",
    ]
  }
  defines = [
    "CONTENT_SHELL_VERSION=\"1.0.0\"",
    "CONTENT_SHELL_MAJOR_VERSION=\"1\"",
  ]
}

group("cobalt_shell_app_deps") {
  visibility = [
    "//cobalt/shell:*",
    "//cobalt/testing:*",
  ]
  testonly = true
  public_deps = [
    ":cobalt_shell_lib",
    "//components/crash/core/app",
    "//components/crash/core/app:test_support",
    "//components/crash/core/common:crash_key",
    "//components/memory_system",
    "//content/public/app",
    "//content/public/renderer",
    "//v8",
  ]
  if (support_web_tests) {
    public_deps += [
      "//content/web_test:web_test_browser",
      "//content/web_test:web_test_common",
      "//content/web_test:web_test_renderer",
    ]
  }
}

repack("pak") {
  sources = [
    "$root_gen_dir/base/tracing/protos/tracing_proto_resources.pak",
    "$root_gen_dir/cobalt/shell/cobalt_shell_resources.pak",
    "$root_gen_dir/content/browser/resources/media/media_internals_resources.pak",
    "$root_gen_dir/content/browser/webrtc/resources/webrtc_internals_resources.pak",
    "$root_gen_dir/content/content_resources.pak",
    "$root_gen_dir/content/dev_ui_content_resources.pak",
    "$root_gen_dir/content/gpu_resources.pak",
    "$root_gen_dir/content/histograms_resources.pak",
    "$root_gen_dir/mojo/public/js/mojo_bindings_resources.pak",
    "$root_gen_dir/third_party/blink/public/resources/blink_resources.pak",
    "$root_gen_dir/third_party/blink/public/resources/blink_scaled_resources_100_percent.pak",
    "$root_gen_dir/third_party/blink/public/resources/inspector_overlay_resources.pak",
    "$root_gen_dir/third_party/blink/public/strings/blink_accessibility_strings_en-US.pak",
    "$root_gen_dir/third_party/blink/public/strings/blink_strings_en-US.pak",
    "$root_gen_dir/ui/resources/ui_resources_100_percent.pak",
    "$root_gen_dir/ui/resources/webui_resources.pak",
    "$root_gen_dir/ui/strings/app_locale_settings_en-US.pak",
    "$root_gen_dir/ui/strings/ax_strings_en-US.pak",
    "$root_gen_dir/ui/strings/ui_strings_en-US.pak",
  ]

  if (!is_official_build) {
    sources += [ "$root_gen_dir/content/test/web_ui_mojo_test_resources.pak" ]
  }

  deps = [
    ":resources",
    "//base/tracing/protos:chrome_track_event_resources",
    "//content:content_resources",
    "//content:dev_ui_content_resources",
    "//content/browser/resources:resources",
    "//content/browser/resources/media:resources",
    "//content/browser/webrtc/resources",
    "//content/test:web_ui_mojo_test_resources",
    "//mojo/public/js:resources",
    "//third_party/blink/public:devtools_inspector_resources",
    "//third_party/blink/public:resources",
    "//third_party/blink/public:scaled_resources_100_percent",
    "//third_party/blink/public/strings",
    "//third_party/blink/public/strings:accessibility_strings",
    "//ui/resources",
    "//ui/strings",
  ]

  if (!is_android) {
    deps += [ "//content/browser/tracing:resources" ]
    sources += [ "$root_gen_dir/content/browser/tracing/tracing_resources.pak" ]
  }

  if (shell_use_toolkit_views) {
    deps += [ "//ui/views/resources" ]
    sources +=
        [ "$root_gen_dir/ui/views/resources/views_resources_100_percent.pak" ]
  }
  if (!is_android && !is_ios) {
    sources +=
        [ "$root_gen_dir/content/browser/devtools/devtools_resources.pak" ]
    deps += [ "//content/browser/devtools:devtools_resources" ]
  }
  output = "$root_out_dir/cobalt_shell.pak"
}

grit("cobalt_shell_resources_grit") {
  # External code should depend on ":resources" instead.
  visibility = [ ":*" ]
  source = "shell_resources.grd"
  outputs = [
    "grit/shell_resources.h",
    "cobalt_shell_resources.pak",
  ]
}

copy("copy_shell_resources") {
  sources = [ "$target_gen_dir/cobalt_shell_resources.pak" ]
  outputs = [ "$root_out_dir/cobalt_shell_resources.pak" ]

  public_deps = [ ":cobalt_shell_resources_grit" ]
}

group("resources") {
  public_deps = [ ":copy_shell_resources" ]
}

mojom("browsertests_mojom") {
  testonly = true
  sources = [
    "common/main_frame_counter_test.mojom",
    "common/power_monitor_test.mojom",
    "common/render_frame_test_helper.mojom",
  ]
  public_deps = [
    "//sandbox/policy/mojom",
    "//third_party/blink/public/mojom/tokens",
  ]
}

mojom("shell_controller_mojom") {
  testonly = true
  sources = [ "common/shell_controller.test-mojom" ]
  public_deps = [ "//mojo/public/mojom/base" ]
}

action("embed_loader_resources_as_header_files") {
  script = "//cobalt/build/generate_data_header.py"
  outputs = [ "$target_gen_dir/embedded_resources/embedded_resources.h" ]
  sources = [
    "embedded_resources/loader_embedded_resources/splash.html",

    # TODO(481826565): hide code path for internal files
    # Default splash video, 1920x1080. Copied from
    # google3/video/youtube/web/living_room/kabuki/splash/chrobalt/splash.webm.
    "embedded_resources/loader_embedded_resources/splash.webm",

    # For low spec TV, 853x480. Copied from
    # google3/googledata/html/external_content/gstatic/ytlr/splash_screen/splash_480p.webm.
    "embedded_resources/loader_embedded_resources/splash_480.webm",

    # For devices that do not support VP9. Copied from
    # google3/video/youtube/web/living_room/kabuki/splash/chrobalt/splash_video_placeholder.png.
    "embedded_resources/loader_embedded_resources/splash.png",
  ]

  args = [
    "LoaderEmbeddedResources",
    rebase_path(outputs[0], root_build_dir),
    rebase_path("embedded_resources/loader_embedded_resources", root_build_dir),
  ]
}

config("embed_polyfilled_javascript_config") {
  include_dirs = [ root_gen_dir ]
}

# TODO(cobalt, b/390710539): migrate to GRIT.
action("embed_polyfilled_javascript") {
  script = "//cobalt/build/generate_data_header.py"
  outputs = [ "$target_gen_dir/embedded_resources/embedded_js.h" ]
  public_configs = [ ":embed_polyfilled_javascript_config" ]

  args = [
    "CobaltJavaScriptPolyfill",
    rebase_path(outputs[0], root_build_dir),
    rebase_path("embedded_resources/cobalt_java_script_polyfill",
                root_build_dir),
  ]
}
