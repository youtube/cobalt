# Copyright 2018 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//extensions/buildflags/buildflags.gni")
import("//ui/webui/resources/tools/build_webui.gni")

build_webui("build") {
  grd_prefix = "history"

  static_files = [
    "history.html",
    "images/compare_sync_promo.svg",
    "images/compare_sync_promo_dark.svg",
    "images/history_embeddings_promo.svg",
    "images/history_embeddings_promo_dark.svg",
    "images/list.svg",
    "images/sign_in_promo.svg",
    "images/sign_in_promo_dark.svg",
    "images/sync_history_promo.svg",
    "images/sync_history_promo_dark.svg",
    "images/avatar_surrounding_illustration_light.svg",
    "images/avatar_surrounding_illustration_dark.svg",
  ]

  ts_files = [
    "app.html.ts",
    "app.ts",
    "browser_service.ts",
    "constants.ts",
    "externs.ts",
    "history.ts",
    "history_embeddings_promo.html.ts",
    "history_embeddings_promo.ts",
    "history_item.html.ts",
    "history_item.ts",
    "history_list.html.ts",
    "history_list.ts",
    "history_toolbar.html.ts",
    "history_toolbar.ts",
    "query_manager.ts",
    "router.ts",
    "searched_label.ts",
    "side_bar.html.ts",
    "side_bar.ts",
    "synced_device_card.html.ts",
    "synced_device_card.ts",
    "synced_device_manager.html.ts",
    "synced_device_manager.ts",
  ]
  if (!is_chromeos) {
    ts_files += [
      "history_sync_promo.html.ts",
      "history_sync_promo.ts",
    ]
  }

  # Files that are passed as input to css_to_wrapper().
  css_files = [
    "app.css",
    "history_embeddings_promo.css",
    "history_item.css",
    "history_list.css",
    "history_toolbar.css",
    "shared_style.css",
    "shared_vars.css",
    "side_bar.css",
    "synced_device_card.css",
    "synced_device_manager.css",
  ]
  if (!is_chromeos) {
    css_files += [ "history_sync_promo.css" ]
  }

  icons_html_files = [ "shared_icons.html" ]

  if (is_chrome_branded) {
    icons_html_files += [ "internal/icons.html" ]
  }

  ts_composite = true
  ts_definitions = [
    "//tools/typescript/definitions/chrome_send.d.ts",
    "//tools/typescript/definitions/metrics_private.d.ts",
  ]

  ts_deps = [
    "//third_party/lit/v3_0:build_ts",
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources/cr_components/help_bubble:build_ts",
    "//ui/webui/resources/cr_components/history:build_ts",
    "//ui/webui/resources/cr_components/history_clusters:build_ts",
    "//ui/webui/resources/cr_components/history_embeddings:build_ts",
    "//ui/webui/resources/cr_components/managed_footnote:build_ts",
    "//ui/webui/resources/cr_elements:build_ts",
    "//ui/webui/resources/js:build_ts",
    "//ui/webui/resources/mojo:build_ts",
  ]

  webui_context_type = "trusted"
  optimize = optimize_webui
  if (optimize) {
    optimize_webui_host = "history"
    optimize_webui_in_files = [ "history.js" ]
  }
}
