# Copyright 2023 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/apple/compile_entitlements.gni")
import("//build/apple/tweak_info_plist.gni")
import("//build/config/ios/rules.gni")
import("//ios/build/chrome_build.gni")

tweak_info_plist("tweak_info_plist") {
  info_plist = "Info.plist"
}

compile_entitlements("entitlements") {
  substitutions = [
    "IOS_BUNDLE_ID_PREFIX=$ios_app_bundle_id_prefix",
    "CHROMIUM_BUNDLE_ID=$ios_chromium_bundle_id",
  ]
  output_name = "$target_gen_dir/widget_kit_extension.appex.entitlements"
  entitlements_templates =
      [ "entitlements/external/widget_kit_extension.appex.entitlements" ]
}

ios_appex_bundle("appex") {
  output_name = "widget_kit_extension"
  entitlements_target = ":entitlements"
  info_plist_target = ":tweak_info_plist"
  bundle_identifier = "$ios_chromium_bundle_id.WidgetKitExtension"
  deps = [ "//ios/chrome/widget_kit_extension:widget_kit_extension_swift" ]
  if (ios_app_extension_privacy_manifest != "") {
    deps += [ ":privacy_manifest" ]
  }
}

if (ios_app_extension_privacy_manifest != "") {
  bundle_data("privacy_manifest") {
    sources = [ ios_app_extension_privacy_manifest ]
    outputs = [ "{{bundle_resources_dir}}/{{source_file_part}}" ]
  }
}
