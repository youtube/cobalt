# Copyright 2025 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(is_win || is_mac || is_linux || is_chromeos || is_android)

import("//chrome/common/features.gni")

source_set("tab_alert_enum") {
  public = [ "tab_alert.h" ]
}

source_set("tab_alert") {
  public = [
    "tab_alert_controller.h",
    "tab_alert_icon.h",
  ]

  public_deps = [
    "//base",
    "//chrome/browser/vr",
    "//content/public/browser",
    "//ui/base/unowned_user_data",
  ]

  deps = [
    "//chrome/browser/actor/ui",
    "//chrome/browser/media/webrtc",
    "//chrome/browser/ui/color:color_headers",
    "//chrome/browser/ui/tabs",
  ]
}

source_set("impl") {
  sources = [
    "tab_alert_controller.cc",
    "tab_alert_icon.cc",
  ]

  deps = [
    ":tab_alert",
    ":tab_alert_enum",
    "//chrome/app/vector_icons",
    "//chrome/browser/actor/resources:grit",
    "//chrome/browser/actor/ui",
    "//chrome/browser/media/webrtc",
    "//chrome/browser/ui:layout_constants",
    "//chrome/browser/ui/color:color_headers",
    "//chrome/common:chrome_features",
    "//components/vector_icons",
  ]

  if (enable_glic) {
    deps += [ "//chrome/browser/glic" ]
  }
}

source_set("unit_tests") {
  testonly = true

  sources = [ "tab_alert_controller_unittest.cc" ]

  deps = [
    ":impl",
    ":tab_alert",
    "//chrome/browser/permissions/system",
    "//chrome/browser/ui:ui_features",
    "//chrome/browser/ui/tabs:test_support",
    "//chrome/test:test_support",
    "//components/tabs:test_support",
    "//content/test:test_support",
    "//testing/gmock",
    "//testing/gtest",
  ]

  if (enable_glic) {
    deps += [
      "//chrome/browser/glic",
      "//chrome/browser/glic:mojo_bindings_shared_cpp_sources",
      "//chrome/browser/glic/test_support",
    ]
  }
}

source_set("interactive_ui_tests") {
  testonly = true
  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

  if (enable_glic) {
    sources = [ "tab_alert_controller_interactive_uitest.cc" ]
  }

  deps = [
    ":impl",
    ":tab_alert",
    "//chrome/browser/ui:ui_features",
    "//chrome/browser/ui/tabs:test_support",
    "//chrome/test:test_support",
    "//components/tabs:test_support",
    "//content/test:test_support",
    "//testing/gmock",
    "//testing/gtest",
  ]

  if (enable_glic) {
    deps += [
      "//chrome/browser/glic",
      "//chrome/browser/glic:mojo_bindings_shared_cpp_sources",
      "//chrome/browser/glic/test_support",
    ]
  }
}
