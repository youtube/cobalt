# Copyright 2017 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//extensions/buildflags/buildflags.gni")

assert(enable_extensions_core,
       "Cannot depend on extensions because enable_extensions_core=false.")

source_set("file_handlers") {
  sources = [
    "app_file_handler_util.cc",
    "app_file_handler_util.h",
    "directory_util.cc",
    "directory_util.h",
    "mime_util.cc",
    "mime_util.h",
  ]

  if (is_chromeos) {
    sources += [ "non_native_file_system_delegate.h" ]
  }

  configs += [ "//build/config/compiler:wexit_time_destructors" ]

  public_deps = [
    "//base",
    "//extensions/common",
  ]

  deps = [
    "//build:chromeos_buildflags",
    "//components/file_access",
    "//components/services/app_service",
    "//content/public/browser",
    "//extensions/browser:browser_sources",
    "//extensions/browser/api:extensions_api_client",
    "//net",
    "//storage/browser",
    "//storage/common",
  ]
}
