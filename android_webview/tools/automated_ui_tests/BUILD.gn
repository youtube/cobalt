# Copyright 2016 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")
import("//testing/test.gni")

group("webview_ui_test_app") {
  testonly = true
  deps = [
    ":webview_ui_test_app_apk",
    ":webview_ui_test_app_test_apk",
  ]
}

android_apk("webview_ui_test_app_apk") {
  apk_name = "WebViewUiTestApp"
  android_manifest = "java/AndroidManifest.xml"
  deps = [
    ":webview_ui_test_app_apk_resources",
    ":webview_ui_test_app_java",
  ]
}

android_library("webview_ui_test_app_java") {
  sources =
      [ "java/src/org/chromium/webview_ui_test/WebViewUiTestActivity.java" ]
  deps = [
    ":webview_ui_test_app_apk_resources",
    "//base:base_java",
  ]
}

android_resources("webview_ui_test_app_apk_resources") {
  sources = [
    "java/res/layout/edittext_webview.xml",
    "java/res/layout/fullscreen_webview.xml",
    "java/res/values/strings.xml",
  ]
}

instrumentation_test_apk("webview_ui_test_app_test_apk") {
  apk_name = "WebViewUiTestAppTest"
  apk_under_test = ":webview_ui_test_app_apk"
  android_manifest = "javatests/AndroidManifest.xml"
  sources = [
    "javatests/src/org/chromium/webview_ui_test/test/ActionModeTest.java",
    "javatests/src/org/chromium/webview_ui_test/test/DropDownListTest.java",
    "javatests/src/org/chromium/webview_ui_test/test/WebViewJSTest.java",
    "javatests/src/org/chromium/webview_ui_test/test/util/Actions.java",
    "javatests/src/org/chromium/webview_ui_test/test/util/Atoms.java",
    "javatests/src/org/chromium/webview_ui_test/test/util/UseLayout.java",
    "javatests/src/org/chromium/webview_ui_test/test/util/WebViewSyncWrapper.java",
    "javatests/src/org/chromium/webview_ui_test/test/util/WebViewUiTestRule.java",
  ]
  deps = [
    ":webview_ui_test_app_apk_resources",
    ":webview_ui_test_app_java",
    "//base:base_java",
    "//base:base_java_test_support",
    "//third_party/android_deps:espresso_java",
    "//third_party/android_sdk:android_test_base_java",
    "//third_party/androidx:androidx_test_monitor_java",
    "//third_party/androidx:androidx_test_runner_java",
    "//third_party/androidx:androidx_test_uiautomator_uiautomator_java",
    "//third_party/hamcrest:hamcrest_java",
    "//third_party/junit",
    "//ui/android:ui_java_test_support",
  ]
  data = [
    "//third_party/chromium-variations",
    "test/data/",
  ]

  # Tell GN to ignore 'incremental_install' even if the user has it in their GN
  # args because it's incompatible with 'use_webview_provider'.
  never_incremental = true
  use_webview_provider = system_webview_apk_target
}
