# Copyright 2016 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/rules.gni")
}

static_library("core") {
  sources = [
    "browsing_data_policies_utils.cc",
    "browsing_data_policies_utils.h",
    "browsing_data_utils.cc",
    "browsing_data_utils.h",
    "clear_browsing_data_tab.h",
    "counters/autofill_counter.cc",
    "counters/autofill_counter.h",
    "counters/bookmark_counter.cc",
    "counters/bookmark_counter.h",
    "counters/browsing_data_counter.cc",
    "counters/browsing_data_counter.h",
    "counters/history_counter.cc",
    "counters/history_counter.h",
    "counters/passwords_counter.cc",
    "counters/passwords_counter.h",
    "counters/sync_tracker.cc",
    "counters/sync_tracker.h",
    "features.cc",
    "features.h",
    "history_notice_utils.cc",
    "history_notice_utils.h",
    "pref_names.cc",
    "pref_names.h",
  ]

  deps = [
    "//base",
    "//components/autofill/core/browser",
    "//components/bookmarks/browser",
    "//components/history/core/browser",
    "//components/password_manager/core/browser",
    "//components/pref_registry",
    "//components/prefs",
    "//components/strings",
    "//components/sync",
    "//components/user_annotations",
    "//components/version_info",
    "//components/webdata/common",
    "//ui/base",
  ]

  configs += [ "//build/config/compiler:wexit_time_destructors" ]
}

source_set("cookie_or_cache_deletion_choice") {
  sources = [ "cookie_or_cache_deletion_choice.h" ]
}

if (is_android) {
  java_cpp_enum("browsing_data_utils_javagen") {
    # External code should depend on ":java" instead.
    visibility = [ ":*" ]
    sources = [ "browsing_data_utils.h" ]
  }
  java_cpp_enum("clear_browsing_data_tab_javagen") {
    # External code should depend on ":java" instead.
    visibility = [ ":*" ]
    sources = [ "clear_browsing_data_tab.h" ]
  }
  java_cpp_enum("cookie_or_cache_deletion_choice_javagen") {
    # External code should depend on ":java" instead.
    visibility = [ ":*" ]
    sources = [ "cookie_or_cache_deletion_choice.h" ]
  }

  android_library("java") {
    srcjar_deps = [
      ":browsing_data_utils_javagen",
      ":clear_browsing_data_tab_javagen",
      ":cookie_or_cache_deletion_choice_javagen",
    ]
    deps = [ "//third_party/androidx:androidx_annotation_annotation_java" ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "browsing_data_policies_utils_unittest.cc",
    "browsing_data_utils_unittest.cc",
    "counters/browsing_data_counter_unittest.cc",
    "history_notice_utils_unittest.cc",
  ]

  if (is_ios) {
    # pref_names_unittest.cc tests a method only available in iOS.
    sources += [ "pref_names_unittest.cc" ]
  }

  deps = [
    ":core",
    "//base/test:test_support",
    "//components/autofill/core/browser:browser",
    "//components/history/core/test:test",
    "//components/password_manager/core/browser:test_support",
    "//components/prefs:test_support",
    "//components/sync",
    "//components/sync:test_support",
    "//components/sync_preferences:test_support",
    "//components/version_info:version_info",
    "//components/webdata/common",
    "//net:test_support",
    "//testing/gtest",
  ]
}
