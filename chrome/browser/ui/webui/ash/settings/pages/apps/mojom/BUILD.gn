# Copyright 2021 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//mojo/public/tools/bindings/mojom.gni")

assert(is_chromeos_ash, "Non-ChromeOS builds cannot depend on //ash")

mojom("mojom") {
  sources = [
    "app_notification_handler.mojom",
    "app_parental_controls_handler.mojom",
  ]

  webui_module_path = "/"

  public_deps = [
    "//mojo/public/mojom/base",
    "//ui/webui/resources/cr_components/app_management:mojo_bindings",
  ]

  cpp_typemaps = [
    {
      types = [
        {
          mojom = "ash.settings.app_notification.mojom.Readiness"
          cpp = "::apps::Readiness"
        },
      ]
      traits_headers = [
        "//chrome/browser/ui/webui/ash/settings/pages/apps/mojom/app_type_mojom_traits.h",
        "//components/services/app_service/public/cpp/app_types.h",
      ]
      traits_sources = [ "//chrome/browser/ui/webui/ash/settings/pages/apps/mojom/app_type_mojom_traits.cc" ]
      traits_public_deps = [ "//components/services/app_service" ]
    },
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [ "app_type_mojom_traits_unittest.cc" ]

  deps = [
    ":mojom",
    "//components/services/app_service",
    "//mojo/public/cpp/test_support:test_utils",
    "//testing/gtest",
  ]
}
