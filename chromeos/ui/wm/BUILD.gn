# Copyright 2021 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")

assert(is_chromeos,
       "Non-Chrome-OS or Lacros builds must not depend on //chromeos")

# C++ headers and sources that can be used by both ash and lacros builds.
source_set("wm") {
  defines = [ "IS_CHROMEOS_UI_WM_IMPL" ]

  sources = [
    "constants.h",
    "debug_util.cc",
    "debug_util.h",
    "desks/chromeos_desks_histogram_enums.h",
    "desks/desks_helper.h",
    "fullscreen/keep_fullscreen_for_url_checker.cc",
    "fullscreen/keep_fullscreen_for_url_checker.h",
    "fullscreen/pref_names.cc",
    "fullscreen/pref_names.h",
    "window_util.cc",
    "window_util.h",
  ]

  deps = [
    "//base",
    "//build:chromeos_buildflags",
    "//chromeos/ui/base",
    "//components/prefs",
    "//components/url_matcher",
    "//ui/accessibility",
    "//ui/aura",
    "//ui/base",
    "//ui/views",
    "//ui/wm/public",
    "//url",
  ]

  if (is_chromeos_ash) {
    sources += [ "desks/ash/desks_helper.cc" ]
    deps += [ "//ash/constants" ]
  }

  if (is_chromeos_lacros) {
    sources += [ "desks/lacros/desks_helper.cc" ]
    deps += [
      "//chromeos/startup:startup",
      "//ui/platform_window:platform_window",
      "//ui/views:views",
    ]
  }
}
