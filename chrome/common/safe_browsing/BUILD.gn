# Copyright 2014 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//components/safe_browsing/buildflags.gni")
import("//third_party/protobuf/proto_library.gni")

proto_library("proto") {
  sources = [ "crx_info.proto" ]
}

if (safe_browsing_mode == 1) {
  source_set("archive_analyzer_results") {
    sources = [
      "archive_analyzer_results.cc",
      "archive_analyzer_results.h",
    ]
    deps = [
      ":binary_feature_extractor",
      ":download_type_util",
      "//base",
      "//base:i18n",
      "//components/safe_browsing/content/common:file_type_policies",
      "//crypto",
    ]
    if (is_mac) {
      deps += [ ":disk_image_type_sniffer_mac" ]
    }
    public_deps = [ "//components/safe_browsing/core/common/proto:csd_proto" ]
  }

  if (is_linux || is_win) {
    source_set("document_analyzer_results") {
      sources = [
        "document_analyzer_results.cc",
        "document_analyzer_results.h",
      ]
      deps = [
        ":download_type_util",
        "//base",
        "//components/safe_browsing/content/common:file_type_policies",
      ]
      public_deps = [ "//components/safe_browsing/core/common/proto:csd_proto" ]
    }
  }

  source_set("download_type_util") {
    sources = [
      "download_type_util.cc",
      "download_type_util.h",
    ]
    deps = [
      "//base",
      "//components/safe_browsing/content/common:file_type_policies",
      "//components/safe_browsing/core/common",
    ]
    public_deps = [ "//components/safe_browsing/core/common/proto:csd_proto" ]
  }

  if (is_linux || is_win) {
    source_set("document_analyzer") {
      sources = [
        "document_analyzer.cc",
        "document_analyzer.h",
      ]

      deps = [
        ":document_analyzer_results",
        ":download_type_util",
        "//base",
        "//components/safe_browsing/content/common:file_type_policies",
        "//components/safe_browsing/core/common",
        "//third_party/maldoca:maldoca",
      ]

      public_deps = [ "//components/safe_browsing/core/common/proto:csd_proto" ]
    }
  }

  if (is_mac) {
    source_set("disk_image_type_sniffer_mac") {
      sources = [
        "disk_image_type_sniffer_mac.cc",
        "disk_image_type_sniffer_mac.h",
      ]

      deps = [ "//base" ]
    }
  }

  source_set("binary_feature_extractor") {
    sources = [
      "binary_feature_extractor.cc",
      "binary_feature_extractor.h",
    ]
    if (is_mac) {
      sources += [
        "binary_feature_extractor_mac.cc",
        "mach_o_image_reader_mac.cc",
        "mach_o_image_reader_mac.h",
      ]
    } else if (is_win) {
      sources += [ "binary_feature_extractor_win.cc" ]
    }
    if (is_posix || is_fuchsia) {
      sources += [ "binary_feature_extractor_posix.cc" ]
    }

    public_deps = [
      "//base",
      "//components/safe_browsing/core/common/proto:csd_proto",
      "//crypto",
    ]
  }

  source_set("mock_binary_feature_extractor") {
    testonly = true

    sources = [
      "mock_binary_feature_extractor.cc",
      "mock_binary_feature_extractor.h",
    ]

    deps = [
      ":binary_feature_extractor",
      "//testing/gmock",
    ]
  }
}

source_set("safe_browsing") {
  deps = []

  if (safe_browsing_mode != 0) {
    deps += [ "//components/safe_browsing/content/common:file_type_policies" ]
  }

  if (safe_browsing_mode == 1) {
    sources = [
      "archive_analyzer.cc",
      "archive_analyzer.h",
      "ipc_protobuf_message_macros.h",
      "ipc_protobuf_message_null_macros.h",
      "protobuf_message_log_macros.h",
      "protobuf_message_read_macros.h",
      "protobuf_message_write_macros.h",
      "rar_analyzer.cc",
      "rar_analyzer.h",
      "seven_zip_analyzer.cc",
      "seven_zip_analyzer.h",
      "zip_analyzer.cc",
      "zip_analyzer.h",
    ]

    deps += [
      ":archive_analyzer_results",
      ":binary_feature_extractor",
      ":download_type_util",
      "//base",
      "//base:i18n",
      "//components/safe_browsing/content/common:file_type_policies",
      "//components/safe_browsing/core/common",
      "//third_party/lzma_sdk/google:seven_zip_reader",
      "//third_party/unrar:unrar",
    ]

    if (is_linux) {
      deps += [ ":document_analyzer" ]
    }

    if (is_mac) {
      # TODO(crbug/1373671): Most of this target should be moved to
      # //chrome/utility/safe_browsing and merged with this one.
      allow_circular_includes_from = [ "//chrome/utility/safe_browsing/mac" ]
      deps += [ "//chrome/utility/safe_browsing/mac" ]
    }

    public_deps = [
      ":proto",
      "//base:i18n",
      "//chrome/common:mojo_bindings",
      "//components/safe_browsing/core/common/proto:csd_proto",
      "//crypto",
      "//ipc",
      "//third_party/zlib/google:zip",
    ]

    if (is_linux || is_win) {
      public_deps += [ ":document_analyzer_results" ]
    }
  }
}
