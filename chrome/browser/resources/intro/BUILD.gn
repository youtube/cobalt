# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//components/signin/features.gni")
import("//ui/webui/resources/tools/build_webui.gni")

assert(enable_dice_support)

build_webui("build") {
  grd_prefix = "intro"

  static_files = [
    "default_browser/default_browser.html",
    "intro.html",
    "images/default_browser_frame.svg",
    "images/default_browser_frame_dark.svg",
  ]

  non_web_component_files = [
    "default_browser/browser_proxy.ts",
    "browser_proxy.ts",
  ]

  if (enable_dice_support) {
    static_files += [
      "images/cloud_upload.svg",
      "images/devices.svg",
    ]

    if (!is_chrome_branded) {
      static_files += [ "images/security.svg" ]
    }

    non_web_component_files += [
      "default_browser/app.html.ts",
      "default_browser/app.ts",
      "dice_app.html.ts",
      "dice_app.ts",
      "sign_in_promo.html.ts",
      "sign_in_promo.ts",
    ]

    css_files = [
      "default_browser/app.css",
      "dice_app.css",
      "sign_in_promo.css",
    ]
  }

  ts_definitions = [ "//tools/typescript/definitions/chrome_send.d.ts" ]
  ts_composite = true
  ts_deps = [
    "//chrome/browser/resources/signin:build_ts",
    "//third_party/lit/v3_0:build_ts",
    "//ui/webui/resources/cr_elements:build_ts",
    "//ui/webui/resources/js:build_ts",
  ]

  ts_path_mappings = [ "chrome://intro/tangible_sync_style_shared.css.js|" + rebase_path(
                           "$root_gen_dir/chrome/browser/resources/signin/tsc/tangible_sync_style_shared.css.d.ts",
                           target_gen_dir) ]

  webui_context_type = "trusted"
}
