# Copyright 2020 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")

assert(is_chromeos_ash, "Non-ChromeOS builds cannot depend on //chromeos/ash")

component("hermes") {
  public_deps = [
    ":hermes_clients",
    ":hermes_fakes",
  ]
}

source_set("hermes_clients") {
  visibility = [ ":*" ]
  defines = [ "IS_HERMES_CLIENT_IMPL" ]
  deps = [
    "//ash/constants",
    "//base",
    "//chromeos/dbus/common",
    "//components/device_event_log",
  ]

  public_deps = [ "//dbus" ]

  sources = [
    "constants.cc",
    "constants.h",
    "hermes_clients.cc",
    "hermes_clients.h",
    "hermes_euicc_client.cc",
    "hermes_euicc_client.h",
    "hermes_manager_client.cc",
    "hermes_manager_client.h",
    "hermes_profile_client.cc",
    "hermes_profile_client.h",
    "hermes_response_status.cc",
    "hermes_response_status.h",
  ]
}

source_set("hermes_fakes") {
  visibility = [ ":*" ]
  deps = [
    ":hermes_clients",
    "//ash/constants",
    "//base",
    "//chromeos/ash/components/dbus/shill",
    "//chromeos/dbus/constants",
    "//dbus",
  ]

  sources = [
    "fake_hermes_euicc_client.cc",
    "fake_hermes_euicc_client.h",
    "fake_hermes_manager_client.cc",
    "fake_hermes_manager_client.h",
    "fake_hermes_profile_client.cc",
    "fake_hermes_profile_client.h",
  ]

  allow_circular_includes_from = [ ":hermes_clients" ]
}

source_set("test_support") {
  testonly = true
  public_deps = [ ":hermes" ]
  deps = [
    "//ash/constants",
    "//base",
    "//base/test:test_support",
    "//chromeos/dbus/common",
    "//dbus:test_support",
    "//testing/gmock",
    "//testing/gtest",
  ]

  sources = [
    "hermes_client_test_base.cc",
    "hermes_client_test_base.h",
    "hermes_euicc_client_unittest.cc",
    "hermes_profile_client_unittest.cc",
    "hermes_test_utils.cc",
    "hermes_test_utils.h",
  ]
}
