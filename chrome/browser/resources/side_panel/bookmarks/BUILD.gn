# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//ui/webui/resources/tools/build_webui.gni")

assert(!is_android)

build_webui("build") {
  grd_prefix = "side_panel_bookmarks"

  static_files = [
    "bookmarks.html",
    "images/bookmarks_empty_dark.svg",
    "images/bookmarks_empty.svg",
    "power_bookmarks.html",
  ]

  # Files holding a Polymer element definition and have an equivalent .html file.
  web_component_files = [
    "bookmark_folder.ts",
    "bookmarks_list.ts",
    "commerce/shopping_list.ts",
    "power_bookmark_row.ts",
    "power_bookmarks_context_menu.ts",
    "power_bookmarks_edit_dialog.ts",
    "power_bookmarks_list.ts",
  ]

  non_web_component_files = [
    "bookmarks_api_proxy.ts",
    "bookmarks_drag_manager.ts",
    "commerce/shopping_list_api_proxy.ts",
    "power_bookmarks_service.ts",
  ]

  icons_html_files = [
    "commerce/icons.html",
    "icons.html",
  ]

  mojo_files_deps = [
    "//chrome/browser/ui/webui/side_panel/bookmarks:mojo_bindings_ts__generator",
    "//components/commerce/core/mojom:mojo_bindings_ts__generator",
  ]
  mojo_files = [
    "$root_gen_dir/chrome/browser/ui/webui/side_panel/bookmarks/bookmarks.mojom-webui.ts",
    "$root_gen_dir/components/commerce/core/mojom/shopping_list.mojom-webui.ts",
  ]

  ts_composite = true
  ts_definitions = [
    "//tools/typescript/definitions/bookmark_manager_private.d.ts",
    "//tools/typescript/definitions/bookmarks.d.ts",
    "//tools/typescript/definitions/metrics_private.d.ts",
    "//tools/typescript/definitions/tabs.d.ts",
  ]
  ts_deps = [
    "../shared:build_ts",
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources/cr_components/color_change_listener:build_ts",
    "//ui/webui/resources/cr_components/page_image_service:build_ts",
    "//ui/webui/resources/cr_elements:build_ts",
    "//ui/webui/resources/js:build_ts",
    "//ui/webui/resources/mojo:build_ts",
  ]
  ts_path_mappings =
      [ "//bookmarks-side-panel.top-chrome/shared/*|" +
        rebase_path(
            "$root_gen_dir/chrome/browser/resources/side_panel/shared/tsc/*",
            target_gen_dir) ]

  enable_source_maps = enable_webui_inline_sourcemaps
}
