# Copyright 2024 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")

assert(is_win || is_mac || is_linux || is_chromeos || is_android)

source_set("picture_in_picture") {
  sources = [
    "picture_in_picture_bounds_cache.h",
    "picture_in_picture_window_manager.h",

    # Common Auto Picture-in-Picture files
    "auto_picture_in_picture_safe_browsing_checker_client.h",
    "auto_picture_in_picture_tab_helper.h",
    "auto_picture_in_picture_tab_observer_helper_base.h",
    "auto_pip_setting_helper.h",
  ]
  public_deps = [
    "//base",
    "//components/safe_browsing/core/browser/db:database_manager",
    "//components/safe_browsing/core/browser/db:util",
    "//components/safe_browsing/core/browser/db:v4_protocol_manager_util",
    "//content/public/browser",
    "//third_party/blink/public/common",
    "//ui/gfx/geometry",
    "//url",
  ]

  if (is_mac || is_win || is_linux || is_chromeos) {
    public_deps += [ "//components/webapps/isolated_web_apps:scheme" ]
  }
  if (!is_android) {
    sources += [
      "auto_picture_in_picture_tab_strip_observer_helper.h",
      "auto_pip_setting_overlay_view.h",
      "auto_pip_setting_view.h",
      "picture_in_picture_input_protector.h",
      "picture_in_picture_occlusion_observer.h",
      "picture_in_picture_occlusion_tracker.h",
      "picture_in_picture_occlusion_tracker_observer.h",
      "picture_in_picture_widget_fade_animator.h",
      "picture_in_picture_window.h",
      "picture_in_picture_window_manager_uma_helper.h",
      "scoped_disallow_picture_in_picture.h",
      "scoped_picture_in_picture_occlusion_observation.h",
      "scoped_tuck_picture_in_picture.h",
    ]
    public_deps += [
      "//chrome/browser/ui/tabs:tab_strip_model_observer",
      "//ui/views",
    ]
  } else {  # is_android
    sources += [ "auto_picture_in_picture_tab_model_observer_helper.h" ]
    public_deps += [
      "//chrome/browser/android:tabs_public",
      "//chrome/browser/ui/android/tab_model",
    ]
  }
}

source_set("impl") {
  sources = [
    "picture_in_picture_bounds_cache.cc",
    "picture_in_picture_window_manager.cc",

    # Common Auto Picture-in-Picture files
    "auto_picture_in_picture_safe_browsing_checker_client.cc",
    "auto_picture_in_picture_tab_helper.cc",
    "auto_picture_in_picture_tab_observer_helper_base.cc",
    "auto_pip_setting_helper.cc",
  ]

  deps = [
    ":picture_in_picture",
    "//base",
    "//chrome/browser:browser_process",
    "//chrome/browser/content_settings:content_settings_factory",
    "//chrome/browser/media/webrtc",
    "//chrome/browser/permissions",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/safe_browsing",
    "//chrome/browser/ui:browser_navigator_params_headers",
    "//components/content_settings/browser",
    "//components/content_settings/core/browser",
    "//components/permissions",
    "//content/public/browser",
    "//content/public/common",
    "//extensions/buildflags",
    "//media",
    "//services/metrics/public/cpp:ukm_builders",
    "//ui/display",
    "//ui/gfx/geometry",
  ]

  if (enable_extensions_core) {
    deps += [ "//extensions/common:common_constants" ]
  }

  if (is_android) {
    sources += [ "auto_picture_in_picture_tab_model_observer_helper.cc" ]
  } else {
    sources += [
      "auto_picture_in_picture_tab_strip_observer_helper.cc",
      "auto_pip_setting_overlay_view.cc",
      "auto_pip_setting_view.cc",
      "picture_in_picture_input_protector.cc",
      "picture_in_picture_occlusion_observer.cc",
      "picture_in_picture_occlusion_tracker.cc",
      "picture_in_picture_occlusion_tracker_observer.cc",
      "picture_in_picture_widget_fade_animator.cc",
      "picture_in_picture_window_manager_uma_helper.cc",
      "scoped_disallow_picture_in_picture.cc",
      "scoped_picture_in_picture_occlusion_observation.cc",
      "scoped_tuck_picture_in_picture.cc",
    ]
    deps += [
      "//chrome/app:generated_resources",
      "//chrome/browser/ui:browser_tab_strip",
      "//chrome/browser/ui/color:color_headers",
      "//components/strings:components_strings",
      "//components/url_formatter",
      "//net",
      "//third_party/blink/public/common:headers",
      "//ui/views",
    ]
  }
  if (use_ozone) {
    deps += [ "//ui/ozone" ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "picture_in_picture_bounds_cache_unittest.cc",
    "picture_in_picture_window_manager_unittest.cc",
  ]

  deps = [
    ":picture_in_picture",
    "//base",
    "//base/test:test_support",
    "//chrome/test:test_support",
    "//content/public/browser",
    "//content/test:test_support",
    "//extensions/buildflags",
    "//testing/gmock",
    "//testing/gtest",
    "//ui/display",
  ]

  if (!is_android) {
    sources += [
      "auto_picture_in_picture_safe_browsing_checker_client_unittest.cc",
      "auto_pip_setting_helper_unittest.cc",
      "auto_pip_setting_overlay_view_unittest.cc",
      "auto_pip_setting_view_unittest.cc",
      "picture_in_picture_input_protector_unittest.cc",
      "picture_in_picture_occlusion_tracker_unittest.cc",
      "picture_in_picture_widget_fade_animator_unittest.cc",
      "picture_in_picture_window_manager_uma_helper_unittest.cc",
    ]
    deps += [
      "//chrome/browser",
      "//chrome/browser/ui/safety_hub:test_support",
      "//ui/events:test_support",
    ]
  }
}

if (!is_android) {
  source_set("browser_tests") {
    testonly = true

    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

    sources = [
      "auto_picture_in_picture_tab_helper_browsertest.cc",
      "auto_picture_in_picture_tab_strip_observer_helper_browsertest.cc",
      "document_picture_in_picture_pixel_browsertest.cc",
      "document_picture_in_picture_window_controller_browsertest.cc",
      "picture_in_picture_metric_integration_browsertest.cc",
      "video_picture_in_picture_window_controller_browsertest.cc",
    ]

    deps = [
      ":picture_in_picture",
      ":test_support_ui",
      "//base",
      "//base/test:test_support",
      "//chrome/browser",
      "//chrome/browser/content_settings:content_settings_factory",
      "//chrome/browser/devtools:test_support",
      "//chrome/browser/preloading:test_support",
      "//chrome/browser/profiles:profile",
      "//chrome/browser/ui",
      "//chrome/browser/ui:ui_features",
      "//chrome/browser/ui/views/page_info",
      "//chrome/browser/ui/web_applications",
      "//chrome/browser/web_applications",
      "//chrome/test:test_support",
      "//chrome/test:webrtc_browser_tests_base",
      "//components/content_settings/core/browser",
      "//components/metrics:content",
      "//components/permissions",
      "//components/viz/common",
      "//content/public/browser",
      "//content/public/common",
      "//content/test:browsertest_support",
      "//content/test:test_support",
      "//media",
      "//net:test_support",
      "//services/media_session/public/cpp",
      "//services/network/public/cpp",
      "//testing/gmock",
      "//testing/gtest",
      "//third_party/blink/public/common:headers",
      "//ui/base:hit_test",
      "//ui/base/mojom:ui_base_types",
      "//ui/compositor:test_support",
      "//ui/display",
      "//ui/events:test_support",
      "//ui/gfx/codec",
      "//ui/views:test_support",
      "//url",
    ]

    if (is_chromeos) {
      deps += [
        "//chromeos/ui/base",
        "//ui/aura",
      ]
    }
    if (use_ozone) {
      deps += [ "//ui/ozone" ]
    }
  }
}

if (!is_android) {
  source_set("test_support_ui") {
    testonly = true
    sources = [
      "document_picture_in_picture_mixin_test_base.cc",
      "document_picture_in_picture_mixin_test_base.h",
    ]
    public_deps = [
      "//base",
      "//chrome/test:test_support",
      "//chrome/test:test_support_ui",
      "//ui/gfx/geometry",
    ]
    deps = [
      "//chrome/browser/ui",
      "//chrome/browser/ui/tabs:tab_strip",
      "//content/public/browser",
      "//content/test:test_support",
    ]
  }
}
