# Copyright 2017 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//components/viz/viz.gni")
import("//mojo/public/tools/bindings/mojom.gni")

mojom("compositing") {
  sources = [
    "begin_frame_observer.mojom",
    "display_private.mojom",
    "external_begin_frame_controller.mojom",
    "frame_sink_manager.mojom",
    "frame_sink_video_capture.mojom",
    "layered_window_updater.mojom",
    "overlay_strategy.mojom",
    "renderer_settings.mojom",
    "vsync_parameter_observer.mojom",
  ]

  public_deps = [
    "//gpu/ipc/common:interfaces",
    "//media/capture/mojom:video_capture",
    "//media/mojo/mojom",
    "//mojo/public/mojom/base",
    "//services/viz/public/mojom",
    "//skia/public/mojom",
    "//ui/gfx/geometry/mojom",
    "//ui/gfx/mojom",
    "//ui/latency/mojom",
  ]

  traits_public_deps = [
    "//components/viz/common",
    "//ui/base:features",
  ]

  enabled_features = []
  if (use_ozone) {
    enabled_features += [ "use_ozone" ]
  }
  if (enable_cast_overlay_strategy) {
    enabled_features += [ "enable_cast_overlay_strategy" ]
  }

  cpp_typemaps = [
    {
      types = [
        {
          mojom = "viz.mojom.RendererSettings"
          cpp = "::viz::RendererSettings"
        },
        {
          mojom = "viz.mojom.DebugRendererSettings"
          cpp = "::viz::DebugRendererSettings"
        },
      ]
      traits_headers = [ "//services/viz/privileged/mojom/compositing/renderer_settings_mojom_traits.h" ]
      traits_sources = [ "//services/viz/privileged/mojom/compositing/renderer_settings_mojom_traits.cc" ]
      traits_public_deps += [ "//ui/gfx/geometry/mojom" ]
    },
  ]
}
