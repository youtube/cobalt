# Copyright 2019 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//chrome/common/features.gni")
import("//components/signin/features.gni")
import("//ui/webui/resources/tools/build_webui.gni")

assert(!is_android)

build_webui("build") {
  grd_prefix = "signin"

  static_files = [
    # The shared images are used on the first run experience, sync confirmation
    # and the profile picker.
    "enterprise_profile_welcome/images/enterprise_profile_welcome_illustration.svg",
    "images/shared_dialog_illustration.svg",
    "images/shared_dialog_illustration_dark.svg",
    "images/shared_left_banner.svg",
    "images/shared_left_banner_dark.svg",
    "images/shared_right_banner.svg",
    "images/shared_right_banner_dark.svg",
    "profile_customization/images/profile_customization_illustration.svg",
    "profile_customization/images/profile_customization_illustration_dark.svg",
    "sync_confirmation/sync_confirmation.html",
    "sync_confirmation/sync_disabled_confirmation.html",
    "sync_confirmation/sync_loading_confirmation.html",
    "sync_confirmation/images/sync_confirmation_refreshed_illustration_dark.svg",
    "sync_confirmation/images/sync_confirmation_illustration.svg",
    "sync_confirmation/images/sync_confirmation_illustration_dark.svg",
    "sync_confirmation/images/sync_confirmation_refreshed_illustration.svg",
    "sync_confirmation/images/sync_confirmation_signin_intercept_illustration.svg",
    "sync_confirmation/images/sync_confirmation_signin_intercept_illustration_dark.svg",
  ]
  if (!is_chromeos_ash) {
    static_files += [
      "enterprise_profile_welcome/enterprise_profile_welcome.html",
      "profile_customization/profile_customization.html",
      "signin_email_confirmation/signin_email_confirmation.html",
      "signin_error/signin_error.html",
    ]
  }
  if (enable_dice_support) {
    static_files += [
      "dice_web_signin_intercept/dice_web_signin_intercept.html",
      "dice_web_signin_intercept/images/split_header.svg",
      "signin_reauth/images/account_passwords_reauth_illustration_dark.svg",
      "signin_reauth/images/account_passwords_reauth_illustration.svg",
      "signin_reauth/signin_reauth.html",
    ]
  }

  web_component_files = [
    "sync_confirmation/sync_confirmation_app.ts",
    "sync_confirmation/sync_disabled_confirmation_app.ts",
  ]

  if (!is_chromeos_ash) {
    web_component_files += [
      "enterprise_profile_welcome/enterprise_profile_welcome_app.ts",
      "profile_customization/profile_customization_app.ts",
      "signin_email_confirmation/signin_email_confirmation_app.ts",
      "signin_error/signin_error_app.ts",
    ]
  }
  if (enable_dice_support) {
    web_component_files += [
      "dice_web_signin_intercept/dice_web_signin_intercept_app.ts",
      "signin_reauth/signin_reauth_app.ts",
    ]
  }

  non_web_component_files = [
    "sync_confirmation/sync_confirmation_browser_proxy.ts",
    "sync_confirmation/sync_confirmation.ts",
  ]

  if (!is_chromeos_ash) {
    non_web_component_files += [
      "enterprise_profile_welcome/enterprise_profile_welcome_browser_proxy.ts",
      "profile_customization/profile_customization_browser_proxy.ts",
      "signin_error/signin_error.ts",
    ]
  }
  if (enable_dice_support) {
    non_web_component_files += [
      "dice_web_signin_intercept/dice_web_signin_intercept_browser_proxy.ts",
      "signin_reauth/signin_reauth_browser_proxy.ts",
    ]
  }

  icons_html_files = [ "icons.html" ]

  css_files = [
    "signin_shared.css",
    "signin_vars.css",
    "tangible_sync_style_shared.css",
  ]

  ts_composite = true
  ts_definitions = [ "//tools/typescript/definitions/chrome_send.d.ts" ]
  ts_deps = [
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources/cr_components/customize_themes:build_ts",
    "//ui/webui/resources/cr_elements:build_ts",
    "//ui/webui/resources/js:build_ts",
  ]
}
