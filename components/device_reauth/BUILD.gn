# Copyright 2021 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/config.gni")
  import("//build/config/android/rules.gni")
}

static_library("device_reauth") {
  sources = [
    "device_authenticator.cc",
    "device_authenticator.h",
    "device_authenticator_common.cc",
    "device_authenticator_common.h",
    "device_reauth_metrics_util.h",
  ]

  deps = [
    "//base",
    "//components/keyed_service/core",
  ]
}

static_library("test_support") {
  testonly = true
  sources = [
    "mock_device_authenticator.cc",
    "mock_device_authenticator.h",
  ]

  deps = [
    ":device_reauth",
    "//base",
    "//testing/gmock",
  ]
}

if (is_android) {
  android_library("device_reauth_java_enums") {
    srcjar_deps = [ ":device_reauth_java_enums_srcjar" ]

    deps = [ "//third_party/androidx:androidx_annotation_annotation_java" ]
  }

  java_cpp_enum("device_reauth_java_enums_srcjar") {
    sources = [
      "device_authenticator.h",
      "device_reauth_metrics_util.h",
    ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [ "device_authenticator_common_unittest.cc" ]

  deps = [
    ":device_reauth",
    "//base",
    "//base/test:test_support",
    "//testing/gmock",
    "//testing/gtest",
  ]
}
