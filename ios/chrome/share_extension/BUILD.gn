# Copyright 2016 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/apple/compile_entitlements.gni")
import("//build/apple/tweak_info_plist.gni")
import("//build/config/ios/rules.gni")
import("//build/config/locales.gni")
import("//ios/build/chrome_build.gni")
import("//ios/build/config.gni")
import("//ios/chrome/extension_repack.gni")
import("//ios/chrome/tools/strings/generate_localizable_strings.gni")
import("//ios/public/provider/chrome/browser/build_config.gni")

assert(
    ios_enable_share_extension,
    "ios_enable_share_extension must be true to depend on //ios/chrome/share_extension")

group("resources") {
  deps = [ "//ios/chrome/share_extension/strings" ]
}

extension_repack_all_locales("packed_resources") {
  extension = "share_extension"
  input_locales = platform_pak_locales
  output_locales = locales_as_apple_outputs
}

tweak_info_plist("tweak_info_plist") {
  info_plist = "Info.plist"
}

compile_entitlements("entitlements") {
  substitutions = [ "IOS_BUNDLE_ID_PREFIX=$ios_app_bundle_id_prefix" ]
  output_name = "$target_gen_dir/share_extension.appex.entitlements"
  entitlements_templates =
      [ "entitlements/external/share_extension.appex.entitlements" ]
}

ios_appex_bundle("share_extension") {
  sources = [
    "share_extension_view.h",
    "share_extension_view.mm",
    "share_view_controller.h",
    "share_view_controller.mm",
    "ui_util.h",
    "ui_util.mm",
  ]

  configs += [ "//build/config/compiler:enable_arc" ]

  deps = [
    ":system_strings",
    "//base",
    "//ios/chrome/common/app_group",
    "//ios/chrome/common/app_group:client",
    "//ios/chrome/common/app_group:command",
    "//ios/chrome/common/crash_report",
    "//ios/chrome/common/ui/colors",
    "//ios/chrome/common/ui/util",
  ]
  bundle_deps_filter = [ "//third_party/icu:icudata" ]

  assert_no_deps = ios_extension_assert_no_deps

  frameworks = [
    "Foundation.framework",
    "Social.framework",
    "CoreGraphics.framework",
    "MobileCoreServices.framework",
    "UIKit.framework",
    "UniformTypeIdentifiers.framework",
  ]

  extra_substitutions = [
    "CHROME_CHANNEL_SCHEME=$url_channel_scheme",
    "CHROMIUM_SHORT_NAME=$chromium_short_name",
  ]

  entitlements_target = ":entitlements"
  info_plist_target = ":tweak_info_plist"
  bundle_identifier = "$chromium_bundle_id.ShareExtension"
}

generate_localizable_strings("system_strings") {
  visibility = [ ":*" ]
  _packed_resources_target = ":packed_resources"
  config_file = "share_extension_localize_strings_config.plist"
  datapack_dir = get_label_info(_packed_resources_target, "target_gen_dir")
  packed_locales = locales_as_apple_outputs
  output_filenames = [ "Localizable.strings" ]
  deps = [ _packed_resources_target ]
}
