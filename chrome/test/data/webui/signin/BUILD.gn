# Copyright 2020 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//components/signin/features.gni")
import("../build_webui_tests.gni")

assert(!is_chromeos_ash)

build_webui_tests("build") {
  resource_path_prefix = "signin"

  files = [
    "profile_card_menu_test.ts",
    "profile_picker_app_test.ts",
    "profile_picker_main_view_test.ts",
    "test_manage_profiles_browser_proxy.ts",
    "enterprise_profile_welcome_test.ts",
    "local_profile_customization_focus_test.ts",
    "local_profile_customization_test.ts",
    "profile_customization_test.ts",
    "profile_switch_test.ts",
    "profile_type_choice_test.ts",
    "sync_confirmation_test.ts",
    "test_enterprise_profile_welcome_browser_proxy.ts",
    "test_profile_customization_browser_proxy.ts",
    "test_sync_confirmation_browser_proxy.ts",
  ]

  if (enable_dice_support) {
    files += [
      "dice_web_signin_intercept_test.ts",
      "signin_reauth_test.ts",
      "test_dice_web_signin_intercept_browser_proxy.ts",
      "test_signin_reauth_browser_proxy.ts",
    ]
  }

  if (is_chromeos_lacros) {
    files += [ "account_selection_lacros_test.ts" ]
  }

  ts_path_mappings = [
    "chrome://profile-picker/profile_picker.js|" + rebase_path(
            "$root_gen_dir/chrome/browser/resources/signin/profile_picker/tsc/profile_picker.d.ts",
            target_gen_dir),
    "chrome://profile-picker/lazy_load.js|" + rebase_path(
            "$root_gen_dir/chrome/browser/resources/signin/profile_picker/tsc/lazy_load.d.ts",
            target_gen_dir),
    "chrome://signin-dice-web-intercept/*|" + rebase_path(
            "$root_gen_dir/chrome/browser/resources/signin/tsc/dice_web_signin_intercept/*",
            target_gen_dir),
    "chrome://enterprise-profile-welcome/*|" + rebase_path(
            "$root_gen_dir/chrome/browser/resources/signin/tsc/enterprise_profile_welcome/*",
            target_gen_dir),
    "chrome://profile-customization/*|" + rebase_path(
            "$root_gen_dir/chrome/browser/resources/signin/tsc/profile_customization/*",
            target_gen_dir),
    "chrome://sync-confirmation/*|" + rebase_path(
            "$root_gen_dir/chrome/browser/resources/signin/tsc/sync_confirmation/*",
            target_gen_dir),
    "chrome://signin-reauth/*|" + rebase_path(
            "$root_gen_dir/chrome/browser/resources/signin/tsc/signin_reauth/*",
            target_gen_dir),
  ]

  ts_deps = [
    "//chrome/browser/resources/signin:build_ts",
    "//chrome/browser/resources/signin/profile_picker:build_ts",
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources/cr_elements:build_ts",
    "//ui/webui/resources/js:build_ts",
  ]
}
