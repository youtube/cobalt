# Copyright 2021 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
import("//build/config/ui.gni")

assert(is_chromeos || is_linux || is_mac || is_win,
       "Image Editor is for desktop plaforms only")
assert(!is_fuchsia, "Fuchsia shouldn't use anything in //chrome")

static_library("image_editor_component_util") {
  sources = [
    "image_editor_component_info.cc",
    "image_editor_component_info.h",
  ]

  deps = [ "//base" ]
}

static_library("image_editor") {
  sources = [
    "screenshot_flow.cc",
    "screenshot_flow.h",
  ]

  deps = [
    "//base",
    "//components/lens:lens",
    "//content/public/browser",
    "//ui/base/cursor",
    "//ui/snapshot",
    "//ui/views",
  ]

  if (use_aura) {
    deps += [ "//ui/wm" ]
  }

  if (is_mac) {
    sources += [
      "event_capture_mac.h",
      "event_capture_mac.mm",
    ]
    deps += [ "//components/remote_cocoa/app_shim" ]
  }

  public_deps = [
    "//base",
    "//ui/base/cursor/mojom:cursor_type_shared",
  ]
}
