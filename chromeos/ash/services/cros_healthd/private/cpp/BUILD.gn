# Copyright 2021 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/args.gni")
import("//build/config/chromeos/ui_mode.gni")

assert(is_chromeos_ash)

source_set("cpp") {
  sources = [
    "data_collector.cc",
    "data_collector.h",
    "dlc_utils.cc",
    "dlc_utils.h",
  ]
  deps = [
    "//ash:ash",
    "//base",
    "//chromeos/ash/components/dbus/dlcservice",
    "//chromeos/ash/components/mojo_service_manager",
    "//content/public/browser:browser",
    "//ui/events/devices",
    "//ui/events/ozone/evdev:event_device_info",
  ]
  public_deps = [ "//chromeos/ash/services/cros_healthd/private/mojom" ]
  data_deps = [ ":mojo_service_manager_policy" ]
  defines = []
  if (is_chromeos_device) {
    defines += [ "USE_EVDEV_GESTURES" ]
  }
  if (use_libinput) {
    defines += [ "USE_LIBINPUT" ]
  }
}

copy("mojo_service_manager_policy") {
  sources = [ "cros_healthd_private_policy.json" ]
  outputs = [ "$root_out_dir/mojo_service_manager/{{source_file_part}}" ]
}

source_set("unit_tests") {
  testonly = true
  sources = [ "data_collector_unittest.cc" ]
  deps = [
    ":cpp",
    "//base/test:test_support",
    "//content/test:test_support",
    "//mojo/public/cpp/bindings",
    "//testing/gtest",
    "//ui/events/devices",
    "//ui/events/devices:test_support",
  ]
}
