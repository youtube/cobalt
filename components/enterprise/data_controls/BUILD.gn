# Copyright 2023 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//components/enterprise/buildflags/buildflags.gni")
import("//third_party/protobuf/proto_library.gni")

assert(enterprise_data_controls)

source_set("features") {
  sources = [ "features.cc" ]
  public = [ "features.h" ]
  public_deps = [ "//base" ]
}

source_set("data_controls") {
  sources = [
    "action_context.h",
    "and_condition.cc",
    "and_condition.h",
    "attributes_condition.cc",
    "attributes_condition.h",
    "condition.h",
    "data_controls_policy_handler.cc",
    "data_controls_policy_handler.h",
    "dlp_histogram_helper.cc",
    "dlp_histogram_helper.h",
    "prefs.cc",
    "prefs.h",
    "rule.cc",
    "rule.h",
  ]

  deps = [
    "//base",
    "//components/policy/core/browser",
    "//components/policy/core/common:common_constants",
    "//components/prefs",
    "//components/url_matcher",
    "//url",
  ]

  if (is_chromeos) {
    sources += [
      "component.cc",
      "component.h",
    ]
    deps += [ "//chromeos/dbus/dlp:dlp_proto" ]
  }
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "and_condition_unittest.cc",
    "attributes_condition_unittest.cc",
    "rule_unittest.cc",
  ]

  deps = [
    ":data_controls",
    "//base",
    "//testing/gtest",
  ]
}

proto_library("dlp_policy_event_proto") {
  sources = [ "dlp_policy_event.proto" ]
}
