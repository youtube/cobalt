# Copyright 2018 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/rules.gni")
  import("//third_party/jni_zero/jni_zero.gni")

  _jni_sources = [
    "java/src/org/chromium/chrome/browser/metrics/AndroidSessionDurationsServiceState.java",
    "java/src/org/chromium/chrome/browser/metrics/AppUpdateInfoUtils.java",
  ]

  _test_jni_sources = [ "javatests/src/org/chromium/chrome/browser/metrics/util/UkmUtilsForTest.java" ]

  android_library("java") {
    srcjar_deps = [ ":jni_headers" ]
    sources = [
      "java/src/org/chromium/chrome/browser/metrics/MainIntentBehaviorMetrics.java",
      "java/src/org/chromium/chrome/browser/metrics/PackageMetrics.java",
    ]
    sources += _jni_sources

    deps = [
      "//base:base_java",
      "//base:service_loader_java",
      "//chrome/browser/preferences:java",
      "//chrome/browser/profiles/android:java",
      "//chrome/browser/ui/android/default_browser_promo:java",
      "//components/browser_ui/util/android:java",
      "//components/ukm/android:java",
      "//content/public/android:content_java",
      "//third_party/androidx:androidx_annotation_annotation_java",
      "//third_party/jni_zero:jni_zero_java",
    ]

    public_deps = [ ":delegate_java" ]
  }

  android_library("delegate_java") {
    sources = [
      "java/src/org/chromium/chrome/browser/metrics/AppUpdateInfoDelegate.java",
    ]
  }

  static_library("ukm_utils_for_test") {
    testonly = true
    sources = [ "ukm_utils_for_test.cc" ]
    deps = [
      "//base",
      "//chrome/browser:browser_process",
      "//chrome/browser/android/metrics:test_jni_headers",
      "//components/metrics_services_manager:metrics_services_manager",
      "//components/ukm",
    ]
  }

  generate_jni("jni_headers") {
    sources = _jni_sources
  }

  generate_jni("test_jni_headers") {
    testonly = true
    sources = _test_jni_sources
  }

  android_library("ukm_java_test_support") {
    testonly = true
    srcjar_deps = [ ":test_jni_headers" ]
    sources = _test_jni_sources
    deps = [ "//third_party/jni_zero:jni_zero_java" ]
  }

  android_library("ukm_javatests") {
    testonly = true
    sources =
        [ "javatests/src/org/chromium/chrome/browser/metrics/UkmTest.java" ]

    deps = [
      ":ukm_java_test_support",
      "//base:base_java_test_support",
      "//base:tasks_java",
      "//chrome/android:chrome_java",
      "//chrome/browser/flags:java",
      "//chrome/browser/profiles/android:java",
      "//chrome/browser/signin/services/android:java",
      "//chrome/browser/tab:java",
      "//chrome/test/android:chrome_java_integration_test_support",
      "//components/browsing_data/core:java",
      "//components/metrics:metrics_java",
      "//content/public/test/android:content_java_test_support",
      "//third_party/androidx:androidx_test_monitor_java",
      "//third_party/androidx:androidx_test_runner_java",
      "//third_party/junit",
    ]
  }
}
