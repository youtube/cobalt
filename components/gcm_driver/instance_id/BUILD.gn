# Copyright 2014 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//components/gcm_driver/config.gni")

if (is_android) {
  import("//build/config/android/rules.gni")
}

static_library("instance_id") {
  sources = [
    "instance_id.cc",
    "instance_id.h",
    "instance_id_driver.cc",
    "instance_id_driver.h",
    "instance_id_profile_service.cc",
    "instance_id_profile_service.h",
  ]

  deps = [
    "//base",
    "//components/gcm_driver:gcm_buildflags",
    "//components/keyed_service/core:core",
    "//components/prefs:prefs",
    "//crypto",
    "//services/network/public/mojom",
  ]

  if (use_gcm_from_platform) {
    assert(is_android)
    sources += [
      "instance_id_android.cc",
      "instance_id_android.h",
    ]
    deps += [ "android:jni_headers" ]
  } else {
    sources += [
      "instance_id_impl.cc",
      "instance_id_impl.h",
    ]
  }
}

static_library("test_support") {
  testonly = true
  sources = [
    "fake_gcm_driver_for_instance_id.cc",
    "fake_gcm_driver_for_instance_id.h",
  ]

  public_deps = [ ":instance_id" ]
  deps = [
    "//base",
    "//components/gcm_driver",
    "//net",
    "//testing/gtest",
  ]

  if (is_android) {
    sources += [
      "scoped_use_fake_instance_id_android.cc",
      "scoped_use_fake_instance_id_android.h",
    ]
    deps += [ "android:test_support_jni_headers" ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [ "instance_id_driver_unittest.cc" ]

  deps = [
    ":instance_id",
    ":test_support",
    "//base",
    "//components/gcm_driver:gcm_buildflags",
    "//google_apis/gcm",
    "//net:test_support",
    "//testing/gtest",
  ]
}

if (is_android) {
  java_cpp_enum("instance_id_java_enums_srcjar") {
    sources = [ "instance_id.h" ]
  }
}
