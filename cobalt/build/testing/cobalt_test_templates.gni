# Copyright 2025 The Cobalt Authors. All Rights Reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

template("cobalt_test") {
  actual_target_name = target_name
  shlib_target_name = actual_target_name
  loader_target_name = "${actual_target_name}_loader"

  shared_library(shlib_target_name) {
    output_name = actual_target_name
    forward_variables_from(invoker, "*", TESTONLY_AND_VISIBILITY)
    testonly = true
    if (!defined(data_deps)) {
      data_deps = []
    }
    data_deps += [
      ":${loader_target_name}($starboard_toolchain)",
      "//starboard:starboard_group($starboard_toolchain)",
    ]
  }

  if (current_toolchain == starboard_toolchain) {
    executable(loader_target_name) {
      sources = [ "//$starboard_path/starboard_loader.cc" ]
      testonly = true
      if (use_asan) {
        sources += [ "//$starboard_path/sanitizer_options.cc" ]
      }
      defines = [ "SB_LOADER_MODULE=\"$actual_target_name\"" ]
      configs += [ "//starboard/build/config:starboard_implementation" ]
      deps = [ "//starboard:starboard_group($starboard_toolchain)" ]
    }
  }
}
