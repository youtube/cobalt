import("//build/config/android/config.gni")
import("//build/config/android/rules.gni")
import("//third_party/icu/config.gni")

cobalt_manifest = "$target_gen_dir/cobalt_manifest/AndroidManifest.xml"

# TODO(cobalt, b/375655377): remove testonly tag
android_resources("cobalt_java_resources") {
  testonly = true
  sources = [
    "apk/app/src/app/res/drawable-xhdpi/app_banner.png",
    "apk/app/src/app/res/mipmap-hdpi/ic_app.png",
    "apk/app/src/app/res/mipmap-mdpi/ic_app.png",
    "apk/app/src/app/res/mipmap-xhdpi/ic_app.png",
    "apk/app/src/app/res/mipmap-xxhdpi/ic_app.png",
    "apk/app/src/app/res/values/strings.xml",
    "apk/app/src/main/res/layout/coat_error_dialog.xml",
    "apk/app/src/main/res/layout/content_shell_activity.xml",
    "apk/app/src/main/res/values/colors.xml",
    "apk/app/src/main/res/values/ids.xml",
    "apk/app/src/main/res/values/overlayable.xml",
    "apk/app/src/main/res/values/rro_variables.xml",
    "apk/app/src/main/res/values/strings.xml",
    "apk/app/src/main/res/values/styles.xml",
  ]
  deps = [ "//third_party/androidx:androidx_leanback_leanback_java" ]
}

jinja_template("cobalt_manifest") {
  testonly = true
  input = "apk/app/src/app/AndroidManifest.xml.jinja2"
  output = cobalt_manifest
  variables = [ "manifest_package=dev.cobalt.coat" ]
}

generate_jni("jni_headers") {
  sources = [ "apk/app/src/main/java/dev/cobalt/coat/StarboardBridge.java" ]
}

generate_jni("content_shell_jni_headers") {
  sources = [
    "apk/app/src/org/chromium/content_shell/Shell.java",
    "apk/app/src/org/chromium/content_shell/ShellManager.java",
  ]
}

android_library("content_shell_apk_java") {
  testonly = true

  resources_package = "org.chromium.content_shell_apk"
  deps = [
    ":cobalt_shell_java",
    "//base:base_java",
    "//base:process_launcher_java",
    "//build/android:build_java",
    "//components/embedder_support/android:view_java",
    "//content/public/android:content_java",
    "//content/shell/android:content_shell_apk_resources",
    "//content/shell/android:content_shell_manifest",
    "//media/capture/video/android:capture_java",
    "//net/android:net_java",
    "//third_party/android_deps:com_google_code_findbugs_jsr305_java",
    "//ui/android:ui_java",
    "//url:gurl_java",
  ]

  sources = [
    "//content/shell/android/shell_apk/src/org/chromium/content_shell_apk/ContentShellActivity.java",
    "//content/shell/android/shell_apk/src/org/chromium/content_shell_apk/ContentShellApplication.java",
  ]
}

android_library("cobalt_shell_java") {
  testonly = true
  resources_package = "org.chromium.content_shell"
  deps = [
    ":cobalt_java_resources",
    "//base:base_java",
    "//base:jni_java",
    "//build/android:build_java",
    "//components/download/internal/common:internal_java",
    "//components/embedder_support/android:content_view_java",
    "//components/embedder_support/android:view_java",
    "//components/viz/service:service_java",
    "//content/public/android:content_java",
    "//content/shell/android:content_shell_java_resources",
    "//media/base/android:media_java",
    "//media/capture/video/android:capture_java",
    "//mojo/public/java:system_java",
    "//net/android:net_java",
    "//ui/android:ui_java",
    "//ui/base/cursor/mojom:cursor_type_java",
    "//url:gurl_java",
  ]
  sources = [
    "apk/app/src/org/chromium/content_shell/Shell.java",
    "apk/app/src/org/chromium/content_shell/ShellManager.java",
    "apk/app/src/org/chromium/content_shell/ShellViewAndroidDelegate.java",
  ]

  annotation_processor_deps = [ "//base/android/jni_generator:jni_processor" ]
}

android_library("cobalt_main_java") {
  testonly = true
  resources_package = "dev.cobalt.coat"
  deps = [
    ":cobalt_java_resources",
    ":cobalt_shell_java",
    ":content_shell_apk_java",
    ":jni_headers",
    "//base:base_java",
    "//base:jni_java",
    "//base:process_launcher_java",
    "//build/android:build_java",
    "//components/embedder_support/android:view_java",
    "//components/version_info/android:version_constants_java",
    "//content/public/android:content_java",
    "//content/shell/android:content_shell_manifest",
    "//media/capture/video/android:capture_java",
    "//net/android:net_java",
    "//third_party/android_deps:com_google_code_findbugs_jsr305_java",
    "//third_party/android_deps:google_play_services_ads_identifier_java",
    "//third_party/androidx:androidx_annotation_annotation_java",
    "//ui/android:ui_java",
    "//ui/android:ui_no_recycler_view_java",
    "//url:gurl_java",
  ]
  sources = [
    "apk/app/src/main/java/dev/cobalt/coat/ArtworkDownloader.java",
    "apk/app/src/main/java/dev/cobalt/coat/ArtworkDownloaderDefault.java",
    "apk/app/src/main/java/dev/cobalt/coat/ArtworkLoader.java",
    "apk/app/src/main/java/dev/cobalt/coat/CaptionSettings.java",
    "apk/app/src/main/java/dev/cobalt/coat/CobaltActivity.java",
    "apk/app/src/main/java/dev/cobalt/coat/CobaltHttpHelper.java",
    "apk/app/src/main/java/dev/cobalt/coat/javabridge/AmatiDeviceInspector.java",
    "apk/app/src/main/java/dev/cobalt/coat/javabridge/CobaltJavaScriptAndroidObject.java",
    "apk/app/src/main/java/dev/cobalt/coat/javabridge/CobaltJavaScriptInterface.java",
    "apk/app/src/main/java/dev/cobalt/coat/javabridge/H5vccPlatformService.java",
    "apk/app/src/main/java/dev/cobalt/coat/javabridge/HTMLMediaElementExtension.java",

    # "apk/app/src/main/java/dev/cobalt/coat/CobaltMediaSession.java",
    "apk/app/src/main/java/dev/cobalt/coat/AdvertisingId.java",
    "apk/app/src/main/java/dev/cobalt/coat/CobaltService.java",
    "apk/app/src/main/java/dev/cobalt/coat/CobaltSystemConfigChangeReceiver.java",
    "apk/app/src/main/java/dev/cobalt/coat/CobaltTextToSpeechHelper.java",
    "apk/app/src/main/java/dev/cobalt/coat/CrashContextUpdateHandler.java",
    "apk/app/src/main/java/dev/cobalt/coat/ErrorDialog.java",
    "apk/app/src/main/java/dev/cobalt/coat/MediaImage.java",
    "apk/app/src/main/java/dev/cobalt/coat/NetworkStatus.java",
    "apk/app/src/main/java/dev/cobalt/coat/NullCobaltFactory.java",
    "apk/app/src/main/java/dev/cobalt/coat/PlatformError.java",
    "apk/app/src/main/java/dev/cobalt/coat/ResourceOverlay.java",
    "apk/app/src/main/java/dev/cobalt/coat/StarboardBridge.java",
    "apk/app/src/main/java/dev/cobalt/coat/VolumeStateReceiver.java",
    "apk/app/src/main/java/dev/cobalt/libraries/services/clientloginfo/ClientLogInfo.java",
    "apk/app/src/main/java/dev/cobalt/libraries/services/clientloginfo/ClientLogInfoModule.java",
    "apk/app/src/main/java/dev/cobalt/media/AudioOutputManager.java",
    "apk/app/src/main/java/dev/cobalt/media/AudioTrackBridge.java",
    "apk/app/src/main/java/dev/cobalt/media/Log.java",
    "apk/app/src/main/java/dev/cobalt/media/MediaCodecBridge.java",
    "apk/app/src/main/java/dev/cobalt/media/MediaCodecBridgeBuilder.java",
    "apk/app/src/main/java/dev/cobalt/media/MediaCodecCapabilitiesLogger.java",
    "apk/app/src/main/java/dev/cobalt/media/MediaCodecStatus.java",
    "apk/app/src/main/java/dev/cobalt/media/MediaCodecUtil.java",
    "apk/app/src/main/java/dev/cobalt/media/MediaDrmBridge.java",
    "apk/app/src/main/java/dev/cobalt/media/MediaFormatBuilder.java",
    "apk/app/src/main/java/dev/cobalt/media/VideoDecoderCache.java",
    "apk/app/src/main/java/dev/cobalt/media/VideoFrameReleaseTimeHelper.java",
    "apk/app/src/main/java/dev/cobalt/media/VideoSurfaceTexture.java",
    "apk/app/src/main/java/dev/cobalt/media/VideoSurfaceView.java",
    "apk/app/src/main/java/dev/cobalt/util/AssetLoader.java",
    "apk/app/src/main/java/dev/cobalt/util/DisplayUtil.java",
    "apk/app/src/main/java/dev/cobalt/util/Holder.java",
    "apk/app/src/main/java/dev/cobalt/util/IsEmulator.java",
    "apk/app/src/main/java/dev/cobalt/util/Log.java",
    "apk/app/src/main/java/dev/cobalt/util/SynchronizedHolder.java",
    "apk/app/src/main/java/dev/cobalt/util/SystemPropertiesHelper.java",
    "apk/app/src/main/java/dev/cobalt/util/UsedByNative.java",
  ]

  annotation_processor_deps = [ "//base/android/jni_generator:jni_processor" ]
}

android_library("cobalt_apk_java") {
  testonly = true
  resources_package = "dev.cobalt.coat"

  deps = [ ":cobalt_main_java" ]

  sources = [
    "apk/app/src/app/java/dev/cobalt/app/CobaltApplication.java",
    "apk/app/src/app/java/dev/cobalt/app/MainActivity.java",
  ]
}

android_assets("cobalt_apk_assets") {
  testonly = true
  sources = [
    "apk/app/src/app/assets/not_empty.txt",
    "apk/app/src/app/assets/test/not_empty.txt",
    "apk/app/src/app/assets/web/cobalt_blue_splash_screen.css",
    "apk/app/src/app/assets/web/cobalt_blue_splash_screen.html",
    "apk/app/src/app/assets/web/cobalt_logo_1024.png",
  ]
  disable_compression = true
}

template("content_shell_apk_tmpl") {
  _target_type = invoker.target_type
  target(_target_type, target_name) {
    forward_variables_from(invoker, "*")
    testonly = true
    if (!defined(deps)) {
      deps = []
    }
    deps += [
      ":cobalt_shell_java",
      ":content_shell_apk_java",
      "//base:base_java_test_support",
      "//components/crash/android:java",
      "//components/crash/core/app:chrome_crashpad_handler_named_as_so",
      "//components/metrics:metrics_java",
      "//content/public/android:content_java",
      "//content/public/test/android:android_test_message_pump_support_java",
      "//content/shell/android:content_shell_assets",
      "//media/capture/video/android:capture_java",
      "//net/android:net_java",
      "//services/shape_detection:shape_detection_java",
      "//third_party/mesa_headers",
      "//ui/android:ui_java",
    ]
    loadable_modules = [ "$root_out_dir/libchrome_crashpad_handler.so" ]
  }
}

# Copied from target: libcontent_shell_content_view
# TODO(b/376867565) rename this library, NO shell
shared_library("libcobalt_content_shell_content_view") {
  # TODO(b/375655377): remove testonly
  testonly = true
  deps = [
    ":content_shell_jni_headers",
    "//cobalt/renderer:renderer",
    "//cobalt/services/crash_annotator",
    "//cobalt/services/crash_annotator/public/mojom",
    "//cobalt/user_agent",

    # TODO: what can be removed in the dependencies?
    "//components/crash/content/browser",
    "//content/shell:content_shell_app",
    "//content/shell:content_shell_lib",
    "//content/shell:pak",
    "//media",
    "//skia",
    "//starboard/android/shared:starboard_jni_state",
    "//third_party/blink/public/common",
  ]

  # Explicit dependency required for JNI registration to be able to
  # find the native side functions.
  if (is_component_build) {
    deps += [
      "//device/gamepad",
      "//media/midi",
    ]
  }
  sources = [
    # TODO(cobalt, b/383301493): move browser-related source files under a
    # //cobalt/browser directory and define a "browser" build target there.
    "//cobalt/cobalt_browser_interface_binders.cc",
    "//cobalt/cobalt_browser_interface_binders.h",
    "//cobalt/cobalt_content_browser_client.cc",
    "//cobalt/cobalt_content_browser_client.h",
    "//cobalt/cobalt_main_delegate.cc",
    "//cobalt/cobalt_main_delegate.h",
    "//cobalt/cobalt_web_contents_observer.cc",
    "//cobalt/cobalt_web_contents_observer.h",
    "cobalt_library_loader.cc",
  ]
  configs -= [ "//build/config/android:hide_all_but_jni_onload" ]
  configs += [ "//build/config/android:hide_all_but_jni" ]
}

content_shell_apk_tmpl("cobalt_apk") {
  target_type = "android_apk"
  apk_name = "Cobalt"
  android_manifest = cobalt_manifest
  android_manifest_dep = ":cobalt_manifest"
  deps = [
    ":cobalt_apk_assets",
    ":cobalt_apk_java",
  ]
  shared_libraries = [ ":libcobalt_content_shell_content_view" ]
  include_size_info = is_official_build
}

robolectric_binary("cobalt_coat_junit_tests") {
  sources = [ "apk/app/src/test/java/dev/cobalt/coat/CobaltActivityTest.java" ]
  deps = [
    ":cobalt_apk_java",
    "//base:base_java",
    "//base:base_java_test_support",
    "//base:base_junit_test_support",
    "//content/public/android:content_java",
    "//third_party/androidx:androidx_test_runner_java",
    "//third_party/google-truth:google_truth_java",
    "//third_party/junit",
  ]
}
