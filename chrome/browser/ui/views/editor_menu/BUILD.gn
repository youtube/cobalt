# Copyright 2023 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")

assert(is_chromeos)

source_set("utils") {
  sources = [
    "utils/focus_search.cc",
    "utils/focus_search.h",
    "utils/pre_target_handler.cc",
    "utils/pre_target_handler.h",
    "utils/preset_text_query.cc",
    "utils/preset_text_query.h",
    "utils/utils.cc",
    "utils/utils.h",
  ]

  deps = [
    "//base",
    "//chrome/browser/ui/views/editor_menu/vector_icons",
    "//components/vector_icons",
    "//ui/aura",
    "//ui/display",
    "//ui/events",
    "//ui/gfx",
    "//ui/views",
  ]
}

source_set("editor_menu") {
  sources = [
    "editor_menu_controller_impl.cc",
    "editor_menu_controller_impl.h",
  ]

  deps = [
    ":utils",
    ":views",
    "//ash/webui/settings/public/constants:mojom",
    "//base",
    "//chrome/browser/profiles:profile",
    "//chromeos/components/editor_menu/public/cpp",
    "//chromeos/crosapi/mojom",
    "//content/public/browser:browser",
    "//ui/gfx",
    "//ui/views",
  ]

  if (is_chromeos_ash) {
    deps += [ "//chrome/browser/ash" ]
  }

  if (is_chromeos_lacros) {
    deps += [ "//chromeos/lacros" ]
  }
}

source_set("views") {
  sources = [
    "editor_menu_chip_view.cc",
    "editor_menu_chip_view.h",
    "editor_menu_gradient_badge.cc",
    "editor_menu_gradient_badge.h",
    "editor_menu_promo_card_view.cc",
    "editor_menu_promo_card_view.h",
    "editor_menu_textfield_view.cc",
    "editor_menu_textfield_view.h",
    "editor_menu_view.cc",
    "editor_menu_view.h",
    "editor_menu_view_delegate.h",
  ]

  deps = [
    ":utils",
    "//base",
    "//chrome/browser/ui/views/editor_menu/vector_icons",
    "//chromeos/strings:strings_grit",
    "//components/vector_icons",
    "//ui/aura",
    "//ui/base",
    "//ui/gfx",
    "//ui/views",
  ]
}
