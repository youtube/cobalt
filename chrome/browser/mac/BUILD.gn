# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/util/process_version.gni")
import("//chrome/updater/branding.gni")
import("//testing/test.gni")

process_version("install") {
  extra_args = [
    "-e",
    "SHEBANGGUARD=\"\"",
    "-e",
    "BROWSER_PRODUCT_NAME=\"$browser_product_name\"",
    "-e",
    "UPDATER_PRODUCT_NAME=\"$updater_product_full_name\"",
    "-e",
    "KEYSTONE_APP_NAME=\"$keystone_app_name\"",
    "-e",
    "UPDATER_COMPANY_SHORT_NAME=\"$updater_company_short_name\"",
  ]
  process_only = true
  executable = true
  template_file = "install.sh.in"
  output = "$target_gen_dir/install.sh"
}

# These tests mutate system state and should not be bundled into unit_tests or
# other commonly-run targets.
test("install_sh_test") {
  testonly = true
  sources = [ "install_test.cc" ]
  deps = [
    "//base",
    "//base/test:test_config",
    "//base/test:test_support",
    "//build:branding_buildflags",
    "//chrome",
    "//testing/gtest",
  ]
}
