# Copyright 2025 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
import("//build/rust/rust_bindgen.gni")
import("//build/rust/rust_executable.gni")
import("//build/rust/rust_static_library.gni")
import("//testing/test.gni")

group("all") {
  # Meta target, don't depend on it from production code
  testonly = true
  deps = [
    ":mojo_c_system_bindings",
    ":rust_mojo_tests",
    "mojom_parser",
  ]
}

rust_bindgen("mojo_c_system_bindings") {
  header = "//mojo/public/c/system/thunks.h"
  deps = [ "//mojo/public/c/system" ]
}

rust_static_library("mojo_ffi") {
  crate_root = "system/ffi.rs"
  sources = [ "system/ffi.rs" ]
  deps = [ ":mojo_c_system_bindings" ]
}

rust_static_library("mojo_rust_system_api") {
  crate_root = "system/lib.rs"
  sources = [
    "system/data_pipe.rs",
    "system/lib.rs",
    "system/mojo_types.rs",
    "system/trap.rs",
  ]
  allow_unsafe = true
  deps = [
    ":mojo_ffi",
    "//third_party/rust/bitflags/v2:lib",
  ]
}

# FOR_RELEASE: This should be run by the //mojo:mojo_unittests target
group("tests") {
  testonly = true
  deps = [
    "mojom_parser:mojom_parser_unittests",
    "test_mojom:test_mojom_rust",
    "test_system:system_api_unittests",
  ]
}

# Standalone testing binary with just rustmojo tests.
# Convenient for development so we only have to build/run our own code.
test("rust_mojo_tests") {
  testonly = true
  deps = [
    ":tests",
    "//base/test:run_all_unittests",
  ]
}
