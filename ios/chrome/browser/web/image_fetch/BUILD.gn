# Copyright 2021 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//ios/web/public/js_messaging/optimize_js.gni")
import("//testing/libfuzzer/fuzzer_test.gni")

source_set("image_fetch") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "image_fetch_java_script_feature.h",
    "image_fetch_java_script_feature.mm",
    "image_fetch_tab_helper.h",
    "image_fetch_tab_helper.mm",
  ]
  deps = [
    ":image_fetch_js",
    "//base",
    "//components/image_fetcher/core",
    "//ios/web",
    "//ios/web/common",
    "//ios/web/public/js_messaging",
    "//url",
  ]
}

optimize_js("image_fetch_js") {
  visibility = [
    ":image_fetch",
    ":unit_tests",
  ]

  primary_script = "resources/image_fetch.js"
  sources = [ "resources/image_fetch.js" ]
}

source_set("unit_tests") {
  configs += [ "//build/config/compiler:enable_arc" ]
  testonly = true
  sources = [
    "image_fetch_java_script_feature_unittest.mm",
    "image_fetch_tab_helper_unittest.mm",
  ]
  deps = [
    ":image_fetch",
    ":image_fetch_js",
    "//base",
    "//base/test:test_support",
    "//ios/chrome/browser/browser_state:test_support",
    "//ios/web/js_messaging:java_script_feature",
    "//ios/web/public/js_messaging:js_messaging",
    "//ios/web/public/test",
    "//ios/web/test:js_test_util_internal",
    "//net:test_support",
    "//services/network:test_support",
  ]
}

fuzzer_test("image_fetch_java_script_feature_fuzzer") {
  additional_configs = [
    "//build/config/compiler:enable_arc",
    "//testing/libfuzzer:build_for_ios_clusterfuzz_job",
  ]

  sources = [ "image_fetch_java_script_feature_fuzzer.mm" ]
  deps = [
    ":image_fetch",
    "//base",
    "//ios/web/public/js_messaging",
    "//ios/web/public/js_messaging/fuzzer_support",
    "//ios/web/public/js_messaging/fuzzer_support:js_message_proto",
    "//ios/web/public/test:fuzzer_support",
    "//third_party/libprotobuf-mutator",
  ]
  seed_corpus = "fuzzer_corpus"
}
