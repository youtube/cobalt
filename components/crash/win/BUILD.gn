# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/compiler/compiler.gni")
import("//build/config/win/manifest.gni")
import("//chrome/process_version_rc_template.gni")
import("//testing/test.gni")

assert(is_win)

process_version_rc_template("chrome_wer_resources") {
  sources = [ "chrome_wer.ver" ]
  output = "$target_gen_dir/chrome_wer_version.rc"
}

shared_library("chrome_wer") {
  sources = [
    "chrome_wer.def",
    "chrome_wer_main.cc",
  ]
  if (!is_asan && !use_clang_profiling) {
    no_default_deps = true
  }
  deps = [
    ":chrome_wer_resources",
    "//third_party/crashpad/crashpad/handler/win/wer:crashpad_wer_handler",
  ]

  # Keep this a tiny dll as it does very little.
  assert_no_deps = [
    "//third_party/abseil-cpp:absl",
    "//third_party/perfetto:libperfetto",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [ "chrome_wer_module_unittest.cc" ]
  deps = [
    "//base:base",
    "//testing/gtest",
  ]
  data_deps = [ ":chrome_wer" ]
}
