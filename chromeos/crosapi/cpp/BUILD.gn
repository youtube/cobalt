# Copyright 2020 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")

# C++ components used by both lacros-chrome and ash-chrome.
config("crosapi_implementation") {
  defines = [ "IS_CROSAPI_IMPL" ]
}

component("cpp") {
  output_name = "crosapi_public_cpp"
  sources = [
    "channel_to_enum.cc",
    "channel_to_enum.h",
    "gurl_os_handler_utils.cc",
    "gurl_os_handler_utils.h",
    "keystore_service_util.cc",
    "keystore_service_util.h",
    "scoped_allow_sync_call.cc",
    "scoped_allow_sync_call.h",
  ]
  configs += [ ":crosapi_implementation" ]
  deps = [
    ":crosapi_constants",
    "//base",
    "//chromeos/crosapi/mojom",
    "//components/version_info",
    "//mojo/public/cpp/bindings",
  ]

  if (is_chromeos_ash) {
    sources += [
      "lacros_startup_state.cc",
      "lacros_startup_state.h",
    ]
  }
}

static_library("crosapi_constants") {
  sources = [
    "crosapi_constants.cc",
    "crosapi_constants.h",
  ]
  deps = [ "//base" ]
}

source_set("unit_tests") {
  testonly = true
  deps = [
    ":cpp",
    ":crosapi_constants",
    "//base/test:test_support",
    "//chromeos/crosapi/mojom",
    "//components/version_info",
    "//testing/gtest",
  ]
  sources = [
    "channel_to_enum_unittest.cc",
    "gurl_os_handler_unittest.cc",
    "keystore_service_util_unittest.cc",
  ]
}

source_set("test_support") {
  testonly = true

  sources = [ "input_method_test_interface_constants.h" ]

  deps = [ "//base" ]
}
