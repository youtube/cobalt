# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")
import("//third_party/jni_zero/jni_zero.gni")

android_library("test_java") {
  testonly = true
  resources_package = "org.chromium.chrome.browser.autofill"

  sources = [
    "android/java/src/org/chromium/chrome/browser/autofill/PersonalDataManagerTest.java",
    "android/java/src/org/chromium/chrome/browser/autofill/SubKeyRequesterIntegrationTest.java",
  ]

  deps = [
    ":test_support_java",
    "//base:base_java",
    "//chrome/browser/autofill/test:test_support_java",
    "//chrome/browser/flags:java",
    "//chrome/test/android:chrome_java_integration_test_support",
    "//components/autofill/android:autofill_java_resources",
    "//components/autofill/android:autofill_payments_java_resources",
    "//components/image_fetcher:java",
    "//components/image_fetcher:test_support_java",
    "//third_party/androidx:androidx_appcompat_appcompat_resources_java",
    "//third_party/androidx:androidx_test_runner_java",
    "//third_party/google-truth:google_truth_java",
    "//third_party/hamcrest:hamcrest_java",
    "//third_party/junit:junit",
    "//url:gurl_java",
  ]
}

android_library("test_support_java") {
  testonly = true
  resources_package = "org.chromium.chrome.browser.autofill"

  sources = [ "android/java/src/org/chromium/chrome/browser/autofill/AutofillTestHelper.java" ]

  deps = [
    "//base:tasks_java",
    "//chrome/browser/profiles/android:java",
    "//content/public/android:content_java",
    "//third_party/android_deps:espresso_java",
    "//third_party/hamcrest:hamcrest_java",
    "//third_party/jni_zero:jni_zero_java",
    "//url:gurl_java",
  ]

  public_deps = [
    "//base:base_java_test_support",
    "//chrome/browser/autofill/android:java",
    "//components/autofill/android:main_autofill_java",
    "//content/public/test/android:content_java_test_support",
  ]

  srcjar_deps = [ ":jni_headers" ]
}

generate_jni("jni_headers") {
  testonly = true
  visibility = [ ":*" ]
  sources = [ "android/java/src/org/chromium/chrome/browser/autofill/AutofillTestHelper.java" ]
}

static_library("test_support") {
  testonly = true
  sources = [ "android/autofill_test_helper_android.cc" ]
  deps = [
    ":jni_headers",
    "//base",
    "//chrome/browser",
    "//chrome/browser/autofill",
    "//components/autofill/core/browser",
  ]
}
