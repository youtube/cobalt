# Copyright 2024 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//extensions/buildflags/buildflags.gni")

assert(enable_extensions)
assert(is_chromeos)

static_library("login") {
  sources = [
    "errors.cc",
    "errors.h",
    "login_api.cc",
    "login_api.h",
    "login_api_prefs.cc",
    "login_api_prefs.h",
  ]

  public_deps = [
    "//base",
    "//chromeos/crosapi/mojom",
    "//extensions/browser",
  ]

  deps = [
    "//build:chromeos_buildflags",
    "//chrome/common:constants",
    "//chrome/common/extensions/api",
    "//components/pref_registry",
    "//components/prefs",
  ]

  if (is_chromeos_ash) {
    sources += [
      "login_api_lock_handler.cc",
      "login_api_lock_handler.h",
      "shared_session_handler.cc",
      "shared_session_handler.h",
    ]
    deps += [
      "//ash/constants",
      "//chrome/browser/ash/login",
      "//chrome/browser/ash/login/lock",
      "//chrome/browser/chromeos/extensions/login_screen/login/cleanup",
      "//chrome/browser/ui/ash/session",
      "//chromeos/ash/components/login/auth/public:authpublic",
      "//chromeos/ash/components/settings",
      "//components/session_manager:base",
      "//components/session_manager/core",
      "//components/user_manager",
      "//crypto",
      "//third_party/boringssl",
    ]
  }
}

if (is_chromeos_ash) {
  source_set("browser_tests") {
    testonly = true

    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

    sources = [ "login_apitest.cc" ]

    deps = [
      ":login",
      "//base",
      "//chrome/browser",
      "//chrome/browser:browser_process",
      "//chrome/browser/ash/login",
      "//chrome/browser/ash/login/lock",
      "//chrome/browser/ash/login/lock:test_support",
      "//chrome/browser/ash/login/test:test_support",
      "//chrome/browser/ash/policy/test_support",
      "//chrome/browser/ash/profiles",
      "//chrome/common:constants",
      "//chrome/test:test_support",
      "//chrome/test:test_support_ui",
      "//chromeos/ash/components/cryptohome",
      "//chromeos/ash/components/dbus/cryptohome:cryptohome_proto",
      "//chromeos/ash/components/dbus/session_manager",
      "//chromeos/ash/components/dbus/userdataauth",
      "//components/policy:generated",
      "//components/policy/core/common",
      "//components/policy/core/common:common_constants",
      "//components/policy/core/common:test_support",
      "//components/prefs",
      "//components/session_manager:base",
      "//components/user_manager:common",
      "//components/version_info",
      "//content/public/browser",
      "//content/test:test_support",
      "//extensions:test_support",
      "//extensions/browser/api/test",
      "//extensions/common",
      "//testing/gtest",
    ]
  }
}

if (is_chromeos_ash) {
  source_set("unit_tests") {
    testonly = true

    sources = [ "login_api_ash_unittest.cc" ]

    deps = [
      ":login",
      "//ash/constants",
      "//base",
      "//chrome/browser:browser_process",
      "//chrome/browser/ash/login",
      "//chrome/browser/ash/login/users:test_support",
      "//chrome/browser/chromeos/extensions/login_screen/login/cleanup",
      "//chrome/browser/chromeos/extensions/login_screen/login/cleanup:test_support",
      "//chrome/browser/chromeos/extensions/login_screen/login/external_logout_done",
      "//chrome/browser/chromeos/extensions/login_screen/login/external_logout_request",
      "//chrome/browser/ui/ash/login:test_support",
      "//chrome/common:constants",
      "//chrome/test:test_support",
      "//chromeos/ash/components/login/auth/public:authpublic",
      "//chromeos/ash/components/settings",
      "//components/account_id",
      "//components/prefs",
      "//components/session_manager:base",
      "//components/user_manager",
      "//content/public/browser",
      "//extensions:test_support",
      "//extensions/common",
      "//testing/gmock",
      "//testing/gtest",
      "//ui/base",
    ]
  }
}
