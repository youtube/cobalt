# Copyright 2024 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(is_win || is_mac || is_linux || is_chromeos)

source_set("content_settings") {
  sources = []
  deps = []
  public_deps = [
    "//base",
    "//chrome/app/vector_icons:vector_icons",
    "//chrome/browser/ui/blocked_content",
    "//components/content_settings/browser",
    "//components/content_settings/core/common",
    "//components/custom_handlers",
    "//services/device/public/cpp/geolocation:buildflags",
  ]

  if (!is_android) {
    sources += [
      "content_setting_bubble_model.h",
      "content_setting_bubble_model_delegate.h",
      "content_setting_image_model.h",
      "content_setting_image_model_states.h",
    ]
    public_deps += [ "//chrome/app/vector_icons" ]
  }
}

source_set("impl") {
  sources = []

  if (!is_android) {
    sources += [
      "content_setting_bubble_model.cc",
      "content_setting_image_model.cc",
      "content_setting_image_model_states.cc",
    ]
  }

  public_deps = [ "//chrome/browser:browser_public_dependencies" ]

  deps = [
    ":content_settings",
    "//chrome/app:generated_resources",
    "//chrome/app/theme:theme_resources",
    "//chrome/browser:browser_process",
    "//chrome/browser/content_settings",
    "//chrome/browser/content_settings:content_settings_factory",
    "//chrome/browser/content_settings:content_settings_util",
    "//chrome/browser/media/webrtc",
    "//chrome/browser/permissions",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/ui:ui_features",
    "//chrome/browser/ui:url_identity",
    "//chrome/browser/web_applications",
    "//chrome/browser/web_applications",
    "//chrome/common:non_code_constants",
    "//components/content_settings/core/browser",
    "//components/content_settings/core/browser:cookie_settings",
    "//components/infobars/content:content",
    "//components/no_state_prefetch/browser",
    "//components/subresource_filter/content/browser",
    "//components/subresource_filter/core/browser",
    "//services/device/public/cpp:device_features",
  ]
}

source_set("browser_tests") {
  testonly = true
  sources = []
  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

  if (!is_android) {
    sources += [
      "content_setting_bubble_model_browsertest.cc",
      "content_setting_image_model_browsertest.cc",
      "framebust_block_browsertest.cc",
    ]
  }

  deps = [
    ":content_settings",
    ":test_support",
    "//chrome/browser:browser_process",
    "//chrome/browser/content_settings:content_settings_factory",
    "//chrome/browser/content_settings:content_settings_util",
    "//chrome/browser/profiles:profile",
    "//chrome/test:test_support",
    "//components/content_settings/core/test:test_support",
    "//components/subresource_filter/core/browser:browser",
  ]
}

source_set("test_support") {
  testonly = true
  sources = [
    "fake_owner.cc",
    "fake_owner.h",
  ]

  deps = [ ":content_settings" ]
}

source_set("unit_tests") {
  testonly = true
  sources = []
  deps = []

  if (!is_android) {
    sources += [
      "content_setting_bubble_model_unittest.cc",
      "content_setting_image_model_unittest.cc",
    ]
  }

  if (is_mac) {
    sources += [
      "content_setting_media_image_model_unittest.mm",
      "media_authorization_wrapper_test.h",
      "media_authorization_wrapper_test.mm",
    ]
    deps += [ "//chrome/browser/permissions/system" ]
  }

  deps += [
    ":content_settings",
    ":test_support",
    "//chrome/browser/content_settings",
    "//chrome/browser/content_settings:content_settings_factory",
    "//chrome/browser/permissions",
    "//chrome/browser/permissions:unit_tests",
    "//chrome/browser/ui:ui_features",
    "//chrome/browser/web_applications:web_applications_test_support",
    "//chrome/test:test_support",
    "//components/custom_handlers:test_support",
    "//components/infobars/content:content",
    "//components/no_state_prefetch/browser",
    "//services/data_decoder/public/cpp:test_support",
  ]
}
