# Copyright 2016 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//extensions/buildflags/buildflags.gni")

assert(enable_extensions,
       "Cannot depend on extensions because enable_extensions=false.")

source_set("system_cpu") {
  sources = [
    "cpu_info_provider.cc",
    "cpu_info_provider.h",
    "system_cpu_api.cc",
    "system_cpu_api.h",
  ]

  configs += [ "//build/config/compiler:wexit_time_destructors" ]

  deps = [
    "//build:chromeos_buildflags",
    "//extensions/browser/api/system_info",
    "//extensions/common/api",
  ]

  if (is_fuchsia) {
    sources += [ "cpu_info_provider_fuchsia.cc" ]
  }

  if (is_mac) {
    sources += [ "cpu_info_provider_mac.cc" ]
  }

  if (is_linux || is_chromeos) {
    sources += [ "cpu_info_provider_linux.cc" ]
  }

  if (is_win) {
    sources += [ "cpu_info_provider_win.cc" ]
  }

  if (is_chromeos) {
    deps += [ "//chromeos/system" ]
  }

  public_deps = [ "//extensions/browser:browser_sources" ]
}
