# Copyright 2014 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//build/config/ui.gni")

component("views_content_client") {
  testonly = true
  sources = [
    "views_content_browser_client.cc",
    "views_content_browser_client.h",
    "views_content_client.cc",
    "views_content_client.h",
    "views_content_client_export.h",
    "views_content_client_main_parts.cc",
    "views_content_client_main_parts.h",
    "views_content_main_delegate.cc",
    "views_content_main_delegate.h",
  ]

  if (is_chromeos_ash) {
    sources += [ "views_content_client_main_parts_chromeos.cc" ]
  }

  if (is_mac) {
    sources += [ "views_content_client_main_parts_mac.mm" ]
  }

  defines = [ "VIEWS_CONTENT_CLIENT_IMPLEMENTATION" ]

  deps = [
    "//base",
    "//base:i18n",
    "//build:chromeos_buildflags",
    "//content",
    "//content/shell:content_shell_lib",
    "//third_party/icu",
    "//ui/base",
    "//ui/base/ime/init",
    "//ui/display",
    "//ui/events",
    "//ui/gfx",
    "//ui/gfx/geometry",
    "//ui/resources",
    "//ui/resources:ui_test_pak",
    "//ui/resources:ui_test_pak_data_200_percent",
    "//ui/views",
    "//ui/views:test_support",
  ]

  if (use_aura) {
    sources += [
      "views_content_client_main_parts_aura.cc",
      "views_content_client_main_parts_aura.h",
    ]

    if (!is_chromeos_ash) {
      sources += [ "views_content_client_main_parts_desktop_aura.cc" ]
    }

    deps += [
      "//ui/aura",
      "//ui/wm",
    ]
  }

  if (is_chromeos_ash) {
    deps += [
      "//ui/aura:test_support",
      "//ui/wm:test_support",
    ]
  }
}
