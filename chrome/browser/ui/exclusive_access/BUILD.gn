# Copyright 2024 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/args.gni")
import("//build/config/ozone.gni")
import("//extensions/buildflags/buildflags.gni")

assert(is_win || is_mac || is_linux || is_chromeos)

source_set("exclusive_access") {
  sources = [
    "exclusive_access_bubble.cc",
    "exclusive_access_bubble.h",
    "exclusive_access_bubble_hide_callback.h",
    "exclusive_access_bubble_type.cc",
    "exclusive_access_bubble_type.h",
    "exclusive_access_context.h",
    "exclusive_access_controller_base.cc",
    "exclusive_access_controller_base.h",
    "exclusive_access_manager.cc",
    "exclusive_access_manager.h",
    "exclusive_access_permission_manager.cc",
    "exclusive_access_permission_manager.h",
    "fullscreen_controller.cc",
    "fullscreen_controller.h",
    "fullscreen_observer.h",
    "fullscreen_within_tab_helper.cc",
    "fullscreen_within_tab_helper.h",
    "keyboard_lock_controller.cc",
    "keyboard_lock_controller.h",
    "pointer_lock_controller.cc",
    "pointer_lock_controller.h",
  ]
  public_deps = [
    "//base",
    "//chrome/browser:browser_public_dependencies",
    "//components/content_settings/core/common",
    "//content/public/browser",
    "//third_party/blink/public/common:headers",
    "//ui/display/types",
    "//url",
  ]
  deps = [
    "//chrome/app:generated_resources",
    "//chrome/app:generated_resources_grit",
    "//chrome/browser:browser_process",
    "//chrome/browser/content_settings:content_settings_factory",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/ui:ui_features",
    "//chrome/browser/ui/blocked_content",
    "//chrome/browser/ui/tabs:tab_strip",
    "//chrome/common",
    "//chrome/common:constants",
    "//chrome/common:non_code_constants",
    "//components/content_settings/core/browser",
    "//components/history/core/browser",
    "//components/input",
    "//components/permissions:permissions_common",
    "//components/prefs",
    "//components/safe_browsing/content/browser:safe_browsing_service",
    "//components/safe_browsing/core/browser/db:database_manager",
    "//components/strings:components_strings",
    "//components/strings:components_strings_grit",
    "//services/metrics/public/cpp:gen_ukm_builders",
    "//services/metrics/public/cpp:metrics_cpp",
    "//services/metrics/public/cpp:ukm_builders",
    "//ui/base",
    "//ui/display",
    "//ui/events:events_base",
  ]
  if (enable_extensions) {
    deps += [
      "//extensions/browser",
      "//extensions/common",
    ]
  }
}

if (!is_android) {
  source_set("test_support") {
    testonly = true
    sources = [
      "exclusive_access_test.cc",
      "exclusive_access_test.h",
      "fullscreen_controller_state_test.cc",
      "fullscreen_controller_state_test.h",
      "fullscreen_controller_state_tests.h",
    ]
    public_deps = [
      ":exclusive_access",
      "//chrome/test:test_support",
      "//chrome/test:test_support_ui",
      "//content/test:test_support",
    ]
  }

  source_set("unit_tests") {
    testonly = true
    sources = [
      "exclusive_access_bubble_unittest.cc",
      "exclusive_access_permission_manager_unittest.cc",
      "fullscreen_controller_state_unittest.cc",
    ]
    deps = [ ":test_support" ]
  }

  source_set("browser_tests") {
    testonly = true
    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
    sources = [
      "exclusive_access_manager_browsertest.cc",
      "fullscreen_controller_browsertest.cc",
      "pointer_lock_controller_browsertest.cc",
    ]

    deps = [
      ":test_support",
      "//chrome/browser/content_settings:content_settings_factory",
      "//chrome/browser/ui:ui_features",
      "//components/content_settings/core/browser",
      "//ui/base:ozone_buildflags",
    ]
  }

  if (!is_chromeos_device) {
    source_set("interactive_ui_tests") {
      testonly = true
      defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
      sources = [
        "fullscreen_controller_interactive_browsertest.cc",
        "fullscreen_controller_state_interactive_browsertest.cc",
        "fullscreen_interactive_browsertest.cc",
      ]
      deps = [
        ":test_support",
        "//chrome/browser/content_settings:content_settings_factory",
        "//chrome/browser/ui:test_support",
        "//chrome/browser/web_applications:web_applications_test_support",
        "//components/blocked_content",
        "//components/metrics:content",
        "//ui/display:test_support",
      ]
      if (use_ozone) {
        deps += [ "//ui/ozone" ]
      }
    }
  }
}
