# Copyright 2020 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")

assert(is_chromeos_ash)

static_library("holding_space") {
  sources = [
    "holding_space_client_impl.cc",
    "holding_space_client_impl.h",
    "holding_space_downloads_delegate.cc",
    "holding_space_downloads_delegate.h",
    "holding_space_file_system_delegate.cc",
    "holding_space_file_system_delegate.h",
    "holding_space_keyed_service.cc",
    "holding_space_keyed_service.h",
    "holding_space_keyed_service_delegate.cc",
    "holding_space_keyed_service_delegate.h",
    "holding_space_keyed_service_factory.cc",
    "holding_space_keyed_service_factory.h",
    "holding_space_metrics_delegate.cc",
    "holding_space_metrics_delegate.h",
    "holding_space_persistence_delegate.cc",
    "holding_space_persistence_delegate.h",
    "holding_space_suggestions_delegate.cc",
    "holding_space_suggestions_delegate.h",
    "holding_space_util.cc",
    "holding_space_util.h",
  ]

  public_deps = [ "//chrome/browser:browser_public_dependencies" ]

  deps = [
    "//ash",
    "//ash/components/arc",
    "//ash/constants",
    "//ash/public/cpp",
    "//ash/resources/vector_icons",
    "//ash/strings",
    "//base",
    "//chrome/browser:browser_process",
    "//chrome/browser/ash/arc",
    "//chrome/browser/ash/arc:arc_util",
    "//chrome/browser/ash/arc/fileapi",
    "//chrome/browser/ash/drive",
    "//chrome/browser/ash/extensions/autotest_private",
    "//chrome/browser/ash/extensions/file_manager",
    "//chrome/browser/ash/file_manager",
    "//chrome/browser/ash/file_suggest",
    "//chrome/browser/ash/fileapi",
    "//chrome/browser/ash/login/session",
    "//chrome/browser/ash/phonehub",
    "//chrome/browser/ash/profiles",
    "//chrome/browser/ash/system_web_apps/apps/camera_app",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/ui/ash/clipboard",
    "//chrome/browser/ui/ash/thumbnail_loader",
    "//chromeos/dbus/power",
    "//components/drive",
    "//components/keyed_service/content",
    "//components/pref_registry",
    "//components/prefs",
    "//content/public/browser",
    "//net",
    "//storage/browser",
    "//ui/chromeos/styles:cros_styles_views_generator",
    "//ui/chromeos/styles:cros_tokens_color_mappings_generator",
    "//url",
  ]

  allow_circular_includes_from = [
    "//chrome/browser/ash/extensions/autotest_private",
    "//chrome/browser/ash/extensions/file_manager",
    "//chrome/browser/ash/login/session",
    "//chrome/browser/ash/phonehub",
    "//chrome/browser/ash/system_web_apps/apps/camera_app",
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [ "holding_space_keyed_service_unittest.cc" ]

  deps = [
    ":holding_space",
    ":test_support",
    "//ash/components/arc",
    "//ash/constants",
    "//ash/public/cpp",
    "//base/test:test_support",
    "//chrome/browser",
    "//chrome/browser:browser_process",
    "//chrome/browser/ash/arc/fileapi",
    "//chrome/browser/ash/file_manager",
    "//chrome/browser/ash/file_suggest",
    "//chrome/browser/ash/file_suggest:test_support",
    "//chrome/browser/ash/profiles",
    "//chrome/browser/file_system_access",
    "//chrome/browser/nearby_sharing/common",
    "//chrome/browser/prefs",
    "//chrome/test:test_support",
    "//chromeos/ash/components/disks",
    "//chromeos/ash/components/disks:test_support",
    "//chromeos/ui/base",
    "//components/account_id",
    "//components/pref_registry",
    "//components/sync_preferences",
    "//components/user_manager",
    "//components/vector_icons",
    "//content/test:test_support",
    "//storage/browser",
    "//storage/browser:test_support",
    "//testing/gmock",
    "//testing/gtest",
    "//ui/chromeos",
    "//ui/chromeos/styles:cros_styles_views_generator",
    "//ui/gfx",
  ]
}

source_set("browser_tests") {
  testonly = true

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

  sources = [
    "holding_space_client_impl_browsertest.cc",
    "holding_space_keyed_service_browsertest.cc",
    "holding_space_ui_browsertest.cc",
  ]

  deps = [
    ":holding_space",
    ":test_support",
    "//ash:test_support",
    "//ash/public/cpp",
    "//ash/public/cpp/holding_space:test_support",
    "//ash/style",
    "//base/test:test_support",
    "//chrome/browser",
    "//chrome/browser/ash/crosapi",
    "//chrome/browser/ash/drive:test_support",
    "//chrome/browser/ash/file_manager",
    "//chrome/browser/ash/file_suggest",
    "//chrome/browser/ash/profiles",
    "//chrome/browser/extensions",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/ui",
    "//chrome/browser/ui:test_support",
    "//chrome/test:test_support_ui",
    "//chrome/test/base/ash/util:test_support",
    "//chromeos/ash/components/browser_context_helper",
    "//chromeos/dbus/power:power",
    "//content/test:test_support",
    "//ui/aura",
    "//ui/base",
    "//ui/base/dragdrop:types",
    "//ui/base/dragdrop/mojom",
    "//ui/events:test_support",
    "//ui/gfx:test_support",
    "//ui/views",
    "//ui/wm/public",
    "//ui/wm/public:test_support",
  ]
}

static_library("test_support") {
  testonly = true

  sources = [
    "holding_space_browsertest_base.cc",
    "holding_space_browsertest_base.h",
    "holding_space_test_util.cc",
    "holding_space_test_util.h",
    "scoped_test_mount_point.cc",
    "scoped_test_mount_point.h",
  ]

  deps = [
    ":holding_space",
    "//ash:test_support",
    "//ash/public/cpp",
    "//ash/public/cpp/holding_space:test_support",
    "//base/test:test_support",
    "//chrome/browser",
    "//chrome/browser/ash/file_manager",
    "//chrome/browser/ash/fileapi",
    "//chrome/browser/ash/login/test:test_support",
    "//chrome/browser/ash/system_web_apps/test_support:test_support_ui",
    "//chrome/browser/extensions",
    "//chrome/browser/ui",
    "//chrome/test:test_support_ui",
    "//chrome/test/base/ash/util:test_support",
    "//chromeos/ash/components/dbus/session_manager",
    "//components/session_manager/core",
    "//ui/events:test_support",
    "//ui/views",
  ]
}
