# Copyright 2024 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//remoting/build/config/remoting_build.gni")

source_set("crash") {
  public_deps = []
  if (is_linux || is_win) {
    public_deps += [ ":crashpad" ]
  }
  if (is_win) {
    public_deps += [
      ":breakpad",
      ":breakpad_utils",
    ]
  }
}

if (is_linux || is_win) {
  source_set("crashpad_db_mgr") {
    sources = [
      "crashpad_database_manager.cc",
      "crashpad_database_manager.h",
    ]
    deps = [
      "//base",
      "//base:i18n",
      "//remoting/base",
      "//third_party/crashpad/crashpad/client",
      "//third_party/crashpad/crashpad/util",
    ]
  }

  source_set("crashpad") {
    sources = [
      "crash_reporting_crashpad.cc",
      "crash_reporting_crashpad.h",
    ]
    if (is_linux) {
      sources += [
        "crashpad_linux.cc",
        "crashpad_linux.h",
      ]
    }
    if (is_win) {
      sources += [
        "crashpad_win.cc",
        "crashpad_win.h",
      ]
    }

    configs += [ "//remoting/build/config:host_implementation" ]

    deps = [
      ":crashpad_db_mgr",
      "//base",
      "//remoting/base",
      "//remoting/base:logging",
      "//remoting/base:remoting_base_version",
      "//third_party/crashpad/crashpad/client",
      "//third_party/crashpad/crashpad/util",
    ]
  }
}

if (is_win) {
  source_set("breakpad_utils") {
    sources = [
      "breakpad_utils.cc",
      "breakpad_utils.h",
    ]
    deps = [
      "//base",
      "//remoting/base:remoting_base_version",
    ]
  }

  source_set("breakpad") {
    sources = [
      "breakpad_server.cc",
      "breakpad_win.cc",
      "breakpad_win.h",
      "crash_reporting_breakpad.cc",
      "crash_reporting_breakpad.h",
    ]

    configs += [
      "//build/config/compiler:wexit_time_destructors",
      "//remoting/build/config:host_implementation",
    ]

    deps = [
      ":breakpad_utils",
      "//base",
      "//remoting/base:logging",
      "//remoting/base:remoting_base_version",
      "//third_party/breakpad:breakpad_handler",
    ]
  }
}
