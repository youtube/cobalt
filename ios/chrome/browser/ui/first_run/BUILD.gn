# Copyright 2016 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

source_set("field_trial") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "ios_first_run_field_trials.cc",
    "ios_first_run_field_trials.h",
  ]
  deps = [
    ":field_trial_ids",
    "//base",
    "//components/prefs",
    "//components/signin/ios/browser:features",
    "//components/version_info",
    "//ios/chrome/app:tests_hook",
    "//ios/chrome/browser/first_run",
    "//ios/chrome/browser/shared/public/features",
    "//ios/chrome/browser/ui/content_suggestions:feature_flags",
    "//ios/chrome/browser/ui/start_surface:feature_flags",
    "//ios/chrome/common",
  ]
}

source_set("field_trial_ids") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "field_trial_ids.cc",
    "field_trial_ids.h",
  ]
  public_deps = [
    "//base",
    "//components/variations",
  ]
}

source_set("first_run") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "first_run_coordinator.h",
    "first_run_coordinator.mm",
    "orientation_limiting_navigation_controller.h",
    "orientation_limiting_navigation_controller.mm",
  ]
  deps = [
    ":constants",
    ":field_trial",
    ":first_run_provider",
    ":screen_delegate",
    ":utils",
    "resources:read_more_arrow",
    "//base",
    "//base:i18n",
    "//components/metrics",
    "//components/policy/core/common:common_constants",
    "//components/prefs",
    "//components/signin/public/identity_manager",
    "//components/web_resource",
    "//ios/chrome/app:tests_hook",
    "//ios/chrome/app/strings",
    "//ios/chrome/browser/application_context",
    "//ios/chrome/browser/crash_report",
    "//ios/chrome/browser/first_run",
    "//ios/chrome/browser/geolocation",
    "//ios/chrome/browser/main",
    "//ios/chrome/browser/policy:policy_util",
    "//ios/chrome/browser/shared/public/commands",
    "//ios/chrome/browser/shared/public/features",
    "//ios/chrome/browser/shared/ui/elements",
    "//ios/chrome/browser/shared/ui/elements:elements_internal",
    "//ios/chrome/browser/shared/ui/util",
    "//ios/chrome/browser/shared/ui/util:terms_util",
    "//ios/chrome/browser/signin",
    "//ios/chrome/browser/sync",
    "//ios/chrome/browser/ui/authentication",
    "//ios/chrome/browser/ui/authentication/signin",
    "//ios/chrome/browser/ui/authentication/signin/add_account_signin",
    "//ios/chrome/browser/ui/authentication/signin/user_signin/logging",
    "//ios/chrome/browser/ui/authentication/unified_consent/identity_chooser",
    "//ios/chrome/browser/ui/authentication/views",
    "//ios/chrome/browser/ui/fancy_ui",
    "//ios/chrome/browser/ui/first_run/default_browser",
    "//ios/chrome/browser/ui/first_run/signin",
    "//ios/chrome/browser/ui/first_run/tangible_sync",
    "//ios/chrome/browser/ui/first_run/tos",
    "//ios/chrome/browser/ui/screen:screen_provider",
    "//ios/chrome/browser/ui/screen:screen_type",
    "//ios/chrome/browser/ui/settings/resources:enterprise_icon",
    "//ios/chrome/browser/ui/settings/utils",
    "//ios/chrome/browser/url:constants",
    "//ios/chrome/browser/web_state_list",
    "//ios/chrome/common:string_util",
    "//ios/chrome/common/ui/colors",
    "//ios/chrome/common/ui/confirmation_alert",
    "//ios/chrome/common/ui/promo_style",
    "//ios/chrome/common/ui/util",
    "//ui/base",
    "//ui/gfx",
    "//url",
  ]
  frameworks = [ "UIKit.framework" ]
}

source_set("first_run_provider") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "first_run_screen_provider.h",
    "first_run_screen_provider.mm",
  ]
  deps = [
    ":field_trial",
    "//base",
    "//ios/chrome/browser/ui/screen:screen_provider",
    "//ios/chrome/browser/ui/screen:screen_provider_protected",
    "//ios/chrome/browser/ui/screen:screen_type",
  ]
}

source_set("utils") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "first_run_util.h",
    "first_run_util.mm",
  ]
  deps = [
    "//base",
    "//components/metrics",
    "//components/policy/core/common:common_constants",
    "//components/signin/public/identity_manager",
    "//components/startup_metric_utils/browser",
    "//ios/chrome/app:tests_hook",
    "//ios/chrome/browser/application_context",
    "//ios/chrome/browser/browser_state",
    "//ios/chrome/browser/crash_report",
    "//ios/chrome/browser/first_run",
    "//ios/chrome/browser/flags:system_flags",
    "//ios/chrome/browser/shared/public/features",
    "//ios/chrome/browser/signin",
    "//ios/chrome/browser/ui/settings/sync/utils",
    "//ios/web/public/thread",
    "//third_party/abseil-cpp:absl",
    "//ui/gfx",
  ]
}

source_set("constants") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "first_run_constants.h",
    "first_run_constants.mm",
  ]
}

source_set("screen_delegate") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [ "first_run_screen_delegate.h" ]
}

source_set("interruptible_chrome_coordinator") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "interruptible_chrome_coordinator.h",
    "interruptible_chrome_coordinator.mm",
  ]
  deps = [
    "//base",
    "//ios/chrome/browser/shared/coordinator/chrome_coordinator",
    "//ios/chrome/browser/ui/authentication/signin:constants",
  ]
  frameworks = [ "UIKit.framework" ]
}

source_set("eg2_tests") {
  configs += [
    "//build/config/compiler:enable_arc",
    "//build/config/ios:xctest_config",
  ]
  testonly = true
  sources = [
    "enterprise_loading_screen_egtest.mm",
    "first_run_egtest.mm",
  ]
  deps = [
    ":constants",
    ":eg_test_support+eg2",
    "//base",
    "//base/test:test_support",
    "//components/enterprise",
    "//components/enterprise:test_support",
    "//components/policy:policy_code_generate",
    "//components/policy/core/common:common_constants",
    "//components/policy/test_support:test_support",
    "//components/signin/internal/identity_manager",
    "//components/signin/ios/browser:features",
    "//ios/chrome/app/strings",
    "//ios/chrome/browser/metrics:eg_test_support+eg2",
    "//ios/chrome/browser/policy:eg_test_support+eg2",
    "//ios/chrome/browser/policy:policy_util",
    "//ios/chrome/browser/shared/public/features",
    "//ios/chrome/browser/shared/ui/elements:constants",
    "//ios/chrome/browser/signin:capabilities_types",
    "//ios/chrome/browser/signin:fake_system_identity",
    "//ios/chrome/browser/signin:test_constants",
    "//ios/chrome/browser/ui/authentication:authentication_constants",
    "//ios/chrome/browser/ui/authentication:eg_test_support+eg2",
    "//ios/chrome/browser/ui/authentication/signin:constants",
    "//ios/chrome/browser/ui/authentication/views:views_constants",
    "//ios/chrome/browser/ui/bookmarks:eg_test_support+eg2",
    "//ios/chrome/browser/ui/settings/google_services:constants",
    "//ios/chrome/common:string_util",
    "//ios/chrome/common/ui/promo_style:constants",
    "//ios/chrome/common/ui/table_view:cells_constants",
    "//ios/chrome/test/earl_grey:eg_test_support+eg2",
    "//ios/chrome/test/earl_grey:switches",
    "//ios/testing/earl_grey:eg_test_support+eg2",
    "//ui/base",
  ]
  frameworks = [ "UIKit.framework" ]
}

source_set("eg_app_support+eg2") {
  configs += [
    "//build/config/compiler:enable_arc",
    "//build/config/ios:xctest_config",
  ]
  testonly = true
  sources = [
    "first_run_app_interface.h",
    "first_run_app_interface.mm",
  ]
  deps = [
    ":first_run",
    "//components/metrics",
    "//components/prefs",
    "//ios/chrome/app:app_internal",
    "//ios/chrome/app:first_run_app_state_agent",
    "//ios/chrome/browser/application_context",
    "//ios/chrome/browser/shared/coordinator/scene:scene_state_header",
    "//ios/chrome/browser/shared/coordinator/scene:scene_testing",
    "//ios/chrome/browser/shared/public/features",
    "//ios/chrome/browser/sync",
    "//ios/chrome/test/app:test_support",
    "//ios/third_party/earl_grey2:app_framework+link",
  ]
}

source_set("eg_test_support+eg2") {
  configs += [
    "//build/config/compiler:enable_arc",
    "//build/config/ios:xctest_config",
  ]
  testonly = true
  sources = [
    "first_run_app_interface.h",
    "first_run_app_interface_stub.mm",
  ]
  deps = [
    "//ios/chrome/test/earl_grey:eg_test_support+eg2",
    "//ios/testing/earl_grey:eg_test_support+eg2",
  ]
}
