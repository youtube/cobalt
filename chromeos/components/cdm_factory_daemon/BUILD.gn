# Copyright 2020 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//media/media_options.gni")

assert(is_chromeos, "Non-ChromeOS builds cannot depend on //chromeos")

component("cdm_factory_daemon_browser") {
  sources = [
    "cdm_factory_daemon_proxy.cc",
    "cdm_factory_daemon_proxy.h",
    "cdm_factory_daemon_proxy_ash.cc",
    "cdm_factory_daemon_proxy_ash.h",
    "output_protection_impl.cc",
    "output_protection_impl.h",
  ]
  public_deps = [ "//chromeos/components/cdm_factory_daemon/mojom" ]
  deps = [
    "//ash",
    "//base",
    "//chromeos/ash/components/dbus/cdm_factory_daemon",
    "//content/public/browser",
    "//mojo/public/cpp/bindings",
    "//ui/display/manager",
  ]
  defines = [ "IS_CDM_FACTORY_DAEMON_IMPL" ]
}

component("cdm_factory_daemon_gpu") {
  sources = [
    "cdm_storage_adapter.cc",
    "cdm_storage_adapter.h",
    "chromeos_cdm_context.h",
    "chromeos_cdm_factory.cc",
    "chromeos_cdm_factory.h",
    "content_decryption_module_adapter.cc",
    "content_decryption_module_adapter.h",
  ]
  public_deps = [
    "//chromeos/components/cdm_factory_daemon/mojom",
    "//media/mojo/mojom",
  ]
  deps = [
    "//base",
    "//mojo/public/cpp/bindings",
  ]
  if (allow_oop_video_decoder) {
    sources += [
      "cdm_context_for_oopvd_impl.cc",
      "cdm_context_for_oopvd_impl.h",
      "remote_cdm_context.cc",
      "remote_cdm_context.h",
    ]
    deps += [ "//media/mojo/common" ]
  }
  defines = [ "IS_CDM_FACTORY_DAEMON_IMPL" ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "cdm_storage_adapter_unittest.cc",
    "content_decryption_module_adapter_unittest.cc",
    "output_protection_impl_unittest.cc",
  ]

  deps = [
    ":cdm_factory_daemon_browser",
    ":cdm_factory_daemon_gpu",
    "//base",
    "//base/test:test_support",
    "//chromeos/components/cdm_factory_daemon/mojom",
    "//chromeos/components/cdm_factory_daemon/mojom:unit_tests",
    "//content/test:test_support",
    "//media:test_support",
    "//mojo/public/cpp/bindings",
    "//testing/gmock",
    "//testing/gtest",
    "//ui/display:test_support",
  ]

  # TODO(crbug.com/40031409): Fix code that adds exit-time destructors and
  # enable the diagnostic by removing this line.
  configs += [ "//build/config/compiler:no_exit_time_destructors" ]
}
