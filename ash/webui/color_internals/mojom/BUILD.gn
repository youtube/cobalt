# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//tools/typescript/ts_library.gni")

assert(is_chromeos_ash)

mojom("mojom") {
  sources = [ "color_internals.mojom" ]

  webui_module_path = "/ash/webui/color_internals"

  public_deps = [ "//skia/public/mojom" ]

  cpp_typemaps = [
    {
      types = [
        {
          mojom = "ash.color_internals.mojom.WallpaperCalculatedColors"
          cpp = "::ash::WallpaperCalculatedColors"
        },
      ]
      traits_headers = [ "color_internals_mojom_traits.h" ]
      traits_sources = [ "color_internals_mojom_traits.cc" ]
      traits_public_deps = [
        "//ash",
        "//skia",
      ]
    },
  ]
}

ts_library("mojom_ts") {
  mojom_gen_dir = "$root_gen_dir/mojom-webui/" + rebase_path(".", "//")
  root_dir = mojom_gen_dir
  out_dir = "$mojom_gen_dir/tsc"
  in_files = [ "color_internals.mojom-webui.js" ]
  deps = [ "//ui/webui/resources/mojo:build_ts" ]
  extra_deps = [ ":mojom_webui_js" ]
  composite = true
}
