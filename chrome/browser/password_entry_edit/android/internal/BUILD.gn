# Copyright 2021 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")
import("//third_party/jni_zero/jni_zero.gni")

generate_jni("jni") {
  sources = [ "java/src/org/chromium/chrome/browser/password_entry_edit/CredentialEditBridge.java" ]
}

android_library("java") {
  visibility = [
    ":*",
    "//chrome/android:chrome_all_java",
  ]

  srcjar_deps = [ ":jni" ]
  sources = [
    "java/src/org/chromium/chrome/browser/password_entry_edit/BlockedCredentialViewBinder.java",
    "java/src/org/chromium/chrome/browser/password_entry_edit/CredentialEditBridge.java",
    "java/src/org/chromium/chrome/browser/password_entry_edit/CredentialEditCoordinator.java",
    "java/src/org/chromium/chrome/browser/password_entry_edit/CredentialEditMediator.java",
    "java/src/org/chromium/chrome/browser/password_entry_edit/CredentialEditProperties.java",
    "java/src/org/chromium/chrome/browser/password_entry_edit/CredentialEditUiFactory.java",
    "java/src/org/chromium/chrome/browser/password_entry_edit/CredentialEditViewBinder.java",
    "java/src/org/chromium/chrome/browser/password_entry_edit/FederatedCredentialViewBinder.java",
  ]

  deps = [
    "//base:base_java",
    "//build/android:build_java",
    "//chrome/browser/feedback/android:factory_java",
    "//chrome/browser/feedback/android:java",
    "//chrome/browser/feedback/android:java_resources",
    "//chrome/browser/password_entry_edit/android:java",
    "//chrome/browser/password_manager/android:java",
    "//chrome/browser/profiles/android:java",
    "//chrome/browser/settings:factory_java",
    "//chrome/browser/ui/android/strings:ui_strings_grd",
    "//components/browser_ui/settings/android:java",
    "//third_party/androidx:androidx_annotation_annotation_java",
    "//third_party/androidx:androidx_fragment_fragment_java",
    "//third_party/androidx:androidx_preference_preference_java",
    "//third_party/jni_zero:jni_zero_java",
    "//ui/android:ui_no_recycler_view_java",
  ]
  resources_package = "org.chromium.chrome.browser.password_entry_edit"
}

robolectric_library("junit") {
  resources_package = "org.chromium.chrome.browser.password_entry_edit"
  sources = [ "java/src/org/chromium/chrome/browser/password_entry_edit/CredentialEditControllerTest.java" ]

  deps = [
    ":java",
    "//base:base_java",
    "//base:base_java_test_support",
    "//base:base_junit_test_support",
    "//chrome/browser/password_entry_edit/android:java",
    "//chrome/browser/password_entry_edit/android:java_resources",
    "//chrome/browser/password_manager/android:java",
    "//chrome/browser/ui/android/strings:ui_strings_grd",
    "//third_party/android_deps:espresso_java",
    "//third_party/androidx:androidx_test_core_java",
    "//third_party/hamcrest:hamcrest_java",
    "//third_party/hamcrest:hamcrest_library_java",
    "//third_party/junit",
    "//third_party/mockito:mockito_java",
    "//ui/android:ui_no_recycler_view_java",
  ]
}

android_library("javatests") {
  testonly = true
  resources_package = "org.chromium.chrome.browser.password_entry_edit"
  sources = [ "java/src/org/chromium/chrome/browser/password_entry_edit/CredentialEditViewTest.java" ]

  deps = [
    ":java",
    "//base:base_java_test_support",
    "//chrome/android:chrome_java",
    "//chrome/browser/feedback/android:java",
    "//chrome/browser/flags:java",
    "//chrome/browser/password_entry_edit/android:java",
    "//chrome/browser/password_entry_edit/android:java_resources",
    "//chrome/browser/profiles/android:java",
    "//chrome/browser/settings:test_support_java",
    "//chrome/test/android:chrome_java_integration_test_support",
    "//content/public/test/android:content_java_test_support",
    "//third_party/android_deps:material_design_java",
    "//third_party/androidx:androidx_test_runner_java",
    "//third_party/hamcrest:hamcrest_java",
    "//third_party/junit",
    "//third_party/mockito:mockito_java",
    "//ui/android:ui_no_recycler_view_java",
  ]
}
