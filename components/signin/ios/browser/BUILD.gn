# Copyright 2014 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")

source_set("browser") {
  sources = [
    "account_consistency_service.h",
    "account_consistency_service.mm",
    "manage_accounts_delegate.h",
    "wait_for_network_callback_helper_ios.cc",
    "wait_for_network_callback_helper_ios.h",
  ]

  deps = [
    "//components/content_settings/core/browser",
    "//components/google/core/common",
    "//components/prefs",
    "//components/signin/core/browser",
    "//google_apis",
    "//ios/net",
    "//ios/web/common:web_view_creation_util",
    "//ios/web/public",
    "//url",
  ]

  public_deps = [
    ":active_state_manager",
    ":features",
    "//base",
    "//components/keyed_service/core",
    "//components/signin/public/identity_manager",
    "//net",
  ]
}

source_set("features") {
  sources = [
    "features.cc",
    "features.h",
  ]

  public_deps = [ "//base" ]

  deps = [ "//components/signin/public/base" ]
}

source_set("active_state_manager") {
  sources = [
    "active_state_manager.h",
    "active_state_manager_impl.h",
    "active_state_manager_impl.mm",
  ]

  deps = [
    "//base",
    "//ios/web",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "account_consistency_service_unittest.mm",
    "active_state_manager_impl_unittest.mm",
    "wait_for_network_callback_helper_ios_unittest.cc",
  ]

  deps = [
    ":active_state_manager",
    ":browser",
    "//base",
    "//components/content_settings/core/browser",
    "//components/prefs",
    "//components/signin/core/browser",
    "//components/signin/public/base:test_support",
    "//components/signin/public/identity_manager",
    "//components/signin/public/identity_manager:test_support",
    "//components/sync_preferences:test_support",
    "//ios/web",
    "//ios/web/public/test",
    "//ios/web/public/test:test_fixture",
    "//ios/web/public/test/fakes",
    "//net:test_support",
    "//testing/gmock",
    "//testing/gtest",
    "//third_party/ocmock",
  ]

  # TODO(crbug.com/1427365): Test currently fails on iOS blink.
  if (is_ios && use_blink) {
    sources -= [
      "account_consistency_service_unittest.mm",
      "active_state_manager_impl_unittest.mm",
    ]
  }
}
