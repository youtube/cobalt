# Copyright 2021 The Cobalt Authors. All Rights Reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/apple/compile_plist.gni")

template("shared_install_target") {
  compile_plist("${target_name}_compile_plist") {
    forward_variables_from(invoker,
                           [
                             "testonly",
                             "visibility",
                           ])

    sdk_info = exec_script("//build/config/apple/sdk_info.py",
                           [
                             "appletvos",
                             "--get_sdk_info",
                             "--get_machine_info",
                           ],
                           "scope")

    installable_target_name = invoker.installable_target_name

    substitutions = [
      "COMPILER_NAME=com.apple.compilers.llvm.clang.1_0",
      "MACOS_BUILD=${sdk_info.machine_os_build}",
      "PLATFORM_BUILD=${sdk_info.sdk_build}",
      "PLATFORM_DISPLAY_NAME=AppleTVOS",
      "PLATFORM_NAME=appletvos",
      "PLATFORM_VERSION=${sdk_info.sdk_version}",
      "SDK_BUILD=${sdk_info.sdk_build}",
      "SDK_NAME=appletvos${sdk_info.sdk_version}",
      "XCODE_BUILD=${sdk_info.xcode_build}",
      "XCODE_VERSION=${sdk_info.xcode_version}",
      "EXECUTABLE_NAME=${installable_target_name}",
      "PRODUCT_BUNDLE_IDENTIFIER=foo.cobalt.${installable_target_name}",
      "PRODUCT_NAME=${installable_target_name}",
    ]

    plist_templates = [ "//starboard/darwin/tvos/shared/BuildInfo.plist" ]
    format = "binary1"
    output_name =
        "$sb_install_output_dir/${installable_target_name}.app/Info.plist"

    deps = invoker.deps
    deps += [ ":$installable_target_name" ]
  }

  action(target_name) {
    forward_variables_from(invoker,
                           [
                             "signing_file",
                             "testonly",
                           ])
    installable_target_name = invoker.installable_target_name
    script = "//starboard/build/run_bash.py"
    install_script = "//starboard/darwin/tvos/shared/platform_deploy.sh"
    bundle_dir = "$sb_install_output_dir/${installable_target_name}.app"
    executable_file = "$root_out_dir/$installable_target_name"

    deps = invoker.deps
    deps += [
      ":$installable_target_name",
      ":${invoker.target_name}_compile_plist",
    ]

    inputs = [
      install_script,
      executable_file,
    ]
    if (defined(signing_file)) {
      inputs += [ signing_file ]
    }

    outputs = [ "$bundle_dir/$installable_target_name" ]

    args = [
      rebase_path(install_script, root_build_dir),
      rebase_path(bundle_dir, root_build_dir),
      rebase_path("$sb_install_output_dir/$sb_install_content_subdir",
                  root_build_dir),
      rebase_path(executable_file, root_build_dir),
    ]
    if (defined(signing_file)) {
      args += [ rebase_path(signing_file, root_build_dir) ]
    }
  }
}
