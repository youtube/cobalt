# Copyright 2021 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chrome_build.gni")
import("//build/config/chromeos/ui_mode.gni")

source_set("structured") {
  sources = []

  deps = [
    "//components/metrics",
    "//components/metrics/structured",
    "//components/metrics/structured:structured_metrics_validator",
    "//components/metrics/structured/lib",
  ]

  if (is_chromeos_ash) {
    deps += [ "//components/metrics/structured:external_metrics" ]
  } else if (is_win || is_linux || is_mac || is_chromeos_lacros) {
    sources += [
      "chrome_event_storage.cc",
      "chrome_event_storage.h",
      "chrome_structured_metrics_recorder.cc",
      "chrome_structured_metrics_recorder.h",
      "key_data_provider_chrome.cc",
      "key_data_provider_chrome.h",
    ]

    deps += [ "//components/prefs" ]
  }
}

source_set("features") {
  sources = [
    "event_logging_features.cc",
    "event_logging_features.h",
  ]

  deps = [
    "//base",
    "//components/metrics/structured:structured_metrics_features",
  ]
}

static_library("test_support") {
  testonly = true
  sources = [
    "test/structured_metrics_mixin.cc",
    "test/structured_metrics_mixin.h",
  ]
  deps = [
    "//chrome/test:test_support",
    "//components/metrics/structured",
    "//components/metrics/structured:structured_metrics_features",
    "//components/metrics/structured:test_support",
    "//components/metrics_services_manager",
  ]
}
