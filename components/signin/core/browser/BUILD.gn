# Copyright 2014 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//components/signin/features.gni")

if (is_android) {
  import("//build/config/android/rules.gni")
}

# This target contains code that is used by clients of //components/signin.
static_library("browser") {
  sources = [
    "about_signin_internals.cc",
    "about_signin_internals.h",
    "account_investigator.cc",
    "account_investigator.h",
    "account_management_type_metrics_recorder.cc",
    "account_management_type_metrics_recorder.h",
    "account_reconcilor.cc",
    "account_reconcilor.h",
    "account_reconcilor_delegate.cc",
    "account_reconcilor_delegate.h",
    "account_reconcilor_throttler.cc",
    "account_reconcilor_throttler.h",
    "active_primary_accounts_metrics_recorder.cc",
    "active_primary_accounts_metrics_recorder.h",
    "chrome_connected_header_helper.cc",
    "chrome_connected_header_helper.h",
    "cookie_settings_util.cc",
    "cookie_settings_util.h",
    "dice_account_reconcilor_delegate.cc",
    "dice_account_reconcilor_delegate.h",
    "dice_header_helper.cc",
    "dice_header_helper.h",
    "mirror_account_reconcilor_delegate.cc",
    "mirror_account_reconcilor_delegate.h",
    "signin_error_controller.cc",
    "signin_error_controller.h",
    "signin_header_helper.cc",
    "signin_header_helper.h",
    "signin_internals_util.cc",
    "signin_internals_util.h",
    "signin_metrics_service.cc",
    "signin_metrics_service.h",
    "signin_status_metrics_provider_helpers.cc",
    "signin_status_metrics_provider_helpers.h",
  ]

  public_deps = [
    "//base",
    "//components/content_settings/core/browser",
    "//components/content_settings/core/browser:cookie_settings",
    "//components/content_settings/core/common",
    "//components/keyed_service/core",
    "//components/prefs",
    "//components/signin/public/base:signin_buildflags",
    "//google_apis",
    "//net",
    "//services/network/public/cpp",
    "//ui/gfx",
    "//url",
  ]
  deps = [
    "//base:i18n",
    "//build:chromeos_buildflags",
    "//components/google/core/common",
    "//components/metrics",
    "//components/os_crypt/sync",
    "//components/signin/public/identity_manager",
    "//components/supervised_user/core/common:buildflags",
    "//components/supervised_user/core/common:features",
    "//components/webdata/common",
    "//crypto",
    "//services/network/public/cpp",
    "//skia",
    "//sql",
    "//third_party/icu",
  ]

  if (is_chromeos) {
    deps += [ "//components/account_manager_core" ]
  }

  if (is_chromeos_ash) {
    deps += [ "//chromeos/crosapi/mojom" ]
  }

  if (is_chromeos_lacros) {
    deps += [
      "//chromeos/crosapi/mojom",
      "//chromeos/lacros",
    ]
    sources += [
      "consistency_cookie_manager.cc",
      "consistency_cookie_manager.h",
      "mirror_landing_account_reconcilor_delegate.cc",
      "mirror_landing_account_reconcilor_delegate.h",
    ]
  }

  if (!enable_dice_support) {
    sources -= [
      "dice_account_reconcilor_delegate.cc",
      "dice_account_reconcilor_delegate.h",
      "dice_header_helper.cc",
      "dice_header_helper.h",
    ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "account_investigator_unittest.cc",
    "account_reconcilor_delegate_unittest.cc",
    "account_reconcilor_unittest.cc",
    "active_primary_accounts_metrics_recorder_unittest.cc",
    "signin_error_controller_unittest.cc",
    "signin_header_helper_unittest.cc",
    "signin_metrics_service_unittest.cc",
    "signin_status_metrics_provider_helpers_unittest.cc",
  ]

  deps = [
    ":browser",
    "//base",
    "//base/test:test_support",
    "//build:chromeos_buildflags",
    "//components/content_settings/core/browser",
    "//components/prefs",
    "//components/prefs:test_support",
    "//components/privacy_sandbox:privacy_sandbox_prefs",
    "//components/signin/public/base:signin_buildflags",
    "//components/signin/public/base:test_support",
    "//components/signin/public/identity_manager",
    "//components/signin/public/identity_manager:test_support",
    "//components/supervised_user/core/common:buildflags",
    "//components/supervised_user/core/common:features",
    "//components/sync_preferences:test_support",
    "//google_apis",
    "//net:test_support",
    "//net/traffic_annotation:test_support",
    "//services/network:test_support",
    "//testing/gmock",
    "//testing/gtest",
    "//url",
  ]

  if (is_chromeos) {
    deps += [ "//components/account_manager_core:test_support" ]
  }

  if (is_chromeos_ash) {
    deps += [
      "//chromeos/ash/components/install_attributes:test_support",
      "//chromeos/crosapi/mojom",
    ]
    sources -= [
      "account_investigator_unittest.cc",
      "signin_status_metrics_provider_helpers_unittest.cc",
    ]
  }

  if (is_chromeos_lacros) {
    deps += [
      "//chromeos/crosapi/mojom",
      "//chromeos/lacros",
      "//chromeos/lacros:test_support",
    ]
    sources += [
      "consistency_cookie_manager_unittest.cc",
      "mirror_landing_account_reconcilor_delegate_unittest.cc",
    ]
  }

  if (enable_dice_support) {
    sources += [ "dice_account_reconcilor_delegate_unittest.cc" ]
  }
}

if (is_android) {
  android_library("signin_enums_java") {
    srcjar_deps = [ ":signin_enums_javagen" ]
    deps = [ "//third_party/androidx:androidx_annotation_annotation_java" ]
  }

  java_cpp_enum("signin_enums_javagen") {
    sources = [ "signin_header_helper.h" ]
    visibility = [ ":*" ]  # Depend on through :signin_enums_java
  }
}
