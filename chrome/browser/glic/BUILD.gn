# Copyright 2024 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//mojo/public/tools/bindings/mojom.gni")

assert(enable_glic)

source_set("glic") {
  sources = [
    "auth_controller.h",
    "glic_border_view.h",
    "glic_button_controller.h",
    "glic_button_controller_delegate.h",
    "glic_cookie_synchronizer.h",
    "glic_enabling.h",
    "glic_focused_tab_manager.h",
    "glic_iph_controller.h",
    "glic_keyed_service.h",
    "glic_keyed_service_factory.h",
    "glic_page_context_fetcher.h",
    "glic_page_handler.h",
    "glic_pref_names.h",
    "glic_profile_configuration.h",
    "glic_profile_manager.h",
    "glic_settings_util.h",
    "glic_tab_data.h",
    "glic_tab_indicator_helper.h",
    "glic_ui.h",
    "glic_vector_icon_manager.h",
    "glic_view.h",
    "glic_web_client_access.h",
    "glic_window_controller.h",
    "guest_util.h",
    "scoped_glic_button_indicator.h",
  ]
  public_deps = [
    ":enum",
    ":mojo_bindings",
    "//base",
    "//chrome/browser:shell_integration",
    "//chrome/browser/content_extraction:content_extraction",
    "//chrome/browser/glic/resources:browser_resources",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/ui:browser_list",
    "//chrome/browser/ui/browser_window:browser_window",
    "//chrome/browser/ui/tabs:tab_strip_model_observer",
    "//components/optimization_guide/content/browser:browser",
    "//components/prefs",
    "//components/signin/public/identity_manager:identity_manager",
    "//content/public/browser",
    "//pdf/mojom:mojom",
    "//ui/views:views",
    "//ui/views/controls/webview",
    "//ui/webui",
  ]
  deps = [ "//components/user_education/common" ]
}

source_set("enum") {
  sources = [ "glic_enums.h" ]
}

source_set("impl") {
  sources = [
    "auth_controller.cc",
    "browser_conditions.cc",
    "browser_conditions.h",
    "fre_util.cc",
    "fre_util.h",
    "glic_border_view.cc",
    "glic_button_controller.cc",
    "glic_cookie_synchronizer.cc",
    "glic_enabling.cc",
    "glic_focused_tab_manager.cc",
    "glic_fre_controller.cc",
    "glic_fre_controller.h",
    "glic_fre_dialog_view.cc",
    "glic_fre_dialog_view.h",
    "glic_fre_page_handler.cc",
    "glic_fre_page_handler.h",
    "glic_iph_controller.cc",
    "glic_keyed_service.cc",
    "glic_keyed_service_factory.cc",
    "glic_metrics.cc",
    "glic_metrics.h",
    "glic_page_context_fetcher.cc",
    "glic_page_handler.cc",
    "glic_profile_configuration.cc",
    "glic_profile_manager.cc",
    "glic_screenshot_capturer.cc",
    "glic_screenshot_capturer.h",
    "glic_settings_util.cc",
    "glic_tab_data.cc",
    "glic_tab_indicator_helper.cc",
    "glic_ui.cc",
    "glic_vector_icon_manager.cc",
    "glic_view.cc",
    "glic_window_animator.cc",
    "glic_window_animator.h",
    "glic_window_controller.cc",
    "glic_window_resize_animation.cc",
    "glic_window_resize_animation.h",
    "guest_util.cc",
    "scoped_glic_button_indicator.cc",
    "webui_contents_container.cc",
    "webui_contents_container.h",
  ]
  public_deps = [ "//chrome/browser:browser_public_dependencies" ]
  deps = [
    ":glic",
    "//chrome/browser:browser_process",
    "//chrome/browser:global_features",
    "//chrome/browser/glic/launcher",
    "//chrome/browser/media/webrtc",
    "//chrome/browser/profiles:profile_util",
    "//chrome/browser/resources/glic:resources",
    "//chrome/browser/themes:themes",
    "//chrome/browser/ui:browser_element_identifiers",
    "//chrome/browser/ui:browser_list",
    "//chrome/browser/ui:browser_navigator_params_headers",
    "//chrome/browser/ui/browser_window:browser_window",
    "//chrome/browser/ui/tabs:tab_strip",
    "//chrome/browser/ui/user_education",
    "//chrome/browser/ui/views",
    "//chrome/common",
    "//components/favicon/content:content",
    "//components/feature_engagement/public:feature_constants",
    "//components/guest_view/browser",
    "//components/language/core/common",
    "//components/pdf/browser:browser",
    "//components/pdf/common:constants",
    "//components/sessions",
    "//extensions/browser:browser",
    "//ui/webui",
    "//url",
  ]

  if (is_mac) {
    deps += [ "//ui/accelerated_widget_mac" ]
  }
}

mojom("mojo_bindings") {
  sources = [ "glic.mojom" ]
  webui_module_path = "/"

  public_deps = [
    "//mojo/public/mojom/base",
    "//mojo/public/mojom/base:protobuf_support",
    "//skia/public/mojom",
    "//ui/gfx/geometry/mojom",
    "//url/mojom:url_mojom_gurl",
    "//url/mojom:url_mojom_origin",
  ]
}

source_set("test_support") {
  testonly = true
  sources = [
    "glic_test_environment.cc",
    "glic_test_environment.h",
    "glic_test_util.cc",
    "glic_test_util.h",
    "interactive_glic_test.cc",
    "interactive_glic_test.h",
  ]
  deps = [
    ":glic",
    ":impl",
    "//components/signin/public/identity_manager:test_support",
  ]
  public_deps = [ "//chrome/test:test_support" ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "glic_button_controller_unittest.cc",
    "glic_cookie_synchronizer_unittest.cc",
    "glic_enabling_unittest.cc",
    "glic_fre_controller_unittest.cc",
    "glic_metrics_unittest.cc",
    "glic_profile_manager_unittest.cc",
  ]
  deps = [
    ":glic",
    ":impl",
    ":test_support",
    "//base/test:test_support",
    "//chrome/browser:global_features",
    "//chrome/browser/glic/launcher:launcher",
    "//chrome/browser/ui",
    "//chrome/common:chrome_features",
    "//chrome/test:test_support",
    "//testing/gmock",
    "//testing/gtest",
  ]
}

source_set("browser_tests") {
  testonly = true
  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
  sources = [
    "glic_browsertest.cc",
    "glic_fre_controller_browsertest.cc",
    "glic_policy_browsertest.cc",
    "guest_util_browsertest.cc",
  ]
  deps = [
    ":glic",
    ":impl",
    ":test_support",
    "//chrome/browser",
    "//chrome/browser:browser_process",
    "//chrome/browser:global_features",
    "//chrome/browser/extensions:extensions",
    "//chrome/browser/glic/launcher",
    "//chrome/browser/policy:test_support",
    "//chrome/browser/ui",
    "//chrome/common:constants",
    "//chrome/test:test_support",
    "//chrome/test:test_support_ui",
    "//content/test:test_support",
    "//extensions/browser",
    "//ui/events:test_support",
  ]
}

source_set("interactive_ui_tests") {
  testonly = true
  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
  sources = [
    "glic_border_view_interactive_uitest.cc",
    "glic_settings_util_interactive_uitest.cc",
    "glic_tab_indicator_helper_interactive_uitest.cc",
    "glic_ui_interactive_uitest.cc",
    "glic_window_controller_interactive_uitest.cc",
    "glic_window_resize_animation_interactive_uitest.cc",
  ]
  deps = [
    ":glic",
    ":impl",
    ":test_support",
    "//chrome/browser",
    "//chrome/browser/glic/launcher",
    "//chrome/browser/ui",
    "//chrome/common",
    "//chrome/test:test_support",
    "//content/test:test_support",
    "//ui/display:test_support",
  ]
  data_deps = [ "//chrome/test/data/webui/glic:generate_test_files" ]
}
