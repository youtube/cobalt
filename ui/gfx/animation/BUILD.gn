# Copyright 2017 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//build/config/ui.gni")

if (is_android) {
  import("//build/config/android/config.gni")
  import("//build/config/android/rules.gni")
}

component("animation") {
  sources = [
    "animation.cc",
    "animation.h",
    "animation_container.cc",
    "animation_container.h",
    "animation_container_element.h",
    "animation_container_observer.h",
    "animation_delegate.h",
    "animation_delegate_notifier.h",
    "animation_export.h",
    "animation_runner.cc",
    "animation_runner.h",
    "linear_animation.cc",
    "linear_animation.h",
    "multi_animation.cc",
    "multi_animation.h",
    "slide_animation.cc",
    "slide_animation.h",
    "tween.cc",
    "tween.h",
  ]

  if (is_android) {
    sources += [ "animation_android.cc" ]
  }

  if (is_chromeos) {
    sources += [ "animation_chromeos.cc" ]
  }

  if (is_mac) {
    sources += [ "animation_mac.mm" ]
  }

  if (is_win) {
    sources += [ "animation_win.cc" ]
  }

  if (is_linux) {
    sources += [ "animation_linux.cc" ]
  }

  if (!is_android) {
    sources += [
      "throb_animation.cc",
      "throb_animation.h",
    ]
  }

  if (is_mac) {
    frameworks = [
      "AppKit.framework",
      "CoreFoundation.framework",
      "CoreGraphics.framework",
      "CoreText.framework",
      "IOSurface.framework",
    ]
  }

  deps = [
    "//base",
    "//build:chromeos_buildflags",
    "//skia",
    "//ui/gfx:gfx_export",
    "//ui/gfx:gfx_switches",
    "//ui/gfx/geometry",
    "//ui/gfx/geometry:geometry_skia",
  ]

  if (is_android) {
    deps += [ "//ui/gfx:gfx_jni_headers" ]
  }

  if (is_linux) {
    deps += [ "//ui/linux:linux_ui" ]
  }

  defines = [ "ANIMATION_IMPLEMENTATION" ]
}
