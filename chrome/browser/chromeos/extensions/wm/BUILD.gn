# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD - style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//extensions/buildflags/buildflags.gni")

assert(enable_extensions,
       "Cannot depend on extensions because enable_extensions=false.")

assert(is_chromeos_lacros || is_chromeos_ash)

source_set("wm") {
  sources = [
    "wm_desks_private_api.cc",
    "wm_desks_private_api.h",
    "wm_desks_private_events.cc",
    "wm_desks_private_events.h",
    "wm_desks_private_feature.h",
  ]

  if (is_chromeos_lacros) {
    sources += [
      "wm_desks_private_feature_lacros.cc",
      "wm_desks_private_feature_lacros.h",
    ]
  }

  if (is_chromeos_ash) {
    sources += [
      "wm_desks_private_feature_ash.cc",
      "wm_desks_private_feature_ash.h",
    ]
  }

  deps = [
    "//base",
    "//chrome/browser/profiles:profile",
    "//chrome/common/extensions/api",
    "//chromeos/crosapi/mojom",
    "//components/keyed_service/content",
    "//content/public/browser",
    "//extensions/browser",
  ]

  if (is_chromeos_lacros) {
    deps += [ "//chromeos/lacros:lacros" ]
  }

  if (is_chromeos_ash) {
    deps += [ "//ash" ]
  }
}
