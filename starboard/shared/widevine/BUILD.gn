# Copyright 2021 The Cobalt Authors. All Rights Reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

config("oemcrypto_external") {
  include_dirs = [
    "//third_party/ce_cdm/core/include",
    "//third_party/ce_cdm/oemcrypto/include",
  ]
}

config("oemcrypto_internal") {
  defines = [
    "COBALT_WIDEVINE_KEYBOX_TRANSFORM_FUNCTION=${sb_widevine_platform}_client",
    "COBALT_WIDEVINE_KEYBOX_TRANSFORM_INCLUDE=\"starboard/keyboxes/${sb_widevine_platform}/${sb_widevine_platform}.h\"",
    "COBALT_WIDEVINE_KEYBOX_INCLUDE=\"starboard/keyboxes/${sb_widevine_platform}_widevine_keybox.h\"",
  ]
}

static_library("oemcrypto") {
  sources = [
    "//starboard/keyboxes/${sb_widevine_platform}/${sb_widevine_platform}.h",
    "//starboard/keyboxes/${sb_widevine_platform}/${sb_widevine_platform}_client.c",
    "//starboard/shared/widevine/widevine_keybox_hash.cc",
    "//starboard/shared/widevine/wv_keybox.cc",
  ]

  public_configs = [ ":oemcrypto_external" ]
  configs -= [ "//starboard/build/config:size" ]
  configs += [
    ":oemcrypto_internal",
    "//starboard/build/config:speed",
    "//third_party/ce_cdm/cdm:shared_config",
  ]

  deps = [
    "//starboard/common",
    "//third_party/boringssl:crypto",
    "//third_party/ce_cdm/oemcrypto/mock:oec_mock",
  ]
}
