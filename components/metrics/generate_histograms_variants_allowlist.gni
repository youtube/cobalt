# Copyright 2023 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# Generates an output file that contains all histograms which share the same
# variant name. This output file will act as an allow list.
#
# Parameters:
#   input_xml_file: Input file that contains the allow list. This should be a
#     .xml file with histogram descriptions and should be a path starting with
#     //tools/metrics/histograms/
#
#   namespace:
#     Namespace in which the generated code should be scoped.
#
#   header_filename:
#     Name of the generated header file.
#
#   source_filename:
#     Name of the generated source file.
#
#   allow_list_name:
#     Name of the variants list that act as an allow list.
#
template("generate_histograms_variants_allowlist") {
  action(target_name) {
    header_filename = "$target_gen_dir/" + invoker.header_filename
    source_filename = "$target_gen_dir/" + invoker.source_filename

    script =
        "//tools/metrics/histograms/generate_histograms_variants_allowlist.py"
    outputs = [
      header_filename,
      source_filename,
    ]

    args = [
      "-a" + invoker.allow_list_name,
      "-n" + invoker.namespace,
      "-o" + rebase_path(root_gen_dir, root_build_dir),
      "-H" + rebase_path(header_filename, root_gen_dir),
      "-s" + rebase_path(source_filename, root_gen_dir),
      "-f" + rebase_path(invoker.input_xml_file, root_build_dir),
    ]
  }
}
