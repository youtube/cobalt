# Copyright 2018 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//extensions/buildflags/buildflags.gni")
import("//ui/webui/resources/tools/build_webui.gni")

build_webui("build") {
  grd_prefix = "history"

  static_files = [
    "history.html",
    "images/list.svg",
    "images/sign_in_promo_dark.svg",
    "images/sign_in_promo.svg",
  ]

  # Files holding a Polymer element definition and have an equivalent .html file.
  web_component_files = [
    "app.ts",
    "history_item.ts",
    "history_list.ts",
    "history_toolbar.ts",
    "router.ts",
    "side_bar.ts",
    "synced_device_card.ts",
    "synced_device_manager.ts",
  ]

  non_web_component_files = [
    "browser_service.ts",
    "constants.ts",
    "externs.ts",
    "history.ts",
    "lazy_load.ts",
    "query_manager.ts",
    "searched_label.ts",
  ]

  # Files that are passed as input to css_to_wrapper().
  css_files = [
    "shared_style.css",
    "shared_vars.css",
  ]

  icons_html_files = [ "shared_icons.html" ]

  ts_composite = true
  ts_definitions = [
    "//tools/typescript/definitions/chrome_send.d.ts",
    "//tools/typescript/definitions/metrics_private.d.ts",
  ]

  ts_deps = [
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources/cr_components/history_clusters:build_ts",
    "//ui/webui/resources/cr_components/managed_footnote:build_ts",
    "//ui/webui/resources/cr_elements:build_ts",
    "//ui/webui/resources/js:build_ts",
    "//ui/webui/resources/mojo:build_ts",
  ]

  optimize = optimize_webui
  if (optimize) {
    optimize_webui_host = "history"
    optimize_webui_in_files = [
      "history.js",
      "lazy_load.js",
    ]
  }

  enable_source_maps = enable_webui_inline_sourcemaps
}
