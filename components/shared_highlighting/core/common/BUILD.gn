# Copyright 2020 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/rules.gni")
}

static_library("common") {
  sources = [
    "disabled_sites.cc",
    "disabled_sites.h",
    "fragment_directives_constants.cc",
    "fragment_directives_constants.h",
    "fragment_directives_utils.cc",
    "fragment_directives_utils.h",
    "shared_highlighting_features.cc",
    "shared_highlighting_features.h",
    "shared_highlighting_metrics.cc",
    "shared_highlighting_metrics.h",
    "text_fragment.cc",
    "text_fragment.h",
  ]

  deps = [
    "//base",
    "//components/search_engines:search_engine_utils",
    "//services/metrics/public/cpp:metrics_cpp",
    "//services/metrics/public/cpp:ukm_builders",
    "//third_party/re2",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "disabled_sites_unittest.cc",
    "fragment_directives_utils_unittest.cc",
    "shared_highlighting_metrics_unittest.cc",
    "text_fragment_unittest.cc",
  ]
  deps = [
    "//base/test:test_support",
    "//components/shared_highlighting/core/common",
    "//components/ukm:test_support",
    "//services/metrics/public/cpp:metrics_cpp",
    "//services/metrics/public/cpp:ukm_builders",
    "//testing/gtest",
    "//url",
  ]
}

source_set("data_driven_testing") {
  testonly = true
  sources = [
    "shared_highlighting_data_driven_test.cc",
    "shared_highlighting_data_driven_test.h",
    "shared_highlighting_data_driven_test_results.h",
  ]
  data = [
    "//components/test/data/shared_highlighting/",
    "//third_party/text-fragments-polyfill/src/test/data-driven/input/",
  ]
  deps = [
    "//base:base",
    "//testing/data_driven_testing",
    "//testing/gtest",
  ]
}

if (is_ios) {
  bundle_data("data_driven_testing_data") {
    testonly = true
    sources = [ "//components/test/data/shared_highlighting/generate_navigate/output/basic_test.out" ]

    outputs = [ "{{bundle_resources_dir}}/" +
                "{{source_root_relative_dir}}/{{source_file_part}}" ]
  }
}

if (is_android) {
  java_cpp_enum("shared_highlighting_android_enums") {
    sources = [ "shared_highlighting_metrics.h" ]
  }

  android_library("shared_highlighting_android_enums_java") {
    deps = [ "//third_party/androidx:androidx_annotation_annotation_java" ]

    srcjar_deps = [ ":shared_highlighting_android_enums" ]
  }
}
