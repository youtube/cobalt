# Copyright 2017 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//mojo/public/tools/bindings/mojom.gni")

source_set("bluetooth_internals") {
  sources = [
    "bluetooth_internals_handler.cc",
    "bluetooth_internals_handler.h",
    "bluetooth_internals_ui.cc",
    "bluetooth_internals_ui.h",
  ]

  configs += [ "//build/config/compiler:wexit_time_destructors" ]

  public_deps = [
    "//chrome/browser:browser_public_dependencies",
    "//device/bluetooth:deprecated_experimental_mojo",
  ]

  deps = [
    ":mojo_bindings",
    "//build:chromeos_buildflags",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/resources/bluetooth_internals:resources",
    "//chrome/common",
    "//content/public/browser",
    "//device/bluetooth",
    "//ui/webui",
  ]

  if (is_chromeos_ash) {
    deps += [
      "//ash/public/cpp",
      "//chrome/browser/ash/bluetooth",
      "//chromeos/ash/components/dbus/debug_daemon",
      "//chromeos/ash/services/bluetooth_config/public/mojom",
    ]
  }
  if (is_android) {
    deps += [
      "//components/permissions:permissions",
      "//ui/android:android",
    ]
  }
}

mojom("mojo_bindings") {
  sources = [ "bluetooth_internals.mojom" ]
  webui_module_path = "/"

  deps =
      [ "//device/bluetooth/public/mojom:deprecated_experimental_interfaces" ]
}
