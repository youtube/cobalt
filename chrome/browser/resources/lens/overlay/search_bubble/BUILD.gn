# Copyright 2024 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chrome_build.gni")
import("//ui/webui/resources/tools/build_webui.gni")

assert(toolkit_views)

build_webui("build") {
  grd_prefix = "lens_search_bubble"

  static_files = [ "search_bubble.html" ]

  if (is_chrome_branded) {
    extra_grdp_deps = [ "//ui/webui/resources/cr_components/searchbox/icons:chrome_branded_build_grdp" ]
    extra_grdp_files = [ "$root_gen_dir/ui/webui/resources/cr_components/searchbox/icons/chrome_branded_resources.grdp" ]
  }

  web_component_files = [ "search_bubble_app.ts" ]

  non_web_component_files = [ "search_bubble_proxy.ts" ]

  ts_deps = [
    "//chrome/browser/resources/lens/shared:build_ts",
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources/cr_components/color_change_listener:build_ts",
    "//ui/webui/resources/cr_components/searchbox:build_ts",
    "//ui/webui/resources/cr_elements:build_ts",
    "//ui/webui/resources/js:build_ts",
    "//ui/webui/resources/mojo:build_ts",
  ]

  mojo_files_deps =
      [ "//chrome/browser/lens/core/mojom:mojo_bindings_ts__generator" ]
  mojo_files = [
    "$root_gen_dir/chrome/browser/lens/core/mojom/search_bubble.mojom-webui.ts",
  ]
  ts_path_mappings =
      [ "/lens/shared/*|" +
        rebase_path("$root_gen_dir/chrome/browser/resources/lens/shared/tsc/*",
                    target_gen_dir) ]

  webui_context_type = "trusted"
}
