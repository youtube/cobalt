# Copyright 2019 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")

assert(is_chromeos_ash)

static_library("cellular_setup") {
  sources = [
    "cellular_setup_base.cc",
    "cellular_setup_base.h",
    "cellular_setup_impl.cc",
    "cellular_setup_impl.h",
    "ota_activator.cc",
    "ota_activator.h",
    "ota_activator_impl.cc",
    "ota_activator_impl.h",
  ]

  deps = [
    "//ash/constants",
    "//base",
    "//chromeos/ash/components/dbus/shill",
    "//chromeos/ash/components/network",
    "//chromeos/ash/services/cellular_setup/public/mojom",
    "//dbus",
    "//url",
  ]
}

static_library("esim_manager") {
  sources = [
    "esim_manager.cc",
    "esim_manager.h",
    "esim_mojo_utils.cc",
    "esim_mojo_utils.h",
    "esim_profile.cc",
    "esim_profile.h",
    "euicc.cc",
    "euicc.h",
  ]

  deps = [
    "//ash/constants",
    "//base",
    "//chromeos/ash/components/dbus/hermes",
    "//chromeos/ash/components/network",
    "//chromeos/ash/services/cellular_setup/public/mojom",
    "//components/device_event_log",
    "//components/qr_code_generator",
    "//components/user_manager",
    "//dbus",
  ]
}

component("in_process_esim_manager") {
  sources = [
    "in_process_esim_manager.cc",
    "in_process_esim_manager.h",
  ]

  defines = [ "IS_IN_PROCESS_ESIM_MANAGER_IMPL" ]

  public_deps = [
    "//chromeos/ash/services/cellular_setup/public/mojom",
    "//mojo/public/cpp/bindings",
  ]

  deps = [
    ":esim_manager",
    "//chromeos/ash/services/cellular_setup/public/mojom",
    "//dbus",
    "//mojo/public/cpp/bindings",
  ]
}

static_library("test_support") {
  testonly = true

  sources = [
    "esim_test_utils.cc",
    "esim_test_utils.h",
    "fake_ota_activator.cc",
    "fake_ota_activator.h",
  ]

  public_deps =
      [ "//chromeos/ash/services/cellular_setup/public/mojom:mojom_headers" ]

  deps = [
    ":cellular_setup",
    "//base",
    "//chromeos/ash/services/cellular_setup/public/cpp:test_support",
    "//chromeos/ash/services/cellular_setup/public/mojom",
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "cellular_setup_impl_unittest.cc",
    "cellular_setup_service_unittest.cc",
    "esim_manager_unittest.cc",
    "esim_profile_unittest.cc",
    "esim_test_base.cc",
    "esim_test_base.h",
    "euicc_unittest.cc",
    "ota_activator_impl_unittest.cc",
  ]

  deps = [
    ":cellular_setup",
    ":esim_manager",
    ":test_support",
    "//ash/constants",
    "//base",
    "//base/test:test_support",
    "//chromeos/ash/components/dbus/hermes",
    "//chromeos/ash/components/dbus/shill",
    "//chromeos/ash/components/network:test_support",
    "//chromeos/ash/services/cellular_setup/public/cpp:test_support",
    "//components/user_manager:test_support",
    "//dbus",
    "//testing/gmock",
    "//testing/gtest",
  ]
}
