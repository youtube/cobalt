# Copyright 2017 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
import("//components/update_client/buildflags.gni")

source_set("lib") {
  sources = [
    "file_patcher_impl.cc",
    "file_patcher_impl.h",
  ]

  deps = [
    "//base",
    "//components/services/filesystem/public/mojom",
    "//components/update_client:buildflags",
    "//courgette:courgette_lib",
    "//mojo/public/cpp/bindings",
  ]

  if (enable_puffin_patches) {
    include_dirs = [ "//third_party/puffin/src/include" ]
    deps += [ "//third_party/puffin:libpuffpatch" ]
  }

  public_deps = [ "//components/services/patch/public/mojom" ]
}

source_set("in_process") {
  # NOTE: In-process file patching is unsafe! Unfortunately on iOS we have no
  # other choice. On all other platforms, this target is restricted to tests.
  testonly = !is_ios

  sources = [
    "in_process_file_patcher.cc",
    "in_process_file_patcher.h",
  ]

  deps = [ ":lib" ]

  public_deps = [
    "//components/services/patch/public/mojom",
    "//mojo/public/cpp/bindings",
  ]
}
