# Copyright 2017 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//build/config/ui.gni")
import("//chrome/common/features.gni")
import("//chrome/test/include_js_tests.gni")
import("//components/compose/features.gni")
import("//components/signin/features.gni")
import("//crypto/features.gni")
import("//extensions/buildflags/buildflags.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//pdf/features.gni")
import("//printing/buildflags/buildflags.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")
import("//ui/webui/webui_features.gni")

assert(!is_android)

source_set("browser_tests") {
  testonly = true

  sources = [
    "access_code_cast/access_code_cast_browsertest.cc",
    "bookmarks/bookmarks_browsertest.cc",
    "chrome_timeticks_browsertest.cc",
    "commander/commander_browsertest.cc",
    "commerce_internals/commerce_internals_browsertest.cc",
    "cr_components/cr_components_browsertest.cc",
    "cr_elements/cr_elements_browsertest.cc",
    "css/css_browsertest.cc",
    "downloads/downloads_browsertest.cc",
    "engagement/site_engagement_browsertest.cc",
    "flags/flags_browsertest.cc",
    "histograms/histograms_internals_browsertest.cc",
    "history/history_supervised_user_browsertest.cc",
    "history/history_ui_browsertest.cc",
    "history_clusters_internals/history_clusters_internals_browsertest.cc",
    "identity_internals/identity_internals_browsertest.cc",
    "inspect/inspect_ui_browsertest.cc",
    "js/js_browsertest.cc",
    "location_internals/location_internals_browsertest.cc",
    "media/media_engagement_browsertest.cc",
    "media/media_history_browsertest.cc",
    "media_internals/media_internals_browsertest.cc",
    "metrics_internals/metrics_internals_browsertest.cc",
    "net_internals/net_internals_browsertest.cc",
    "new_tab_page/new_tab_page_a11y_browsertest.cc",
    "new_tab_page/new_tab_page_browsertest.cc",
    "optimization_guide_internals/optimization_guide_internals_browsertest.cc",
    "password_manager/password_manager_browsertest.cc",
    "password_manager_internals/password_manager_internals_browsertest.cc",
    "privacy_sandbox/privacy_sandbox_dialog_browsertest.cc",
    "sandbox/sandbox_browsertest.cc",
    "settings/settings_browsertest.cc",
    "side_panel/bookmarks/sp_bookmarks_browsertest.cc",
    "side_panel/commerce/commerce_browsertest.cc",
    "side_panel/customize_chrome/customize_chrome_browsertest.cc",
    "side_panel/performance_controls/performance_controls_browsertest.cc",
    "side_panel/read_anything/read_anything_browsertest.cc",
    "side_panel/user_notes/user_notes_browsertest.cc",
    "sync_internals/sync_internals_browsertest.cc",
    "tab_search/tab_search_browsertest.cc",
    "usb_internals/usb_internals_browsertest.cc",
    "whats_new/whats_new_browsertest.cc",
  ]

  if (is_chrome_branded) {
    sources += [ "media_router/cast_feedback_ui_browsertest.cc" ]
  }

  if (enable_dice_support) {
    sources += [ "welcome/welcome_browsertest.cc" ]
  }

  if (enable_extensions) {
    sources += [ "extensions/extensions_browsertest.cc" ]
  }

  if (enable_webui_tab_strip) {
    sources += [ "tab_strip/tab_strip_browsertest.cc" ]
  }

  if (enable_search_engine_choice) {
    sources += [ "search_engine_choice/search_engine_choice_js_browsertest.cc" ]
  }

  if (enable_print_preview) {
    sources += [ "print_preview/print_preview_browsertest.cc" ]
  }

  if (enable_webui_certificate_viewer) {
    sources +=
        [ "certificate_viewer_dialog/certificate_viewer_dialog_browsertest.cc" ]
  }

  if (is_chromeos_ash) {
    sources += [
      "chromeos/enterprise_reporting/enterprise_reporting_browsertest.cc",
      "chromeos/personalization_app/personalization_app_browsertest.cc",
    ]
  } else {
    sources += [
      "intro/intro_browsertest.cc",
      "signin/profile_picker_browsertest.cc",
      "signin/signin_browsertest.cc",
    ]
  }

  if (is_chromeos_ash || is_win || is_mac) {
    sources += [ "inline_login/inline_login_browsertest.cc" ]
  }

  if (enable_dice_support || is_chromeos_ash) {
    sources += [ "gaia_auth_host/gaia_auth_host_browsertest.cc" ]
  }

  if (is_win || is_mac || is_linux || is_chromeos) {
    sources += [ "discards/discards_browsertest.cc" ]
  }

  if (is_win || is_mac || is_linux || is_fuchsia) {
    sources += [ "app_settings/app_settings_browsertest.cc" ]
  }

  if (is_win || is_mac || is_linux) {
    sources += [ "app_home/app_home_browsertest.cc" ]
  }

  if (!is_chromeos_lacros) {
    sources += [
      "bluetooth_internals/bluetooth_internals_browsertest.cc",
      "webview/webui_webview_browsertest.cc",
    ]
  }

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

  deps = [
    "//build/config/coverage:buildflags",
    "//chrome/app:command_ids",
    "//chrome/browser/devtools:devtools",
    "//chrome/browser/devtools:test_support",
    "//chrome/browser/media/router/discovery/access_code:access_code_cast_feature",
    "//chrome/browser/optimization_guide:test_support",
    "//chrome/test:test_support",
    "//components/bookmarks/browser:browser",
    "//components/bookmarks/managed:managed",
    "//components/commerce/core:commerce_constants",
    "//components/commerce/core:feature_list",
    "//components/compose/core/browser:features",
    "//components/history/core/common:common",
    "//components/history_clusters/core:core",
    "//components/history_clusters/history_clusters_internals/webui:constants",
    "//components/optimization_guide/core:core",
    "//components/optimization_guide/optimization_guide_internals/webui:url_constants",
    "//components/password_manager/content/browser:browser",
    "//components/password_manager/content/common:common",
    "//components/performance_manager:performance_manager",
    "//components/power_bookmarks/core:features",
    "//components/privacy_sandbox:features",
    "//components/privacy_sandbox/privacy_sandbox_attestations:privacy_sandbox_attestations",
    "//components/privacy_sandbox/privacy_sandbox_attestations:test_support",
    "//components/safe_browsing/core/common:common",
    "//components/search:search",
    "//components/site_engagement/content:content",
    "//components/user_notes:features",
    "//net:test_support",
  ]

  if (is_chromeos_ash) {
    deps += [
      "//chrome/browser/ash:ash",
      "//chrome/browser/ash:test_support",
      "//chromeos/ash/components/standalone_browser",
    ]
  }
  if (enable_compose) {
    sources += [ "compose/compose_browsertest.cc" ]
    deps += [ "//components/compose/core/browser:features" ]
  }
}

source_set("interactive_ui_tests") {
  testonly = true

  sources = [
    "bookmarks/bookmarks_focus_test.cc",
    "cr_components/cr_components_interactive_test.cc",
    "cr_elements/cr_elements_focus_test.cc",
    "history/history_focus_test.cc",
    "new_tab_page/new_tab_page_focus_test.cc",
    "password_manager/password_manager_focus_test.cc",
    "settings/settings_focus_test.cc",
    "support_tool/support_tool_focus_test.cc",
    "tab_search/tab_search_focus_test.cc",
  ]

  if (enable_webui_tab_strip) {
    sources += [ "tab_strip/tab_strip_focus_test.cc" ]
  }

  if (enable_extensions) {
    sources += [ "extensions/extensions_focus_test.cc" ]
  }

  if (enable_print_preview) {
    sources += [ "print_preview/print_preview_focus_test.cc" ]
  }

  data = [ "//chrome/test/data/" ]

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

  deps = [
    "//chrome/test:test_support",
    "//components/password_manager/content/common:common",
    "//components/search:search",
  ]
}

if (is_chromeos_ash) {
  group("closure_compile") {
    testonly = true
    deps = [
      "chromeos:closure_compile",
      "cr_components/chromeos:closure_compile",
    ]
  }
}

grit("resources") {
  testonly = true
  defines = chrome_grit_defines

  # These arguments are needed since the grd is generated at build time.
  enable_input_discovery_for_gn_analyze = false
  source = "$target_gen_dir/resources.grd"
  deps = [ ":build_grd" ]

  grd_prefix = "webui_test"
  outputs = [
    "test/data/grit/${grd_prefix}_resources.h",
    "test/data/grit/${grd_prefix}_resources_map.cc",
    "test/data/grit/${grd_prefix}_resources_map.h",
    "${grd_prefix}_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
}

generate_grd("build_chai_grdp") {
  testonly = true
  grd_prefix = "webui_test"
  out_grd = "$target_gen_dir/chai_resources.grdp"
  input_files_base_dir =
      rebase_path("//third_party/node/node_modules/chai", "//")
  input_files = [ "chai.js" ]
}

generate_grd("build_mocha_grdp") {
  testonly = true
  grd_prefix = "webui_test"
  out_grd = "$target_gen_dir/mocha_resources.grdp"
  input_files_base_dir =
      rebase_path("//third_party/node/node_modules/mocha", "//")
  input_files = [ "mocha.js" ]
}

if (is_chromeos_ash) {
  mojom("web_ui_test_bindings") {
    sources = [ "web_ui_test.mojom" ]
    webui_module_path = "/"

    # Used by legacy MojoWebUIBrowserTest.
    use_typescript_sources = false
  }

  generate_grd("build_web_ui_test_mojo_grdp") {
    testonly = true
    grd_prefix = "webui_test"
    out_grd = "$target_gen_dir/web_ui_test_mojo_resources.grdp"
    input_files_base_dir =
        rebase_path("$root_gen_dir/mojom-webui/chrome/test/data/webui/",
                    root_build_dir)
    input_files = [ "web_ui_test.mojom-webui.js" ]
    deps = [ ":web_ui_test_bindings_js__generator" ]
  }

  generate_grd("build_web_ui_test_mojo_lite_grdp") {
    testonly = true
    grd_prefix = "webui_test"
    out_grd = "$target_gen_dir/web_ui_test_mojo_lite_resources.grdp"
    input_files_base_dir = rebase_path(target_gen_dir, root_build_dir)
    input_files = [ "web_ui_test.mojom-lite.js" ]
    deps = [ ":web_ui_test_bindings_js__generator" ]
  }
}

ts_library("build_ts") {
  root_dir = "."
  out_dir = "$target_gen_dir/tsc"
  composite = true
  tsconfig_base = "tsconfig_base.json"
  in_files = [
    "chai_assert.ts",
    "chrome_timeticks_test.ts",
    "fake_chrome_event.ts",
    "fake_settings_private.ts",
    "metrics_test_support.ts",
    "mocha_adapter_simple.ts",
    "mock_controller.ts",
    "mocked_metrics_reporter.ts",
    "mock_timer_test.ts",
    "mock_timer.ts",
    "polymer_test_util.ts",
    "test_api.ts",
    "test_browser_proxy.ts",
    "test_mock.ts",
    "test_open_window_proxy.ts",
    "test_plural_string_proxy.ts",
    "test_store_ts.ts",
    "test_util.ts",
    "trusted_html.ts",
  ]

  if (is_chromeos_ash) {
    in_files += [
      "cr_focus_row_behavior_test.ts",
      "mocha_adapter.js",
      "mojo_webui_test_support.js",
    ]
  }

  if (enable_webui_certificate_viewer) {
    in_files +=
        [ "certificate_viewer_dialog/certificate_viewer_dialog_test.js" ]
  }

  if (!is_chromeos_lacros) {
    in_files += [
      "bluetooth_internals/bluetooth_internals_test.js",
      "bluetooth_internals/test_utils.js",
    ]
  }

  deps = [
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources/js:build_ts",
  ]
  if (is_chromeos_ash) {
    deps += [ "//ash/webui/common/resources:build_ts" ]
  }
  definitions = [
    "//tools/typescript/definitions/chrome_send.d.ts",
    "//tools/typescript/definitions/chrome_timeticks.d.ts",
    "//tools/typescript/definitions/dom_automation_controller.d.ts",
    "//tools/typescript/definitions/metrics_private.d.ts",
    "//tools/typescript/definitions/settings_private.d.ts",
  ]
}

generate_grd("build_grd") {
  testonly = true
  grd_prefix = "webui_test"
  output_files_base_dir = "test/data/grit"
  out_grd = "$target_gen_dir/resources.grd"

  deps = [
    ":build_chai_grdp",
    ":build_mocha_grdp",
    ":build_ts",
    "access_code_cast:build_grdp",
    "bookmarks:build_grdp",
    "commander:build_grdp",
    "commerce_internals:build_grdp",
    "cr_components:build_grdp",
    "cr_elements:build_grdp",
    "css:build_grdp",
    "discards:build_grdp",
    "downloads:build_grdp",
    "engagement:build_grdp",
    "flags:build_grdp",
    "histograms:build_grdp",
    "history:build_grdp",
    "history_clusters_internals:build_grdp",
    "identity_internals:build_grdp",
    "inspect:build_grdp",
    "js:build_grdp",
    "location_internals:build_grdp",
    "media:build_grdp",
    "media_internals:build_grdp",
    "metrics_internals:build_grdp",
    "mojo:build_grdp",
    "net_internals:build_grdp",
    "new_tab_page:build_grdp",
    "optimization_guide_internals:build_grdp",
    "password_manager:build_grdp",
    "password_manager_internals:build_grdp",
    "privacy_sandbox:build_grdp",
    "sandbox:build_grdp",
    "settings:build_grdp",
    "side_panel:build_grdp",
    "side_panel/customize_chrome:build_grdp",
    "support_tool:build_grdp",
    "sync_internals:build_grdp",
    "tab_search:build_grdp",
    "usb_internals:build_grdp",
    "whats_new:build_grdp",
  ]

  grdp_files = [
    "$target_gen_dir/access_code_cast/resources.grdp",
    "$target_gen_dir/bookmarks/resources.grdp",
    "$target_gen_dir/chai_resources.grdp",
    "$target_gen_dir/commander/resources.grdp",
    "$target_gen_dir/commerce_internals/resources.grdp",
    "$target_gen_dir/cr_components/resources.grdp",
    "$target_gen_dir/cr_elements/resources.grdp",
    "$target_gen_dir/css/resources.grdp",
    "$target_gen_dir/discards/resources.grdp",
    "$target_gen_dir/downloads/resources.grdp",
    "$target_gen_dir/engagement/resources.grdp",
    "$target_gen_dir/flags/resources.grdp",
    "$target_gen_dir/histograms/resources.grdp",
    "$target_gen_dir/history/resources.grdp",
    "$target_gen_dir/history_clusters_internals/resources.grdp",
    "$target_gen_dir/identity_internals/resources.grdp",
    "$target_gen_dir/inspect/resources.grdp",
    "$target_gen_dir/js/resources.grdp",
    "$target_gen_dir/location_internals/resources.grdp",
    "$target_gen_dir/media_internals/resources.grdp",
    "$target_gen_dir/media/resources.grdp",
    "$target_gen_dir/metrics_internals/resources.grdp",
    "$target_gen_dir/mocha_resources.grdp",
    "$target_gen_dir/mojo/resources.grdp",
    "$target_gen_dir/net_internals/resources.grdp",
    "$target_gen_dir/new_tab_page/resources.grdp",
    "$target_gen_dir/optimization_guide_internals/resources.grdp",
    "$target_gen_dir/password_manager/resources.grdp",
    "$target_gen_dir/password_manager_internals/resources.grdp",
    "$target_gen_dir/privacy_sandbox/resources.grdp",
    "$target_gen_dir/sandbox/resources.grdp",
    "$target_gen_dir/settings/resources.grdp",
    "$target_gen_dir/side_panel/customize_chrome/resources.grdp",
    "$target_gen_dir/side_panel/resources.grdp",
    "$target_gen_dir/support_tool/resources.grdp",
    "$target_gen_dir/sync_internals/resources.grdp",
    "$target_gen_dir/tab_search/resources.grdp",
    "$target_gen_dir/usb_internals/resources.grdp",
    "$target_gen_dir/whats_new/resources.grdp",
  ]

  if (enable_extensions) {
    deps += [ "extensions:build_grdp" ]
    grdp_files += [ "$target_gen_dir/extensions/resources.grdp" ]
  }

  if (enable_print_preview) {
    deps += [ "print_preview:build_grdp" ]
    grdp_files += [ "$target_gen_dir/print_preview/resources.grdp" ]
  }

  if (enable_webui_tab_strip) {
    deps += [ "tab_strip:build_grdp" ]
    grdp_files += [ "$target_gen_dir/tab_strip/resources.grdp" ]
  }

  if (enable_pdf) {
    # Include PDF Viewer tests, since they are also served by
    # chrome://webui-test, even though they reside in chrome/test/data/pdf/.
    deps += [ "../pdf:build_grdp" ]
    grdp_files += [ "$target_gen_dir/../pdf/resources.grdp" ]
  }

  if (enable_search_engine_choice) {
    deps += [ "search_engine_choice:build_grdp" ]
    grdp_files += [ "$target_gen_dir/search_engine_choice/resources.grdp" ]
  }

  if (is_chrome_branded) {
    deps += [ "media_router:build_grdp" ]
    grdp_files += [ "$target_gen_dir/media_router/resources.grdp" ]
  }

  if (is_win || is_mac || is_linux) {
    deps += [
      "app_home:build_grdp",
      "app_settings:build_grdp",
    ]
    grdp_files += [
      "$target_gen_dir/app_settings/resources.grdp",
      "$target_gen_dir/app_home/resources.grdp",
    ]
  }

  if (enable_dice_support || is_chromeos_ash) {
    deps += [ "gaia_auth_host:build_grdp" ]
    grdp_files += [ "$target_gen_dir/gaia_auth_host/resources.grdp" ]
  }

  if (is_chromeos_ash) {
    deps += [
      ":build_web_ui_test_mojo_grdp",
      ":build_web_ui_test_mojo_lite_grdp",
      "chromeos:build_grdp",
      "chromeos/ash_common:build_grdp",
      "chromeos/borealis_installer:build_grdp",
      "chromeos/cloud_upload:build_grdp",
      "chromeos/diagnostics:build_grdp",
      "chromeos/edu_coexistence:build_grdp",
      "chromeos/emoji_picker:build_grdp",
      "chromeos/enterprise_reporting:build_grdp",
      "chromeos/firmware_update:build_grdp",
      "chromeos/manage_mirrorsync:build_grdp",
      "chromeos/office_fallback:build_grdp",
      "chromeos/parent_access:build_grdp",
      "chromeos/personalization_app:build_grdp",
      "chromeos/print_management:build_grdp",
      "chromeos/scanning:build_grdp",
      "chromeos/shortcut_customization:build_grdp",
      "cr_components/chromeos:build_grdp",
      "nearby_share:build_grdp",
      "settings/chromeos:build_grdp",
      "//ui/file_manager:build_tests_gen_grdp",
      "//ui/file_manager:build_tests_grdp",
    ]
    grdp_files += [
      "$target_gen_dir/chromeos/resources.grdp",
      "$target_gen_dir/chromeos/ash_common/resources.grdp",
      "$target_gen_dir/chromeos/borealis_installer/resources.grdp",
      "$target_gen_dir/chromeos/cloud_upload/resources.grdp",
      "$target_gen_dir/chromeos/diagnostics/resources.grdp",
      "$target_gen_dir/chromeos/edu_coexistence/resources.grdp",
      "$target_gen_dir/chromeos/emoji_picker/resources.grdp",
      "$target_gen_dir/chromeos/enterprise_reporting/resources.grdp",
      "$target_gen_dir/chromeos/firmware_update/resources.grdp",
      "$target_gen_dir/chromeos/office_fallback/resources.grdp",
      "$target_gen_dir/chromeos/parent_access/resources.grdp",
      "$target_gen_dir/chromeos/personalization_app/resources.grdp",
      "$target_gen_dir/chromeos/scanning/resources.grdp",
      "$target_gen_dir/chromeos/shortcut_customization/resources.grdp",
      "$target_gen_dir/chromeos/print_management/resources.grdp",
      "$target_gen_dir/chromeos/manage_mirrorsync/resources.grdp",
      "$target_gen_dir/cr_components/chromeos/resources.grdp",
      "$target_gen_dir/nearby_share/resources.grdp",
      "$target_gen_dir/settings/chromeos/resources.grdp",
      "$target_gen_dir/web_ui_test_mojo_lite_resources.grdp",
      "$target_gen_dir/web_ui_test_mojo_resources.grdp",
      "$root_gen_dir/ui/file_manager/tests_resources.grdp",
      "$root_gen_dir/ui/file_manager/tests_gen_resources.grdp",
    ]
  } else {
    deps += [
      "intro:build_grdp",
      "signin:build_grdp",
      "welcome:build_grdp",
    ]
    grdp_files += [
      "$target_gen_dir/intro/resources.grdp",
      "$target_gen_dir/welcome/resources.grdp",
      "$target_gen_dir/signin/resources.grdp",
    ]
  }

  if (is_chromeos_ash || is_win || is_mac) {
    deps += [ "inline_login:build_grdp" ]
    grdp_files += [ "$target_gen_dir/inline_login/resources.grdp" ]
  }

  if (!is_chromeos_lacros) {
    deps += [ "webview:build_grdp" ]
    grdp_files += [ "$target_gen_dir/webview/resources.grdp" ]
  }
  if (enable_compose) {
    deps += [ "compose:build_grdp" ]
    grdp_files += [ "$target_gen_dir/compose/resources.grdp" ]
  }

  manifest_files =
      filter_include(get_target_outputs(":build_ts"), [ "*_manifest.json" ])
}
