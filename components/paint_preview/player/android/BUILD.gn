# Copyright 2019 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")
import("//third_party/jni_zero/jni_zero.gni")

java_cpp_enum("compositor_status_generated_enum") {
  sources = [ "//components/paint_preview/player/compositor_status.h" ]
}

source_set("android") {
  sources = [
    "convert_to_java_bitmap.cc",
    "convert_to_java_bitmap.h",
    "player_compositor_delegate_android.cc",
    "player_compositor_delegate_android.h",
  ]

  deps = [
    ":jni_headers",
    "//base",
    "//components/paint_preview/browser",
    "//components/paint_preview/player",
    "//ui/accessibility:ax_assistant",
    "//ui/accessibility:ax_base",
    "//ui/gfx",
    "//url",
  ]

  public_deps =
      [ "//components/services/paint_preview_compositor/public/mojom" ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "convert_to_java_bitmap_unittest.cc",
    "player_compositor_delegate_android_unittest.cc",
  ]
  deps = [
    ":android",
    "//base",
    "//base/test:test_support",
    "//components/paint_preview/browser",
    "//components/paint_preview/player",
    "//components/services/paint_preview_compositor/public/mojom",
    "//skia",
    "//testing/gmock",
    "//testing/gtest",
  ]
}

generate_jni("jni_headers") {
  sources = [ "java/src/org/chromium/components/paintpreview/player/PlayerCompositorDelegateImpl.java" ]
}

android_library("java") {
  resources_package = "org.chromium.components.paintpreview.player"

  sources = [
    "java/src/org/chromium/components/paintpreview/player/LinkClickHandler.java",
    "java/src/org/chromium/components/paintpreview/player/OverscrollHandler.java",
    "java/src/org/chromium/components/paintpreview/player/PaintPreviewFrame.java",
    "java/src/org/chromium/components/paintpreview/player/PlayerCompositorDelegate.java",
    "java/src/org/chromium/components/paintpreview/player/PlayerCompositorDelegateImpl.java",
    "java/src/org/chromium/components/paintpreview/player/PlayerGestureListener.java",
    "java/src/org/chromium/components/paintpreview/player/PlayerManager.java",
    "java/src/org/chromium/components/paintpreview/player/PlayerSwipeRefreshHandler.java",
    "java/src/org/chromium/components/paintpreview/player/PlayerUserActionRecorder.java",
    "java/src/org/chromium/components/paintpreview/player/PlayerUserFrustrationDetector.java",
    "java/src/org/chromium/components/paintpreview/player/accessibility/PlayerAccessibilityDelegate.java",
    "java/src/org/chromium/components/paintpreview/player/frame/PlayerFrameBitmapPainter.java",
    "java/src/org/chromium/components/paintpreview/player/frame/PlayerFrameBitmapState.java",
    "java/src/org/chromium/components/paintpreview/player/frame/PlayerFrameBitmapStateController.java",
    "java/src/org/chromium/components/paintpreview/player/frame/PlayerFrameCoordinator.java",
    "java/src/org/chromium/components/paintpreview/player/frame/PlayerFrameGestureDetector.java",
    "java/src/org/chromium/components/paintpreview/player/frame/PlayerFrameGestureDetectorDelegate.java",
    "java/src/org/chromium/components/paintpreview/player/frame/PlayerFrameMediator.java",
    "java/src/org/chromium/components/paintpreview/player/frame/PlayerFrameMediatorDelegate.java",
    "java/src/org/chromium/components/paintpreview/player/frame/PlayerFrameProperties.java",
    "java/src/org/chromium/components/paintpreview/player/frame/PlayerFrameScaleController.java",
    "java/src/org/chromium/components/paintpreview/player/frame/PlayerFrameScrollController.java",
    "java/src/org/chromium/components/paintpreview/player/frame/PlayerFrameView.java",
    "java/src/org/chromium/components/paintpreview/player/frame/PlayerFrameViewBinder.java",
    "java/src/org/chromium/components/paintpreview/player/frame/PlayerFrameViewDelegate.java",
    "java/src/org/chromium/components/paintpreview/player/frame/PlayerFrameViewport.java",
  ]

  srcjar_deps = [
    ":compositor_status_generated_enum",
    ":jni_headers",
  ]

  deps = [
    "//base:base_java",
    "//build/android:build_java",
    "//components/browser_ui/styles/android:java",
    "//components/browser_ui/widget/android:java",
    "//components/paint_preview/browser/android:java",
    "//content/public/android:content_java",
    "//third_party/android_swipe_refresh:android_swipe_refresh_java",
    "//third_party/androidx:androidx_annotation_annotation_java",
    "//third_party/jni_zero:jni_zero_java",
    "//ui/accessibility:ax_base_java",
    "//ui/android:ui_java",
    "//ui/android:ui_java_resources",
    "//url:gurl_java",
  ]
}

android_library("player_java_test_support") {
  testonly = true

  srcjar_deps = [ ":javatests_jni_headers" ]
  sources = [
    "javatests/src/org/chromium/components/paintpreview/player/FrameData.java",
    "javatests/src/org/chromium/components/paintpreview/player/PaintPreviewTestRule.java",
    "javatests/src/org/chromium/components/paintpreview/player/PaintPreviewTestService.java",
  ]

  deps = [
    ":java",
    "//base:base_java",
    "//base:base_java_test_support",
    "//build/android:build_java",
    "//components/paint_preview/browser/android:java",
    "//components/signin/public/android:java",
    "//components/signin/public/android:signin_java_test_support",
    "//content/public/android:content_java",
    "//content/public/test/android:content_java_test_support",
    "//third_party/jni_zero:jni_zero_java",
    "//third_party/junit",
    "//url:gurl_java",
  ]
}

android_library("javatests") {
  testonly = true

  sources = [ "javatests/src/org/chromium/components/paintpreview/player/PaintPreviewPlayerTest.java" ]
  deps = [
    ":java",
    ":player_java_test_support",
    "//base:base_java",
    "//base:base_java_test_support",
    "//components/paint_preview/browser/android:java",
    "//content/public/android:content_java",
    "//content/public/test/android:content_java_test_support",
    "//third_party/android_deps:espresso_java",
    "//third_party/androidx:androidx_test_monitor_java",
    "//third_party/androidx:androidx_test_runner_java",
    "//third_party/androidx:androidx_test_uiautomator_uiautomator_java",
    "//third_party/hamcrest:hamcrest_java",
    "//third_party/junit",
    "//ui/android:ui_java_test_support",
    "//url:gurl_java",
  ]
}

generate_jni("javatests_jni_headers") {
  testonly = true

  sources = [ "javatests/src/org/chromium/components/paintpreview/player/PaintPreviewTestService.java" ]
}

source_set("test_support") {
  testonly = true

  sources = [
    "javatests/paint_preview_test_service.cc",
    "javatests/paint_preview_test_service.h",
  ]

  deps = [
    ":android",
    ":javatests_jni_headers",
    "//base",
    "//components/paint_preview/browser",
    "//components/paint_preview/browser:test_support",
  ]
}

robolectric_binary("paint_preview_junit_tests") {
  sources = [
    "junit/src/org/chromium/components/paintpreview/player/PlayerManagerTest.java",
    "junit/src/org/chromium/components/paintpreview/player/PlayerUserFrustrationDetectorTest.java",
    "junit/src/org/chromium/components/paintpreview/player/frame/PaintPreviewCustomFlingingShadowScroller.java",
    "junit/src/org/chromium/components/paintpreview/player/frame/PlayerFrameBitmapPainterTest.java",
    "junit/src/org/chromium/components/paintpreview/player/frame/PlayerFrameCoordinatorTest.java",
    "junit/src/org/chromium/components/paintpreview/player/frame/PlayerFrameMediatorTest.java",
    "junit/src/org/chromium/components/paintpreview/player/frame/PlayerFrameScaleControllerTest.java",
    "junit/src/org/chromium/components/paintpreview/player/frame/PlayerFrameScrollControllerTest.java",
    "junit/src/org/chromium/components/paintpreview/player/frame/PlayerFrameViewportTest.java",
  ]
  deps = [
    ":java",
    "//base:base_java",
    "//base:base_java_test_support",
    "//base:base_junit_test_support",
    "//third_party/androidx:androidx_annotation_annotation_java",
    "//ui/android:ui_full_java",
    "//url:gurl_java",
  ]
}
