# Copyright 2019 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//testing/test.gni")

group("tools") {
  if (is_win) {
    deps = [ ":certificate_tag" ]
  }
}

source_set("certificate_tag_lib") {
  sources = [
    "certificate_tag.cc",
    "certificate_tag.h",
  ]

  deps = [ "//base" ]
}

executable("certificate_tag_exe") {
  sources = [ "main.cc" ]
  deps = [
    ":certificate_tag_lib",
    "//base",
  ]
}

copy("certificate_tag") {
  # For arm64 builds, we need to create an x86_64 certificate_tag executable.
  if (current_cpu != "x64" && is_win) {
    tag_tool = ":certificate_tag_exe(//build/toolchain/win:win_clang_x64)"
  } else {
    tag_tool = ":certificate_tag_exe"
  }
  if (is_win) {
    suffix = ".exe"
  } else {
    suffix = ""
  }
  sources = [ get_label_info(tag_tool, "root_out_dir") +
              "/certificate_tag_exe" + suffix ]
  outputs = [ "$root_build_dir/certificate_tag.exe" ]
  deps = [ tag_tool ]
}

if (is_mac) {
  executable("keystone_ticketstore_reader") {
    sources = [ "keystone_ticketstore_reader.mm" ]
    deps = [
      "//base",
      "//chrome/updater:ks_ticket",
    ]

    frameworks = [ "Foundation.framework" ]
  }
}

source_set("unittest") {
  testonly = true

  sources = [ "certificate_tag_unittest.cc" ]

  deps = [
    ":certificate_tag_lib",
    "//base",
    "//base/test:test_support",
    "//testing/gtest",
    "//third_party/zlib/google:compression_utils",
  ]

  data = [ "../test/data/signed.exe.gz" ]
}
