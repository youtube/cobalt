# Copyright 2022 The Cobalt Authors. All Rights Reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

static_library("layout_test_utils") {
  testonly = true
  has_pedantic_warnings = true

  sources = [
    "layout_snapshot.cc",
    "layout_snapshot.h",
    "test_parser.cc",
    "test_parser.h",
    "test_utils.cc",
    "test_utils.h",
    "web_platform_test_parser.cc",
    "web_platform_test_parser.h",
  ]

  deps = [
    "//base/test:test_support",
    "//cobalt/base",
    "//cobalt/browser",
    "//cobalt/cssom",
    "//cobalt/layout_tests/testdata:layout_copy_test_data",
    "//cobalt/math",
    "//cobalt/network",
    "//cobalt/render_tree",
    "//cobalt/script",
    "//net",
    "//starboard:starboard_headers_only",
    "//url",
  ]
}

target(gtest_target_type, "layout_tests") {
  testonly = true
  has_pedantic_warnings = true

  sources = [ "layout_tests.cc" ]

  deps = [
    ":layout_test_utils",
    "//cobalt/base",
    "//cobalt/browser",
    "//cobalt/cssom",
    "//cobalt/math",
    "//cobalt/render_tree:animations",
    "//cobalt/renderer:render_tree_pixel_tester",
    "//cobalt/renderer/backend:renderer_backend",
    "//cobalt/script",
    "//cobalt/test:run_all_unittests",
    "//testing/gtest",
    "//url",
  ]

  data_deps = [
    "//cobalt/layout_tests/testdata:layout_copy_test_data",
    "//cobalt/network:copy_ssl_certificates",
    "//third_party/icu:icudata",
  ]
  if (cobalt_font_package == "empty") {
    data_deps += [ "//cobalt/content/fonts:copy_font_data" ]
  } else {
    data_deps += [
      "//cobalt/content/fonts:copy_fonts",
      "//cobalt/content/fonts:fonts_xml",
    ]
  }
}

target(gtest_target_type, "web_platform_tests") {
  testonly = true
  has_pedantic_warnings = true

  sources = [ "web_platform_tests.cc" ]

  deps = [
    ":layout_test_utils",
    "//cobalt/base",
    "//cobalt/browser",
    "//cobalt/cssom",
    "//cobalt/math",
    "//cobalt/media",
    "//cobalt/network",
    "//cobalt/render_tree",
    "//cobalt/test:run_all_unittests",
    "//testing/gtest",
    "//url",
  ]

  data_deps = [ "//cobalt/layout_tests/testdata:layout_copy_test_data" ]
}
