# Copyright 2019 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/rules.gni")
}

component("tab_groups") {
  sources = [
    "tab_group_color.cc",
    "tab_group_color.h",
    "tab_group_id.h",
    "tab_group_info.cc",
    "tab_group_info.h",
    "tab_group_visual_data.cc",
    "tab_group_visual_data.h",
    "token_id.h",
  ]

  defines = [ "IS_TAB_GROUPS_IMPL" ]

  deps = [
    "//base",
    "//components/strings",
    "//components/tab_groups/public/mojom:mojo_bindings_ts__generator",
    "//skia",
    "//ui/base",
    "//ui/gfx",
  ]
}

if (is_android) {
  java_cpp_enum("java_enums") {
    visibility = [ ":*" ]
    sources = [ "//components/tab_groups/tab_group_color.h" ]
  }

  android_library("tab_groups_java") {
    resources_package = "org.chromium.components.tab_groups"
    sources = [ "android/java/src/org/chromium/components/tab_groups/TabGroupColorPickerUtils.java" ]
    deps = [
      ":java_resources",
      "//components/browser_ui/styles/android:java",
      "//third_party/android_deps:material_design_java",
      "//third_party/androidx:androidx_annotation_annotation_java",
      "//third_party/androidx:androidx_core_core_java",
      "//ui/android:ui_java",
    ]
    srcjar_deps = [ ":java_enums" ]
  }

  android_resources("java_resources") {
    sources = [
      "android/java/res/values-night/colors.xml",
      "android/java/res/values/colors.xml",
      "android/java/res/values/dimens.xml",
    ]
    deps = [ "//components/strings:components_strings_grd" ]
  }
}
