# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")

source_set("lib") {
  if (!is_ios) {
    sources = [
      "csv_password_parser_impl.cc",
      "csv_password_parser_impl.h",
    ]
    deps = [
      "//base",
      "//components/password_manager/core/browser/import:csv",
      "//mojo/public/cpp/bindings",
    ]
  }

  public_deps =
      [ "//components/password_manager/services/csv_password/public/mojom" ]
  if (is_ios) {
    public_deps +=
        [ "//components/password_manager/services/csv_password/ios:lib" ]
  }

  configs += [ "//build/config/compiler:wexit_time_destructors" ]
}

source_set("service") {
  public_deps = [
    "//components/password_manager/services/csv_password/public/mojom",
    "//mojo/public/cpp/bindings",
  ]
  if (is_ios) {
    public_deps +=
        [ "//components/password_manager/services/csv_password/ios:service" ]
  } else {
    sources = [
      "csv_password_parser_service.cc",
      "csv_password_parser_service.h",
    ]

    deps = [
      "//components/strings",
      "//content/public/browser",
    ]
  }

  configs += [ "//build/config/compiler:wexit_time_destructors" ]
}

source_set("fake_password_parser_service") {
  testonly = true

  sources = [
    "fake_password_parser_service.cc",
    "fake_password_parser_service.h",
  ]

  deps = [ "//components/password_manager/services/csv_password/public/mojom" ]
}

source_set("unit_tests") {
  testonly = true
  sources = [ "csv_password_parser_impl_unittest.cc" ]

  deps = [
    "//base",
    "//base/test:test_support",
    "//components/password_manager/services/csv_password:lib",
    "//testing/gtest",
  ]
}
