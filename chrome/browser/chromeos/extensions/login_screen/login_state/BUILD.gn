# Copyright 2024 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//extensions/buildflags/buildflags.gni")

assert(enable_extensions)
assert(is_chromeos)

static_library("login_state") {
  sources = [
    "login_state_api.cc",
    "login_state_api.h",
    "session_state_changed_event_dispatcher.cc",
    "session_state_changed_event_dispatcher.h",
  ]

  public_deps = [
    "//base",
    "//chrome/common/extensions/api",
    "//chromeos/crosapi/mojom",
    "//extensions/browser",
    "//mojo/public/cpp/bindings",
  ]

  deps = [
    "//build:chromeos_buildflags",
    "//chrome/browser/profiles:profile",
    "//chrome/common:non_code_constants",
    "//content/public/browser",
  ]
}

source_set("browser_tests") {
  testonly = true

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

  sources = [ "login_state_apitest.cc" ]

  deps = [
    ":login_state",
    "//build:chromeos_buildflags",
    "//chrome/test:test_support",
    "//content/test:test_support",
    "//testing/gtest",
  ]

  if (is_chromeos_ash) {
    sources += [ "session_state_changed_event_dispatcher_ash_apitest.cc" ]
    deps += [
      "//components/session_manager:base",
      "//components/session_manager/core",
      "//extensions:test_support",
    ]
  }
}

source_set("unit_tests") {
  testonly = true

  sources = [ "login_state_api_unittest.cc" ]

  deps = [
    ":login_state",
    "//base",
    "//build:chromeos_buildflags",
    "//chrome/common:non_code_constants",
    "//chrome/test:test_support",
    "//extensions:test_support",
    "//extensions/common",
    "//testing/gtest",
  ]

  if (is_chromeos_ash) {
    sources += [ "session_state_changed_event_dispatcher_ash_unittest.cc" ]
    deps += [
      "//chrome/browser/ash/crosapi",
      "//chrome/browser/ash/crosapi:test_support",
      "//chrome/common/extensions/api",
      "//chromeos/ash/components/login/login_state",
      "//chromeos/crosapi/mojom",
      "//components/session_manager:base",
      "//components/session_manager/core",
      "//content/test:test_support",
      "//extensions/browser",
      "//extensions/browser:test_support",
      "//testing/gmock",
    ]
  }
}
