# Copyright 2024 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/buildflag_header.gni")
import("//build/config/chrome_build.gni")
import("//build/util/process_version.gni")
import("//chrome/common/request_header_integrity/buildflags.gni")

config("header_name_defines") {
  defines = []

  if (channel_name_header_name != "") {
    defines += [ "CHANNEL_NAME_HEADER_NAME=\"$channel_name_header_name\"" ]
  }

  if (lastchange_year_header_name != "") {
    defines +=
        [ "LASTCHANGE_YEAR_HEADER_NAME=\"$lastchange_year_header_name\"" ]
  }

  if (validate_header_name != "") {
    defines += [ "VALIDATE_HEADER_NAME=\"$validate_header_name\"" ]
  }

  if (copyright_header_name != "") {
    defines += [ "COPYRIGHT_HEADER_NAME=\"$copyright_header_name\"" ]
  }
}

source_set("request_header_integrity") {
  sources = [
    "request_header_integrity_url_loader_throttle.cc",
    "request_header_integrity_url_loader_throttle.h",
  ]

  configs += [ ":header_name_defines" ]

  deps = [
    "//base",
    "//build:branding_buildflags",
    "//chrome/common:channel_info",
    "//components/embedder_support:user_agent",
    "//components/google/core/common",
    "//google_apis",
    "//services/network/public/cpp",
    "//services/network/public/mojom",
    "//third_party/blink/public/common:headers",
  ]

  if (is_chrome_branded) {
    sources += [ "internal/google_header_names.h" ]
    deps += [
      "internal:build_derived_values",
      "internal:internal_header_integrity_seed",
    ]
  }
}

source_set("unit_tests") {
  testonly = true

  sources = [ "request_header_integrity_url_loader_throttle_unittest.cc" ]

  deps = [
    ":request_header_integrity",
    "//build:branding_buildflags",
    "//net",
    "//services/network/public/cpp",
    "//testing/gmock",
    "//testing/gtest",
    "//url",
  ]

  if (is_chrome_branded) {
    sources += [ "internal/google_header_names.h" ]
    deps += [ "//chrome/test:test_support" ]
  }
}

if (!is_android) {
  source_set("browser_tests") {
    testonly = true

    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

    sources = [ "request_header_integrity_url_loader_throttle_browsertest.cc" ]

    deps = [
      ":request_header_integrity",
      "//base",
      "//build:branding_buildflags",
      "//chrome/test:test_support_ui",
      "//components/network_session_configurator/common",
      "//content/test:test_support",
      "//net:test_support",
      "//services/network/public/mojom",
      "//url",
    ]

    if (is_chrome_branded) {
      sources += [ "internal/google_header_names.h" ]
    }
  }
}

buildflag_header("buildflags") {
  header = "buildflags.h"
  flags = [ "ENABLE_REQUEST_HEADER_INTEGRITY=$enable_request_header_integrity" ]
}
