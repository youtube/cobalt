# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chrome_build.gni")
import("//build/config/chromeos/ui_mode.gni")

assert(is_chromeos_ash, "Non-ChromeOS builds cannot depend on //ash")

static_library("common") {
}

static_library("keyboard_diagram_strings") {
  sources = [
    "keyboard_diagram_strings.cc",
    "keyboard_diagram_strings.h",
  ]

  deps = [
    "//chromeos/strings/",
    "//content/public/browser",
    "//ui/base",
  ]
}

static_library("shortcut_input_key_strings") {
  sources = [
    "shortcut_input_key_strings.cc",
    "shortcut_input_key_strings.h",
  ]

  deps = [
    "//ash",
    "//build:branding_buildflags",
    "//chromeos/strings/",
    "//content/public/browser",
    "//ui/base",
    "//ui/events/ash:ash",
  ]

  if (is_chrome_branded) {
    deps += [ "//chromeos/ash/resources/internal/strings:strings_grit" ]
  }
}

source_set("sea_pen") {
  sources = [
    "sea_pen_provider.h",
    "sea_pen_resources.cc",
    "sea_pen_resources.h",
    "sea_pen_resources_generated.cc",
    "sea_pen_resources_generated.h",
  ]

  deps = [
    "//ash/constants",
    "//ash/webui/common/mojom:sea_pen",
    "//chromeos/strings/",
    "//content/public/browser",
    "//ui/base",
  ]
}

source_set("chrome_os_webui_config") {
  sources = [ "chrome_os_webui_config.h" ]

  deps = [
    "//base",
    "//content/public/browser",
    "//content/public/common",
  ]
}

source_set("trusted_types_util") {
  sources = [
    "trusted_types_util.cc",
    "trusted_types_util.h",
  ]

  deps = [
    "//content/public/browser",
    "//services/network/public/mojom:url_loader_base",
  ]
}

source_set("trusted_types_test_util") {
  testonly = true

  sources = [
    "trusted_types_test_util.cc",
    "trusted_types_test_util.h",
  ]

  deps = [ "//content/test:test_support" ]
}

group("closure_compile") {
  deps = [ "resources:closure_compile" ]
}
