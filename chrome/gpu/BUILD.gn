# Copyright 2016 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//media/media_options.gni")
import("//third_party/widevine/cdm/widevine.gni")

static_library("gpu") {
  deps = [
    "//build:chromeos_buildflags",
    "//chrome/common/profiler",
    "//components/heap_profiling/in_process",
    "//components/metrics:child_call_stack_profile_builder",
    "//components/sampling_profiler",
    "//content/public/child",
    "//content/public/common",
    "//content/public/gpu",
    "//media:media_buildflags",
    "//services/tracing/public/cpp",
    "//third_party/widevine/cdm:buildflags",
  ]

  sources = [
    "browser_exposed_gpu_interfaces.cc",
    "browser_exposed_gpu_interfaces.h",
    "chrome_content_gpu_client.cc",
    "chrome_content_gpu_client.h",
  ]

  if (is_chromeos) {
    deps +=
        [ "//chromeos/components/cdm_factory_daemon:cdm_factory_daemon_gpu" ]
  }
  if (is_chromeos_ash) {
    deps += [
      "//ash/components/arc/mojom:media",
      "//gpu/command_buffer/service",
      "//services/service_manager/public/cpp",
      "//ui/ozone",
    ]
    if (use_v4l2_codec || use_vaapi) {
      deps += [ "//ash/components/arc/video_accelerator" ]
    }
  }
}
