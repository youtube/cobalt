# Copyright 2017 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//printing/buildflags/buildflags.gni")
import("//testing/test.gni")

source_set("lib") {
  sources = []

  deps = []

  public_deps = []

  if (enable_print_preview) {
    sources = [
      "pdf_nup_converter.cc",
      "pdf_nup_converter.h",
      "pdf_to_pwg_raster_converter.cc",
      "pdf_to_pwg_raster_converter.h",
      "printing_service.cc",
      "printing_service.h",
    ]

    public_deps += [
      "//base",
      "//chrome/services/printing/public/mojom",
      "//mojo/public/cpp/bindings",
    ]

    deps += [
      "//build:chromeos_buildflags",
      "//components/crash/core/common:crash_key",
      "//components/pwg_encoder",
      "//pdf",
      "//printing",
      "//printing/backend",
      "//printing/mojom",
    ]
  }

  if (is_chromeos) {
    sources += [
      "pdf_flattener.cc",
      "pdf_flattener.h",
    ]

    deps += [ "//pdf" ]
  }

  if (is_chromeos_ash) {
    sources += [
      "pdf_thumbnailer.cc",
      "pdf_thumbnailer.h",
    ]

    deps += [
      "//components/discardable_memory/client",
      "//content/public/child",
      "//content/public/utility",
      "//pdf",
      "//ui/gfx/codec",
    ]
  }

  if (is_win) {
    sources += [
      "pdf_to_emf_converter.cc",
      "pdf_to_emf_converter.h",
      "pdf_to_emf_converter_factory.cc",
      "pdf_to_emf_converter_factory.h",
    ]

    deps += [
      "//components/discardable_memory/client",
      "//content/public/child",
      "//content/public/utility",
      "//skia",
      "//ui/gfx",
    ]
  }

  if (enable_oop_printing) {
    sources += [
      "print_backend_service_impl.cc",
      "print_backend_service_impl.h",
    ]

    public_deps += [
      "//base",
      "//chrome/services/printing/public/mojom",
      "//mojo/public/cpp/bindings",
    ]

    deps += [
      "//chrome/common/printing",
      "//components/crash/core/common",
      "//printing:printing_base",
      "//ui/gfx",
    ]

    if (is_linux) {
      deps += [
        "//ui/linux:linux_ui",
        "//ui/linux:linux_ui_factory",
      ]
    }
  }
}

if (is_chromeos_ash) {
  assert(enable_print_preview)
  source_set("pdf_thumbnailer_test") {
    testonly = true
    sources = [ "pdf_thumbnailer_unittest.cc" ]

    deps = [
      ":lib",
      "//base/test:test_support",
      "//build:chromeos_buildflags",
      "//skia",
      "//testing/gtest",
    ]
  }
}
