# Copyright 2025 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")

mojom("mojom") {
  sources = [ "origin_matcher.mojom" ]

  public_deps = [
    "//mojo/public/mojom/base",
    "//third_party/blink/public/mojom:mojom_core",
  ]

  cpp_typemaps = [
    {
      types = [
        {
          mojom = "origin_matcher.mojom.OriginMatcher"
          cpp = "::origin_matcher::OriginMatcher"
        },
        {
          mojom = "origin_matcher.mojom.OriginMatcherRule"
          cpp = "::std::unique_ptr<::origin_matcher::OriginMatcherRule>"
          move_only = true
        },
      ]
      traits_headers = [
        "origin_matcher_mojom_traits.h",
        "origin_matcher.h",
      ]
      traits_sources = [ "origin_matcher_mojom_traits.cc" ]
      traits_public_deps = [ ":origin_matcher" ]
    },
  ]
  overridden_deps = [ "//third_party/blink/public/mojom:mojom_core" ]
  component_deps = [ "//third_party/blink/public/common" ]
}

source_set("origin_matcher") {
  public = [ "origin_matcher.h" ]
  sources = [
    "origin_matcher.cc",
    "origin_matcher_internal.cc",
    "origin_matcher_internal.h",
  ]
  public_deps = [ ":features" ]
  deps = [
    "//base",
    "//net",
    "//url",
  ]

  if (is_android) {
    sources += [ "android/origin_matcher_bindings.cc" ]

    deps += [ "//components/origin_matcher/android:jni" ]
  }

  # origin_matcher_internal is needed by mojom traits and tests.
  friend = [ ":*" ]
}

component("features") {
  defines = [ "IS_ORIGIN_MATCHER_IMPL" ]

  sources = [
    "features.cc",
    "features.h",
  ]

  deps = [ "//base" ]
}

source_set("unit_tests") {
  testonly = true
  sources = [ "origin_matcher_unittest.cc" ]
  deps = [
    ":mojom",
    ":origin_matcher",
    "//base",
    "//base/test:test_support",
    "//mojo/public/cpp/test_support:test_utils",
    "//url",
  ]
}
