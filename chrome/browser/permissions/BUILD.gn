# Copyright 2023 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//components/optimization_guide/features.gni")
import("//components/safe_browsing/buildflags.gni")
import("//device/vr/buildflags/buildflags.gni")
import("//services/device/public/cpp/geolocation/buildflags.gni")
import("//third_party/protobuf/proto_library.gni")

source_set("permissions") {
  sources = [
    "chrome_permissions_client.cc",
    "chrome_permissions_client.h",
    "crowd_deny_preload_data.cc",
    "crowd_deny_preload_data.h",
    "crowd_deny_safe_browsing_request.cc",
    "crowd_deny_safe_browsing_request.h",
    "notifications_engagement_service_factory.cc",
    "notifications_engagement_service_factory.h",
    "notifications_permission_revocation_config.cc",
    "notifications_permission_revocation_config.h",
    "one_time_permissions_tracker.cc",
    "one_time_permissions_tracker.h",
    "one_time_permissions_tracker_factory.cc",
    "one_time_permissions_tracker_factory.h",
    "one_time_permissions_tracker_helper.cc",
    "one_time_permissions_tracker_helper.h",
    "one_time_permissions_tracker_observer.h",
    "origin_keyed_permission_action_service_factory.cc",
    "origin_keyed_permission_action_service_factory.h",
    "permission_actions_history_factory.cc",
    "permission_actions_history_factory.h",
    "permission_decision_auto_blocker_factory.cc",
    "permission_decision_auto_blocker_factory.h",
    "permission_manager_factory.cc",
    "permission_manager_factory.h",
    "permission_revocation_request.cc",
    "permission_revocation_request.h",
    "prediction_service/language_detection_observer.cc",
    "prediction_service/language_detection_observer.h",
    "prediction_service/passage_embedder_delegate.cc",
    "prediction_service/passage_embedder_delegate.h",
    "prediction_service/permissions_ai_ui_selector.cc",
    "prediction_service/permissions_ai_ui_selector.h",
    "prediction_service/prediction_model_handler_provider.cc",
    "prediction_service/prediction_model_handler_provider.h",
    "prediction_service/prediction_service_factory.cc",
    "prediction_service/prediction_service_factory.h",
    "prediction_service/prediction_service_request.cc",
    "prediction_service/prediction_service_request.h",
    "pref_based_quiet_permission_ui_selector.cc",
    "pref_based_quiet_permission_ui_selector.h",
    "quiet_notification_permission_ui_config.cc",
    "quiet_notification_permission_ui_config.h",
    "quiet_notification_permission_ui_state.cc",
    "quiet_notification_permission_ui_state.h",
  ]

  public_deps = [
    "//chrome/browser:browser_public_dependencies",
    "//chrome/browser/media/webrtc",
    "//chrome/browser/permissions/system",
    "//chrome/browser/ui:browser_navigator_params_headers",
  ]

  deps = [
    ":permissions_proto",
    "//chrome/browser:browser_process",
    "//chrome/browser/app_mode",
    "//chrome/browser/background_fetch",
    "//chrome/browser/background_sync",
    "//chrome/browser/bluetooth",
    "//chrome/browser/content_extraction",
    "//chrome/browser/content_settings:content_settings_factory",
    "//chrome/browser/favicon",
    "//chrome/browser/metrics",
    "//chrome/browser/optimization_guide",
    "//chrome/browser/profiles",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/safe_browsing",
    "//chrome/browser/search_engines",
    "//chrome/browser/serial",
    "//chrome/browser/storage_access_api",
    "//chrome/browser/top_level_storage_access_api:permissions",
    "//chrome/browser/translate",
    "//chrome/browser/ui/hats",
    "//chrome/browser/ui/page_info",
    "//chrome/browser/ui/safety_hub",
    "//chrome/browser/usb",
    "//chrome/common",
    "//chrome/common:chrome_features",
    "//chrome/common:constants",
    "//components/background_sync",
    "//components/content_extraction/content/browser",
    "//components/content_settings/core/browser",
    "//components/content_settings/core/browser:cookie_settings",
    "//components/embedder_support:permissions",
    "//components/google/core/common",
    "//components/infobars/content",
    "//components/keyed_service/content",
    "//components/language_detection/core",
    "//components/optimization_guide/core",
    "//components/optimization_guide/proto:optimization_guide_proto",
    "//components/permissions",
    "//components/permissions/prediction_service",
    "//components/pref_registry",
    "//components/prefs",
    "//components/privacy_sandbox:tracking_protection_settings",
    "//components/safe_browsing/core/browser:safe_browsing_metrics_collector",
    "//components/safe_browsing/core/browser/db:database_manager",
    "//components/safe_browsing/core/common",
    "//components/safe_browsing/core/common:safe_browsing_prefs",
    "//components/site_engagement/content",
    "//components/subresource_filter/content/browser",
    "//components/supervised_user/core/common:features",
    "//components/unified_consent",
    "//components/vector_icons",
    "//content/public/browser",
    "//extensions/buildflags",
    "//services/device/public/cpp/geolocation",
  ]

  if (is_android) {
    sources += [
      "permission_blocked_dialog_controller_android.cc",
      "permission_blocked_dialog_controller_android.h",
      "permission_blocked_message_delegate_android.cc",
      "permission_blocked_message_delegate_android.h",
      "permission_update_message_controller_android.cc",
      "permission_update_message_controller_android.h",
      "permission_update_message_delegate_android.cc",
      "permission_update_message_delegate_android.h",
      "permission_update_requester_android.cc",
      "permission_update_requester_android.h",
      "quiet_permission_prompt_model_android.cc",
      "quiet_permission_prompt_model_android.h",
    ]

    deps += [
      "//chrome/android:chrome_jni_headers",
      "//components/messages/android",
      "//components/messages/android:feature_flags",
      "//ui/android",
    ]

    if (enable_openxr) {
      deps += [ "//device/vr/public/cpp:features" ]
    }
  }
  allow_circular_includes_from = []

  if (is_chromeos) {
    deps += [
      "//chrome/browser/ash/app_mode/isolated_web_app",
      "//chrome/browser/ash/app_mode/web_app",
      "//chrome/browser/ash/privacy_hub",
      "//chrome/browser/ash/shimless_rma",
      "//chromeos/ash/components/browser_context_helper",
      "//chromeos/components/kiosk",
    ]
    allow_circular_includes_from += [
      "//chrome/browser/ash/app_mode/web_app",
      "//chrome/browser/ash/shimless_rma",
    ]
  }

  if (build_with_tflite_lib) {
    sources += [
      "prediction_service/prediction_model_handler_provider_factory.cc",
      "prediction_service/prediction_model_handler_provider_factory.h",
    ]
    if (!is_android) {
      deps += [ "//third_party/tflite_support" ]
    }
    deps += [
      "//chrome/browser/passage_embeddings",
      "//components/passage_embeddings",
      "//components/segmentation_platform/internal:optimization_guide_segmentation_handler",
    ]
  }

  if (!is_android) {
    deps += [
      "//chrome/browser/actor",
      "//chrome/browser/ui/permission_bubble",
    ]
  }

  if (safe_browsing_mode != 0) {
    sources += [
      "prediction_service/contextual_notification_permission_ui_selector.cc",
      "prediction_service/contextual_notification_permission_ui_selector.h",
    ]
  }
}

proto_library("permissions_proto") {
  sources = [ "crowd_deny.proto" ]
}

source_set("test_support") {
  testonly = true

  sources = [
    "//chrome/browser/permissions/test/enums_to_string.cc",
    "//chrome/browser/permissions/test/enums_to_string.h",
    "//chrome/browser/permissions/test/mock_passage_embedder.cc",
    "//chrome/browser/permissions/test/mock_passage_embedder.h",
    "system/mock_platform_handle.cc",
    "system/mock_platform_handle.h",
  ]

  deps = [
    ":permissions",
    "//components/passage_embeddings:test_support",
    "//components/permissions:test_support",
    "//testing/gmock",
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "crowd_deny_preload_data_unittest.cc",
    "crowd_deny_safe_browsing_request_unittest.cc",
    "notifications_engagement_service_unittest.cc",
    "permission_context_base_permissions_policy_unittest.cc",
    "permission_manager_unittest.cc",
    "permission_request_manager_unittest.cc",
    "permission_revocation_request_unittest.cc",
    "permission_subscription_unittest.cc",
    "prediction_service/contextual_notification_permission_ui_selector_unittest.cc",
    "prediction_service/permissions_ai_ui_selector_unittest.cc",
    "pref_based_quiet_permission_ui_selector_unittest.cc",
  ]

  if (!is_android) {
    sources += [ "one_time_permissions_tracker_unittest.cc" ]
  }

  deps = [
    ":permissions",
    ":permissions_proto",
    ":test_support",
    "//chrome/browser/content_settings:content_settings_factory",
    "//chrome/browser/history",
    "//chrome/browser/optimization_guide:test_support",
    "//chrome/browser/permissions/system",
    "//chrome/browser/search_engines",
    "//chrome/browser/ui/safety_hub",
    "//chrome/browser/ui/safety_hub:test_support",
    "//chrome/test:test_support",
    "//components/optimization_guide/core:test_support",
    "//components/permissions/prediction_service",
    "//components/safe_browsing/core/browser:safe_browsing_metrics_collector",
    "//components/site_engagement/content",
    "//components/ukm/content",
    "//components/unified_consent",
    "//testing/gmock",
    "//testing/gtest",
  ]

  if (is_android) {
    sources += [
      "permission_blocked_message_delegate_android_unittest.cc",
      "permission_update_message_controller_android_unittest.cc",
    ]
    deps += [
      "//components/messages/android:test_support",
      "//components/ukm:test_support",
    ]
  } else {
    sources += [ "permission_hats_trigger_unittest.cc" ]
    deps += [ "//chrome/browser/ui/hats" ]
  }

  if (is_chromeos) {
    deps += [
      "//chrome/browser/ash/app_mode/web_app",
      "//chrome/browser/ash/login/users:test_support",
    ]
  }

  # TODO(crbug.com/40031409): Fix code that adds exit-time destructors and
  # enable the diagnostic by removing this line.
  configs += [ "//build/config/compiler:no_exit_time_destructors" ]
}

if (!is_android) {
  source_set("browser_tests") {
    testonly = true

    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

    sources = [
      "one_time_permissions_browsertest.cc",
      "permission_delegation_browsertest.cc",
      "permission_element_browsertest.cc",
      "permission_element_origin_trial_browsertest.cc",
      "permission_manager_browsertest.cc",
      "permission_request_manager_browsertest.cc",
      "permission_settings_page_browsertest.cc",
    ]

    deps = [
      ":permissions",
      ":test_support",
      "//chrome/browser/content_settings:content_settings_factory",
      "//chrome/browser/custom_handlers",
      "//chrome/browser/media/webrtc",
      "//chrome/browser/optimization_guide",
      "//chrome/browser/optimization_guide:test_support",
      "//chrome/browser/permissions/system",
      "//chrome/browser/ui:browser_element_identifiers",
      "//chrome/browser/ui:ui_features",
      "//chrome/test:test_support",
      "//components/back_forward_cache",
      "//components/optimization_guide/core:test_support",
      "//components/passage_embeddings:test_support",
    ]

    if (is_chromeos) {
      sources += [ "system/system_permission_settings_chromeos_unittest.cc" ]
      deps += [ "//chrome/browser/ash/privacy_hub" ]
    }

    if (build_with_tflite_lib) {
      sources += [ "prediction_service/prediction_service_browsertest.cc" ]
      deps += [ "//components/permissions/prediction_service" ]
    }

    # TODO(crbug.com/40031409): Fix code that adds exit-time destructors and
    # enable the diagnostic by removing this line.
    configs += [ "//build/config/compiler:no_exit_time_destructors" ]
  }
}

if (!is_android && !is_chromeos_device) {
  source_set("interactive_ui_tests") {
    testonly = true

    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

    sources = [ "permissions_security_model_interactive_uitest.cc" ]

    deps = [
      ":permissions",
      "//chrome/browser/content_settings:content_settings_factory",
      "//chrome/browser/permissions/system",
      "//chrome/browser/ui/extensions:test_support",
      "//chrome/test:test_support",
      "//components/content_settings/browser",
      "//components/embedder_support",
      "//content/test:browsertest_support",
    ]
  }
}
