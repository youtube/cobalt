# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/buildflag_header.gni")
import("//mojo/public/tools/bindings/mojom.gni")

static_library("webui") {
  sources = [
    "optimization_guide_internals_page_handler_impl.cc",
    "optimization_guide_internals_page_handler_impl.h",
  ]
  deps = [
    "//base",
    "//components/optimization_guide/core",
    "//components/optimization_guide/core:interfaces",
    "//third_party/abseil-cpp:absl",
    "//ui/base",
  ]
  if (!is_ios) {
    deps += [ "//ui/webui" ]
  }
  public_deps = [
    ":mojo_bindings",
    ":url_constants",
    "//components/optimization_guide/optimization_guide_internals/resources:resources",
  ]
  configs += [ "//build/config/compiler:wexit_time_destructors" ]
}

source_set("url_constants") {
  sources = [ "url_constants.h" ]
}

mojom("mojo_bindings") {
  sources = [ "optimization_guide_internals.mojom" ]
  webui_module_path = "/"
  public_deps = [
    "//components/optimization_guide/core:interfaces",
    "//mojo/public/mojom/base",
  ]
}
