# Copyright 2018 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//crypto/features.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//ui/webui/resources/tools/build_webui.gni")

assert(chrome_root_store_cert_management_ui)

mojom("mojom") {
  sources = [ "certificate_manager_v2.mojom" ]

  public_deps = [ "//mojo/public/mojom/base" ]

  webui_module_path = "chrome://resources/cr_components/certificate_manager/"
}

build_webui("build") {
  grd_prefix = "cr_components_certificate_manager"

  web_component_files = []
  ts_files = []
  static_files = []
  css_files = []

  if (chrome_root_store_cert_management_ui) {
    static_files += [ "favicon.svg" ]
    web_component_files += [
      "certificate_entry_v2.ts",
      "certificate_list_v2.ts",
      "certificate_manager_v2.ts",
      "certificate_subpage_v2.ts",
      "crs_section_v2.ts",
      "local_certs_section_v2.ts",
    ]
    ts_files += [
      "certificate_confirmation_dialog.html.ts",
      "certificate_confirmation_dialog.ts",
      "certificate_info_dialog.html.ts",
      "certificate_info_dialog.ts",
      "certificate_password_dialog.html.ts",
      "certificate_password_dialog.ts",
      "certificates_v2_browser_proxy.ts",
      "navigation_v2.ts",
    ]

    mojo_files_deps = [ ":mojom_ts__generator" ]
    mojo_files = [ "$target_gen_dir/certificate_manager_v2.mojom-webui.ts" ]

    icons_html_files = [ "certificate_manager_v2_icons.html" ]
    html_to_wrapper_template = "detect"
    css_files += [ "certificate_manager_style_v2.css" ]
  }

  if (is_chromeos) {
    web_component_files += [
      "certificate_provisioning_details_dialog.ts",
      "certificate_provisioning_entry.ts",
      "certificate_provisioning_list.ts",
    ]
    css_files += [ "certificate_shared.css" ]
    ts_files += [
      "certificate_provisioning_browser_proxy.ts",
      "certificate_manager_types.ts",
    ]
  }

  ts_out_dir =
      "$root_gen_dir/ui/webui/resources/tsc/cr_components/certificate_manager"
  ts_definitions = [
    "//tools/typescript/definitions/chrome_send.d.ts",
    "//tools/typescript/definitions/metrics_private.d.ts",
  ]
  ts_composite = true
  ts_deps = [
    "//third_party/lit/v3_0:build_ts",
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources/cr_elements:build_ts",
    "//ui/webui/resources/js:build_ts",
  ]
  if (chrome_root_store_cert_management_ui) {
    ts_deps += [ "//ui/webui/resources/mojo:build_ts" ]
  }

  webui_context_type = "relative"
  generate_grdp = true
  grd_resource_path_prefix = rebase_path(".", "//ui/webui/resources")
}
