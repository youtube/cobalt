# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//ui/webui/webui_features.gni")

template("merge_js_source_maps") {
  assert(enable_webui_inline_sourcemaps)

  action(target_name) {
    forward_variables_from(invoker,
                           [
                             "sources",
                             "outputs",
                             "deps",
                           ])
    script = "//tools/code_coverage/js_source_maps/merge_js_source_maps/merge_js_source_maps.py"
    args = [
      "--out-dir",
      rebase_path(invoker.out_dir, root_build_dir),
      "--response-file-name",
      "{{response_file_name}}",
    ]
    inputs = [ "//tools/code_coverage/js_source_maps/merge_js_source_maps/merge_js_source_maps.js" ]
    response_file_contents =
        [ "--manifest-files" ] +
        rebase_path(invoker.manifest_files, root_out_dir) + [ "--sources" ] +
        rebase_path(invoker.sources, root_out_dir) + [ "--outputs" ] +
        rebase_path(invoker.outputs, root_out_dir)
    foreach(manifest, invoker.manifest_files) {
      outputs += [ get_path_info(manifest, "dir") + "/" +
                   get_path_info(manifest, "name") + "__processed." +
                   get_path_info(manifest, "extension") ]
    }
  }
}
