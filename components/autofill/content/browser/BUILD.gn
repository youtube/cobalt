# Copyright 2014 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/protobuf/proto_library.gni")

proto_library("autofill_shared_storage_proto") {
  sources = [ "autofill_shared_storage.proto" ]
}

static_library("browser") {
  sources = [
    "autofill_log_router_factory.cc",
    "autofill_log_router_factory.h",
    "bad_message.cc",
    "bad_message.h",
    "content_autofill_client.cc",
    "content_autofill_client.h",
    "content_autofill_driver.cc",
    "content_autofill_driver.h",
    "content_autofill_driver_factory.cc",
    "content_autofill_driver_factory.h",
    "content_autofill_shared_storage_handler.cc",
    "content_autofill_shared_storage_handler.h",
    "content_identity_credential_delegate.cc",
    "content_identity_credential_delegate.h",
    "email_verifier_delegate.cc",
    "email_verifier_delegate.h",
    "integrators/glic/autofill_annotations_provider_impl.cc",
    "integrators/glic/autofill_annotations_provider_impl.h",
    "renderer_forms_from_browser_form.cc",
    "renderer_forms_from_browser_form.h",
    "risk/fingerprint.cc",
    "risk/fingerprint.h",
    "suggestions/identity_credential_suggestion_generator.cc",
    "suggestions/identity_credential_suggestion_generator.h",
  ]

  public_deps = [
    ":risk_proto",
    "//base",
    "//components/autofill/content/common/mojom",
    "//components/autofill/core/browser",
    "//components/autofill/core/common",
    "//components/keyed_service/content",
    "//components/optimization_guide/content/browser",
    "//components/services/storage",
    "//content/public/common",
    "//mojo/public/cpp/bindings",
    "//skia",
    "//third_party/abseil-cpp:absl",
  ]
  deps = [
    ":autofill_shared_storage_proto",
    "//base:i18n",
    "//components/autofill/core/browser/integrators/password_manager",
    "//components/credential_management:browser",
    "//components/prefs",
    "//components/profile_metrics",
    "//components/resources",
    "//components/strings",
    "//components/user_prefs",
    "//components/version_info",
    "//components/webauthn/core/browser",
    "//content/public/browser",
    "//content/public/common:buildflags",
    "//gpu/config",
    "//mojo/public/cpp/system",
    "//net",
    "//services/device/public/cpp/geolocation",
    "//services/device/public/mojom",
    "//services/service_manager/public/cpp",
    "//sql",
    "//ui/base",
    "//ui/display",
    "//ui/gfx",
    "//ui/gfx/geometry",
    "//url",
  ]
  configs += [ "//build/config/compiler:wexit_time_destructors" ]

  if (!is_android) {
    deps += [ "//components/webauthn/content/browser" ]
  }
}

proto_library("risk_proto") {
  sources = [ "risk/proto/fingerprint.proto" ]
  proto_out_dir = "components/autofill/content/browser/risk/proto"
}

source_set("test_support") {
  testonly = true
  sources = [
    "autofill_test_utils.cc",
    "autofill_test_utils.h",
    "content_autofill_driver_factory_test_api.cc",
    "content_autofill_driver_factory_test_api.h",
    "content_autofill_driver_test_api.h",
    "test_autofill_client_injector.cc",
    "test_autofill_client_injector.h",
    "test_autofill_driver_injector.cc",
    "test_autofill_driver_injector.h",
    "test_content_autofill_client.cc",
    "test_content_autofill_client.h",
    "test_content_autofill_driver.h",
  ]

  # iOS port provides its own versions of test_autofill_manager_injector.h/mm.
  if (!is_ios) {
    sources += [
      "test_autofill_manager_injector.cc",
      "test_autofill_manager_injector.h",
    ]
  }

  public_deps = [
    ":browser",
    "//components/autofill/core/browser:test_support",
    "//components/credential_management:browser",
    "//content/public/browser",
    "//content/test:test_support",
  ]

  deps = [ "//base" ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "autofill_internals_log_router_unittest.cc",
    "content_autofill_driver_factory_unittest.cc",
    "content_autofill_driver_unittest.cc",
    "content_identity_credential_delegate_unittest.cc",
    "email_verifier_delegate_unittest.cc",
    "integrators/glic/autofill_annotations_provider_impl_unittest.cc",
    "suggestions/identity_credential_suggestion_generator_unittest.cc",
  ]

  deps = [
    ":browser",
    ":test_support",
    "//base",
    "//components/autofill/content/common/mojom",
    "//components/autofill/core/browser",
    "//components/autofill/core/browser:test_support",
    "//components/autofill/core/common",
    "//components/version_info",
    "//content/public/browser",
    "//content/public/common",
    "//content/test:test_support",
    "//mojo/public/cpp/system",
    "//net:test_support",
    "//services/service_manager/public/cpp",
    "//testing/gmock",
    "//testing/gtest",
  ]

  # TODO(crbug.com/40031409): Fix code that adds exit-time destructors and
  # enable the diagnostic by removing this line.
  configs += [ "//build/config/compiler:no_exit_time_destructors" ]
}
source_set("browser_tests") {
  testonly = true
  sources = [ "risk/fingerprint_browsertest.cc" ]

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

  deps = [
    ":test_support",
    "//base",
    "//content/public/browser",
    "//content/test:browsertest_support",
    "//content/test:test_support",
    "//services/device/public/cpp:test_support",
    "//services/device/public/cpp/geolocation",
    "//services/device/public/mojom",
    "//testing/gmock",
    "//testing/gtest",
    "//ui/gfx/geometry",
  ]
}
