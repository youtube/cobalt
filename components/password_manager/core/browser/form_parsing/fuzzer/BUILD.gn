# Copyrightn Gerrit 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//testing/libfuzzer/fuzzer_test.gni")
import("//third_party/protobuf/proto_library.gni")

static_library("fuzzer_support") {
  sources = [
    "data_accessor.cc",
    "data_accessor.h",
    "form_data_producer.cc",
    "form_data_producer.h",
  ]

  deps = [
    "//base",
    "//components/autofill/core/browser",
    "//components/autofill/core/common",
    "//components/password_manager/core/browser/form_parsing",
    "//url",
  ]
}

# The part of fuzzer support which depends on protobufs is separate from the
# rest, which is also linked in unittests. This is because protobuf support for
# the fuzzer gets compiled with 'lite' runtime for unittests but without it for
# fuzzers. As a result, if the proto_library target is shared both by the fuzzer
# and the unittest target, linker errors are the result.
static_library("fuzzer_support_proto") {
  sources = [
    "form_data_proto_producer.cc",
    "form_data_proto_producer.h",
  ]

  deps = [
    ":form_data_essentials_proto",
    "//base",
    "//components/autofill/core/common",
    "//components/password_manager/core/browser/form_parsing",
    "//url",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [ "data_accessor_unittest.cc" ]

  deps = [
    ":fuzzer_support",
    "//base",
    "//testing/gtest",
  ]
}

# TODO(crbug.com/845426): There are currently four fuzzers, all combinations of
# two binary parameters. The first parameter is whether the input data for the
# fuzzed code is prepared directly from the raw fuzzing string, or from a
# protobuf description. The second parameter is whether the fuzzed code is the
# iOS-specific FormData parser, or the generic one. Ultimately, there will be
# only the generic one, but currently it is under development, so both parsers
# are used (and need to be fuzzed). The iOS parser is older and its fuzzer's
# name did not hint at the "iOS" restriction. It is better to keep that name
# the same, because the security team watches statistics of the fuzzers
# (https://crbug.com/828705#c11) and renaming causes confusion. So instead, the
# new fuzzer has the "_generic" suffix on its target name.

fuzzer_test("password_manager_form_parser_fuzzer") {
  sources = [ "form_parser_fuzzer.cc" ]

  deps = [
    ":fuzzer_support",
    "//base",
    "//base:i18n",
    "//components/autofill/core/common",
    "//components/password_manager/core/browser/form_parsing",
  ]

  dict = "form_parser_fuzzer.dict"
}

fuzzer_test("password_manager_form_parser_proto_fuzzer") {
  sources = [ "form_parser_proto_fuzzer.cc" ]

  deps = [
    ":form_data_essentials_proto",
    ":fuzzer_support",
    ":fuzzer_support_proto",
    "//base",
    "//base:i18n",
    "//components/autofill/core/common",
    "//components/password_manager/core/browser/form_parsing",
    "//third_party/libprotobuf-mutator",
  ]

  dict = "form_parser_fuzzer.dict"
}

proto_library("form_data_essentials_proto") {
  sources = [ "form_data_essentials.proto" ]
}
