# Copyright 2019 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")
import("//chrome/android/modules/buildflags.gni")

android_library("java") {
  sources = [ "java/src/org/chromium/chrome/modules/test_dummy/TestDummyProviderImpl.java" ]
  deps = [
    "//chrome/android/modules/test_dummy/public:java",
    "//chrome/browser/test_dummy:java",
    "//chrome/browser/test_dummy/internal:java",
  ]
}

# This group is effectively alias representing the module's native code,
# allowing it to be named "native" for clarity in module descriptors. The
# component target must be named according to the feature, so that the component
# build's .cr.co library is named properly (ie. libtest_dummy.cr.so).
group("native") {
  deps = [ ":test_dummy" ]
}

component("test_dummy") {
  sources = [ "entrypoints.cc" ]
  deps = [
    ":jni_registration($default_toolchain)",
    "//base",
    "//chrome/browser/test_dummy/internal:native",
  ]

  # Test dummy native entrypoints belong in the partition.
  if (use_native_partitions) {
    cflags = [ "-fsymbol-partition=test_dummy_partition" ]
  }
}

if (current_toolchain == default_toolchain) {
  generate_jni_registration("jni_registration") {
    no_transitive_deps = true
    targets = [ "//chrome/browser/test_dummy/internal:java" ]
    manual_jni_registration = true
    namespace = "test_dummy"
    module_name = "test_dummy"
    if (allow_jni_multiplexing) {
      enable_jni_multiplexing = true
    }
  }
}
