# Copyright 2017 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

source_set("ui_bundled") {
  sources = [
    "omnibox_controller_delegate.h",
    "omnibox_focus_delegate.h",
    "web_location_bar.cc",
    "web_location_bar.h",
  ]
  deps = [
    "//base",
    "//components/omnibox/browser",
    "//components/search_engines",
    "//ui/base",
  ]
}

source_set("omnibox_popup_shared") {
  sources = [
    "omnibox_constants.h",
    "omnibox_constants.mm",
    "omnibox_keyboard_delegate.h",
  ]
}

# This util file must be separate from the below
# omnibox_util file because it is used in showcase. This
# means that its dependencies should be limited, avoiding
# components code.
source_set("omnibox_suggestion_icon_util") {
  sources = [
    "omnibox_suggestion_icon_util.h",
    "omnibox_suggestion_icon_util.mm",
  ]
  deps = [
    "//base",
    "//ios/chrome/browser/shared/ui/symbols",
    "//ios/chrome/browser/shared/ui/symbols:buildflags",
  ]
}

# See the comment above on omnibox_suggestion_icon_util
# for why these two targets are separated.
source_set("omnibox_util") {
  sources = [
    "omnibox_metrics_helper.h",
    "omnibox_metrics_helper.mm",
    "omnibox_text_change_delegate.h",
    "omnibox_text_field_delegate.h",
    "omnibox_util.h",
    "omnibox_util.mm",
  ]
  deps = [
    ":omnibox_suggestion_icon_util",
    "//base",
    "//components/omnibox/browser",
    "//components/safe_browsing/core/common:common",
    "//components/security_state/core",
    "//ios/chrome/browser/omnibox/public:features",
    "//ios/chrome/browser/shared/public/features",
    "//ios/chrome/browser/shared/ui/symbols",
    "//third_party/omnibox_proto",
  ]
  public_deps = [
    ":omnibox_icon_type_constants",
    "//ios/chrome/app/theme",
  ]
}

# This target can also be used on EG2 tests, so it should only contain constants
source_set("omnibox_icon_type_constants") {
  sources = [
    "omnibox_icon_type.h",
    "omnibox_icon_type.mm",
  ]
  deps = [
    "//base",
    "//ios/chrome/browser/shared/ui/symbols",
  ]
}

# Build target allowed while moving files from ui_bundled to other folders.
# TODO(crbug.com/394567614): Move model files to model folder.
source_set("model_interface") {
  sources = [ "omnibox_view_ios.h" ]

  deps = [
    ":omnibox_util",
    "//base",
    "//components/omnibox/browser",
    "//components/omnibox/browser:location_bar",
    "//ios/chrome/browser/omnibox/ui_bundled/popup",
  ]
}

source_set("omnibox_internal") {
  sources = [
    "chrome_omnibox_client_ios.h",
    "chrome_omnibox_client_ios.mm",
    "fake_suggestions_database.cc",
    "fake_suggestions_database.h",
    "omnibox_consumer.h",
    "omnibox_container_view.h",
    "omnibox_container_view.mm",
    "omnibox_coordinator.h",
    "omnibox_coordinator.mm",
    "omnibox_mediator.h",
    "omnibox_mediator.mm",
    "omnibox_return_key_forwarding_delegate.h",
    "omnibox_return_key_forwarding_delegate.mm",
    "omnibox_text_field_ios.h",
    "omnibox_text_field_ios.mm",
    "omnibox_text_field_paste_delegate.h",
    "omnibox_text_field_paste_delegate.mm",
    "omnibox_thumbnail_button.h",
    "omnibox_thumbnail_button.mm",
    "omnibox_view_consumer.h",
    "omnibox_view_controller.h",
    "omnibox_view_controller.mm",
    "omnibox_view_ios.h",
    "omnibox_view_ios.mm",
    "text_field_view_containing.h",
    "web_location_bar_impl.h",
    "web_location_bar_impl.mm",
    "zero_suggest_prefetch_helper.h",
    "zero_suggest_prefetch_helper.mm",
  ]
  deps = [
    ":omnibox_popup_shared",
    ":omnibox_suggestion_icon_util",
    ":omnibox_util",
    ":ui_bundled",
    "//base",
    "//components/favicon/ios",
    "//components/feature_engagement/public",
    "//components/open_from_clipboard:",
    "//components/open_from_clipboard:open_from_clipboard_impl",
    "//components/resources",
    "//components/search_engines",
    "//components/security_state/core",
    "//components/strings",
    "//ios/chrome/app/strings",
    "//ios/chrome/browser/autocomplete/model",
    "//ios/chrome/browser/bookmarks/model",
    "//ios/chrome/browser/bookmarks/model:model_utils",
    "//ios/chrome/browser/default_browser/model:default_browser_interest_signals",
    "//ios/chrome/browser/default_promo/ui_bundled:coordinator",
    "//ios/chrome/browser/favicon/model",
    "//ios/chrome/browser/feature_engagement/model",
    "//ios/chrome/browser/fullscreen/ui_bundled",
    "//ios/chrome/browser/https_upgrades/model",
    "//ios/chrome/browser/intents:intents_donation_helper",
    "//ios/chrome/browser/lens/ui_bundled:lens_entrypoint",
    "//ios/chrome/browser/location_bar/ui_bundled:constants",
    "//ios/chrome/browser/main/ui_bundled:default_browser_scene_agent",
    "//ios/chrome/browser/net/model",
    "//ios/chrome/browser/ntp/model",
    "//ios/chrome/browser/ntp/shared/metrics:home_metrics",
    "//ios/chrome/browser/omnibox/model",
    "//ios/chrome/browser/omnibox/model:ui_bundled_interface",
    "//ios/chrome/browser/omnibox/public:features",
    "//ios/chrome/browser/omnibox/ui_bundled/popup",
    "//ios/chrome/browser/omnibox/ui_bundled/popup:popup_ui_protocols",
    "//ios/chrome/browser/orchestrator/ui_bundled:ui_bundled",
    "//ios/chrome/browser/prerender/model",
    "//ios/chrome/browser/search_engines/model",
    "//ios/chrome/browser/search_engines/model:search_engines_util",
    "//ios/chrome/browser/sessions/model",
    "//ios/chrome/browser/shared/coordinator/chrome_coordinator",
    "//ios/chrome/browser/shared/coordinator/layout_guide",
    "//ios/chrome/browser/shared/coordinator/scene:scene_state_header",
    "//ios/chrome/browser/shared/model/browser",
    "//ios/chrome/browser/shared/model/profile",
    "//ios/chrome/browser/shared/model/url:constants",
    "//ios/chrome/browser/shared/public/commands",
    "//ios/chrome/browser/shared/public/features",
    "//ios/chrome/browser/shared/public/features:system_flags",
    "//ios/chrome/browser/shared/ui/elements",
    "//ios/chrome/browser/shared/ui/symbols",
    "//ios/chrome/browser/shared/ui/util",
    "//ios/chrome/browser/shared/ui/util:omnibox_util",
    "//ios/chrome/browser/shared/ui/util:util_swift",
    "//ios/chrome/browser/toolbar/ui_bundled/public:constants",
    "//ios/chrome/browser/url_loading/model",
    "//ios/chrome/common",
    "//ios/chrome/common/ui/colors",
    "//ios/chrome/common/ui/favicon:favicon",
    "//ios/chrome/common/ui/favicon:favicon_constants",
    "//ios/chrome/common/ui/util",
    "//ios/chrome/common/ui/util:dynamic_type_util",
    "//ios/chrome/common/ui/util:image_util",
    "//ios/components/security_interstitials/https_only_mode",
    "//ios/public/provider/chrome/browser/branded_images:branded_images_api",
    "//ios/public/provider/chrome/browser/lens:lens_api",
    "//ios/web/common:uikit",
    "//ios/web/public",
    "//net",
    "//skia",
    "//ui/base",
    "//ui/gfx",
    "//ui/gfx/geometry",
    "//url",
  ]
  public_deps = [
    ":model_interface",
    ":omnibox_icon_type_constants",
    "//components/omnibox/browser",
  ]
  frameworks = [
    "CoreText.framework",
    "MobileCoreServices.framework",
    "QuartzCore.framework",
    "UIKit.framework",
    "UniformTypeIdentifiers.framework",
  ]
}

source_set("test_support") {
  testonly = true
  sources = [
    "test_web_location_bar.cc",
    "test_web_location_bar.h",
  ]
  deps = [
    ":ui_bundled",
    "//base",
    "//components/omnibox/browser:location_bar",
    "//ios/web/public",
    "//url",
  ]
  frameworks = [ "UIKit.framework" ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "chrome_omnibox_client_ios_unittest.mm",
    "fake_suggestions_database_unittest.cc",
    "omnibox_text_field_ios_unittest.mm",
    "zero_suggest_prefetch_helper_unittest.mm",
  ]
  deps = [
    ":omnibox_internal",
    ":resources_unit_tests",
    ":test_support",
    ":ui_bundled",
    "//base",
    "//base/test:test_support",
    "//components/bookmarks/browser",
    "//components/feature_engagement/public",
    "//components/feature_engagement/test:test_support",
    "//components/omnibox/browser:test_support",
    "//components/search_engines",
    "//components/search_engines:test_support",
    "//ios/chrome/app/strings",
    "//ios/chrome/browser/autocomplete/model",
    "//ios/chrome/browser/main/model",
    "//ios/chrome/browser/shared/model/browser/test:test_support",
    "//ios/chrome/browser/shared/model/paths",
    "//ios/chrome/browser/shared/model/profile/test",
    "//ios/chrome/browser/shared/model/url:constants",
    "//ios/chrome/browser/shared/model/web_state_list",
    "//ios/chrome/browser/shared/model/web_state_list/test:test_support",
    "//ios/chrome/browser/shared/ui/util",
    "//ios/chrome/test:block_cleanup_test",
    "//ios/testing:nserror_support",
    "//ios/web/common:uikit",
    "//ios/web/public/test/fakes",
    "//testing/gtest",
    "//third_party/ocmock",
    "//ui/base",
  ]
}

bundle_data("resources_unit_tests") {
  visibility = [ ":unit_tests" ]
  testonly = true
  sources = [ "//ios/chrome/test/data/omnibox/selected_ranges.txt" ]
  outputs = [ "{{bundle_resources_dir}}/" +
              "ios/chrome/test/data/omnibox/{{source_file_part}}" ]
}
