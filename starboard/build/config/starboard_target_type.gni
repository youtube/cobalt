# Copyright 2023 The Cobalt Authors. All Rights Reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//starboard/build/config/os_definitions.gni")
declare_args() {
  starboard_target_type = ""
  if (is_android) {
    # The apk packaging can't deal with any shlib extension other than .so.
    starboard_shared_library_extension = "so"
  } else {
    starboard_shared_library_extension = "so.${sb_api_version}"
  }
}

if (starboard_target_type == "") {
  if (sb_is_modular && !sb_is_evergreen) {
    starboard_target_type = "shared_library"
  } else {
    starboard_target_type = "component"
  }
}

template("starboard_platform_target") {
  target(starboard_target_type, target_name) {
    forward_variables_from(invoker, [ "extra_configs" ])

    if (starboard_target_type == "shared_library") {
      output_extension = starboard_shared_library_extension
    }

    if (defined(invoker.extra_configs)) {
      configs += extra_configs
    }

    all_dependent_configs = [
      "//starboard/build/config/sabi",
      "//starboard/build/config:starboard",
    ]
    if (is_android) {
      # TODO(b/374300500): - Find a way to avoid wrapping these.
      ldflags = [
        # "-Wl,--wrap=close",
        # "-Wl,--wrap=open",
        # "-Wl,--wrap=stat",
        # "-Wl,--wrap=opendir",
        # "-Wl,--wrap=closedir",
        # "-Wl,--wrap=readdir_r",
        "-Wl,--wrap=eglSwapBuffers",
      ]
    }

    public_deps = [
      "//starboard/common",
      "//starboard/egl_and_gles",
    ]
    if (!sb_is_evergreen) {
      public_deps += [ "//$starboard_path:starboard_platform" ]
    }
    if (sb_is_modular) {
      # For modular builds (including Evergreen), include POSIX ABI wrappers that
      # handle cross-compatibility with musl-based Cobalt code.
      public_deps +=
          [ "//starboard/shared/modular:starboard_layer_posix_abi_wrappers" ]
    }
  }
}
