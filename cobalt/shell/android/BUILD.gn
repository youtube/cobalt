# Copyright 2025 The Cobalt Authors. All Rights Reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/android/config.gni")
import("//build/config/android/rules.gni")
import("//third_party/icu/config.gni")
import("//third_party/jni_zero/jni_zero.gni")

cobalt_shell_test_manifest =
    "$target_gen_dir/cobalt_shell_test_manifest/AndroidManifest.xml"

generate_jni("cobalt_shell_jni_headers") {
  sources = [
    "java/src/dev/cobalt/shell/ContentViewRenderView.java",
    "java/src/dev/cobalt/shell/Shell.java",
    "java/src/dev/cobalt/shell/ShellManager.java",
  ]
}

shared_library("libcobalt_native_test") {
  testonly = true
  deps = [
    "//base",
    "//base:base_javatests_lib",
    "//cobalt/shell:cobalt_shell_app",
    "//cobalt/shell:cobalt_shell_lib",
    "//content/public/test/android:content_native_test_support",
  ]

  sources = [ "shell_test_library_loader.cc" ]
  configs -= [ "//build/config/android:hide_all_but_jni_onload" ]
  configs += [ "//build/config/android:hide_all_but_jni" ]
}

android_resources("cobalt_shell_java_resources") {
  testonly = true
  sources = [
    "java/res/drawable-xhdpi/app_banner.png",
    "java/res/layout/coat_error_dialog.xml",
    "java/res/mipmap-hdpi/ic_app.png",
    "java/res/mipmap-mdpi/ic_app.png",
    "java/res/mipmap-xhdpi/ic_app.png",
    "java/res/mipmap-xxhdpi/ic_app.png",
    "java/res/values/colors.xml",
    "java/res/values/ids.xml",
    "java/res/values/overlayable.xml",
    "java/res/values/rro_variables.xml",
    "java/res/values/strings.xml",
    "java/res/values/styles.xml",
  ]

  deps = [ "//third_party/androidx:androidx_leanback_leanback_java" ]
}

android_library("cobalt_shell_java") {
  testonly = true

  resources_package = "dev.cobalt.shell"
  deps = [
    ":cobalt_shell_java_resources",
    ":cobalt_shell_test_manifest",
    "//base:base_java",
    "//build/android:build_java",
    "//components/download/internal/common:internal_java",
    "//components/embedder_support/android:view_java",
    "//components/viz/service:service_java",
    "//content/public/android:content_java",
    "//media/base/android:media_java",
    "//media/capture/video/android:capture_java",
    "//mojo/public/java:system_java",
    "//net/android:net_java",
    "//third_party/jni_zero:jni_zero_java",
    "//ui/android:ui_java",
    "//ui/android:ui_no_recycler_view_java",
    "//ui/base/cursor/mojom:cursor_type_java",
    "//url:gurl_java",
  ]
  srcjar_deps = [ ":cobalt_shell_jni_headers" ]
  sources = [
    "java/src/dev/cobalt/shell/ContentViewRenderView.java",
    "java/src/dev/cobalt/shell/Shell.java",
    "java/src/dev/cobalt/shell/ShellManager.java",
    "java/src/dev/cobalt/shell/ShellViewAndroidDelegate.java",
  ]
}

jinja_template("cobalt_shell_test_manifest") {
  testonly = true
  input = "javatests/AndroidManifest.xml"
  includes = [ "shell_apk/AndroidManifest.xml.jinja2" ]
  output = cobalt_shell_test_manifest
  variables = [
    "manifest_package=dev.cobalt.app",
    "build_type=$build_type",
    "enable_ad_id_permission=0",
  ]
}

android_library("cobalt_shell_test_apk_java") {
  testonly = true

  srcjar_deps = [ ":content_javatests_aidl" ]

  resources_package = "org.chromium.content_shell_apk"
  deps = [
    ":cobalt_shell_java",
    ":cobalt_shell_java_resources",
    ":cobalt_shell_test_manifest",
    "//base:base_java",
    "//base:process_launcher_java",
    "//build/android:build_java",
    "//components/embedder_support/android:view_java",
    "//content/public/android:content_java",
    "//media/capture/video/android:capture_java",
    "//net/android:net_java",
    "//third_party/android_deps:com_google_code_findbugs_jsr305_java",
    "//ui/android:ui_java",
    "//url:gurl_java",
  ]

  sources = [
    "//cobalt/android/apk/app/src/app/java/dev/cobalt/app/CobaltApplication.java",
    "//cobalt/android/apk/app/src/main/java/dev/cobalt/coat/CobaltActivity.java",
  ]
}

android_aidl("content_javatests_aidl") {
  import_include = [ "shell_apk/src" ]
  sources =
      [ "shell_apk/src/org/chromium/content_shell_apk/IChildProcessTest.aidl" ]
}

android_assets("cobalt_shell_assets") {
  testonly = true
  sources = [ "$root_out_dir/cobalt_shell.pak" ]
  disable_compression = true
  deps = [
    "//cobalt/shell:pak",
    "//gin:v8_snapshot_assets",
    "//third_party/icu:icu_assets",
  ]
}

template("cobalt_shell_apk_tmpl") {
  _target_type = invoker.target_type
  target(_target_type, target_name) {
    forward_variables_from(invoker, "*")
    testonly = true
    if (!defined(deps)) {
      deps = []
    }
    deps += [
      ":cobalt_shell_assets",
      ":cobalt_shell_java",
      ":cobalt_shell_test_apk_java",
      "//base:base_java_test_support",
      "//components/metrics:metrics_java",
      "//content/public/android:content_java",
      "//content/public/test/android:android_test_message_pump_support_java",
      "//media/capture/video/android:capture_java",
      "//net/android:net_java",
      "//services/shape_detection:shape_detection_java",
      "//third_party/mesa_headers",
      "//ui/android:ui_java",
    ]
  }
}

cobalt_shell_apk_tmpl("cobalt_shell_test_apk") {
  target_type = "instrumentation_test_apk"
  apk_name = "CobaltShellTest"
  android_manifest = cobalt_shell_test_manifest
  android_manifest_dep = ":cobalt_shell_test_manifest"
  shared_libraries = [ ":libcobalt_native_test" ]
  deps = [
    "//base:base_java_test_support",
    "//base:base_javatests",
    "//content/public/android:content_javatests",
    "//net/android:net_tests_java",
  ]
  data_deps = [ "//testing/buildbot/filters:content_shell_test_apk_filters" ]

  additional_apks = [ "//net/android:net_test_support_apk" ]

  # TODO(crbug.com/1099536): Fix tests that break when this is increased.
  target_sdk_version = 24
}

android_library("cobalt_shell_test_java") {
  testonly = true
  deps = [
    ":cobalt_shell_java",
    ":cobalt_shell_test_apk_java",
    "//base:base_java",
    "//base:base_java_test_support",
    "//content/public/android:content_java",
    "//content/public/test/android:content_java_test_support",
    "//mojo/public/java/system:test_support_java",
    "//third_party/androidx:androidx_test_monitor_java",
    "//third_party/androidx:androidx_test_runner_java",
    "//third_party/hamcrest:hamcrest_java",
    "//third_party/junit:junit",
    "//ui/android:ui_java",
    "//url:gurl_java",
  ]
  sources = [
    "javatests/src/org/chromium/content_shell_apk/ContentShellActivityTestRule.java",
    "javatests/src/org/chromium/content_shell_apk/ContentShellPreconditionsTest.java",
    "javatests/src/org/chromium/content_shell_apk/ContentShellShellManagementTest.java",
    "javatests/src/org/chromium/content_shell_apk/ContentShellUrlTest.java",
  ]
}
