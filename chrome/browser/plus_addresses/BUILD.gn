# Copyright 2023 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(is_win || is_mac || is_linux || is_chromeos || is_android)

source_set("plus_addresses") {
  sources = [
    "plus_address_service_factory.h",
    "plus_address_setting_service_factory.h",
  ]

  public_deps = [
    "//base",
    "//chrome/browser/affiliations",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/sync:factories",
    "//components/affiliations/core/browser:affiliations",
    "//components/autofill/core/common:features",
    "//components/plus_addresses",
    "//components/plus_addresses:features",
    "//components/plus_addresses:hats_utils",
    "//components/plus_addresses/settings",
    "//components/signin/public/identity_manager",
    "//components/sync/model",
    "//components/variations/service",
    "//services/network/public/cpp",
  ]
}

source_set("impl") {
  sources = [
    "plus_address_service_factory.cc",
    "plus_address_setting_service_factory.cc",
  ]
  deps = [ ":plus_addresses" ]
}

if (!is_android) {
  source_set("browser_tests") {
    testonly = true
    sources = [ "plus_address_browsertest.cc" ]
    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
    public_deps = [ ":plus_addresses" ]
    deps = [
      "//base",
      "//base/test:test_support",
      "//chrome/test:test_support",
      "//chrome/test:test_support_ui",
      "//components/plus_addresses",
      "//content/test:test_support",
      "//testing/gtest",
    ]
  }
}
