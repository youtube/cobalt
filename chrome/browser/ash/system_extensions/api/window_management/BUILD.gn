# Copyright 2021 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//mojo/public/tools/bindings/mojom.gni")

assert(is_chromeos_ash)

source_set("window_management") {
  sources = [
    "cros_window_management_context.cc",
    "cros_window_management_context.h",
    "cros_window_management_context_factory.cc",
    "cros_window_management_context_factory.h",
    "window_management_impl.cc",
    "window_management_impl.h",
  ]

  # Enable the "exit_time_destructors" warning here to avoid accidentally
  # adding exit time destructors and because the flag is not enabled by default.
  # TODO(http://crbug.com/101600):  Remove once exit_time_destructors is enabled
  # by default.
  configs += [ "//build/config/compiler:wexit_time_destructors" ]

  deps = [
    "//ash",
    "//chrome/browser/apps/app_service",
    "//chrome/browser/ash/system_extensions",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/ui",
    "//components/keyed_service/content",
    "//components/services/app_service/public/cpp:instance_update",
    "//content/public/browser",
    "//third_party/blink/public/mojom:mojom_platform",
    "//ui/aura",
    "//ui/views",
    "//ui/webui",
  ]
}

source_set("browser_tests") {
  testonly = true

  sources = [ "cros_window_browsertest.cc" ]

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

  deps = [
    ":test_helper",
    ":test_helper_js_data_deps",
    ":window_management",
    "//chrome/browser/apps/app_service",
    "//chrome/browser/apps/app_service:app_registry_cache_waiter",
    "//chrome/browser/ash/system_extensions",
    "//chrome/browser/ash/system_extensions/api/test_support",
    "//chrome/browser/ash/system_web_apps/test_support",
    "//chrome/browser/ui/ash/system_web_apps",
    "//chrome/browser/web_applications:web_applications_test_support",
    "//chrome/test:test_support",
    "//ui/events:test_support",
  ]

  data = [
    "//third_party/blink/web_tests/resources/testharness.js",
    "//third_party/blink/web_tests/resources/testharness-helpers.js",
    "//chrome/browser/ash/system_extensions/api/window_management/test/",
  ]
}

mojom("test_helper") {
  testonly = true
  sources = [ "cros_window_management_test_helper.test-mojom" ]

  public_deps = [
    "//mojo/public/mojom/base",
    "//ui/events/mojom",
    "//ui/gfx/geometry/mojom",
  ]
}
