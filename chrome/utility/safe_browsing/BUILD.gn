# Copyright 2023 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//components/safe_browsing/buildflags.gni")

if (is_linux || is_win) {
  source_set("document_analyzer") {
    sources = [
      "document_analyzer.cc",
      "document_analyzer.h",
    ]

    deps = [
      "//base",
      "//chrome/common/safe_browsing:document_analyzer_results",
      "//chrome/common/safe_browsing:download_type_util",
      "//components/safe_browsing/content/common:file_type_policies",
      "//components/safe_browsing/core/common",
      "//third_party/maldoca:maldoca",
    ]

    public_deps = [ "//components/safe_browsing/core/common/proto:csd_proto" ]
  }
}

source_set("safe_browsing") {
  deps = []

  if (safe_browsing_mode == 1) {
    sources = [
      "archive_analyzer.cc",
      "archive_analyzer.h",
      "rar_analyzer.cc",
      "rar_analyzer.h",
      "seven_zip_analyzer.cc",
      "seven_zip_analyzer.h",
      "zip_analyzer.cc",
      "zip_analyzer.h",
    ]

    if (is_mac) {
      sources += [
        "mac/dmg_analyzer.cc",
        "mac/dmg_analyzer.h",
        "mac/dmg_iterator.cc",
        "mac/dmg_iterator.h",
      ]
    }

    deps += [
      "//chrome/common/safe_browsing",
      "//chrome/common/safe_browsing:archive_analyzer_results",
      "//chrome/common/safe_browsing:download_type_util",
      "//components/safe_browsing/content/common:file_type_policies",
      "//components/safe_browsing/core/common",
      "//third_party/lzma_sdk/google:seven_zip_reader",
      "//third_party/unrar:unrar",
      "//third_party/zlib",
    ]

    if (is_linux) {
      deps += [ ":document_analyzer" ]
    }

    if (is_mac) {
      deps += [
        "//base",
        "//chrome/common/safe_browsing:archive_analyzer_results",
        "//chrome/common/safe_browsing:binary_feature_extractor",
        "//chrome/utility/safe_browsing/mac:dmg_common",
        "//components/safe_browsing/content/common:file_type_policies",
        "//components/safe_browsing/core/common",
        "//components/safe_browsing/core/common/proto:csd_proto",
      ]
    }
  }
}
