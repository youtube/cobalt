# Copyright 2014 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//media/media_options.gni")

if (enable_media_drm_storage) {
  source_set("browser") {
    # See //components/cdm/README.md
    visibility = [
      "//android_webview/*",
      "//chrome/*",
      "//chromecast/*",
      "//components/cdm/*",
      "//content/shell/*",
      "//fuchsia_web/*",
    ]

    if (is_android) {
      visibility += [ "//components/browser_ui/site_settings/android" ]
    }

    sources = [
      "media_drm_storage_impl.cc",
      "media_drm_storage_impl.h",
    ]

    public_deps = [
      "//base",
      "//content/public/browser",
      "//media/mojo/mojom",
      "//url",
    ]

    deps = [
      "//components/prefs",
      "//components/user_prefs",
      "//content/public/common",
      "//media",
    ]
  }

  source_set("unit_tests") {
    testonly = true
    sources = [ "media_drm_storage_impl_unittest.cc" ]
    deps = [
      ":browser",
      "//base/test:test_support",
      "//components/prefs:test_support",
      "//content/test:test_support",
      "//media/mojo/services",
      "//testing/gtest",
    ]
  }
}
