# Copyright 2025 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//components/proto_extras/proto_extras.gni")
import("//testing/test.gni")
import("//third_party/protobuf/proto_library.gni")

proto_library("proto") {
  sources = [ "proto/legion.proto" ]
}

# Generate serialization helper.
proto_extras("proto_extras") {
  sources = [ "proto/legion.proto" ]
  deps = [ ":proto" ]
}

source_set("features") {
  sources = [
    "features.cc",
    "features.h",
  ]
  deps = [ "//base" ]
}

source_set("legion") {
  sources = [
    "attestation_handler.h",
    "attestation_handler_impl.cc",
    "attestation_handler_impl.h",
    "client.cc",
    "client.h",
    "error_code.h",
    "legion_common.h",
    "secure_channel.h",
    "secure_channel_impl.cc",
    "secure_channel_impl.h",
    "secure_session.h",
    "secure_session_async_impl.cc",
    "secure_session_async_impl.h",
    "transport.h",
    "websocket_client.cc",
    "websocket_client.h",
  ]
  public_deps = [ ":features" ]
  deps = [
    ":proto",
    ":proto_extras",
    "//base",
    "//components/legion/crypto",
    "//components/legion/phosphor",
    "//crypto",
    "//mojo/public/cpp/system",
    "//net",
    "//services/network/public/cpp",
    "//services/network/public/mojom",
    "//third_party/boringssl",
    "//third_party/oak:oak_proto",
    "//third_party/oak:oak_proto_extras",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "attestation_handler_impl_unittest.cc",
    "client_unittest.cc",
    "secure_channel_impl_unittest.cc",
    "secure_session_async_impl_unittest.cc",
  ]
  deps = [
    ":legion",
    ":proto",
    "//base",
    "//base/test:test_support",
    "//components/legion/crypto",
    "//components/legion/crypto:test_support",
    "//components/legion/crypto:unit_tests",
    "//components/legion/phosphor:unit_tests",
    "//testing/gmock",
    "//testing/gtest",
    "//third_party/oak:oak_proto",
    "//third_party/oak:oak_proto_extras",
    "//third_party/oak:oak_proto_test_extras",
  ]
}
