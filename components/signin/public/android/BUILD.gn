import("//build/config/android/rules.gni")
import("//third_party/jni_zero/jni_zero.gni")

android_library("java") {
  deps = [
    "$google_play_services_package:google_play_services_auth_base_java",
    "$google_play_services_package:google_play_services_base_java",
    "//base:base_java",
    "//build/android:build_java",
    "//components/externalauth/android:java",
    "//net/android:net_java",
    "//third_party/android_deps:chromium_play_services_availability_java",
    "//third_party/androidx:androidx_annotation_annotation_java",
    "//third_party/jni_zero:jni_zero_java",
  ]

  srcjar_deps = [
    ":account_capabilities_constants_javagen",
    ":jni_headers",
    ":signin_java_features_srcjar",
    ":tribool_javagen",
    "//components/signin/public/base:consent_level_enum_javagen",
    "//components/signin/public/base:signin_metrics_enum_javagen",
    "//components/signin/public/identity_manager:primary_account_mutator_enum_javagen",
  ]

  sources = [
    "java/src/org/chromium/components/signin/AccessTokenData.java",
    "java/src/org/chromium/components/signin/AccountCapabilitiesFetcher.java",
    "java/src/org/chromium/components/signin/AccountEmailDomainDisplayability.java",
    "java/src/org/chromium/components/signin/AccountManagerDelegate.java",
    "java/src/org/chromium/components/signin/AccountManagerFacade.java",
    "java/src/org/chromium/components/signin/AccountManagerFacadeImpl.java",
    "java/src/org/chromium/components/signin/AccountManagerFacadeProvider.java",
    "java/src/org/chromium/components/signin/AccountReauthenticationUtils.java",
    "java/src/org/chromium/components/signin/AccountRenameChecker.java",
    "java/src/org/chromium/components/signin/AccountRestrictionPatternReceiver.java",
    "java/src/org/chromium/components/signin/AccountUtils.java",
    "java/src/org/chromium/components/signin/AccountsChangeObserver.java",
    "java/src/org/chromium/components/signin/AuthException.java",
    "java/src/org/chromium/components/signin/ChildAccountInfoFetcher.java",
    "java/src/org/chromium/components/signin/ConnectionRetry.java",
    "java/src/org/chromium/components/signin/PatternMatcher.java",
    "java/src/org/chromium/components/signin/SigninFeatureMap.java",
    "java/src/org/chromium/components/signin/SystemAccountManagerDelegate.java",
    "java/src/org/chromium/components/signin/base/AccountCapabilities.java",
    "java/src/org/chromium/components/signin/base/AccountInfo.java",
    "java/src/org/chromium/components/signin/base/CoreAccountId.java",
    "java/src/org/chromium/components/signin/base/CoreAccountInfo.java",
    "java/src/org/chromium/components/signin/base/GoogleServiceAuthError.java",
    "java/src/org/chromium/components/signin/identitymanager/AccountInfoService.java",
    "java/src/org/chromium/components/signin/identitymanager/AccountInfoServiceImpl.java",
    "java/src/org/chromium/components/signin/identitymanager/AccountInfoServiceProvider.java",
    "java/src/org/chromium/components/signin/identitymanager/AccountTrackerService.java",
    "java/src/org/chromium/components/signin/identitymanager/IdentityManager.java",
    "java/src/org/chromium/components/signin/identitymanager/IdentityMutator.java",
    "java/src/org/chromium/components/signin/identitymanager/PrimaryAccountChangeEvent.java",
    "java/src/org/chromium/components/signin/identitymanager/ProfileOAuth2TokenServiceDelegate.java",
  ]

  # Add the actual implementation where necessary so that downstream targets
  # can provide their own implementations.
  jar_excluded_patterns = [ "*/AccountEmailDomainDisplayability.class" ]
}

# Expose Signin feature flags to Java code
java_cpp_features("signin_java_features_srcjar") {
  visibility = [ ":*" ]
  sources = [ "//components/signin/public/base/signin_switches.cc" ]
  template = "java/src/org/chromium/components/signin/SigninFeatures.java.tmpl"
}

android_library("account_email_domain_displayability_java") {
  deps = [ ":java" ]
  sources = [ "java/src/org/chromium/components/signin/AccountEmailDomainDisplayability.java" ]
}

generate_jni("jni_headers") {
  namespace = "signin"
  sources = [
    "java/src/org/chromium/components/signin/AccountCapabilitiesFetcher.java",
    "java/src/org/chromium/components/signin/ChildAccountInfoFetcher.java",
    "java/src/org/chromium/components/signin/SigninFeatureMap.java",
    "java/src/org/chromium/components/signin/base/AccountCapabilities.java",
    "java/src/org/chromium/components/signin/base/AccountInfo.java",
    "java/src/org/chromium/components/signin/base/CoreAccountId.java",
    "java/src/org/chromium/components/signin/base/CoreAccountInfo.java",
    "java/src/org/chromium/components/signin/base/GoogleServiceAuthError.java",
    "java/src/org/chromium/components/signin/identitymanager/AccountTrackerService.java",
    "java/src/org/chromium/components/signin/identitymanager/IdentityManager.java",
    "java/src/org/chromium/components/signin/identitymanager/IdentityMutator.java",
    "java/src/org/chromium/components/signin/identitymanager/PrimaryAccountChangeEvent.java",
    "java/src/org/chromium/components/signin/identitymanager/ProfileOAuth2TokenServiceDelegate.java",
  ]
}

java_cpp_template("account_capabilities_constants_javagen") {
  sources = [ "java_templates/AccountCapabilitiesConstants.template" ]
  inputs = [
    "//components/signin/internal/identity_manager/account_capabilities_list.h",
  ]
}

java_cpp_enum("tribool_javagen") {
  sources = [ "//components/signin/public/identity_manager/tribool.h" ]
}

generate_jni("test_support_jni_headers") {
  namespace = "signin"
  sources = [
    "java/src/org/chromium/components/signin/test/util/AccountCapabilitiesFetcherTestUtil.java",
    "java/src/org/chromium/components/signin/test/util/AccountManagerFacadeUtil.java",
  ]
}

android_library("signin_java_test_support") {
  testonly = true
  deps = [
    ":java",
    ":signin_test_resources",
    ":test_support_jni_headers",
    "//base:base_java",
    "//base:base_java_test_support",
    "//content/public/test/android:content_java_test_support",
    "//third_party/android_deps:guava_android_java",
    "//third_party/androidx:androidx_annotation_annotation_java",
    "//third_party/jni_zero:jni_zero_java",
    "//third_party/mockito:mockito_java",
  ]
  sources = [
    "java/src/org/chromium/components/signin/test/util/AccountCapabilitiesBuilder.java",
    "java/src/org/chromium/components/signin/test/util/AccountCapabilitiesFetcherTestUtil.java",
    "java/src/org/chromium/components/signin/test/util/AccountHolder.java",
    "java/src/org/chromium/components/signin/test/util/AccountManagerFacadeUtil.java",
    "java/src/org/chromium/components/signin/test/util/FakeAccountInfoService.java",
    "java/src/org/chromium/components/signin/test/util/FakeAccountManagerDelegate.java",
    "java/src/org/chromium/components/signin/test/util/FakeAccountManagerFacade.java",
  ]
  resources_package = "org.chromium.components.signin.test.util"
}

android_resources("java_resources") {
  sources = [
    "java/res/drawable/account_circle.xml",
    "java/res/drawable/logo_avatar_anonymous.xml",
  ]
}

android_resources("signin_test_resources") {
  testonly = true
  sources = [ "javatests/res/drawable/test_profile_picture.xml" ]
}

android_library("unit_device_javatests") {
  testonly = true
  deps = [
    ":java",
    ":signin_java_test_support",
    "//base:base_java",
    "//base:base_java_test_support",
    "//content/public/test/android:content_java_test_support",
    "//third_party/androidx:androidx_test_monitor_java",
    "//third_party/androidx:androidx_test_runner_java",
    "//third_party/jni_zero:jni_zero_java",
    "//third_party/junit",
    "//third_party/mockito:mockito_java",
  ]

  sources = [
    "java/src/org/chromium/components/signin/AccountManagerFacadeTest.java",
    "java/src/org/chromium/components/signin/identitymanager/ProfileOAuth2TokenServiceDelegateTest.java",
  ]
}

robolectric_library("junit") {
  sources = [
    "junit/src/org/chromium/components/signin/AccountManagerFacadeImplTest.java",
    "junit/src/org/chromium/components/signin/AccountReauthenticationUtilsTest.java",
    "junit/src/org/chromium/components/signin/AccountRenameCheckerTest.java",
    "junit/src/org/chromium/components/signin/AccountUtilsTest.java",
    "junit/src/org/chromium/components/signin/PatternMatcherTest.java",
    "junit/src/org/chromium/components/signin/SystemAccountManagerDelegateTest.java",
    "junit/src/org/chromium/components/signin/base/AccountCapabilitiesTest.java",
    "junit/src/org/chromium/components/signin/identitymanager/AccountInfoServiceImplTest.java",
    "junit/src/org/chromium/components/signin/identitymanager/AccountTrackerServiceTest.java",
  ]
  deps = [
    ":java",
    ":signin_java_test_support",
    "$google_play_services_package:google_play_services_auth_base_java",
    "//base:base_java",
    "//base:base_java_test_support",
    "//base:base_java_test_support_uncommon",
    "//base:base_junit_test_support",
    "//components/externalauth/android:google_delegate_public_impl_java",
    "//components/externalauth/android:java",
    "//testing/android/junit:junit_test_support",
    "//third_party/android_deps:guava_android_java",
    "//third_party/androidx:androidx_annotation_annotation_java",
    "//third_party/androidx:androidx_test_rules_java",
    "//third_party/jni_zero:jni_zero_java",
    "//third_party/junit",
    "//third_party/mockito:mockito_java",
  ]
}
