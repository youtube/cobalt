# Copyright 2024 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(is_win || is_mac || is_linux || is_chromeos)

source_set("importer") {
  sources = [
    "external_process_importer_client.h",
    "external_process_importer_host.h",
    "firefox_profile_lock.h",
    "importer_list.h",
    "importer_lock_dialog.h",
    "importer_progress_observer.h",
    "importer_uma.h",
    "in_process_importer_bridge.h",
    "profile_writer.h",
  ]

  public_deps = [
    "//base",
    "//chrome/app:generated_resources",
    "//chrome/common/importer",
    "//chrome/common/importer:interfaces",
    "//components/bookmarks/browser",
    "//components/favicon_base",
    "//components/history/core/browser",
    "//components/search_engines",
    "//mojo/public/cpp/bindings",
    "//ui/gfx",
    "//url",
  ]
}

source_set("impl") {
  public_deps = [ "//chrome/browser:browser_public_dependencies" ]

  sources = [
    "external_process_importer_client.cc",
    "external_process_importer_host.cc",
    "firefox_profile_lock.cc",
    "importer_list.cc",
    "importer_uma.cc",
    "in_process_importer_bridge.cc",
    "profile_writer.cc",
  ]

  deps = [
    ":importer",
    "//chrome/browser/favicon",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/search_engines",
    "//chrome/common/importer:interfaces",
    "//components/bookmarks/browser",
    "//components/bookmarks/common",
    "//components/favicon/core",
    "//components/password_manager/core/browser:password_form",
    "//components/password_manager/core/browser/password_store:password_store_interface",
  ]

  if (is_win) {
    sources += [ "firefox_profile_lock_win.cc" ]
  } else {
    sources += [ "firefox_profile_lock_posix.cc" ]
  }
}

source_set("browser_tests") {
  testonly = true
  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
  sources = [
    "importer_unittest_utils.cc",
    "importer_unittest_utils.h",
  ]

  deps = [
    "//base",
    "//chrome/common/importer",
    "//testing/gtest",
  ]

  if (is_win) {
    sources += [
      "edge_importer_browsertest_win.cc",
      "ie_importer_browsertest_win.cc",
    ]

    deps += [
      ":importer",
      "//chrome/browser/ui",
      "//chrome/common:constants",
      "//chrome/test:test_support",
      "//chrome/test:test_support_ui",
      "//components/favicon_base",
      "//components/search_engines",
      "//content/test:test_support",
      "//third_party/zlib/google:compression_utils",
    ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "firefox_profile_lock_unittest.cc",
    "profile_writer_unittest.cc",
  ]

  deps = [
    ":importer",
    "//base",
    "//base/test:test_support",
    "//chrome/browser",
    "//chrome/browser/search_engines",
    "//chrome/common:constants",
    "//chrome/common/importer",
    "//chrome/test:test_support",
    "//components/bookmarks/browser",
    "//components/bookmarks/test",
    "//components/history/core/browser",
    "//components/password_manager/core/browser:test_support",
    "//components/password_manager/core/browser/password_store:test_support",
    "//components/password_manager/core/common",
    "//components/prefs",
    "//components/query_parser",
    "//content/test:test_support",
    "//testing/gtest",
  ]
}
