# Copyright 2021 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//chrome/common/features.gni")
import("//ui/webui/resources/tools/build_webui.gni")

assert(!is_android)

build_webui("build") {
  grd_prefix = "feedback"

  static_files = [
    "feedback.html",
    "html/autofill_metadata_info.html",
    "html/system_info.html",
    "images/2x/button_butter_bar_close_hover.png",
    "images/2x/button_butter_bar_close.png",
    "images/2x/button_butter_bar_close_pressed.png",
    "images/button_butter_bar_close_hover.png",
    "images/button_butter_bar_close.png",
    "images/button_butter_bar_close_pressed.png",
  ]

  non_web_component_files = [
    "app.html.ts",
    "app.ts",
    "autofill_metadata_app.html.ts",
    "autofill_metadata_app.ts",
    "js/feedback_browser_proxy.ts",
    "js/feedback_regexes.ts",
    "js/feedback_util.ts",
    "js/questionnaire.ts",
    "js/take_screenshot.ts",
    "system_info_app.html.ts",
    "system_info_app.ts",
  ]

  css_files = [
    "app.css",
    "css/about_sys.css",
    "feedback_shared_styles.css",
    "feedback_shared_vars.css",
  ]

  if (is_chromeos_ash) {
    non_web_component_files += [ "js/jelly_colors.ts" ]
  }

  ts_composite = true
  ts_definitions = [
    "//tools/typescript/definitions/feedback_private.d.ts",
    "//tools/typescript/definitions/chrome_send.d.ts",
  ]
  ts_deps = [
    "//chrome/browser/resources/key_value_pair_viewer_shared:build_ts",
    "//third_party/lit/v3_0:build_ts",
    "//ui/webui/resources/cr_elements:build_ts",
    "//ui/webui/resources/js:build_ts",
  ]
  if (is_chromeos_ash) {
    ts_deps +=
        [ "//ui/webui/resources/cr_components/color_change_listener:build_ts" ]
  }
  ts_path_mappings = [ "/shared/key_value_pair_viewer/*|" + rebase_path(
                           "$root_gen_dir/chrome/browser/resources/key_value_pair_viewer_shared/tsc/*",
                           target_gen_dir) ]
  webui_context_type = "trusted"
}
