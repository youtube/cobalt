# Copyright 2024 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chrome_build.gni")
import("//chrome/process_version_rc_template.gni")
import("//testing/test.gni")

executable("elevated_tracing_service") {
  sources = [
    "elevated_tracing_service.cc",
    "elevated_tracing_service.rc",
  ]

  configs -= [
    "//build/config/compiler:cet_shadow_stack",
    "//build/config/win:console",
  ]
  configs += [ "//build/config/win:windowed" ]

  deps = [
    ":lib",
    ":service_lib",
    ":version_resources",
    "//build/win:default_exe_manifest",
    "//chrome/windows_services/service_program",
    "//components/tracing:tracing_service_idl",
    "//mojo/core/embedder",
  ]
}

source_set("lib") {
  visibility = [ ":*" ]

  sources = [
    "process_watcher.cc",
    "process_watcher.h",
    "session_registry.cc",
    "session_registry.h",
    "system_tracing_session.cc",
    "system_tracing_session.h",
  ]

  deps = [
    "//base/version_info:channel",
    "//base/version_info:version_info",
    "//chrome/install_static:install_static_util",
    "//chrome/windows_services/service_program:common",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/platform",
    "//mojo/public/cpp/system",
    "//services/tracing/public/cpp",
    "//services/tracing/public/cpp:traced_process",
    "//services/tracing/public/mojom",
    "//third_party/abseil-cpp:absl",
  ]

  public_deps = [
    "//base",
    "//components/tracing:tracing_service_idl",
  ]

  libs = [ "rpcrt4.lib" ]
}

source_set("service_lib") {
  visibility = [ ":*" ]

  public = [ "elevated_tracing_service_delegate.h" ]
  public_deps = [
    "//base",
    "//chrome/windows_services/service_program:delegate",
  ]
  sources = [ "elevated_tracing_service_delegate.cc" ]
  configs -= [ "//build/config/win:winver" ]
  configs += [
    "//chrome/windows_services/service_program:classic_com",
    "//chrome/windows_services/service_program:winver",
  ]
  deps = [
    ":lib",
    "//chrome/common/win:eventlog_provider",
    "//chrome/install_static:install_static_util",
    "//services/tracing/public/cpp",
  ]
}

process_version_rc_template("version_resources") {
  sources = [ "elevated_tracing_service_exe.ver" ]
  output = "$target_gen_dir/elevated_tracing_service_exe.rc"
}

test("elevated_tracing_service_unittests") {
  sources = [
    "elevated_tracing_service_unittest.cc",
    "process_watcher_unittest.cc",
    "run_all_unittests.cc",
    "session_registry_unittest.cc",
    "system_tracing_session_unittest.cc",
    "with_child_test.cc",
    "with_child_test.h",
  ]

  deps = [
    ":lib",
    ":service_lib",
    "//base",
    "//base/test:test_support",
    "//chrome/common/win:eventlog_provider",
    "//chrome/install_static:install_static_util",
    "//chrome/install_static/test:test_support",
    "//chrome/windows_services/service_program",
    "//chrome/windows_services/service_program:common",
    "//chrome/windows_services/service_program:lib",
    "//chrome/windows_services/service_program/test_support",
    "//chrome/windows_services/service_program/test_support:unit_tests",
    "//components/tracing:tracing_service_idl",
    "//mojo/core/embedder",
    "//mojo/public/cpp/platform",
    "//mojo/public/cpp/system",
    "//services/tracing/public/mojom",
    "//testing/gtest",
    "//third_party/abseil-cpp:absl",
  ]

  data_deps = [ ":elevated_tracing_service" ]
}
