# Copyright 2017 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(is_apple)

import("//testing/test.gni")

source_set("metal_util_types") {
  sources = [ "types.h" ]
}

component("metal_util") {
  output_name = "metal"

  defines = [ "METAL_UTIL_IMPLEMENTATION" ]

  sources = [
    "device.h",
    "device.mm",
    "hdr_copier_layer.h",
    "hdr_copier_layer.mm",
    "metal_util_export.h",
  ]

  public_deps = [ ":metal_util_types" ]

  deps = [
    "//base",
    "//components/crash/core/common:crash_key",
    "//skia:skcms",
    "//ui/gfx",
  ]

  frameworks = [
    "IOSurface.framework",
    "QuartzCore.framework",
  ]

  weak_frameworks = [
    "Metal.framework",
    "MetalKit.framework",
  ]

  if (is_mac) {
    sources += [
      "device_removal.h",
      "device_removal.mm",
    ]
    frameworks += [ "Cocoa.framework" ]
  }
}
