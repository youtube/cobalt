# Copyright 2017 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/compiled_action.gni")

# Generates a proto file based on the real list.
action_foreach("generate_preload_list") {
  script = "//tools/media_engagement_preload/make_dafsa.py"

  sources = [ "test.json" ]
  outputs = [ "$target_gen_dir/{{source_name_part}}.pb" ]

  # The first argument is the build directory.
  args = [
    ".",
    "{{source}}",
    rebase_path("${target_gen_dir}/{{source_name_part}}.pb", root_build_dir),
  ]

  deps = [
    "//chrome/browser/media:media_engagement_preload_proto",
    "//third_party/protobuf:py_proto",
  ]
}

# Copy over protobuf files to output directory.
copy("test_data") {
  sources = [
    "bad_format.pb",
    "empty.pb",
  ]

  outputs = [ "$target_gen_dir/{{source_file_part}}" ]
}
