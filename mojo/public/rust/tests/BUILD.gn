# Copyright 2023 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/rust.gni")
import("//build/rust/rust_static_library.gni")
import("//mojo/public/interfaces/bindings/tests/validation_data_files.gni")
import("//mojo/public/tools/bindings/mojom.gni")

rust_static_library("mojo_rust_system_tests") {
  testonly = true
  is_gtest_unittests = true
  allow_unsafe = true
  crate_root = "system/lib.rs"
  sources = [ "system/lib.rs" ]
  deps = [
    ":c_test_support",
    ":test_util",
    "//mojo/public/rust:mojo",
    "//testing/rust_gtest_interop",
    "//third_party/rust/lazy_static/v1:lib",
  ]

  # This should be removed; we should not rely on unstable features. But it's in
  # experimental code and won't block compiler rolls. If it becomes a problem
  # just disable the whole build target.
  configs -= [ "//build/config/compiler:disallow_unstable_features" ]
  rustflags = [ "-Zallow-features=maybe_uninit_write_slice,assert_matches" ]
}

if (enable_rust_mojom_bindings) {
  rust_static_library("mojo_rust_bindings_tests") {
    testonly = true
    is_gtest_unittests = true
    crate_root = "bindings/lib.rs"
    sources = [
      "bindings/lib.rs",
      "bindings/validation.rs",
    ]

    inputs = []
    foreach(f, mojo_public_interfaces_bindings_test_validation_data_files) {
      inputs += [ get_path_info(
              rebase_path(f, ".", "//mojo/public/interfaces/bindings/tests/"),
              "abspath") ]
    }

    deps = [
      ":test_util",
      "//mojo/public/interfaces/bindings/tests:test_interfaces_rust",
      "//mojo/public/interfaces/bindings/tests:test_mojom_import_rust",
      "//mojo/public/rust:mojo_bindings",
      "//testing/rust_gtest_interop",
      "//third_party/rust/bytemuck/v1:lib",
    ]
  }
}

rust_static_library("test_util") {
  testonly = true
  allow_unsafe = true
  crate_root = "util/lib.rs"
  edition = "2021"

  sources = [ "util/lib.rs" ]
  cxx_bindings = [ "util/lib.rs" ]
  deps = [
    ":c_test_support",
    "//testing/rust_gtest_interop",
  ]
}

static_library("c_test_support") {
  testonly = true
  sources = [
    "test_support.cc",
    "test_support.h",
  ]
  deps = [
    "//base",
    "//mojo/core/embedder",
    "//mojo/public/cpp/bindings/tests:mojo_public_bindings_test_utils",
  ]
  visibility = [ ":*" ]
}
