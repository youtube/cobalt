# Copyright 2019 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/config.gni")
import("//components/spellcheck/spellcheck_build_features.gni")
import("//weblayer/variables.gni")

source_set("lib") {
  sources = [
    "aw_main_delegate.cc",
    "aw_main_delegate.h",
    "webview_jni_onload.cc",
    "webview_jni_onload.h",
  ]

  deps = [
    "//android_webview/browser",
    "//android_webview/browser/gfx",
    "//android_webview/common",
    "//android_webview/gpu",
    "//android_webview/js_sandbox/service:js_sandbox",
    "//android_webview/renderer",
    "//base",
    "//cc/base",
    "//components/autofill/core/common",
    "//components/crash/core/common",
    "//components/embedder_support:embedder_support",
    "//components/gwp_asan/common:crash_keys",
    "//components/memory_system",
    "//components/metrics",
    "//components/power_scheduler",
    "//components/safe_browsing/android:safe_browsing_api_handler",
    "//components/services/heap_profiling/public/cpp",
    "//components/spellcheck:buildflags",
    "//components/translate/core/common",
    "//components/variations",
    "//components/version_info",
    "//components/version_info/android:channel_getter",
    "//components/viz/common",
    "//content/public/app",
    "//content/public/browser",
    "//content/public/common",
    "//content/public/common:content_descriptor_keys",
    "//device/base:base",
    "//gin",
    "//gpu/command_buffer/service",
    "//gpu/config",
    "//media",
    "//media:media_buildflags",
    "//third_party/blink/public/common:headers",
    "//tools/v8_context_snapshot:buildflags",
    "//ui/base",
    "//ui/events:gesture_detection",
  ]

  if (enable_spellcheck) {
    deps += [ "//components/spellcheck/common" ]
  }
}

source_set("webview_entry_point") {
  deps = [
    ":lib",
    "//base",
  ]
  sources = [ "webview_entry_point.cc" ]
  defines = []

  if (allow_jni_multiplexing) {
    defines += [ "JNI_REGISTRATION_REQUIRED" ]
    deps += [ "//android_webview:webview_jni_registration($default_toolchain)" ]
  }

  if (webview_includes_weblayer) {
    defines += [ "WEBVIEW_INCLUDES_WEBLAYER" ]
    deps += [ "//weblayer:weblayer_lib" ]
  }
}
