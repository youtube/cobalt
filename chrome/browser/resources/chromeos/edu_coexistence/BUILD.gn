# Copyright 2020 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
import("//ui/webui/resources/tools/build_webui.gni")

build_webui("build") {
  grd_prefix = "edu_coexistence"
  grd_resource_path_prefix = "edu_coexistence"
  static_files = [ "edu_coexistence.html" ]
  web_component_files = [
    "edu_coexistence_app.ts",
    "edu_coexistence_button.ts",
    "edu_coexistence_error.ts",
    "edu_coexistence_offline.ts",
    "edu_coexistence_template.ts",
    "edu_coexistence_ui.ts",
  ]
  ts_files = [
    "edu_coexistence_browser_proxy.ts",
    "edu_coexistence_controller.ts",
  ]
  css_files = [ "common.css" ]
  ts_composite = true
  ts_definitions = [
    "//chrome/browser/resources/gaia_auth_host/authenticator.d.ts",
    "//chrome/browser/resources/gaia_auth_host/saml_password_attributes.d.ts",
    "//tools/typescript/definitions/chrome_event.d.ts",
    "//tools/typescript/definitions/chrome_send.d.ts",
    "//tools/typescript/definitions/context_menus.d.ts",
    "//tools/typescript/definitions/extension_types.d.ts",
    "//tools/typescript/definitions/tabs.d.ts",
    "//tools/typescript/definitions/web_request.d.ts",
    "//tools/typescript/definitions/webview_tag.d.ts",
  ]
  ts_deps = [
    "//ash/webui/common/resources:build_ts",
    "//ash/webui/common/resources/cr_elements:build_ts",
    "//chrome/browser/resources/chromeos/gaia_action_buttons:build_ts",
    "//chrome/browser/resources/chromeos/supervision:build_ts",
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources/js:build_ts",
    "//ui/webui/resources/mojo:build_ts",
  ]
  ts_path_mappings = [
    "chrome://chrome-signin/gaia_auth_host/*|" +
        rebase_path("//chrome/browser/resources/gaia_auth_host/*",
                    target_gen_dir),
    "chrome://chrome-signin/gaia_action_buttons/gaia_action_buttons.js|" + rebase_path(
            "${root_gen_dir}/chrome/browser/resources/chromeos/gaia_action_buttons/tsc/gaia_action_buttons.d.ts",
            target_gen_dir),
    "/supervision/*|" + rebase_path(
            "${root_gen_dir}/chrome/browser/resources/chromeos/supervision/tsc/*",
            target_gen_dir),
  ]
  webui_context_type = "trusted"
}
