# Copyright 2025 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")

assert(is_android)

source_set("tab_model") {
  public = [
    "android_live_tab_context.h",
    "tab_collection_tab_model_impl.h",
    "tab_model.h",
    "tab_model_jni_bridge.h",
    "tab_model_list.h",
    "tab_model_list_observer.h",
    "tab_model_observer.h",
    "tab_model_observer_jni_bridge.h",
  ]
  public_deps = [
    "//base",
    "//build:android_buildflags",
    "//chrome/browser/flags:chrome_browser_flags_enums_srcjar",
    "//chrome/browser/flags:flags_android",
    "//chrome/browser/ui/tabs:tab_enums",
    "//chrome/browser/ui/tabs:tab_list_interface",
    "//chrome/browser/ui/tabs:utils",
    "//components/omnibox/browser:location_bar",
    "//components/sessions",
    "//components/sync_sessions",
    "//components/tab_groups",
    "//components/tabs:public",
    "//components/tabs/public/android",
    "//ui/base/unowned_user_data",
    "//url",
  ]
}

source_set("impl") {
  sources = [
    "android_live_tab_context.cc",
    "android_live_tab_context_wrapper.cc",
    "android_live_tab_context_wrapper.h",
    "tab_collection_tab_model_impl.cc",
    "tab_model.cc",
    "tab_model_jni_bridge.cc",
    "tab_model_list.cc",
    "tab_model_observer.cc",
    "tab_model_observer_jni_bridge.cc",
  ]

  public_deps = [ "//chrome/browser:browser_public_dependencies" ]

  deps = [
    ":tab_model",
    "//chrome/android:chrome_jni_headers",
    "//chrome/browser:browser_process",
    "//chrome/browser/content_settings:content_settings_factory",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/sessions",
    "//chrome/browser/sync",
    "//chrome/browser/tab",
    "//chrome/browser/ui:browser_navigator_params_headers",
    "//components/content_settings/core/browser",
    "//content/public/browser",
    "//content/public/common",
    "//ui/base:types",
  ]

  if (is_desktop_android) {
    deps += [
      "//chrome/browser/ui/browser_window",
      "//chrome/browser/ui/browser_window/internal",
    ]
  }
}

java_cpp_enum("enums_java") {
  sources = [ "tab_model.h" ]
}

source_set("test_support") {
  testonly = true
  sources = [
    "tab_model_test_helper.cc",
    "tab_model_test_helper.h",
  ]
  public_deps = [
    ":tab_model",
    "//chrome/browser",
    "//content/test:test_support",
    "//ui/base/unowned_user_data",
  ]
  deps = [
    "//chrome/browser/flags:flags_android",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/ui/android/tab_model",
  ]

  if (is_desktop_android) {
    deps += [ "//chrome/browser/ui/browser_window" ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [ "tab_model_list_unittest.cc" ]
  deps = [
    ":test_support",
    "//chrome/test:test_support",
    "//content/public/browser",
  ]
}
