# Copyright 2023 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
import("//testing/test.gni")
import("//third_party/protobuf/proto_library.gni")

executable("evaluate_seed") {
  sources = [ "evaluate_seed_main.cc" ]
  deps = [
    ":evaluate_seed_lib",
    "//base",
  ]
  assert_no_deps = [ "//chrome " ]
}

source_set("evaluate_seed_lib") {
  sources = [
    "cros_safe_seed_manager.cc",
    "cros_safe_seed_manager.h",
    "cros_variations_field_trial_creator.cc",
    "cros_variations_field_trial_creator.h",
    "early_boot_enabled_state_provider.cc",
    "early_boot_enabled_state_provider.h",
    "early_boot_safe_seed.cc",
    "early_boot_safe_seed.h",
    "evaluate_seed.cc",
    "evaluate_seed.h",
  ]
  deps = [
    "//base",
    "//build:branding_buildflags",
    "//chromeos/ash/components/dbus/featured:proto",
    "//chromeos/crosapi/cpp",
    "//chromeos/crosapi/cpp:crosapi_constants",
    "//components/variations",
    "//components/variations/proto",
    "//components/variations/service",
  ]

  # restrict to only other targets in this BUILD.gn file.
  visibility = [ ":*" ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "cros_safe_seed_manager_unittest.cc",
    "early_boot_enabled_state_provider_unittest.cc",
    "early_boot_safe_seed_unittest.cc",
    "evaluate_seed_unittest.cc",
  ]
  deps = [
    ":evaluate_seed_lib",
    "//base",
    "//base/test:test_support",
    "//build:branding_buildflags",
    "//build/config/chromebox_for_meetings:buildflags",
    "//chromeos/ash/components/dbus/featured:proto",
    "//components/prefs:test_support",
    "//components/test:test_support",
    "//components/variations",
    "//components/variations/service:service",
    "//testing/gmock",
    "//testing/gtest",
    "//third_party/zlib/google:compression_utils",
  ]
}

test("variations_cros_unittests") {
  sources = [ "//components/test/run_all_unittests.cc" ]
  deps = [
    ":unit_tests",
    "//components/test:test_support",
  ]
}
