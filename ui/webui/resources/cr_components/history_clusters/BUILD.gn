# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")
import("//ui/webui/resources/tools/build_webui.gni")

assert(!is_android)

mojom("mojo_bindings") {
  sources = [ "history_clusters.mojom" ]
  public_deps = [
    "//components/history_clusters/public/mojom:mojo_bindings",
    "//mojo/public/mojom/base",
    "//ui/base/mojom",
    "//ui/gfx/geometry/mojom",
    "//url/mojom:url_mojom_gurl",
  ]
  webui_module_path = ""
}

build_webui("build") {
  grd_prefix = "cr_components_history_clusters"

  web_component_files = [
    "cluster.ts",
    "clusters.ts",
    "cluster_menu.ts",
    "horizontal_carousel.ts",
    "page_favicon.ts",
    "search_query.ts",
    "url_visit.ts",
  ]

  non_web_component_files = [
    "browser_proxy.ts",
    "metrics_proxy.ts",
    "utils.ts",
  ]

  css_files = [
    "history_clusters_shared_style.css",
    "shared_vars.css",
  ]

  static_files = [ "hide_source_gm_grey_24dp.svg" ]

  mojo_files_deps = [
    ":mojo_bindings_ts__generator",
    "//components/history_clusters/public/mojom:mojo_bindings_ts__generator",
  ]
  mojo_files = [
    "$root_gen_dir/components/history_clusters/public/mojom/history_cluster_types.mojom-webui.ts",
    "$target_gen_dir/history_clusters.mojom-webui.ts",
  ]

  ts_out_dir =
      "$root_gen_dir/ui/webui/resources/tsc/cr_components/history_clusters"
  ts_definitions = [ "//tools/typescript/definitions/metrics_private.d.ts" ]
  ts_composite = true
  ts_deps = [
    "../page_image_service:build_ts",
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources/cr_elements:build_ts",
    "//ui/webui/resources/js:build_ts",
    "//ui/webui/resources/mojo:build_ts",
  ]
  generate_grdp = true
  grd_resource_path_prefix = rebase_path(".", "//ui/webui/resources")
}
