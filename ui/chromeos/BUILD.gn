# Copyright 2014 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//build/config/ui.gni")
import("//testing/test.gni")

component("chromeos") {
  output_name = "ui_chromeos"
  sources = [
    "devicetype_utils.cc",
    "devicetype_utils.h",
  ]
  defines = [ "UI_CHROMEOS_IMPLEMENTATION" ]
  deps = [
    "//base",
    "//build:chromeos_buildflags",
    "//chromeos/constants",
    "//services/device/public/mojom",
    "//ui/base",
    "//ui/chromeos/strings",
  ]

  if (is_chromeos_ash) {
    sources += [
      "shill_error.cc",
      "shill_error.h",
      "user_activity_power_manager_notifier.cc",
      "user_activity_power_manager_notifier.h",
    ]
    deps += [
      "//ash/constants",
      "//chromeos/ash/components/network",
      "//chromeos/dbus/power",
      "//chromeos/dbus/power:power_manager_proto",
      "//components/device_event_log",
      "//mojo/public/cpp/bindings",
      "//services/device/public/mojom",
      "//services/service_manager/public/cpp",
      "//ui/events",
      "//ui/events:gesture_detection",
      "//ui/events/devices:devices",
    ]
  }
}

test("ui_chromeos_unittests") {
  use_xvfb = use_xvfb_in_this_config

  sources = [ "run_all_unittests.cc" ]
  deps = [
    ":chromeos",
    "//base/test:test_support",
    "//build:chromeos_buildflags",
    "//mojo/core/embedder",
    "//skia",
    "//testing/gtest",
    "//ui/aura:test_support",
    "//ui/compositor",
    "//ui/events:test_support",
    "//ui/gl:test_support",
    "//ui/message_center",
    "//ui/views",
    "//ui/views:test_support",
  ]
  data = [ "$root_out_dir/locales/en-US.pak" ]
  data_deps = [ "//ui/resources:ui_test_pak_data" ]
}
