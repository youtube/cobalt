# Copyright 2017 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")

mojom("mojom") {
  generate_java = true

  sources = [
    "display.mojom",
    "display_color_management.mojom",
    "display_configuration_params.mojom",
    "display_constants.mojom",
    "display_layout.mojom",
    "display_mode.mojom",
    "display_snapshot.mojom",
    "gamma_ramp_rgb_entry.mojom",
    "screen_info.mojom",
    "screen_infos.mojom",
    "screen_orientation.mojom",
  ]

  public_deps = [
    "//mojo/public/mojom/base",
    "//skia/public/mojom",
    "//ui/gfx/geometry/mojom",
    "//ui/gfx/mojom",
  ]

  shared_cpp_typemaps = [
    {
      types = [
        {
          mojom = "display.mojom.Rotation"
          cpp = "::display::Display::Rotation"
        },
      ]
      traits_headers = [ "display_mojom_traits.h" ]
      traits_public_deps = [ ":shared_mojom_traits" ]
    },
    {
      types = [
        {
          mojom = "display.mojom.ScreenInfo"
          cpp = "::display::ScreenInfo"
        },
      ]
      traits_headers = [ "screen_info_mojom_traits.h" ]
      traits_public_deps = [ ":shared_mojom_traits" ]
    },
    {
      types = [
        {
          mojom = "display.mojom.ScreenInfos"
          cpp = "::display::ScreenInfos"
        },
      ]
      traits_headers = [ "screen_infos_mojom_traits.h" ]
      traits_public_deps = [ ":shared_mojom_traits" ]
    },
  ]

  cpp_typemaps = [
    {
      types = [
        {
          mojom = "display.mojom.Display"
          cpp = "::display::Display"
        },
        {
          mojom = "display.mojom.TouchSupport"
          cpp = "::display::Display::TouchSupport"
        },
        {
          mojom = "display.mojom.AccelerometerSupport"
          cpp = "::display::Display::AccelerometerSupport"
        },
      ]
      traits_headers = [ "display_mojom_traits.h" ]
      traits_public_deps = [ ":shared_mojom_traits" ]
    },
    {
      types = [
        {
          mojom = "display.mojom.DisplayConnectionType"
          cpp = "::display::DisplayConnectionType"
        },
        {
          mojom = "display.mojom.HDCPState"
          cpp = "::display::HDCPState"
        },
        {
          mojom = "display.mojom.ContentProtectionMethod"
          cpp = "::display::ContentProtectionMethod"
        },
        {
          mojom = "display.mojom.PrivacyScreenState"
          cpp = "::display::PrivacyScreenState"
        },
        {
          mojom = "display.mojom.VariableRefreshRateState"
          cpp = "::display::VariableRefreshRateState"
        },
        {
          mojom = "display.mojom.ModesetFlags"
          cpp = "::display::ModesetFlags"
          copyable_pass_by_value = true
        },
      ]
      traits_sources = [ "display_constants_mojom_traits.cc" ]
      traits_headers = [ "display_constants_mojom_traits.h" ]
      traits_public_deps = [ "//ui/display" ]
    },
    {
      types = [
        {
          mojom = "display.mojom.Position"
          cpp = "::display::DisplayPlacement::Position"
        },
        {
          mojom = "display.mojom.OffsetReference"
          cpp = "::display::DisplayPlacement::OffsetReference"
        },
        {
          mojom = "display.mojom.DisplayPlacement"
          cpp = "::display::DisplayPlacement"
        },
        {
          mojom = "display.mojom.DisplayLayout"
          cpp = "::std::unique_ptr<::display::DisplayLayout>"
          move_only = true
        },
      ]
      traits_sources = [ "display_layout_mojom_traits.cc" ]
      traits_headers = [ "display_layout_mojom_traits.h" ]
      traits_public_deps = [ "//ui/display" ]
    },
    {
      types = [
        {
          mojom = "display.mojom.DisplayMode"
          cpp = "::std::unique_ptr<::display::DisplayMode>"
          move_only = true
          nullable_is_same_type = true
        },
      ]
      traits_sources = [ "display_mode_mojom_traits.cc" ]
      traits_headers = [ "display_mode_mojom_traits.h" ]
      traits_public_deps = [ "//ui/display" ]
      traits_deps = [ "//ui/gfx/geometry" ]
    },
    {
      types = [
        {
          mojom = "display.mojom.DisplaySnapshot"
          cpp = "::std::unique_ptr<::display::DisplaySnapshot>"
          move_only = true
        },
        {
          mojom = "display.mojom.DisplaySnapshotColorInfo"
          cpp = "::display::DisplaySnapshot::ColorInfo"
        },
      ]
      traits_sources = [ "display_snapshot_mojom_traits.cc" ]
      traits_headers = [ "display_snapshot_mojom_traits.h" ]
      traits_public_deps = [ "//ui/display" ]
      traits_deps = [ "//ui/gfx/geometry" ]
    },
    {
      types = [
        {
          mojom = "display.mojom.GammaRampRGBEntry"
          cpp = "::display::GammaRampRGBEntry"
        },
      ]
      traits_sources = [ "gamma_ramp_rgb_entry_mojom_traits.cc" ]
      traits_headers = [ "gamma_ramp_rgb_entry_mojom_traits.h" ]
      traits_public_deps = [ "//ui/display" ]
    },
    {
      types = [
        {
          mojom = "display.mojom.GammaCurve"
          cpp = "::display::GammaCurve"
        },
      ]
      traits_sources = [ "display_color_management_mojom_traits.cc" ]
      traits_headers = [ "display_color_management_mojom_traits.h" ]
      traits_public_deps = [ "//ui/display" ]
    },
    {
      types = [
        {
          mojom = "display.mojom.ColorCalibration"
          cpp = "::display::ColorCalibration"
        },
        {
          mojom = "display.mojom.ColorTemperatureAdjustment"
          cpp = "::display::ColorTemperatureAdjustment"
        },
        {
          mojom = "display.mojom.DisplayConfigurationParams"
          cpp = "::display::DisplayConfigurationParams"
        },
        {
          mojom = "display.mojom.GammaAdjustment"
          cpp = "::display::GammaAdjustment"
        },
      ]
      traits_sources = [ "display_configuration_params_mojom_traits.cc" ]
      traits_headers = [ "display_configuration_params_mojom_traits.h" ]
      traits_public_deps = [ "//ui/display" ]
      traits_deps = [ "//ui/gfx/geometry" ]
    },
  ]

  cpp_typemaps += shared_cpp_typemaps
  blink_cpp_typemaps = shared_cpp_typemaps
}

component("shared_mojom_traits") {
  output_name = "display_shared_mojom_traits"
  defines = [ "IS_DISPLAY_SHARED_MOJOM_TRAITS_IMPL" ]
  configs += [ "//build/config/compiler:wexit_time_destructors" ]
  sources = [
    "display_mojom_traits.cc",
    "display_mojom_traits.h",
    "screen_info_mojom_traits.cc",
    "screen_info_mojom_traits.h",
    "screen_infos_mojom_traits.cc",
    "screen_infos_mojom_traits.h",
  ]
  public_deps = [
    ":mojom_shared",
    "//base",
    "//ui/display",
    "//ui/gfx/mojom",
  ]
}
