# Copyright 2015 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chrome_build.gni")
import("//build/config/locales.gni")
import("//tools/grit/grit_rule.gni")

group("strings") {
  public_deps = [
    ":ios_branded_strings",
    ":ios_strings",
  ]
}

grit("ios_strings") {
  source = "ios_strings.grd"
  output_dir = "$root_gen_dir/ios/chrome"
  outputs = [ "grit/ios_strings.h" ]
  foreach(locale, locales_with_pseudolocales) {
    outputs += [ "ios_strings_$locale.pak" ]
  }
}

grit("ios_branded_strings") {
  source = "ios_${branding_path_product}_strings.grd"
  output_dir = "$root_gen_dir/ios/chrome"
  outputs = [ "grit/ios_branded_strings.h" ]
  foreach(locale, all_chrome_locales) {
    outputs += [ "ios_${branding_path_product}_strings_$locale.pak" ]
  }
}
