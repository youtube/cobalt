# Copyright 2021 The Cobalt Authors. All Rights Reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//starboard/build/platform_path.gni")

static_library("nb") {
  if (is_starboard) {
    sources = [
      "allocator.cc",
      "allocator.h",
      "first_fit_reuse_allocator.cc",
      "first_fit_reuse_allocator.h",
      "fixed_no_free_allocator.cc",
      "fixed_no_free_allocator.h",
      "pointer_arithmetic.h",
      "reuse_allocator_base.cc",
      "reuse_allocator_base.h",
      "starboard_aligned_memory_deleter.h",
      "starboard_memory_allocator.h",
    ]

    deps = [
      "//starboard:starboard_headers_only",
      "//starboard/common",
    ]

    # TODO: b/295615064 Remove nb platform specific dependency.
    if (defined(has_nb_platform) && has_nb_platform &&
        current_toolchain == starboard_toolchain) {
      deps += [ "//$starboard_path/nb:nb_platform" ]
    }
  }
}

target(gtest_target_type, "nb_test") {
  testonly = true

  if (is_starboard) {
    sources = [
      "first_fit_reuse_allocator_test.cc",
      "fixed_no_free_allocator_test.cc",
      "run_all_unittests.cc",
    ]

    deps = [
      ":nb",
      "//starboard",
      "//testing/gmock:gmock",
      "//testing/gtest",
    ]
  }
}

target(gtest_target_type, "reuse_allocator_benchmark") {
  testonly = true
  sources = [ "reuse_allocator_benchmark.cc" ]
  deps = [
    ":nb",
    ":nb_copy_test_data",
    "//starboard",
  ]
}

copy("nb_copy_test_data") {
  sources = [
    "testdata/mem_history_cobalt.txt",
    "testdata/mem_history_gpu.txt",
    "testdata/mem_history_main.txt",
  ]
  outputs = [
    "$sb_static_contents_output_data_dir/test/nb/{{source_target_relative}}",
  ]
}
