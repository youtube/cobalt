# Copyright 2023 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

declare_args() {
  # Name of the target that define the autofill fullcolor branding icon to use.
  ios_image_autofill_branding_icon_fullcolor =
      "//ios/chrome/browser/ui/autofill/resources:fullcolor_branding_icon"
}

source_set("branding") {
  sources = [
    "branding_coordinator.h",
    "branding_coordinator.mm",
    "branding_mediator.h",
    "branding_mediator.mm",
  ]
  deps = [
    ":branding_ui",
    "//components/prefs",
    "//ios/chrome/browser/shared/coordinator/chrome_coordinator",
    "//ios/chrome/browser/shared/model/application_context",
    "//ios/chrome/browser/shared/model/prefs:pref_names",
  ]
}

source_set("branding_ui") {
  sources = [
    "branding_consumer.h",
    "branding_view_controller.h",
    "branding_view_controller.mm",
    "branding_view_controller_delegate.h",
  ]
  deps = [
    "//base",
    "//ios/chrome/browser/shared/public/features",
    "//ios/chrome/browser/shared/ui/util",
    "//ios/chrome/common/ui/util",
    ios_image_autofill_branding_icon_fullcolor,
  ]
  frameworks = [ "UIKit.framework" ]
}

source_set("eg2_tests") {
  configs += [ "//build/config/ios:xctest_config" ]
  testonly = true
  sources = [ "branding_view_controller_egtest.mm" ]
  deps = [
    "//base/test:test_support",
    "//ios/chrome/browser/shared/model/prefs:pref_names",
    "//ios/chrome/browser/ui/autofill:eg_test_support+eg2",
    "//ios/chrome/test/earl_grey:eg_test_support+eg2",
    "//ios/testing/earl_grey:eg_test_support+eg2",
    "//net:test_support",
    "//ui/base",
  ]
}
