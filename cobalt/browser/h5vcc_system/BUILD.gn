# Copyright 2025 The Cobalt Authors. All Rights Reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

if (is_ios) {
  # For `target_environment` and `target_platform`.
  import("//build/config/apple/mobile_config.gni")
}

source_set("h5vcc_system") {
  sources = [
    "h5vcc_system_impl_base.cc",
    "h5vcc_system_impl_base.h",
  ]

  if (is_android) {
    sources += [ "h5vcc_system_impl_android.cc" ]
  } else if (is_starboard) {
    sources += [ "h5vcc_system_impl_starboard.cc" ]
  } else if (is_ios && target_platform == "tvos") {
    sources += [ "h5vcc_system_impl_tvos.mm" ]
  }

  deps = [
    "//base",
    "//cobalt/browser/h5vcc_system/public/mojom",
    "//content/public/browser",
    "//mojo/public/cpp/bindings",
    "//starboard:starboard_group",
    "//starboard/common",
  ]

  if (is_starboard) {
    deps += [ "//cobalt/configuration" ]
  }

  if (is_ios && target_platform == "tvos") {
    frameworks = [
      "AdSupport.framework",
      "AppTrackingTransparency.framework",
    ]
  }
}
