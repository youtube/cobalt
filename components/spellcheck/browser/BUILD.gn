# Copyright 2016 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//components/spellcheck/spellcheck_build_features.gni")

if (is_android) {
  import("//build/config/android/rules.gni")
}

source_set("browser") {
  sources = [
    "platform_spell_checker.h",
    "pref_names.cc",
    "pref_names.h",
    "spell_check_host_impl.cc",
    "spell_check_host_impl.h",
    "spellcheck_dictionary.h",
    "spellcheck_host_metrics.cc",
    "spellcheck_host_metrics.h",
    "spellcheck_platform.h",
  ]

  if (enable_spelling_service) {
    sources += [
      "spelling_service_client.cc",
      "spelling_service_client.h",
    ]
  }

  if (is_win) {
    sources += [
      "spellcheck_platform_win.cc",
      "windows_spell_checker.cc",
      "windows_spell_checker.h",
    ]
  }

  public_deps = [
    "//base",
    "//components/spellcheck:buildflags",
    "//components/spellcheck/common",
  ]
  deps = [
    "//components/prefs",
    "//components/user_prefs",
    "//content/public/browser",
    "//content/public/common",
    "//google_apis",
    "//net",
    "//services/network/public/cpp",
    "//services/network/public/mojom",
  ]

  if (is_mac) {
    sources += [ "spellcheck_platform_mac.mm" ]
  }

  if (is_android) {
    sources += [
      "spellcheck_platform_android.cc",
      "spellchecker_session_bridge_android.cc",
      "spellchecker_session_bridge_android.h",
    ]
    deps += [ "android:jni_headers" ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [ "spellcheck_host_metrics_unittest.cc" ]

  if (is_win) {
    sources += [ "windows_spell_checker_unittest.cc" ]
  }

  if (is_mac) {
    sources += [ "spellcheck_platform_mac_unittest.cc" ]
  }

  deps = [
    ":browser",
    "//base",
    "//base/test:test_support",
    "//components/spellcheck/common",
    "//components/variations",
    "//content/test:test_support",
    "//mojo/public/cpp/bindings",
    "//net:test_support",
    "//services/network:test_support",
    "//services/network/public/cpp",
    "//testing/gtest",
  ]
}
