# Copyright 2017 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//components/viz/viz.gni")
import("//mojo/public/tools/bindings/mojom.gni")

mojom_component("features") {
  sources = [ "features.mojom" ]
  output_prefix = "services_viz_privileged_features_mojom"
  macro_prefix = "SERVICES_VIZ_PRIVILEGED_FEATURES_MOJOM"
}

mojom("compositing") {
  sources = [
    "begin_frame_observer.mojom",
    "display_private.mojom",
    "external_begin_frame_controller.mojom",
    "frame_sink_manager.mojom",
    "frame_sink_manager_test_api.mojom",  # test helpers
    "frame_sink_video_capture.mojom",
    "frame_sinks_metrics_recorder.mojom",  # test helpers
    "layered_window_updater.mojom",
    "overlay_strategy.mojom",
    "renderer_settings.mojom",
    "vsync_parameter_observer.mojom",
  ]

  public_deps = [
    ":features",
    "//components/input:mojom",
    "//gpu/ipc/common:interfaces",
    "//gpu/ipc/common:surface_handle",
    "//media/capture/mojom:video_capture",
    "//media/mojo/mojom",
    "//mojo/public/mojom/base",
    "//services/viz/public/mojom",
    "//skia/public/mojom",
    "//third_party/blink/public/mojom:mojom_platform",
    "//ui/display/mojom",
    "//ui/gfx/geometry/mojom",
    "//ui/gfx/mojom",
    "//ui/latency/mojom",
  ]

  overridden_deps = [ "//third_party/blink/public/mojom:mojom_platform" ]

  component_deps = [ "//third_party/blink/public/common" ]

  traits_public_deps = [
    "//components/viz/common",
    "//ui/base:features",
  ]

  enabled_features = []
  if (use_ozone) {
    enabled_features += [ "use_ozone" ]
  }
  if (ozone_platform_x11) {
    enabled_features += [ "ozone_platform_x11" ]
  }
  if (is_linux && ozone_platform_x11) {
    enabled_features += [ "linux_x11" ]
  }
  if (enable_cast_overlay_strategy) {
    enabled_features += [ "enable_cast_overlay_strategy" ]
  }
  if (is_android || is_chromeos_ash) {
    enabled_features += [ "can_set_refresh_rate" ]
  }

  cpp_typemaps = [
    {
      types = [
        {
          mojom = "viz.mojom.RendererSettings"
          cpp = "::viz::RendererSettings"
        },
        {
          mojom = "viz.mojom.DebugRendererSettings"
          cpp = "::viz::DebugRendererSettings"
        },
      ]
      traits_headers =
          [ "//services/viz/privileged/cpp/renderer_settings_mojom_traits.h" ]
      traits_sources =
          [ "//services/viz/privileged/cpp/renderer_settings_mojom_traits.cc" ]
      traits_public_deps += [ "//ui/gfx/geometry/mojom" ]
    },
  ]
}
