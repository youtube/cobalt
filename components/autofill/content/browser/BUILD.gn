# Copyright 2014 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/protobuf/proto_library.gni")

proto_library("autofill_shared_storage_proto") {
  sources = [ "autofill_shared_storage.proto" ]
}

static_library("browser") {
  sources = [
    "autofill_log_router_factory.cc",
    "autofill_log_router_factory.h",
    "bad_message.cc",
    "bad_message.h",
    "content_autofill_client.cc",
    "content_autofill_client.h",
    "content_autofill_driver.cc",
    "content_autofill_driver.h",
    "content_autofill_driver_factory.cc",
    "content_autofill_driver_factory.h",
    "content_autofill_shared_storage_handler.cc",
    "content_autofill_shared_storage_handler.h",
    "renderer_forms_with_server_predictions.cc",
    "renderer_forms_with_server_predictions.h",
    "risk/fingerprint.cc",
    "risk/fingerprint.h",
    "scoped_autofill_managers_observation.cc",
    "scoped_autofill_managers_observation.h",
  ]

  public_deps = [
    ":risk_proto",
    "//base",
    "//components/autofill/content/common/mojom",
    "//components/autofill/core/browser",
    "//components/autofill/core/common",
    "//components/keyed_service/content",
    "//components/services/storage",
    "//content/public/common",
    "//mojo/public/cpp/bindings",
    "//skia",
    "//third_party/abseil-cpp:absl",
  ]
  deps = [
    ":autofill_shared_storage_proto",
    "//base:i18n",
    "//components/autofill/content/common:features",
    "//components/os_crypt/sync",
    "//components/prefs",
    "//components/profile_metrics",
    "//components/resources",
    "//components/strings",
    "//components/user_prefs",
    "//components/version_info",
    "//components/webauthn/core/browser",
    "//content/public/browser",
    "//gpu/config",
    "//mojo/public/cpp/system",
    "//net",
    "//ppapi/buildflags",
    "//services/device/public/cpp/geolocation",
    "//services/device/public/mojom",
    "//services/service_manager/public/cpp",
    "//sql",
    "//ui/base",
    "//ui/display",
    "//ui/gfx",
    "//ui/gfx/geometry",
    "//url",
  ]
  configs += [ "//build/config/compiler:wexit_time_destructors" ]

  if (!is_android) {
    deps += [ "//components/webauthn/content/browser" ]
  }
}

proto_library("risk_proto") {
  sources = [ "risk/proto/fingerprint.proto" ]
  proto_out_dir = "components/autofill/content/browser/risk/proto"
}

source_set("test_support") {
  testonly = true
  sources = [
    "autofill_test_utils.cc",
    "autofill_test_utils.h",
    "content_autofill_driver_factory_test_api.cc",
    "content_autofill_driver_factory_test_api.h",
    "content_autofill_driver_test_api.h",
    "test_autofill_client_injector.cc",
    "test_autofill_client_injector.h",
    "test_autofill_driver_injector.cc",
    "test_autofill_driver_injector.h",
    "test_autofill_manager_injector.cc",
    "test_autofill_manager_injector.h",
    "test_content_autofill_client.cc",
    "test_content_autofill_client.h",
    "test_content_autofill_driver.h",
  ]

  public_deps = [
    ":browser",
    "//components/autofill/core/browser:test_support",
    "//content/public/browser",
    "//content/test:test_support",
  ]

  deps = [ "//base" ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "autofill_internals_log_router_unittest.cc",
    "content_autofill_driver_factory_unittest.cc",
    "content_autofill_driver_unittest.cc",
    "scoped_autofill_managers_observation_unittest.cc",
  ]

  deps = [
    ":browser",
    ":test_support",
    "//base",
    "//components/autofill/content/common/mojom",
    "//components/autofill/core/browser",
    "//components/autofill/core/browser:test_support",
    "//components/autofill/core/common",
    "//components/version_info",
    "//content/public/browser",
    "//content/public/common",
    "//content/test:test_support",
    "//mojo/public/cpp/system",
    "//net:test_support",
    "//services/service_manager/public/cpp",
    "//testing/gmock",
    "//testing/gtest",
  ]
}
source_set("browser_tests") {
  testonly = true
  sources = [ "risk/fingerprint_browsertest.cc" ]

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

  deps = [
    ":test_support",
    "//base",
    "//components/autofill/content/common:features",
    "//content/public/browser",
    "//content/test:browsertest_support",
    "//content/test:test_support",
    "//services/device/public/cpp:test_support",
    "//services/device/public/cpp/geolocation",
    "//services/device/public/mojom",
    "//testing/gmock",
    "//testing/gtest",
    "//ui/gfx/geometry",
  ]
}
