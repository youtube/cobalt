# Copyright 2018 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//third_party/closure_compiler/js_unit_tests.gni")
import("//tools/polymer/html_to_js.gni")
import("//tools/polymer/html_to_wrapper.gni")
import("//ui/file_manager/base/gn/js_test_type_check.gni")

visibility = [ "//ui/file_manager/file_manager/foreground/*" ]

group("elements") {
  visibility += [ "//ui/file_manager/*" ]
  public_deps =
      [ "//ui/file_manager/file_manager/foreground/elements:files_spinner" ]
}

group("closure_compile") {
  testonly = true
  visibility += [ "//ui/file_manager:closure_compile" ]
  deps = [
    ":closure_compile_jsmodules",
    ":js_test_type_check",
  ]
}

js_type_check("closure_compile_jsmodules") {
  is_polymer3 = true
  closure_flags =
      default_closure_args + [
        "language_in=ECMASCRIPT_2020",
        "js_module_root=gen/ui/file_manager/",
        "js_module_root=" + rebase_path("//ui/file_manager/", root_build_dir),
      ]
  deps = [
    ":files_format_dialog",
    ":files_metadata_box",
    ":files_metadata_entry",
    ":files_password_dialog",
    ":files_quick_view",
    ":files_safe_media",
    ":files_spinner",
    ":files_tooltip",
    ":xf_button",
    ":xf_circular_progress",
    ":xf_display_panel",
    ":xf_panel_item",
  ]
}

html_to_js("web_components") {
  visibility += [ "//ui/file_manager/*" ]
  js_files = [
    "files_format_dialog.js",
    "files_metadata_box.js",
    "files_metadata_entry.js",
    "files_password_dialog.js",
    "files_quick_view.js",
    "files_safe_media.js",
    "files_spinner.js",
    "files_tooltip.js",
    "icons.js",
    "xf_button.js",
    "xf_circular_progress.js",
    "xf_display_panel.js",
    "xf_panel_item.js",
  ]
}

js_library("files_format_dialog") {
  deps = [
    "//ash/webui/common/resources:i18n_behavior",
    "//third_party/polymer/v3_0/components-chromium/iron-icon:iron-icon",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/file_manager/file_manager/common/js:files_app_entry_types",
    "//ui/file_manager/file_manager/common/js:util",
    "//ui/file_manager/file_manager/common/js:volume_manager_types",
    "//ui/file_manager/file_manager/externs:volume_info",
    "//ui/file_manager/file_manager/foreground/js:file_rename",
  ]
  externs_list = [
    "//ui/webui/resources/cr_elements/cr_button/cr_button_externs.js",
    "//ui/webui/resources/cr_elements/cr_dialog/cr_dialog_externs.js",
    "//ui/webui/resources/cr_elements/cr_input/cr_input_externs.js",
  ]
}

js_library("files_metadata_box") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("files_metadata_entry") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_unittest("files_password_dialog_unittest") {
  deps = [
    ":files_password_dialog",
    "//ash/webui/common/resources:assert",
    "//chrome/test/data/webui/chromeos:chai_assert",
    "//ui/file_manager/file_manager/common/js:test_error_reporting",
  ]
  externs_list =
      [ "//ui/webui/resources/cr_elements/cr_dialog/cr_dialog_externs.js" ]
}

js_library("files_password_dialog") {
  deps = [
    "//ash/webui/common/resources:load_time_data.m",
    "//ui/file_manager/file_manager/common/js:async_util",
  ]
  externs_list = [
    "//ui/webui/resources/cr_elements/cr_button/cr_button_externs.js",
    "//ui/webui/resources/cr_elements/cr_dialog/cr_dialog_externs.js",
    "//ui/webui/resources/cr_elements/cr_input/cr_input_externs.js",
  ]
}

js_library("files_quick_view") {
  deps = [
    ":files_metadata_box",
    ":files_safe_media",
    ":files_tooltip",
    "//ash/webui/common/resources:assert",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
  externs_list = [ "$externs_path/chrome_extensions.js" ]
}

js_library("files_safe_media") {
  deps = [
    "//ash/webui/common/resources:assert",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
  externs_list = [ "//ui/file_manager/file_manager/externs/quick_view.js" ]
}

js_library("files_spinner") {
}

html_to_wrapper("polymer_elements") {
  visibility += [ "//ui/file_manager/*" ]
  in_files = [ "files_toast.html" ]
  scheme = "relative"
  minify = true
}

# TODO(tapted): Move this to //ui/file_manager/base.
js_unittest("files_tooltip_unittest") {
  deps = [
    ":files_tooltip",
    "//chrome/test/data/webui/chromeos:chai_assert",
    "//ui/file_manager/file_manager/common/js:test_error_reporting",
  ]
}

js_library("files_tooltip") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_unittest("files_toast_unittest") {
  deps = [ "//chrome/test/data/webui/chromeos:chai_assert" ]
  externs_list = [
    "//ui/file_manager/file_manager/externs/polymer_elements/files_toast.js",
  ]
}

js_unittest("files_xf_elements_unittest") {
  deps = [
    ":xf_display_panel",
    "//ash/webui/common/resources:assert",
    "//ash/webui/common/resources:load_time_data.m",
    "//chrome/test/data/webui/chromeos:chai_assert",
    "//ui/file_manager/file_manager/common/js:test_error_reporting",
    "//ui/file_manager/file_manager/common/js:util",
  ]

  externs_list = [ "$externs_path/file_manager_private.js" ]
}

js_library("xf_button") {
  externs_list =
      [ "//ui/webui/resources/cr_elements/cr_button/cr_button_externs.js" ]
}

js_library("xf_circular_progress") {
  deps = [ "//ash/webui/common/resources:assert" ]
}

js_library("xf_display_panel") {
  deps = [
    ":xf_panel_item",
    "//ui/file_manager/file_manager/common/js:util",
  ]
}

js_test_type_check("js_test_type_check") {
  is_polymer3 = true
  deps = [
    ":files_password_dialog_unittest",
    ":files_toast_unittest",
    ":files_tooltip_unittest",
    ":files_xf_elements_unittest",
  ]

  closure_flags =
      strict_error_checking_closure_args + [
        "language_in=ECMASCRIPT_2020",
        "generate_exports=false",
        "js_module_root=./gen/ui",
        "js_module_root=" + rebase_path("//ui", root_build_dir),
        "browser_resolver_prefix_replacements=\"chrome://webui-test/=./\"",
        "hide_warnings_for=third_party/",
      ]
}

js_library("xf_panel_item") {
  deps = [
    ":xf_button",
    ":xf_circular_progress",
    "//ash/webui/common/resources:assert",
    "//ui/file_manager/file_manager/common/js:util",
  ]
}
