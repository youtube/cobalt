# Copyright 2021 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")

mojom("mojo_bindings") {
  sources = [ "app_service_internals.mojom" ]
  webui_module_path = "/"
}

if (!is_android) {
  source_set("app_service_internals") {
    sources = [
      "app_service_internals_page_handler_impl.cc",
      "app_service_internals_page_handler_impl.h",
      "app_service_internals_ui.cc",
      "app_service_internals_ui.h",
    ]

    public_deps = [
      ":mojo_bindings",
      "//base",
      "//chrome/browser/profiles:profile",
      "//content/public/browser",
      "//mojo/public/cpp/bindings",
      "//ui/webui",
    ]

    deps = [
      "//chrome/browser/apps/app_service",
      "//chrome/browser/resources/app_service_internals:resources",
      "//chrome/common",
      "//components/services/app_service",
    ]
  }

  source_set("browser_tests") {
    testonly = true
    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

    sources = [ "app_service_internals_browsertest.cc" ]

    deps = [
      "//chrome/browser/ui",
      "//chrome/browser/ui/tabs:tab_strip",
      "//chrome/browser/web_applications",
      "//chrome/test:test_support",
      "//chrome/test:test_support_ui",
      "//content/test:test_support",
      "//net:test_support",
    ]
  }
}
