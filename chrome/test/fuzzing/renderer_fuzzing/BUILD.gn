# Copyright 2024 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/test/fuzzing/in_process_fuzzer.gni")
import("//chrome/test/fuzzing/renderer_fuzzing/in_process_renderer_fuzzing.gni")
import(
    "//chrome/test/fuzzing/renderer_fuzzing/ipc_fuzzing/mojom_interfaces.gni")

group("test") {
  testonly = true
}

if (fuzzing_engine_supports_custom_main) {
  source_set("renderer_in_process_fuzzer_runner") {
    testonly = true
    sources = [ "in_process_renderer_fuzzing.h" ]
    deps = [
      "//base",
      "//chrome/test:test_support",
      "//chrome/test/fuzzing:in_process_fuzzer_runner",
      "//chrome/test/fuzzing:in_process_proto_fuzzer_runner",
      "//testing/libfuzzer:renderer_fuzzing",
    ]
  }
}

if (!is_android) {
  action("renderer_in_process_mojolpm_fuzzer_generator") {
    testonly = true
    script = "//chrome/test/fuzzing/renderer_fuzzing/ipc_fuzzing/generate_testcase.py"
    args = [
      "-n",
      "renderer_in_process_mojolpm_fuzzer",
      "-d",
      rebase_path("${target_gen_dir}/", root_gen_dir),
      "-o",
      rebase_path("${target_gen_dir}/testcase.h", root_build_dir),
      "-c",
    ]
    foreach(interface, context_browser_exposed_interfaces) {
      args += [ interface[1] ]
    }
    args += [ "-p" ]
    foreach(interface, process_browser_exposed_interfaces) {
      args += [ interface[1] ]
    }
    outputs = [ "${target_gen_dir}/testcase.h" ]
  }
  in_process_renderer_mojolpm_generated_fuzzer(
      "renderer_in_process_mojolpm_fuzzer") {
    sources = [ "renderer_in_process_mojolpm_fuzzer.cc" ]

    interfaces = context_browser_exposed_interfaces
    interfaces += process_browser_exposed_interfaces

    _interfaces_deps = [
      "//chrome/browser/lens/core/mojom:mojo_bindings_mojolpm",
      "//chrome/browser/media:mojo_bindings_mojolpm",
      "//chrome/browser/new_tab_page/modules/file_suggestion:mojo_bindings_mojolpm",
      "//chrome/browser/new_tab_page/modules/photos:mojo_bindings_mojolpm",
      "//chrome/browser/new_tab_page/modules/v2/history_clusters:mojo_bindings_mojolpm",
      "//chrome/browser/new_tab_page/modules/v2/tab_resumption:mojo_bindings_mojolpm",
      "//chrome/browser/ui/webui/access_code_cast:mojo_bindings_mojolpm",
      "//chrome/browser/ui/webui/app_home:mojo_bindings_mojolpm",
      "//chrome/browser/ui/webui/app_service_internals:mojo_bindings_mojolpm",
      "//chrome/browser/ui/webui/bluetooth_internals:mojo_bindings_mojolpm",
      "//chrome/browser/ui/webui/connectors_internals:mojo_bindings_mojolpm",
      "//chrome/browser/ui/webui/discards:mojo_bindings_mojolpm",
      "//chrome/browser/ui/webui/downloads:mojo_bindings_mojolpm",
      "//chrome/browser/ui/webui/internals/user_education:mojo_bindings_mojolpm",
      "//chrome/browser/ui/webui/location_internals:mojo_bindings_mojolpm",
      "//chrome/browser/ui/webui/new_tab_page:mojo_bindings_mojolpm",
      "//chrome/browser/ui/webui/new_tab_page/foo:mojo_bindings_mojolpm",
      "//chrome/browser/ui/webui/new_tab_page_third_party:mojo_bindings_mojolpm",
      "//chrome/browser/ui/webui/omnibox:mojo_bindings_mojolpm",
      "//chrome/browser/ui/webui/privacy_sandbox:mojo_bindings_mojolpm",
      "//chrome/browser/ui/webui/reset_password:mojo_bindings_mojolpm",
      "//chrome/browser/ui/webui/search_engine_choice:mojo_bindings_mojolpm",
      "//chrome/browser/ui/webui/segmentation_internals:mojo_bindings_mojolpm",
      "//chrome/browser/ui/webui/side_panel/bookmarks:mojo_bindings_mojolpm",
      "//chrome/browser/ui/webui/side_panel/customize_chrome:mojo_bindings_mojolpm",
      "//chrome/browser/ui/webui/side_panel/reading_list:mojo_bindings_mojolpm",
      "//chrome/browser/ui/webui/suggest_internals:mojo_bindings_mojolpm",
      "//chrome/browser/ui/webui/tab_search:mojo_bindings_mojolpm",
      "//chrome/browser/ui/webui/tab_strip:mojo_bindings_mojolpm",
      "//chrome/browser/ui/webui/usb_internals:mojo_bindings_mojolpm",
      "//chrome/browser/ui/webui/web_app_internals:mojo_bindings_mojolpm",
      "//chrome/common:mojo_bindings_mojolpm",
      "//chrome/common/accessibility:mojo_bindings_mojolpm",
      "//chrome/common/cart:mojo_bindings_mojolpm",
      "//components/browsing_topics/mojom:mojo_bindings_mojolpm",
      "//components/commerce/core/internals/mojom:mojo_bindings_mojolpm",
      "//components/dom_distiller/content/common/mojom:mojom_mojolpm",
      "//components/history_clusters/history_clusters_internals/webui:mojo_bindings_mojolpm",
      "//components/metrics/public/mojom:call_stack_mojo_bindings_mojolpm",
      "//components/metrics/public/mojom:single_sample_metrics_mojo_bindings_mojolpm",
      "//components/network_hints/common:mojo_bindings_mojolpm",
      "//components/no_state_prefetch/common:mojo_bindings_mojolpm",
      "//components/optimization_guide/optimization_guide_internals/webui:mojo_bindings_mojolpm",
      "//components/page_image_service/mojom:mojo_bindings_mojolpm",
      "//components/performance_manager/public/mojom:mojom_mojolpm",
      "//components/safe_browsing/content/common:interfaces_mojolpm",
      "//components/site_engagement/core/mojom:mojo_bindings_mojolpm",
      "//components/spellcheck/common:interfaces_mojolpm",
      "//components/translate/content/common:common_mojolpm",
      "//content/browser/attribution_reporting:internals_mojo_bindings_mojolpm",
      "//content/browser/indexed_db:internals_mojo_bindings_mojolpm",
      "//content/browser/private_aggregation:mojo_bindings_mojolpm",
      "//content/browser/process_internals:mojo_bindings_mojolpm",
      "//content/browser/tracing/trace_report:mojo_bindings_mojolpm",
      "//content/browser/xr/webxr_internals/mojom:mojo_bindings_mojolpm",
      "//content/common:mojo_bindings_mojolpm",
      "//device/gamepad/public/mojom:mojom_mojolpm",
      "//device/vr/public/mojom:vr_service_mojolpm",
      "//extensions/common/api:mojom_mojolpm",
      "//media/capture/mojom:image_capture_mojolpm",
      "//media/capture/mojom:video_capture_mojolpm",
      "//media/midi:mojo_mojolpm",
      "//media/mojo/mojom:mojom_mojolpm",
      "//media/mojo/mojom:remoting_mojolpm",
      "//media/mojo/mojom:speech_recognition_mojolpm",
      "//services/device/public/mojom:mojom_mojolpm",
      "//services/image_annotation/public/mojom:mojom_mojolpm",
      "//services/network/public/mojom:cookies_mojom_mojolpm",
      "//services/network/public/mojom:mojom_mojolpm",
      "//services/network/public/mojom:url_loader_base_mojolpm",
      "//services/resource_coordinator/public/mojom:mojom_mojolpm",
      "//services/screen_ai/public/mojom:mojom_mojolpm",
      "//services/shape_detection/public/mojom:mojom_mojolpm",
      "//services/viz/public/mojom:mojom_mojolpm",
      "//storage/browser/quota:mojo_bindings_mojolpm",
      "//third_party/blink/public/mojom:android_mojo_bindings_mojolpm",
      "//third_party/blink/public/mojom:authenticator_test_mojo_bindings_mojolpm",
      "//third_party/blink/public/mojom:embedded_frame_sink_mojo_bindings_mojolpm",
      "//third_party/blink/public/mojom:mojom_core_mojolpm",
      "//third_party/blink/public/mojom:mojom_modules_mojolpm",
      "//third_party/blink/public/mojom:mojom_platform_mojolpm",
      "//third_party/blink/public/mojom:web_bluetooth_mojo_bindings_mojolpm",
      "//third_party/blink/public/mojom/gpu:gpu_mojolpm",
      "//third_party/blink/public/mojom/origin_trial_state:origin_trial_state_mojolpm",
      "//third_party/blink/public/mojom/quota:quota_mojolpm",
      "//third_party/blink/public/mojom/usb:usb_mojolpm",
      "//ui/webui/resources/cr_components/app_management:mojo_bindings_mojolpm",
      "//ui/webui/resources/cr_components/certificate_manager:mojom_mojolpm",
      "//ui/webui/resources/cr_components/color_change_listener:mojom_mojolpm",
      "//ui/webui/resources/cr_components/commerce:mojo_bindings_mojolpm",
      "//ui/webui/resources/cr_components/customize_color_scheme_mode:mojom_mojolpm",
      "//ui/webui/resources/cr_components/customize_themes:mojom_mojolpm",
      "//ui/webui/resources/cr_components/help_bubble:mojo_bindings_mojolpm",
      "//ui/webui/resources/cr_components/history_clusters:mojo_bindings_mojolpm",
      "//ui/webui/resources/cr_components/most_visited:mojom_mojolpm",
      "//ui/webui/resources/cr_components/searchbox:mojo_bindings_mojolpm",
      "//ui/webui/resources/cr_components/theme_color_picker:mojom_mojolpm",
      "//ui/webui/resources/js/browser_command:mojo_bindings_mojolpm",
      "//ui/webui/resources/js/metrics_reporter:mojo_bindings_mojolpm",
    ]

    deps = [
      ":renderer_in_process_mojolpm_fuzzer_generator",
      "//chrome/test:test_support",
      "//chrome/test/fuzzing:in_process_proto_fuzzer_runner",
      "//content/test/fuzzer:mojolpm_fuzzer_support",
      "//testing/libfuzzer:renderer_fuzzing",
      "//testing/libfuzzer/proto:url_proto_converter",
      "//third_party/blink/public/common:storage_key_proto_converter",
    ]
    deps += _interfaces_deps

    proto_deps = _interfaces_deps
  }
}
