# Copyright 2022 The Cobalt Authors. All Rights Reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

declare_args() {
  starboard_path = ""
}

if (starboard_path == "") {
  declare_args() {
    target_platform = ""
  }

  if (target_platform == "") {
    if (target_os == "android") {
      if (target_cpu == "arm") {
        target_platform = "android-arm"
      } else if (target_cpu == "arm64") {
        target_platform = "android-arm64"
      } else if (target_cpu == "x86") {
        target_platform = "android-x86"
      } else {
        assert(false, "Unsupported android target cpu ${target_cpu}.")
      }
    } else if (target_os == "linux") {
      if (target_cpu == "x64") {
        target_platform = "linux-x64x11"
      } else if (target_cpu == "arm") {
        target_platform = "raspi-2"
      } else {
        assert(false, "Unsupported linux target cpu ${target_cpu}.")
      }
    } else {
      # Default to linux-x64x11 for chromium toolchains.
      target_platform = "linux-x64x11"
    }
  }

  starboard_path = exec_script("//starboard/build/platforms.py",
                               [ target_platform ],
                               "trim string")
}

assert(defined(starboard_path),
       "Please add your platform to starboard/build/platforms.py")

assert(target_platform != "",
       "Please add your target_platform in the platform's args.gn file")
