# Copyright 2024 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chromecast/build/tests/cast_test.gni")
import("//chromecast/chromecast.gni")
import("//chromecast/starboard/starboard.gni")
import("//testing/test.gni")

if (!remove_starboard_headers) {
  import(
      "//chromecast/starboard/third_party/starboard/sabi/base_configuration.gni")
}

# This is necessary until code under //chromecast/starboard/media/renderer is
# hooked up to production code. This way, gn can find those test targets.
cast_test_group("starboard_renderer_tests") {
  tests =
      [ "//chromecast/starboard/media/renderer:starboard_renderer_unittests" ]
}

source_set("starboard") {
  public = [
    "media_pipeline_backend_starboard.h",
    "starboard_audio_decoder.h",
    "starboard_decoder.h",
    "starboard_video_decoder.h",
    "starboard_video_plane.h",
  ]
  sources = [
    "cast_media_starboard.cc",
    "media_pipeline_backend_starboard.cc",
    "starboard_audio_decoder.cc",
    "starboard_decoder.cc",
    "starboard_video_decoder.cc",
    "starboard_video_plane.cc",
    "starboard_volume_control.cc",
  ]
  public_deps = [
    ":drm_util",
    ":starboard_api_wrapper",
    "//base",
    "//chromecast/public",
    "//chromecast/public/media",
    "//chromecast/starboard/chromecast/starboard_cast_api:cast_starboard_api_types",
    "//chromecast/starboard/media/cdm:starboard_drm_wrapper",
  ]
  deps = [
    ":mime_utils",
    ":starboard_resampler",
    "//chromecast/base",
    "//chromecast/starboard/chromecast/starboard_adapter",
    "//chromecast/starboard/media/cdm:starboard_drm_key_tracker",
    "//ui/display",
    "//ui/gfx/geometry",
  ]
}

source_set("starboard_resampler") {
  public = [ "starboard_resampler.h" ]
  sources = [ "starboard_resampler.cc" ]
  public_deps = [
    "//base",
    "//chromecast/public/media",
    "//chromecast/starboard/chromecast/starboard_cast_api:cast_starboard_api_types",
    "//media",
  ]
}

source_set("drm_util") {
  public = [ "drm_util.h" ]
  sources = [ "drm_util.cc" ]
  public_deps = [
    ":starboard_api_wrapper",
    "//chromecast/public/media",
    "//media",
  ]
}

source_set("mock_starboard_api_wrapper") {
  testonly = true
  public = [ "mock_starboard_api_wrapper.h" ]
  sources = [ "mock_starboard_api_wrapper.cc" ]
  public_deps = [
    ":starboard_api_wrapper",
    "//testing/gmock",
  ]
}

cast_source_set("starboard_api_wrapper") {
  sources = []
  deps = []
  public_deps = [ "//base" ]

  # TODO(b/333131992): remove this case.
  if (remove_starboard_headers) {
    sources += [
      "starboard_api_wrapper.cc",
      "starboard_api_wrapper.h",
      "starboard_api_wrapper_dummy.cc",
    ]
  } else {
    sources += [
      "starboard_api_wrapper.cc",
      "starboard_api_wrapper.h",
      "starboard_api_wrapper_base.cc",
      "starboard_api_wrapper_base.h",
    ]
    if (sb_api_version == 15 || sb_api_version == 16) {
      sources += [ "starboard_api_wrapper_15.cc" ]
    } else {
      assert(sb_api_version == 13 || sb_api_version == 14,
             "Only SB 13 and 14 are supported by starboard_api_wrapper_14.cc")
      sources += [ "starboard_api_wrapper_14.cc" ]
    }
    deps += [
      "//chromecast/common:timing_tracker",
      "//chromecast/starboard/chromecast/starboard_adapter",
      "//chromecast/starboard/chromecast/starboard_cast_dummy:cast_starboard_api",
    ]
  }
}

cast_source_set("mime_utils") {
  sources = [
    "mime_utils.cc",
    "mime_utils.h",
  ]
  public_deps = [
    "//chromecast/public/media",
    "//media",
  ]
  deps = [
    "//base",
    "//chromecast/media/base:media_codec_support",
  ]
}

source_set("test_matchers") {
  testonly = true
  public = [ "test_matchers.h" ]
  sources = [ "test_matchers.cc" ]
  public_deps = [
    ":starboard_api_wrapper",
    "//testing/gmock",
  ]
}

test("starboard_media_unittests") {
  sources = [
    "drm_util_test.cc",
    "media_pipeline_backend_starboard_test.cc",
    "mime_utils_test.cc",
    "starboard_audio_decoder_test.cc",
    "starboard_resampler_test.cc",
    "starboard_video_decoder_test.cc",
    "starboard_video_plane_test.cc",
  ]
  deps = [
    ":drm_util",
    ":mime_utils",
    ":mock_starboard_api_wrapper",
    ":starboard",
    ":starboard_api_wrapper",
    ":starboard_resampler",
    ":test_matchers",
    "//base/test:run_all_unittests",
    "//base/test:test_support",
    "//chromecast/media/base",
    "//chromecast/media/base:media_codec_support",
    "//chromecast/media/cma/base",
    "//chromecast/public",
    "//chromecast/public/media",
    "//chromecast/starboard/chromecast/starboard_cast_api:cast_starboard_api_types",
    "//chromecast/starboard/media/cdm:mocks",
    "//chromecast/starboard/media/cdm:starboard_drm_key_tracker",
    "//chromecast/starboard/media/cdm:starboard_drm_wrapper",
    "//media",
    "//testing/gmock",
    "//testing/gtest",
  ]
}
