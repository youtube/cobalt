# Copyright 2018 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//remoting/build/config/remoting_build.gni")

source_set("input_monitor") {
  public = [ "local_input_monitor.h" ]
  deps = []
  defines = []

  sources = [
    "local_hotkey_input_monitor.h",
    "local_input_monitor.cc",
    "local_keyboard_input_monitor.h",
    "local_pointer_input_monitor.h",
  ]

  if (remoting_use_x11) {
    defines += [ "REMOTING_USE_X11" ]
    sources += [
      "local_hotkey_input_monitor_linux.cc",
      "local_keyboard_input_monitor_linux.cc",
      "local_mouse_input_monitor_linux.cc",
    ]
    deps += [
      "//base",
      "//remoting/host/linux:wayland",
    ]
  }

  deps += [
    "//remoting/host:client_session_control",
    "//remoting/proto",
    "//third_party/webrtc_overrides:webrtc_component",
    "//ui/events",
    "//ui/events:dom_keycode_converter",
  ]

  if (use_ozone) {
    deps += [ "//ui/ozone" ]
  }

  if (is_mac) {
    sources += [
      "local_hotkey_input_monitor_mac.mm",
      "local_keyboard_input_monitor_mac.mm",
      "local_mouse_input_monitor_mac.mm",
    ]
  }

  if (remoting_use_x11) {
    sources += [
      "local_hotkey_input_monitor_x11.cc",
      "local_hotkey_input_monitor_x11.h",
      "local_input_monitor_x11_common.cc",
      "local_input_monitor_x11_common.h",
      "local_mouse_input_monitor_x11.cc",
      "local_mouse_input_monitor_x11.h",
    ]

    deps += [
      "//ui/events/devices/x11",
      "//ui/gfx/x",
    ]
  }

  if (is_win) {
    sources += [
      "local_hotkey_input_monitor_win.cc",
      "local_input_monitor_win.cc",
      "local_input_monitor_win.h",
      "local_keyboard_input_monitor_win.cc",
      "local_mouse_input_monitor_win.cc",
    ]
  }

  if (is_chromeos_ash) {
    sources += [
      "local_hotkey_input_monitor_chromeos.cc",
      "local_keyboard_input_monitor_chromeos.cc",
      "local_pointer_input_monitor_chromeos.cc",
    ]

    deps += [ "//remoting/host/chromeos:chromeos" ]
  }
}
