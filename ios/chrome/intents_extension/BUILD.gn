# Copyright 2021 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/apple/compile_entitlements.gni")
import("//build/apple/tweak_info_plist.gni")
import("//build/config/ios/rules.gni")
import("//ios/build/chrome_build.gni")

tweak_info_plist("info_plist") {
  info_plist = "Info.plist"
}

compile_entitlements("entitlements") {
  substitutions = [
    "IOS_BUNDLE_ID_PREFIX=$ios_app_bundle_id_prefix",
    "CHROMIUM_BUNDLE_ID=$chromium_bundle_id",
  ]
  entitlements_templates = [ "intents_extension.entitlements" ]
  output_name = "$target_gen_dir/chrome_intents.entitlements"
}

ios_appex_bundle("intents_extension") {
  sources = [
    "chrome_intents_handler.h",
    "chrome_intents_handler.mm",
  ]

  deps = [ "//ios/chrome/common/intents" ]

  frameworks = [ "Intents.framework" ]

  configs += [ "//build/config/compiler:enable_arc" ]

  entitlements_target = ":entitlements"
  info_plist_target = ":info_plist"
  bundle_identifier = "$chromium_bundle_id.IntentsExtension"
}
