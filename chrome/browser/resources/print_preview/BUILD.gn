# Copyright 2018 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//printing/buildflags/buildflags.gni")

import("//ui/webui/resources/tools/build_webui.gni")

assert(!is_chromeos,
       "ChromeOS should use chrome/browser/resources/ash/print_preview")
assert(enable_print_preview, "enable_print_preview check failed")

build_webui("build") {
  grd_prefix = "print_preview"

  static_files = [ "print_preview.html" ]

  # Files holding a Polymer element definition and have an equivalent .html file.
  web_component_files = [
    "ui/advanced_options_settings.ts",
    "ui/advanced_settings_dialog.ts",
    "ui/advanced_settings_item.ts",
    "ui/app.ts",
    "ui/button_strip.ts",
    "ui/color_settings.ts",
    "ui/copies_settings.ts",
    "ui/destination_dialog.ts",
    "ui/destination_list_item.ts",
    "ui/destination_select.ts",
    "ui/dpi_settings.ts",
    "ui/duplex_settings.ts",
    "ui/header.ts",
    "ui/layout_settings.ts",
    "ui/link_container.ts",
    "ui/margin_control.ts",
    "ui/margin_control_container.ts",
    "ui/margins_settings.ts",
    "ui/media_size_settings.ts",
    "ui/more_settings.ts",
    "ui/number_settings_section.ts",
    "ui/other_options_settings.ts",
    "ui/pages_per_sheet_settings.ts",
    "ui/pages_settings.ts",
    "ui/preview_area.ts",
    "ui/print_preview_search_box.ts",
    "ui/scaling_settings.ts",
    "ui/settings_section.ts",
    "ui/settings_select.ts",
    "ui/sidebar.ts",
  ]

  ts_files = [
    "dark_mode_mixin.ts",
    "data/cdd.ts",
    "data/coordinate2d.ts",
    "data/destination.ts",
    "data/destination_match.ts",
    "data/destination_store.ts",
    "data/document_info.ts",
    "data/local_parsers.ts",
    "data/margins.ts",
    "data/measurement_system.ts",
    "data/model.ts",
    "data/observable.ts",
    "data/printable_area.ts",
    "data/scaling.ts",
    "data/size.ts",
    "data/state.ts",
    "metrics.ts",
    "native_layer.ts",
    "print_preview.ts",
    "print_preview_utils.ts",
    "ui/debouncer.ts",
    "ui/destination_list.html.ts",
    "ui/destination_list.ts",
    "ui/destination_settings.html.ts",
    "ui/destination_settings.ts",
    "ui/highlight_utils.ts",
    "ui/input_mixin.ts",
    "ui/plugin_proxy.ts",
    "ui/select_mixin.ts",
    "ui/settings_mixin.ts",
  ]

  # Files that are passed as input to css_to_wrapper().
  css_files = [
    "ui/advanced_options_settings.css",
    "ui/advanced_settings_dialog.css",
    "ui/advanced_settings_item.css",
    "ui/app.css",
    "ui/button_strip.css",
    "ui/copies_settings.css",
    "ui/destination_dialog.css",
    "ui/destination_list.css",
    "ui/destination_list_item.css",
    "ui/destination_select.css",
    "ui/dpi_settings.css",
    "ui/duplex_settings.css",
    "ui/header.css",
    "ui/link_container.css",
    "ui/margin_control.css",
    "ui/margin_control_container.css",
    "ui/media_size_settings.css",
    "ui/more_settings.css",
    "ui/number_settings_section.css",
    "ui/other_options_settings.css",
    "ui/pages_settings.css",
    "ui/preview_area.css",
    "ui/print_preview_search_box.css",
    "ui/print_preview_shared.css",
    "ui/print_preview_vars.css",
    "ui/settings_section.css",
    "ui/settings_select.css",
    "ui/sidebar.css",
    "ui/throbber.css",
  ]

  icons_html_files = [ "ui/icons.html" ]
  html_to_wrapper_template = "detect"

  ts_composite = true
  ts_definitions = [
    "//tools/typescript/definitions/chrome_event.d.ts",
    "//tools/typescript/definitions/chrome_send.d.ts",
    "//tools/typescript/definitions/mime_handler_private.d.ts",
    "//tools/typescript/definitions/pending.d.ts",
  ]

  ts_deps = [
    "//chrome/browser/resources/pdf:build_ts",
    "//third_party/lit/v3_0:build_ts",
    "//ui/webui/resources/cr_elements:build_ts",
    "//ui/webui/resources/js:build_ts",
  ]

  ts_path_mappings = [ "chrome://print/pdf/*|" + rebase_path(
                           "$root_gen_dir/chrome/browser/resources/pdf/tsc/*",
                           target_gen_dir) ]

  webui_context_type = "trusted"
  optimize = optimize_webui
  if (optimize) {
    optimize_webui_host = "print"
    optimize_webui_in_files = [ "print_preview.js" ]
    optimize_webui_excludes = [ "chrome://print/pdf/pdf_scripting_api.js" ]
  }
}
