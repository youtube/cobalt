# Copyright 2024 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")

assert(is_chromeos_ash)

static_library("select_file_dialog_extension") {
  sources = [
    "select_file_dialog_extension.cc",
    "select_file_dialog_extension.h",
    "select_file_dialog_extension_factory.cc",
    "select_file_dialog_extension_factory.h",
  ]

  public_deps = [ "//chrome/browser:browser_public_dependencies" ]

  deps = [
    "//chrome/browser/apps/platform_apps",
    "//chrome/browser/ash/arc/fileapi",
    "//chrome/browser/ash/extensions/file_manager",
    "//chrome/browser/ash/file_manager",
    "//chrome/browser/ash/policy/dlp",
    "//chrome/browser/ash/profiles",
    "//chrome/browser/chromeos/policy/dlp",
    "//chrome/browser/extensions",
    "//chrome/browser/ui/ash/login",
    "//chrome/browser/ui/webui/ash/system_web_dialog",
    "//ui/chromeos/styles:cros_tokens_color_mappings_generator",
  ]

  allow_circular_includes_from = [
    "//chrome/browser/ash/file_manager",
    "//chrome/browser/ash/arc/fileapi",
    "//chrome/browser/ash/extensions/file_manager",
  ]
}

source_set("browser_tests") {
  testonly = true

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

  sources = [ "select_file_dialog_extension_browsertest.cc" ]

  deps = [
    ":select_file_dialog_extension",
    "//chrome/browser/ash/file_manager:test_support",
    "//chrome/browser/ash/policy/dlp",
    "//chrome/browser/chromeos/policy/dlp",
    "//chrome/browser/chromeos/policy/dlp/test:test_support",
    "//chrome/browser/ui",
    "//chrome/browser/ui:browser_navigator_params_headers",
    "//chrome/browser/ui/ash/keyboard",
    "//chrome/common:constants",
    "//chrome/test:test_support",
    "//chrome/test:test_support_ui",
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [ "select_file_dialog_extension_unittest.cc" ]

  deps = [
    ":select_file_dialog_extension",
    "//base",
    "//testing/gtest",
    "//ui/shell_dialogs",
  ]
}
