# Copyright 2024 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//components/signin/features.gni")

assert(!is_fuchsia && !is_android)

# Removes circular dep between |login| and |signin|.
if (!is_chromeos) {
  source_set("signin_ui_error") {
    sources = [
      "signin_ui_error.cc",
      "signin_ui_error.h",
    ]

    public_deps = [ "//base" ]
    deps = [
      "//chrome/app:branded_strings",
      "//chrome/app:generated_resources",
      "//google_apis",
      "//ui/base",
    ]

    if (is_win) {
      public_deps += [ "//chrome/credential_provider/common:common_constants" ]
    }
  }
}

# Removes circular dep between |login| and |signin|.
source_set("signin_utils") {
  sources = [
    "signin_url_utils.h",
    "signin_utils.h",
  ]

  public_deps = [
    "//base",
    "//build:chromeos_buildflags",
    "//components/signin/public/base",
    "//components/signin/public/base:signin_buildflags",
    "//components/signin/public/identity_manager:identity_manager",
    "//url",
  ]

  if (!is_chromeos) {
    sources += [ "signin_utils_desktop.h" ]
  }
}

source_set("signin_utils_impl") {
  sources = [
    "signin_url_utils.cc",
    "signin_utils.cc",
  ]

  public_deps = [ "//chrome/browser:browser_public_dependencies" ]

  deps = [
    ":signin_utils",
    "//base",
    "//chrome/browser:browser_process",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/profiles:profile_util",
    "//chrome/common",
    "//chrome/common:non_code_constants",
    "//components/prefs",
    "//components/signin/public/base",
    "//components/signin/public/identity_manager",
    "//content/public/browser",
    "//extensions/browser",
    "//google_apis",
    "//net",
  ]

  if (!is_chromeos) {
    sources += [ "signin_utils_desktop.cc" ]
    deps += [ ":signin_ui_error" ]
  }
}

source_set("signin") {
  sources = [
    "signin_web_dialog_ui.h",
    "sync_confirmation_handler.h",
    "sync_confirmation_ui.h",
  ]

  public_deps = [
    ":login",
    "//base",
    "//chrome/browser/signin",
    "//chrome/browser/ui:browser_list",
    "//components/signin/public/identity_manager",
    "//components/sync/base",
    "//content/public/browser",
    "//ui/web_dialogs",
  ]

  if (enable_dice_support) {
    sources += [
      "dice_web_signin_intercept_handler.h",
      "dice_web_signin_intercept_ui.h",
    ]
  }

  if (!is_chromeos) {
    sources += [
      "signin_email_confirmation_dialog.h",
      "signin_email_confirmation_ui.h",
      "signin_error_handler.h",
      "signin_error_ui.h",
      "signin_reauth_handler.h",
      "signin_reauth_ui.h",
      "turn_sync_on_helper.h",
      "turn_sync_on_helper_delegate_impl.h",
      "turn_sync_on_helper_policy_fetch_tracker.h",
    ]
    public_deps += [
      ":signin_utils",
      "//chrome/browser/search_engine_choice",
      "//chrome/browser/sync",
      "//chrome/browser/ui/webui",
    ]
  }
}

source_set("signin_impl") {
  sources = [
    "signin_web_dialog_ui.cc",
    "sync_confirmation_handler.cc",
    "sync_confirmation_ui.cc",
  ]

  public_deps = [ "//chrome/browser:browser_public_dependencies" ]

  deps = [
    ":signin",
    ":signin_utils",
    "//base",
    "//chrome/browser:browser_process",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/profiles:profile_util",
    "//chrome/browser/resources/signin:resources",
    "//chrome/browser/sync",
    "//chrome/browser/ui/signin",
    "//chrome/browser/ui/webui:webui_util",
    "//chrome/common",
    "//chrome/common/themes:autogenerated_theme_util",
    "//components/consent_auditor",
    "//components/signin/public/base",
    "//components/signin/public/identity_manager",
    "//content/public/browser",
    "//extensions/browser",
    "//google_apis",
    "//net",
  ]

  if (enable_dice_support) {
    sources += [
      "dice_web_signin_intercept_handler.cc",
      "dice_web_signin_intercept_ui.cc",
    ]
    deps += [ "//chrome/browser/ui/webui" ]
  }

  if (!is_chromeos) {
    sources += [
      "signin_email_confirmation_dialog.cc",
      "signin_email_confirmation_ui.cc",
      "signin_error_handler.cc",
      "signin_error_ui.cc",
      "signin_reauth_handler.cc",
      "signin_reauth_ui.cc",
      "turn_sync_on_helper.cc",
      "turn_sync_on_helper_delegate_impl.cc",
      "turn_sync_on_helper_policy_fetch_tracker.cc",
    ]

    deps += [
      ":login",
      "//chrome/browser/ui/tabs:tab_strip",
      "//chrome/browser/ui/webui:webui_util",
      "//components/keyed_service/content",
      "//components/policy/core/browser",
      "//components/signin/core/browser",
      "//components/unified_consent",
    ]
  }
}

source_set("login") {
  sources = [
    "inline_login_handler.h",
    "login_ui_service.h",
    "login_ui_service_factory.h",
  ]

  public_deps = [
    "//base",
    "//build:chromeos_buildflags",
    "//chrome/browser/profiles:profile",
    "//components/keyed_service/core",
    "//content/public/browser",
    "//google_apis",
    "//net",
  ]

  if (!is_chromeos) {
    public_deps += [ ":signin_ui_error" ]
  }

  if (enable_dice_support) {
    sources += [ "inline_login_handler_impl.h" ]
  }

  if (enable_dice_support || is_chromeos) {
    sources += [ "inline_login_ui.h" ]
    public_deps += [
      "//chrome/common",
      "//ui/web_dialogs",
    ]
  }
}

source_set("login_impl") {
  sources = [
    "inline_login_handler.cc",
    "login_ui_service.cc",
    "login_ui_service_factory.cc",
  ]

  public_deps = [ "//chrome/browser:browser_public_dependencies" ]

  deps = [
    ":login",
    "//base",
    "//build:chromeos_buildflags",
    "//chrome/browser:browser_process",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/ui:browser_navigator_params_headers",
    "//chrome/common",
    "//chrome/common:constants",
    "//components/metrics:metrics_pref_names",
    "//components/prefs",
    "//components/signin/public/base",
    "//content/public/browser",
    "//google_apis",
    "//net",
    "//services/network/public/mojom:cookies_mojom",
    "//third_party/blink/public/common:headers",
  ]

  if (enable_dice_support) {
    sources += [ "inline_login_handler_impl.cc" ]
    deps += [
      ":signin",
      ":signin_utils",
      "//chrome/browser/new_tab_page/chrome_colors:generate_colors_info",
      "//chrome/browser/themes",
      "//components/password_manager/core/browser",
      "//components/signin/core/browser",
    ]
  }

  if (enable_dice_support || is_chromeos) {
    sources += [ "inline_login_ui.cc" ]
    deps += [
      "//chrome/browser/extensions",
      "//chrome/browser/prefs",
      "//chrome/browser/resources/gaia_auth_host:resources",
      "//chrome/browser/ui/webui:webui_util",
    ]

    if (is_chromeos) {
      deps += [
        "//chrome/browser/ash/account_manager",
        "//chrome/browser/ash/profiles",
        "//chrome/browser/resources/ash/inline_login:resources",
        "//chrome/browser/resources/chromeos/arc_account_picker:resources",
        "//chrome/browser/resources/chromeos/edu_coexistence:resources",
        "//chrome/browser/resources/chromeos/gaia_action_buttons:resources",
        "//chrome/browser/resources/chromeos/supervision:resources",
        "//chrome/browser/ui/webui/ash/edu_coexistence",
        "//chrome/browser/ui/webui/signin/ash",
        "//ui/chromeos",
        "//ui/strings:ui_strings_grit",
      ]
    } else {
      deps += [ "//chrome/browser/resources/inline_login:resources" ]
    }
  }
}

if (!is_chromeos) {
  source_set("profile") {
    sources = [
      "managed_user_profile_notice_handler.h",
      "managed_user_profile_notice_ui.h",
      "profile_customization_handler.h",
      "profile_customization_ui.h",
      "profile_picker_handler.h",
      "profile_picker_ui.h",
    ]

    public_deps = [
      ":signin_utils",
      "//base",
      "//build:buildflag_header_h",
      "//build:chromeos_buildflags",
      "//chrome/browser/profiles:profile",
      "//chrome/browser/profiles:profile_util",
      "//chrome/browser/ui:browser_list",
      "//chrome/browser/ui/webui/top_chrome",
      "//chrome/common",
      "//components/signin/public/identity_manager",
      "//components/user_education/webui",
      "//content/public/browser",
      "//google_apis",
      "//mojo/public/cpp/bindings",
      "//skia",
      "//ui/gfx/geometry",
      "//ui/webui",
      "//ui/webui/resources/cr_components/help_bubble:mojo_bindings",
    ]
  }

  source_set("profile_impl") {
    sources = [
      "managed_user_profile_notice_handler.cc",
      "managed_user_profile_notice_ui.cc",
      "profile_customization_handler.cc",
      "profile_customization_ui.cc",
      "profile_picker_handler.cc",
      "profile_picker_ui.cc",
    ]

    public_deps = [ "//chrome/browser:browser_public_dependencies" ]

    deps = [
      ":login",
      ":profile",
      ":signin_ui_error",
      ":signin_utils",
      "//base",
      "//build:buildflag_header_h",
      "//build:chromeos_buildflags",
      "//chrome/app:branded_strings",
      "//chrome/app:generated_resources",
      "//chrome/browser:browser_process",
      "//chrome/browser:resources",
      "//chrome/browser/new_tab_page/chrome_colors:generate_chrome_colors_info",
      "//chrome/browser/profiles:profile",
      "//chrome/browser/profiles:profile_util",
      "//chrome/browser/resources/signin:resources",
      "//chrome/browser/resources/signin/profile_picker:resources",
      "//chrome/browser/search/background",
      "//chrome/browser/themes",
      "//chrome/browser/ui:browser_element_identifiers",
      "//chrome/browser/ui:browser_list",
      "//chrome/browser/ui:ui_features",
      "//chrome/browser/ui/color:color_headers",
      "//chrome/browser/ui/signin",
      "//chrome/browser/ui/webui:webui_util",
      "//chrome/browser/ui/webui/top_chrome",
      "//chrome/common",
      "//components/policy/core/browser:browser",
      "//components/policy/core/common:common",
      "//components/prefs",
      "//components/signin/public/base",
      "//components/signin/public/identity_manager",
      "//components/startup_metric_utils:startup_metric_utils",
      "//components/strings:components_strings",
      "//components/supervised_user/core/common:features",
      "//content/public/browser",
      "//google_apis",
      "//services/network/public/mojom:url_loader_base",
      "//skia",
      "//ui/base",
      "//ui/color:color_headers",
      "//ui/gfx",
      "//ui/native_theme",
      "//ui/resources",
      "//ui/strings:ui_strings",
      "//url",
    ]
  }
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "login_ui_service_unittest.cc",
    "signin_url_utils_unittest.cc",
    "sync_confirmation_handler_unittest.cc",
    "sync_confirmation_ui_unittest.cc",
  ]

  deps = [
    ":login",
    ":signin",
    ":signin_utils",
    "//base",
    "//base/test:test_support",
    "//build:buildflag_header_h",
    "//build:chromeos_buildflags",
    "//chrome/browser",
    "//chrome/browser:browser_process",
    "//chrome/browser/extensions",
    "//chrome/browser/extensions:test_support",
    "//chrome/browser/profiles:profile_util",
    "//chrome/browser/signin",
    "//chrome/browser/ui",
    "//chrome/common",
    "//chrome/test:test_support",
    "//components/consent_auditor:test_support",
    "//components/signin/public/base",
    "//components/signin/public/identity_manager:test_support",
    "//components/sync:test_support",
    "//components/sync/base",
    "//content/public/browser",
    "//content/test:test_support",
    "//extensions:test_support",
    "//google_apis",
    "//testing/gmock",
    "//testing/gtest",
    "//url",
  ]

  if (!is_chromeos) {
    sources += [
      "managed_user_profile_notice_handler_unittest.cc",
      "profile_picker_handler_unittest.cc",
      "signin_error_handler_unittest.cc",
      "turn_sync_on_helper_unittest.cc",
    ]
    deps += [
      ":signin_ui_error",
      "//chrome/browser/search_engines",
      "//components/unified_consent",
      "//components/user_manager",
    ]
  }

  if (enable_dice_support) {
    sources += [ "dice_web_signin_intercept_handler_unittest.cc" ]
  }
}

source_set("test_support_ui") {
  testonly = true
  sources = [
    "login_ui_test_utils.cc",
    "login_ui_test_utils.h",
  ]

  public_deps = [
    ":signin",
    "//base",
    "//components/signin/public/base",
  ]
  deps = [
    ":login",
    ":signin_utils",
    "//base/test:test_support",
    "//build:chromeos_buildflags",
    "//chrome/browser",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/ui",
    "//chrome/browser/ui/signin",
    "//chrome/test:test_support_ui",
    "//components/signin/public/identity_manager",
    "//content/public/browser",
    "//content/test:test_support",
  ]
}

if (!is_chromeos) {
  source_set("browser_tests") {
    testonly = true

    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

    sources = [ "turn_sync_on_helper_browsertest.cc" ]

    deps = [
      ":signin",
      "//base",
      "//base/test:test_support",
      "//build:buildflag_header_h",
      "//chrome/browser",
      "//chrome/browser/profiles:profile",
      "//chrome/browser/ui",
      "//chrome/test:test_support",
      "//components/signin/core/browser",
      "//components/signin/public/base",
      "//components/signin/public/identity_manager",
      "//components/signin/public/identity_manager:test_support",
      "//content/test:test_support",
      "//testing/gmock",
      "//testing/gtest",
    ]
  }
}
