# Copyright 2014 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")

static_library("webdata_services") {
  output_name = "webdata_services"

  sources = [
    "web_data_service_wrapper.cc",
    "web_data_service_wrapper.h",
  ]

  deps = [
    "//base",
    "//build:blink_buildflags",
    "//components/autofill/core/browser",
    "//components/autofill/core/common",
    "//components/keyed_service/core",
    "//components/password_manager/core/browser",
    "//components/plus_addresses/webdata",
    "//components/search_engines",
    "//components/signin/public/webdata",
    "//components/sync",
    "//components/webdata/common",
    "//sql",
  ]

  public_deps = [ "//components/os_crypt/async/browser" ]

  if (use_blink) {
    sources += [
      "web_data_service_wrapper_factory.cc",
      "web_data_service_wrapper_factory.h",
    ]
    deps += [
      "//components/keyed_service/content",
      "//components/payments/content:utils",
      "//content/public/browser",
    ]
  }
}

source_set("unit_tests") {
  testonly = true

  sources = [ "web_data_service_wrapper_unittest.cc" ]

  deps = [
    ":webdata_services",
    "//base",
    "//base/test:test_support",
    "//components/os_crypt/async/browser:test_support",
    "//components/search_engines",
    "//testing/gtest",
  ]
}
