# Copyright 2018 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//extensions/buildflags/buildflags.gni")

assert(enable_extensions)

# TODO(crbug.com/346472679): Delete web_applications/extensions/ directory.
source_set("extensions") {
  sources = [
    "extension_status_utils.cc",
    "extensions_manager.cc",
    "web_app_extension_shortcut.cc",
    "web_app_extension_shortcut.h",
    "web_app_uninstall_and_replace_job.cc",
  ]
  if (is_mac) {
    sources += [ "web_app_extension_shortcut_mac.mm" ]
  }

  # Enable the "exit_time_destructors" warning here to avoid accidentally
  # adding exit time destructors and because the flag is not enabled by default.
  # TODO(http://crbug.com/101600):  Remove once exit_time_destructors is enabled
  # by default.
  configs += [ "//build/config/compiler:wexit_time_destructors" ]

  public_deps = [ "//chrome/browser:browser_public_dependencies" ]

  deps = [
    "//base",
    "//chrome/browser:browser_process",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/web_applications",
    "//chrome/common",
    "//components/pref_registry",
    "//components/services/app_service",
    "//components/sync/model",
    "//components/webapps/browser:browser",
    "//components/webapps/common",
    "//content/public/browser",
    "//extensions/browser",
    "//skia",
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [ "web_app_uninstall_and_replace_job_unittest.cc" ]

  deps = [
    ":extensions",
    "//build:chromeos_buildflags",
    "//chrome/browser/web_applications",
    "//chrome/browser/web_applications:web_applications_test_support",
    "//chrome/browser/web_applications:web_applications_unit_tests",
    "//chrome/common",
    "//chrome/test:test_support",
    "//components/webapps/browser",
    "//components/webapps/common",
    "//content/public/browser",
    "//content/test:test_support",
    "//extensions/browser/install",
    "//extensions/common",
    "//skia",
    "//testing/gtest",
  ]

  if (is_chromeos) {
    deps += [
      "//chrome/browser",
      "//components/policy/core/common:common_constants",
    ]
  }

  if (is_chromeos_ash) {
    deps += [
      "//ash/constants",
      "//chrome/browser/ash/system_web_apps/test_support",
    ]
  }
}
