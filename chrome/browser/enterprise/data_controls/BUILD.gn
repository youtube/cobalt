# Copyright 2023 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//components/enterprise/buildflags/buildflags.gni")

assert(enterprise_data_controls)

source_set("data_controls") {
  sources = [
    "chrome_dlp_rules_manager.cc",
    "chrome_dlp_rules_manager.h",
    "chrome_rules_service.cc",
    "chrome_rules_service.h",
    "data_controls_dialog.cc",
    "data_controls_dialog.h",
    "dlp_reporting_manager.cc",
    "dlp_reporting_manager.h",
    "reporting_service.cc",
    "reporting_service.h",
  ]

  public_deps = [
    "//components/enterprise/data_controls",
    "//components/enterprise/data_controls:features",
  ]

  deps = [
    "//base",
    "//chrome/app:generated_resources",
    "//chrome/browser/extensions",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/safe_browsing",
    "//components/constrained_window",
    "//components/enterprise/content",
    "//components/enterprise/data_controls:dlp_policy_event_proto",
    "//components/keyed_service/core",
    "//components/policy/core/browser",
    "//components/prefs",
    "//components/reporting/client:report_queue",
    "//components/reporting/client:report_queue_configuration",
    "//components/reporting/client:report_queue_factory",
    "//components/reporting/proto:record_constants",
    "//components/reporting/util:status",
    "//components/strings:components_strings_grit",
    "//components/url_matcher",
    "//content/public/browser",
    "//third_party/abseil-cpp:absl",
    "//ui/views",
    "//url",
  ]
  if (is_chromeos) {
    deps += [ "//chromeos/startup" ]
  }
  if (is_chromeos_ash) {
    deps += [ "//components/user_manager" ]
  }
}

source_set("test_support") {
  testonly = true
  sources = [
    "data_controls_dialog_test_helper.cc",
    "data_controls_dialog_test_helper.h",
    "dlp_reporting_manager_test_helper.cc",
    "dlp_reporting_manager_test_helper.h",
  ]
  public_deps = [
    ":data_controls",
    "//base",
    "//base/test:test_support",
    "//chrome/test:test_support",
    "//components/account_id",
    "//components/enterprise/data_controls:dlp_policy_event_proto",
    "//components/enterprise/data_controls:test_support",
    "//components/prefs",
    "//components/reporting/client:test_support",
    "//components/reporting/util:status",
    "//content/test:test_support",
    "//testing/gmock",
    "//testing/gtest",
    "//third_party/abseil-cpp:absl",
  ]
  if (is_chromeos) {
    public_deps += [ "//chromeos/startup" ]
  }
  if (is_chromeos_ash) {
    public_deps += [ "//components/user_manager" ]
  }
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "chrome_rules_service_unittest.cc",
    "reporting_service_unittest.cc",
  ]
  deps = [
    ":data_controls",
    ":test_support",
    "//chrome/browser/enterprise/connectors/test:test_support",
    "//components/enterprise/data_controls:unit_tests",
  ]
  if (is_chromeos_ash) {
    deps += [ "//chrome/browser/ash:test_support" ]
  }
  if (is_chromeos_lacros) {
    deps += [ "//chromeos/lacros" ]
  }
}
