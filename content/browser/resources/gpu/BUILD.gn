# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//tools/grit/grit_rule.gni")
import("//tools/polymer/html_to_wrapper.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

generated_grd = "$target_gen_dir/resources.grd"

web_component_files = [
  "info_view.js",
  "info_view_table.js",
  "info_view_table_row.js",
]

# Files that are passed as input to html_to_wrapper().
html_files = []
foreach(f, web_component_files) {
  html_files +=
      [ string_replace(f, "." + get_path_info(f, "extension"), ".html") ]
}

# Files that are generated by html_to_wrapper().
html_wrapper_files = []
foreach(f, html_files) {
  html_wrapper_files += [ f + ".js" ]
}

non_component_files = [
  "browser_bridge.js",
  "gpu_internals.js",
  "vulkan_info.js",
]

html_to_wrapper("html_wrapper_files") {
  in_files = html_files
  template = "native"
  use_js = true
}

generate_grd("build_html_wrapper_grdp") {
  out_grd = "$target_gen_dir/html_wrapper_resources.grdp"
  grd_prefix = "gpu"
  input_files = html_wrapper_files
  input_files_base_dir = rebase_path(target_gen_dir, root_build_dir)
  deps = [ ":html_wrapper_files" ]
}

generate_grd("build_mojo_grdp") {
  out_grd = "$target_gen_dir/mojo_resources.grdp"
  grd_prefix = "gpu"
  input_files = [
    "vulkan_info.mojom-webui.js",
    "vulkan_types.mojom-webui.js",
  ]
  input_files_base_dir =
      rebase_path("${root_gen_dir}/mojom-webui/gpu/ipc/common", root_build_dir)
  deps = [ "//gpu/ipc/common:vulkan_interface_js__generator" ]
}

generate_grd("build_grd") {
  out_grd = generated_grd
  grd_prefix = "gpu"
  input_files =
      [ "gpu_internals.html" ] + web_component_files + non_component_files
  input_files_base_dir = rebase_path(".", "//")

  grdp_files = [
    "$target_gen_dir/html_wrapper_resources.grdp",
    "$target_gen_dir/mojo_resources.grdp",
  ]

  deps = [
    ":build_html_wrapper_grdp",
    ":build_mojo_grdp",
  ]
}

grit("resources") {
  source = generated_grd
  enable_input_discovery_for_gn_analyze = false
  deps = [ ":build_grd" ]

  outputs = [
    "grit/gpu_resources.h",
    "grit/gpu_resources_map.cc",
    "grit/gpu_resources_map.h",
    "gpu_resources.pak",
  ]
  output_dir = "$root_gen_dir/content"
}
